macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" [] (? (&& [= $scr_w %1] [= $scr_h %2]) "radioboxon" "radiobox")
]

checkboxdesc = [
    if (= $arg2 1) [
        guitext (format "^fa^f(textures/checkboxtwo) %1" $arg1)
    ] [
        guitext (format "^fa^f(textures/checkboxon) %1  ^f(textures/checkboxtwo) %2" $arg1 $arg2)
    ]
]

newgui options [
    guione [
        guifont "emphasis" [
            guicenter [ guibutton "graphics" [ showgui options_graphics ] ]
            guicenter [ guibutton "display" [ showgui options_display ] ]
            guicenter [ guibutton "user interface" [ showgui options_ui ] ]
            guicenter [ guibutton "sound" [ showgui options_sound ] ]
            guicenter [ guibutton "controls" [ showgui options_controls ] ]
            guistrut 0.5
            guicenter [ guibutton "autoexec" [ showgui options_autoexec ] ]
        ]
    ]
    cases $guirollovername "graphics" [
        guitooltip "configure graphical quality settings"
    ] "display" [
        guitooltip "configure display options and resolution"
    ] "user interface" [
        guitooltip "configure user interface and in-game hud settings"
    ] "sound" [
        guitooltip "configure sound and music settings"
    ] "controls" [
        guitooltip "configure controls, keyboard and mouse settings"
    ] "autoexec" [
        guitooltip "modify your autoexec.cfg, which is executed every launch"
    ]
    guistatus "select an option to edit your configuration options"
]

newgui options_graphics [
    guiheader "graphics"
    guistrut 67 1
    guitext "performance key: ^fgfast^f~, ^fymoderate^f~, ^foslow and pretty^f~"
    guibar
    guilist [
        guilist [
            if (&& [< $shaders 0] [= $renderpath 0]) [
                guicheckbox     "^fyshaders"            shaders 1 -1
            ] [
                guicheckbox     "^fyshaders"            shaders
            ]
            guitext "water" blank
            guitext (? (> $renderpath 0) (result "waterfalls") (result "^fAwaterfalls")) blank
            if (= $renderpath 0) [
                guicheckbox     "^foshadow maps"        shadowmap
                if (>= $maxtmus 3) [
                    guicheckbox "^fydynamic lights"     ffdynlights 5 0
                ] [ guinohitfx [ 
                    guitext "^fAdynamic lights" checkdisable
                ] ]
            ] [
                guicheckbox     "^fosoft shadows"       shadowmap
                if $glare [
                    glarepreset = 0
                    if (= $glarescale 1) [
                        if (= $blurglare 4) [glarepreset = 1]
                        if (= $blurglare 7) [glarepreset = 3]
                    ]
                    if (= $glarescale 2) [
                        if (= $blurglare 3) [glarepreset = 2]
                        if (= $blurglare 7) [glarepreset = 4]
                    ]
                    guicheckbox "^foglare"              glare
                ] [
                    guicheckbox "^foglare"              glare
                ]
            ]
            if $usetexrect [
                guicheckbox "^fomotion blur"            motionblur
            ] [ guinohitfx [ 
                guitext "^fAmotion blur" checkdisable
            ] ]
            guicheckbox "^fograss"                      grass
            if (> $renderpath 0) [
                guicheckbox "^fgdynamic lights"         maxdynlights 3 0
                guicheckbox "^fysoft particles"         depthfx
            ]
            guicheckbox "^fgglass reflection"           glassenv
            guicheckbox "^fgdecals"                     decals
            guicheckbox "^fgfix t-joints (world sparklies)" filltjoints
            guitext "textures" blank
            guitext "models" blank
            guitext "animation" blank
        ]
        guispring 1
        guibar
        guispring 1
        guilist [
            guilist [   // shaders
                if $shaders [
                    guiradio "^fglow detail" shaderdetail 1
                    guistrut 1
                    guiradio "^fyhigh detail" shaderdetail 3
                    //if $hasglsl [
                    //    guicheckbox "^foGLSL only" forceglsl
                    //]
                ] [ guinohitfx [ 
                    guitext "^fAlow detail" radiodisable
                    guistrut 1
                    guitext "^fAhigh detail" radiodisable
                    //if $hasglsl [
                    //    guitext "^fAGLSL only" checkdisable
                    //]
                ] ]
            ]
            guilist [   // water
                guicheckbox "^fyrefraction" waterrefract
                guistrut 1
                guicheckbox "^foreflection" waterreflect
                guistrut 1
                guicheckbox "^fgcaustics" caustics
            ]
            guilist [   // waterfalls
                if (> $renderpath 0) [
                    guicheckbox "^fyrefraction" waterfallrefract
                    guistrut 1
                    guicheckbox "^fgreflection" waterfallenv
                ] [ guinohitfx [ 
                    guitext "^fArefraction" checkdisable
                    guistrut 1
                    guitext "^fAreflection" checkdisable
                ] ]
            ]
            guilist [   // shadows
                if (= $renderpath 0) [
                    if $shadowmap [
                        guiradio "^fymedium quality" shadowmapsize 9 [blurshadowmap 1]
                        guistrut 1
                        guiradio "^fohigh quality" shadowmapsize 10 [blurshadowmap 2]
                    ] [
                        guicheckbox "^fgblob shadows" blobs
                    ]
                ] [
                    if $shadowmap [
                        guiradio "^fymedium quality" shadowmapsize 9 [blurshadowmap 1]
                        guistrut 1
                        guiradio "^fohigh quality" shadowmapsize 10 [blurshadowmap 2]
                    ] [
                        guicheckbox "^fgblob shadows" blobs
                    ]
                ]
            ]
            guilist [   // glare
                if (!= $renderpath 0) [
                    if $glare [
                        guiradio "^fysubtle" glarepreset 1 [blurglare 4; glarescale 1]
                        guistrut 1
                        guiradio "^fyglowy" glarepreset 2 [blurglare 3; glarescale 2]
                        guistrut 1
                        guiradio "^fosoft" glarepreset 3 [blurglare 7; glarescale 1]
                        guistrut 1
                        guiradio "^fointense" glarepreset 4 [blurglare 7; glarescale 2]
                    ] [ guinohitfx [ 
                        guitext "^fAsubtle" radiodisable
                        guistrut 1
                        guitext "^fAglowy" radiodisable
                        guistrut 1
                        guitext "^fAsoft" radiodisable
                        guistrut 1
                        guitext "^fAintense" radiodisable
                    ] ]
                ] [ guitext " " ] // spacer
            ]
            guilist [   // motion blur
                if (&& $motionblur $usetexrect) [
                    guiradio "^fosubtle" motionblurscale 0.5
                    guistrut 1
                    guiradio "^fomoderate" motionblurscale 1
                ] [ guinohitfx [ 
                    guitext "^fAsubtle" radiodisable
                    guistrut 1
                    guitext "^fAmoderate" radiodisable
                ] ]
            ]
            guilist [   // grass
                if $grass [
                    guiradio "^fyquick fade" grassdist 128
                    guistrut 1
                    guiradio "^fymoderate fade" grassdist 256
                    guistrut 1
                    guiradio "^foslow fade" grassdist 512
                ] [ guinohitfx [ 
                    guitext "^fAquick fade" radiodisable
                    guistrut 1
                    guitext "^fAmoderate fade" radiodisable
                    guistrut 1
                    guitext "^fAslow fade" radiodisable
                ] ]
            ]
            guilist [   // dynamic lights
                if (> $renderpath 0) [
                    if $maxdynlights [
                        guiradio "^fgmedium quality" maxdynlights 3
                        guistrut 1
                        guiradio "^fyhigh quality" maxdynlights 5
                    ] [ guinohitfx [ 
                        guitext "^fAmedium quality" radiodisable
                        guistrut 1
                        guitext "^fAhigh quality" radiodisable
                    ] ]
                ]
            ]
            guilist [   // soft particles
                if $depthfx [
                    guiradio "^fglow quality" depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
                    guistrut 1
                    guiradio "^fymedium quality" depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                    guistrut 1
                    guiradio "^fohigh quality" depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                ] [ guinohitfx [ 
                    guitext "^fAlow quality" radiodisable
                    guistrut 1
                    guitext "^fAmedium quality" radiodisable
                    guistrut 1
                    guitext "^fAhigh quality" radiodisable
                ] ]
            ]
            guitext " "  // spacer for glass reflection
            guilist [   // decals
                if $decals [
                    guiradio "^fgmedium quality" maxdecaltris 1024
                    guistrut 1
                    guiradio "^fyhigh quality" maxdecaltris 4096
                ] [ guinohitfx [ 
                    guitext "^fAmedium quality" radiodisable
                    guistrut 1
                    guitext "^fAhigh quality" radiodisable
                ] ]
            ]
            guitext " " // spacer for t-joints
            guilist [   // textures
                guiradio "^fglow quality" maxtexsize 256
                guistrut 1
                guiradio "^fgmedium quality" maxtexsize 512
                guistrut 1
                guiradio "^fyhigh quality" maxtexsize 0
            ]
            guilist [   // models
                guicheckbox "^fglighting" lightmodels
                guistrut 1
                guicheckbox "^fgreflection" envmapmodels
                guistrut 1
                guicheckbox "^fgglow" glowmodels
                if (> $renderpath 0) [
                    guicheckbox "^fybumpmap" bumpmodels
                ] [ guinohitfx [ 
                    guitext "^fAbumpmap" checkdisable
                ] ]
            ]
            guilist [   // animation
                guiradio "^fgmedium quality" matskel 1
                guistrut 1
                guiradio "^fghigh quality" matskel 0
            ]
        ]
    ]

    guitab postfx
    guistayopen [
        guibutton "(effect OFF)" "clearpostfx"
        guibutton "bloom (subtle: 30%)" "bloom 0.3"
        guibutton "bloom (bright: 55%)" "bloom 0.55"
        guibutton "bloom (intense: 80%)" "bloom 0.8"
        guibutton "rotoscope" "rotoscope 1"
        guibutton "rotoscope + blur3" "rotoscope 1 1"
        guibutton "rotoscope + blur5" "rotoscope 1 2"
        guibutton "sharpen" "setpostfx sharpen"
        guibutton "sobel" "setpostfx sobel"
        guibutton "invert" "setpostfx invert"
        guibutton "gbr" "setpostfx gbr"
        guibutton "bw" "setpostfx bw"
        guibutton "blur3" "setpostfx hblur3; addpostfx vblur3"
        guibutton "blur5" "setpostfx hblur5; addpostfx vblur5"
    ]
]

newgui options_display [
    guiheader "display"
    guistrut 50 1
    guilist [
        guitext "v-sync: "
        guiradio "on " vsync 1
        guiradio "off " vsync 0
        guiradio "set by display driver" vsync -1
    ]
    guilist [
        guitext "fullscreen: "
        guiradio "on " fullscreen 1
        guiradio "off " fullscreen 0
    ]
    guistrut 1
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 0)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"

    guitab "resolution"
    guistayopen [
        guilist [
            guilist [
                guifont "emphasis" [ guitext "^fa4:3" ]
                @(resbutton 320 240)
                @(resbutton 640 480)
                @(resbutton 800 600)
                @(resbutton 1024 768)
                @(resbutton 1152 864)
                @(resbutton 1280 960)
                @(resbutton 1400 1050)
                @(resbutton 1600 1200)
                @(resbutton 1792 1344)
                @(resbutton 1856 1392)
                @(resbutton 1920 1440)
                @(resbutton 2048 1536)
                @(resbutton 2800 2100)
                @(resbutton 3200 2400)
            ]
            guistrut 0.5
            guibar
            guistrut 0.5
            guilist [
                guifont "emphasis" [ guitext "^fa16:10" ]
                @(resbutton 320 200)
                @(resbutton 640 400)
                @(resbutton 1024 640)
                @(resbutton 1280 800)
                @(resbutton 1440 900)
                @(resbutton 1600 1000)
                @(resbutton 1680 1050)
                @(resbutton 1920 1200)
                @(resbutton 2048 1280)
                @(resbutton 2560 1600)
                @(resbutton 2880 1800)
                @(resbutton 3200 2000)
                @(resbutton 3840 2400)
            ]
            guistrut 0.5
            guibar
            guistrut 0.5
            guilist [
                guifont "emphasis" [ guitext "^fa16:9" ]
                @(resbutton 1024 600)
                @(resbutton 1280 720)
                @(resbutton 1366 768)
                @(resbutton 1600 900)
                @(resbutton 1920 1080)
                @(resbutton 2048 1152)
                @(resbutton 2560 1440)
                @(resbutton 2880 1620)
                @(resbutton 3200 1800)
                @(resbutton 3840 2160)
            ]
            guistrut 0.5
            guibar
            guistrut 0.5
            guilist [
                guifont "emphasis" [ guitext "^fa5:4" ]
                @(resbutton 600 480)
                @(resbutton 1280 1024)
                @(resbutton 1600 1280)
                @(resbutton 2560 2048)
            ]
            guistrut 0.5
            guibar
            guistrut 0.5
            guilist [
                guifont "emphasis" [ guitext "^fa5:3" ]
                @(resbutton 800 480)
                @(resbutton 1280 768)
                guibar
                guitext "Custom"
                guilist [
                    customw = $scr_w
                    customh = $scr_h
                    guifield customw 4 [scr_w $customw]
                    guifield customh 4 [scr_h $customh]
                ]
            ]
        ]
    ]
]

newgui options_ui [
    guiheader "hud"
    guistayopen [
        guistrut 40 1
        guifont "emphasis" [ guitext "^faradar" ]
        guibar
        guistrut 0.5
        guilist [
            guilist [
                guicheckbox2 "show radar" showradar 1
                guicheckbox2 "show items" radaritems 1
                guicheckbox2 "show players" radarplayers 1
            ]
            guistrut 2
            guilist [
                guicheckbox2 "show affinities" radaraffinity 1
                guicheckbox2 "show item names" radaritemnames 1
                guicheckbox2 "show player names" radarplayernames 1
            ]
        ]
        guistrut 0.3
        guifont "little" [ guitext "^t^fakey:  ^f(textures/checkboxon) not visible in SpecTV   ^f(textures/checkboxtwo) visible always" ]
        guistrut 0.5
        guitext "radar style ^fa(default: compass-distance)"
        guilist [
            guiradio2 "compass"             radarstyle 0
            guistrut 1
            guiradio2 "compass-distance"    radarstyle 1
            guistrut 1
            guiradio2 "full-hud"            radarstyle 2
            guistrut 1
            guiradio2 "corner"              radarstyle 3
        ]
        guistrut 0.5
        guilist [
            guitext "radar opacity:"
            guispring 1
            guilist [
                guistrut 25 1
                radarblendstorage = (*f $radarblend 100)
                guislider radarblendstorage 0 100 [ radarblend (divf $radarblendstorage 100) ]
            ]
        ]
        guifont "little" [ guitext "^fadefault: 100%" ]
        guistrut 1
        guifont "emphasis" [ guitext "^facrosshair" ]
        guibar
        guistrut 0.5
        guilist [
            guilist [
                guicheckbox2 "show crosshair" showcrosshair 1
                guicheckbox2 "weapon crosshairs" crosshairweapons 1
                guicheckbox2 "critical flash" crosshairflash
        guistrut 0.5
        guitext "weapon ammo/clip surrounding crosshair:"
        guilist [
            guiradio2 "no visible clip"             showclips 0
            guistrut 1
            guiradio2 "solid bar of clip"    showclips 1
            guistrut 1
            guiradio2 "individual clip"            showclips 2
        ]
        guistrut 0.5
            ]
            guispring 1
            guilist [
                guilist [
                    guispring 1
                    guiimage $crosshairtex [showgui options_xhair] 2 1 [] [crosshairtex "crosshairs/cross-01"]
                ]
                guilist [
                    guispring 1
                    guifont "little" [ guitext (stringreplace $crosshairtex "crosshairs/" "") ]
                ]
            ]
        ]
        guifont "emphasis" [ guitext "^fastats" ]
        guibar
        guistrut 0.5
        guilist [
            guilist [
                guicheckbox2 "names above head" aboveheadnames
                guicheckbox2 "team above head" aboveheadteam
                guicheckbox2 "damage above head" aboveheaddamage
            ]
            guistrut 2
            guilist [
                guicheckbox2 "impulse meter" inventoryimpulse 1
                guicheckbox2 "velocity meter" inventoryvelocity 1
            ]
            guistrut 2
            guilist [
                guibitfield "spectator input" inventoryinput 2
                guibitfield "own input" inventoryinput 1
            ]
        ]
        guistrut 0.5
        guilist [
            guitext "top scores:"
            guispring 1
            guilist [
                guistrut 25 1
                guislider inventoryscore 1 8
            ]
        ]
        guifont "little" [ guitext "^fadefault: 1" ]
        guistrut 1
        guifont "little" [
            cases $guirollovername "show radar" [
                guitext "toggles visibility of the radar completely"
            ] "show affinities" [
                guitext "toggles visibility of affinities (bomb, flag)"
            ] "show items" [
                guitext "toggles visibility of items"
            ] "show item names" [
                guitext "toggles visibility of item names"
            ] "show players" [
                guitext "toggles visibility of players"
            ] "show player names" [
                guitext "toggles visibility of player names"
            ] "compass" [
                guitext "compass-like radar showing direction of targets (classic style)"
            ] "compass-distance" [
                guitext "compass-like radar showing direction and relative distance of targets"
            ] "full-hud" [
                guitext "compass-distance style radar with affinities overlaid onto hud like beacons"
            ] "corner" [
                guitext "top-down view of map with targets' actual positions"
            ] "show crosshair" [
                guitext "toggles visibility of crosshair"
                checkboxdesc "blends crosshair based on accuracy" 1
            ] "weapon crosshairs" [
                guitext "uses weapon-specific crosshairs
                checkboxdesc "apply weapon color to crosshair" 1
            ] "critical flash" [
                guitext "crosshair flashes red at critical health"
            ] "show clips" [
                guitext "toggles visibility of weapon clip around crosshair"
            ] "showgui options_xhair" [
                guitext "click to change crosshair texture, opacity and sizing"
            ] "impulse meter" [
                guitext "toggles visibility of impulse meter"
                checkboxdesc "textual percentage" "graphical bar"
            ] "velocity meter" [
                guitext "toggles visibility of velocity meter"
                checkboxdesc "visible only in time race" "always visible"
            ] "spectator input" [
                guitext "toggles visibility of keyboard input when watching other players"
            ] "own input" [
                guitext "toggles visibility of your own keyboard input when playing"
            ] "damage above head" [
                guitext "toggles visibility of damage received above a player's head"
            ] "names above head" [
                guitext "toggles visibility of player names above head"
            ] "team above head" [
                guitext "toggles visibility of player team above head"
            ] () [
                guitext "hover over an item for a description"
                guitext " "
            ]
        ]
    ]

    guitab "scoreboard"
    guistayopen [
        guifont "emphasis" [ guitext "^facustomize scoreboard layout" ]
        guibar
        guistrut 0.5
        guilist [
            guilist [
                guicheckbox2 "show frags" scorefrags 1
                guicheckbox2 "show points" scorepoints
                guicheckbox2 "show status icons" scoreicons
                guicheckbox2 "show spectators" scorespectators
                guicheckbox2 "show account handle" scorehandles
            ]
            guistrut 2
            guilist [
                guicheckbox2 "show client numbers" scoreclientnum
                guicheckbox2 "show host info" scorehostinfo
                guicheckbox2 "show ping" scoreping
                guicheckbox2 "show packet jump" scorepj
                guicheckbox2 "show bot info" scorebotinfo
            ]
        ]
        guistrut 1
        guifont "emphasis" [ guitext "^faname hilight color" ]
        guibar
        guistrut 0.5
        guilist [
            scolr = (& (>> $scolrval 16) 0xFF)
            scolg = (& (>> $scolrval 8) 0xFF)
            scolb = (& $scolrval 0xFF)
            guilist [
                guibackground $scolrval
                guistrut 6 1
                guistrut 3
            ]
            guistrut 1
            guilistsplit n 3 "0 1 2" [
                guilist [
                    guistrut 30 1
                    guislider scol@(at "r g b" $n) 0 255 [
                        scolrval = (| (| (<< $scolr 16) (<< $scolg 8)) $scolb)
                        scorehilight $scolrval
                    ] 0 1 0x@(at "FF0000 00FF00 0000FF" $n)
                ]
            ]
        ]
        guistrut 1
        guibutton "reset default" [ resetvar scorehilight; scolrval = $scorehilight ]
        guistrut 2
        guifont "little" [
            cases $guirollovername "show frags" [
                guitext "toggles visibility of frag count"
                checkboxdesc "only visible in deathmatch" "visible in all modes"
            ] "show points" [
                guitext "toggles visibility of total points"
            ] "show laps" [
                guitext "toggles visibilty of lap count"
                checkboxdesc "only visible in lap modes" "visible in race"
            ] "show status icons" [
                guitext "toggles visibility of status icons (dead, alive, queued)"
            ] "show spectators" [
                guitext "toggles visibility of spectating players"
            ] "show account handle" [
                guitext "toggles visibility of player account handles"
            ] "show client numbers" [
                guitext "toggles visibility of player client numbers (cn)"
            ] "show host info" [
                guitext "toggles visibility of player hostnames"
            ] "show ping" [
                guitext "toggles visibility of player latency (ping)"
            ] "show packet jump" [
                guitext "toggles visibility of packet jump measurement"
            ] "show bot info" [
                guitext "toggles visibility of bot skill levels and their owner"
            ]
        ]
    ]

    guitab "console"
    guicheckbox "centered console" concenter
    guistrut 0.5
    guilist [
        guilist [
            guilist [
                guitext "passive lines:"
                guistrut 1
                guifield consize 3
            ]
        ]
        guistrut 2
        guilist [
            guilist [
                guitext "overflow lines:"
                guistrut 1
                guifield conoverflow 3
            ]
        ]
    ]
    guistrut 0.5
    guilist [
        guitext "font scale:"
        guistrut 1
        guifield conscale 5
    ]
    guistrut 0.5
    guilist [
        guitext "console opacity:"
        guispring 1
        guilist [
            guistrut 25 1
            conblendstorage = (*f $conblend 100)
            guislider conblendstorage 0 100 [ conblend (divf $conblendstorage 100) ]
        ]
    ]
    guifont "little" [ guitext "^fadefault: 60%" ]
    guistrut 0.5
    guitext "obituaries"
    guilist [
        guilist [
            guiradio2 "off"                                 showobituaries 0
            guiradio2 "only me"                             showobituaries 1
            guiradio2 "me + announcements"                  showobituaries 2
            guiradio2 "announcements; exclude dying bots"   showobituaries 3
            guiradio2 "announcements; exclude bot vs. bot"  showobituaries 4
            guiradio2 "everything"                          showobituaries 5
        ]
    ]
    guistrut 1
    guicheckbox "show obituary distances" showobitdists
    guicheckbox "show obituary health left" showobithpleft
    guistrut 1
    guifont "emphasis" [ guitext "^fachat console" ]
    guibar
    guistrut 0.5
    guilist [
        guistrut 0.5
        guicheckbox "show chat colors" colourchat [ON]
    ]
    guistrut 0.5
    guilist [
        guilist [
            guilist [
                guitext "passive lines:"
                guistrut 1
                guifield chatconsize 3
            ]
        ]
        guistrut 2
        guilist [
            guilist [
                guitext "overflow lines:"
                guistrut 1
                guifield chatconoverflow 3
            ]
        ]
    ]
    guistrut 0.5
    guilist [
        guitext "font scale:"
        guistrut 1
        guifield chatconscale 5
    ]
    guistrut 0.5
    guilist [
        guitext "console opacity:"
        guispring 1
        guilist [
            guistrut 25 1
            chatconblendstorage = (*f $chatconblend 100)
            guislider chatconblendstorage 0 100 [ chatconblend (divf $chatconblendstorage 100) ]
        ]
    ]
    guifont "little" [ guitext "^fadefault: 100%" ]
    guistrut 0.5
    guilist [
        guitext "text blink rate (ms):"
        guispring 1
        guilist [
            guistrut 25 1
            guilistslider textblinking "0 50 100 150 200 250 300 350 400 450 500 550 600 650 700 750 800 850 900 950 1000"
        ]
    ]
    guifont "little" [ guitext "^fadefault: 350ms; 0 = off" ]
    guistrut 0.5
    guilist [
        guitext "text intensity:"
        guispring 1
        guilist [
            guistrut 25 1
            guislider textminintensity
        ]
    ]
    guifont "little" [ guitext "^fadefault: 32" ]

    guitab "misc"
    guifont "emphasis" [ guitext "^faborders" ]
    guibar
    guilist [
        guitext "playing"
        guispring 1
        guilist [
            guistrut 5 1
            guislider playborder 0 3
        ]
        guistrut 1
        guitext "size:"
        guistrut 0.5
        guilist [
            guistrut 22 1
            _playbordersize = (*f $playbordersize 20)
            guislider _playbordersize 0 20 [ playbordersize (divf $_playbordersize 20) ]
        ]
    ]
    guilist [
        guitext "editing"
        guispring 1
        guilist [
            guistrut 5 1
            guislider editborder 0 3
        ]
        guistrut 1
        guitext "size:"
        guistrut 0.5
        guilist [
            guistrut 22 1
            _editbordersize = (*f $editbordersize 20)
            guislider _editbordersize 0 20 [ editbordersize (divf $_editbordersize 20) ]
        ]
    ]
    guilist [
        guitext "spectator"
        guispring 1
        guilist [
            guistrut 5 1
            guislider specborder 0 3
        ]
        guistrut 1
        guitext "size:"
        guistrut 0.5
        guilist [
            guistrut 22 1
            _specbordersize = (*f $specbordersize 20)
            guislider _specbordersize 0 20 [ specbordersize (divf $_specbordersize 20) ]
        ]
    ]
    guilist [
        guitext "waiting"
        guispring 1
        guilist [
            guistrut 5 1
            guislider waitborder 0 3
        ]
        guistrut 1
        guitext "size:"
        guistrut 0.5
        guilist [
            guistrut 22 1
            _waitbordersize = (*f $waitbordersize 20)
            guislider _waitbordersize 0 20 [ waitbordersize (divf $_waitbordersize 20) ]
        ]
    ]
    guilist [
        guitext "background"
        guispring 1
        guilist [
            guistrut 5 1
            guislider backgroundborder 0 3
        ]
        guistrut 1
        guitext "size:"
        guistrut 0.5
        guilist [
            guistrut 22 1
            _backgroundbordersize = (*f $backgroundbordersize 20)
            guislider _backgroundbordersize 0 20 [ backgroundbordersize (divf $_backgroundbordersize 20) ]
        ]
    ]
    guistrut 1
    guifont "emphasis" [ guitext "^fatext info" ]
    guibar
    guilist [
        guilist [
            guitext "fps display:"
            guiradio2 "off" showfps 0
            guiradio2 "number only" showfps 1
            guiradio2 "verbose" showfps 2
            guiradio2 "more verbose" showfps 3
        ]
        guistrut 3
        guilist [
            guitext "notice messages:"
            guiradio2 "off" shownotices 0
            guiradio2 "basic notices" shownotices 1
            guiradio2 "level 2" shownotices 2
            guiradio2 "level 3" shownotices 3
            guiradio2 "everything" shownotices 4
        ]
    ]
    guistrut 1
    guifont "emphasis" [ guitext "^famenus" ]
    guibar
            guicheckbox "show tooltips" guitooltips [ON]
            guicheckbox "show game hints" guistatusline [ON]
            guicheckbox "show client GPU info" showloadinggpu [ON]
            guicheckbox "show versioning info" showloadingversion [ON]
            guicheckbox "show official site link" showloadingurl [ON]
] [
    if (= $guipasses 0) [
        scolrval = $scorehilight
    ]
]

newgui options_xhair [
    guiheader "crosshair"
    guistayopen [
        guicenter [
            guibutton "main" [ xhairprefix = "crosshair" ] [] (? (=s $xhairprefix "crosshair") "radioboxon" "radiobox")
            guistrut 1
            guibutton "hit"  [ xhairprefix = "hithair" ] [] (? (=s $xhairprefix "hithair") "radioboxon" "radiobox")
            guistrut 1
            guibutton "zoom" [ xhairprefix = "zoomcrosshair" ] [] (? (=s $xhairprefix "zoomcrosshair") "radioboxon" "radiobox")
            guistrut 1
            guibutton "team" [ xhairprefix = "teamcrosshair" ] [] (? (=s $xhairprefix "teamcrosshair") "radioboxon" "radiobox")
        ]
        xhairtex = (concatword $xhairprefix "tex")
        xhairblend = (concatword $xhairprefix "blend")
        xhairblendstorage = (concatword $xhairprefix "blendstorage")
        xhairsize = (concatword $xhairprefix "size")

        guistrut 0.5
        guilist [
            guistrut $crosshairsboxlen 1
            numcrosshairs = (listlen $crosshairlist)
            crosshairindex = (min (max 0 (- $numcrosshairs $crosshairsboxlen)) $crosshairindex)
            guilist [
                guistrut 35 1
                loop i $crosshairsboxlen [
                    q = (+ $crosshairindex $i)
                    curcrosshair = (at $crosshairlist $q)
                    crosshairname = (concatword "crosshairs/" $curcrosshair)
                    guibody [
                        guilist [
                            guistrut 35 1
                            guistrut 0.3
                            guilist [
                                guiimage $crosshairname [] 1.5 1
                                guistrut 1
                                guilist [
                                    guistrut 1
                                    guibutton (format "%1%2" (? (=s $$xhairtex $crosshairname) "^fy" "^fa") $curcrosshair)
                                ]
                            ]
                            guistrut 0.3
                        ]
                    ] [ ($xhairtex @crosshairname) ]
                ]
            ]
            guislider crosshairindex 0 (max 0 (- $numcrosshairs $crosshairsboxlen)) [] 1 1
        ]
        guistrut 1
        if (&& (!=s $xhairprefix "hithair") (!=s $xhairprefix "teamcrosshair")) [
            guilist [
                guitext "crosshair opacity:"
                guispring 1
                guilist [
                    guistrut 20 1
                    $xhairblendstorage = (*f $$xhairblend 100)
                    guislider $xhairblendstorage 0 100 [ ($xhairblend (divf $$xhairblendstorage 100)) ]
                ]
            ]
            guifont "little" [ guitext "^fadefault: 100%" ]
            guistrut 0.5
            guilist [
                guitext "crosshair size:"
                guistrut 1
                guifield $xhairsize 6
            ]
            guifont "little" [ guitext "^fadefault: 0.04" ]
        ] [ guistrut 4.5 ]
    ]
] [
    if (= $guipasses 0) [
        crosshairlist = ""
        crosshairsboxlen = 5
        xhairprefix = "crosshair"
        xhairtex = (concatword $xhairprefix "tex")
        xhairblend = (concatword $xhairprefix "blend")
        xhairblendstorage = (concatword $xhairprefix "blendstorage")
        xhairsize = (concatword $xhairprefix "size")

        loopfiles crosshairs crosshairs png [
            crosshairlist = (concat $crosshairlist $crosshairs)
        ]
        crosshairlist = (sortlist $crosshairlist a b [<s $a $b])
        crosshairindex = (max 0 (listfind mcurcrosshair $crosshairlist [stringcmp (concatword "crosshairs/" $mcurcrosshair) $$xhairtex]))
    ]
]

newgui options_sound [
    guiheader   "sound"
    guilist [
        guilist [
            guistrut 70 1
            guitext "master volume"
            guislider mastervol

            guitext "sound volume"
            guislider soundvol
            guitext "music volume"
            guislider musicvol
            guilist [ guitext "allocated sound channels"; guifont "little" [ guitext "(allocation, NOT the number of speakers)" ] ]
            guislider soundachans
            guitext "sound frequency"
            guilistslider soundfreq "11025 22050 44100"
            guitext "sound buffer length"
            guislider soundbufferlen
        ]
    ]
    guistrut 1
    guilist [
        guilist [
            guitext "in-game music:"
            guitext "edit mode music:"
        ]
        guistrut 2
        guilist [
            guilist [
                guiradio "off" 	    musictype 0
                guistrut 1
                guiradio "mapmusic" musictype 1
                guistrut 1
                guiradio "random"   musictype 2
            ]
            guilist [
                guiradio "off" 	    musicedit 0
                guistrut 1
                guiradio "mapmusic" musicedit 1
                guistrut 1
                guiradio "random"   musicedit 2
            ]
        ]
    ]
    guistrut 1
    guicheckbox "mumble positional audio" mumble
    guicheckbox "play map-specific sounds" soundenvvol [1.0 0]
    guistrut 1
    guibutton "^fyopen sound debugger" [showgui cursounds]
]

newgui options_autoexec [
    guiheader "autoexec"
    guitext "autoexec.cfg"
    showfileeditor "autoexec.cfg" -80 20
]

bindtypes = [ default spectator editing waiting ]
bindcalls = [ "" spec edit wait ]

bindactions = [
    forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
    "primary" "secondary" "reload" "use" "jump" "walk" "crouch" "special" "drop" "affinity"
    "weapon 1" "weapon 2" "weapon 3" "weapon 4" "weapon 5" "weapon 6" "weapon 7" "weapon 8" "weapon 9" "weapon 10"
    "saycommand /" "saytextcommand (getsaycolour)" "sayteamcommand (getsaycolour)" toggleconsole edittoggle thirdpersonswitch screenshot
    addbot delbot "showgui maps 1" "showgui maps 2" "setpriv 1" "showgui profile 2" "showgui team"
    "showcompass voice" "showcompass team"
]
bindtitles = [
    forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
    "primary" "secondary" "reload" "use" "jump" "walk" "crouch" "parkour/kick" "drop" "flag/bomb"
    "weap slot 1" "weap slot 2" "weap slot 3" "weap slot 4" "weap slot 5" "weap slot 6" "weap slot 7" "weap slot 8" "weap slot 9" "weap slot 10"
    "cmd input" "all chat" "team chat" "toggle console" "toggle editing" "toggle thirdperson" "screenshot"
    "add bot" "delete bot" "maps menu" "maps voting" "claim privileges" "loadout menu" "team menu"
    "voice compass" "team compass"
]

specbindactions = [
    forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
    "saycommand /" "saytextcommand (getsaycolour)" "sayteamcommand (getsaycolour)" toggleconsole edittoggle thirdpersonswitch screenshot
    addbot delbot "showgui maps 1" "showgui maps 2" "setpriv 1" "showgui profile 2" "showgui team"
    "showcompass voice" "showcompass team"
]
specbindtitles = [
    forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
    "cmd input" "all chat" "team chat" "toggle console" "toggle editing" "toggle thirdperson" "screenshot"
    "add bot" "delete bot" "maps menu" "maps voting" "claim privileges" "loadout menu" "team menu"
    "voice compass" "team compass"
]

waitbindactions = [
    forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
    "saycommand /" "saytextcommand (getsaycolour)" "sayteamcommand (getsaycolour)" toggleconsole edittoggle thirdpersonswitch screenshot
    addbot delbot "showgui maps 1" "showgui maps 2" "setpriv 1" "showgui profile 2" "showgui team"
    "showcompass voice" "showcompass team"
]
waitbindtitles = [
    forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
    "cmd input" "all chat" "team chat" "toggle console" "toggle editing" "toggle thirdperson" "screenshot"
    "add bot" "delete bot" "maps menu" "maps voting" "claim privileges" "loadout menu" "team menu"
    "voice compass" "team compass"
]

editbindactions = [
    forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
    "saycommand /" "saytextcommand (getsaycolour)" "sayteamcommand (getsaycolour)" toggleconsole edittoggle screenshot
    "showtexgui" "showgui editing" "remip" "fullbright 0; patchlight" "fullbright 0; calclight -1" "fullbright 0; calclight 1"
    savemap "changeoutline 1"
]
editbindtitles = [
    forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
    "cmd input" "all chat" "team chat" "toggle console" "toggle editing" "screenshot"
    "texture menu" "editing menu" "remip" "patch lights" "quick calclight" "full calclight"
    "save map" "change outline"
]

curbindtype = 0
curbindshow = 0

newgui options_controls [
    guiheader  "keys"
    guilist [
        guilist [ guistrut 28 ]
        guilist [
            guitext "basic keybinds, for anything more use the 'bind' command"
            guitext "select action to bind and press desired keys (ESC when done):"
            guistrut 1
            guilist [
                guitext "state:"
                guistrut 1
                loop i 4 [ guiradio (at $bindtypes $i) curbindtype $i; guistrut 1 ]
                guicheckbox "show default bind if not found" curbindshow
            ]
            guistrut 1
            curbindsign = (at $bindcalls $curbindtype)
            curbindcall = (concatword $curbindsign bind)
            curbindacts = (concatword $curbindsign bindactions)
            curbindtits = (concatword $curbindsign bindtitles)
            curbindsrch = (concatword search $curbindsign binds)
            guilistsplit n 2 $[@curbindacts] [
                guilist [
                    guitext (tabify (concatword (at $[@curbindtits] $gli) " ") 5)
                    [newbinds@curbindsign@gli] = ([@curbindsrch] $n 0 "" "" " " " " @curbindshow)
                    guikeyfield [newbinds@curbindsign@gli] -24 [
                        oldbinds = ([@@curbindsrch] @n 0 "" "" " " " "  @@curbindshow)
                        looplist j $oldbinds [echo @@curbindcall " " $j; @@curbindcall $j ""]
                        looplist j $[newbinds@@curbindsign@@gli] [echo @@curbindcall $j " " @@n; @@curbindcall $j [@@@n]]
                    ]
                ]
            ] [ guistrut 2 ]
        ]
    ]

    guitab  "mouse"
    guilist [
        guilist [
            guitext "mouse overall sensitivity  "
            guitext "mouse yaw sensitivity"
            guitext "mouse pitch sensitivity"
            guistrut 1
            guicheckbox "invert y-axis" mouseinvert
        ]
        guilist [
            guistrut 50 1
            guislider sensitivity 1 100
            guislider yawsensitivity 1 100
            guislider pitchsensitivity 1 100
        ]
    ]
] [
    if (= $guipasses 0) [ curbindtype = 0 ]
]
