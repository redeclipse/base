// Made by Maxim (acerspyro) Therrien, completed on November 14th, 2014 (CC-BY-SA)
// last modification: March 1st, 2015 (bonifarz)

//guifont super = guifont emphasis
chosenmode = -1
chosenmuts = 0
chosenmap = ""
chosendemo = -1
chosendemoimage = ""
chosendemofilename = ""

mapfavs = ""
setpersist mapfavs 1
setcomplete mapfavs 1
demofavs = ""
setpersist demofavs 1
setcomplete demofavs 1
custommapname = ""
mapsearch = ""
mapsearchon = 0
mapresults = ""
mapocta = 0

mapindex = 0

mapcategory = 0
maplist = ""

voteindex = 0
votenum = 0

modevar = [
    local g m s
    guistayopen [ guilist [
        g = $arg4
        m = $$arg3
        s = $$arg5
        if (ismodelocked $g $m 0 $s) [ guinohitfx [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" ] ] [
             guibutton $arg1 [
             @arg2 = @arg4
             if (= $chosenmode 0) [mapcategory = -1] [
                chosendemoimage = ""
                if (= $mapcategory -1) [mapcategory = 0]
             ]
             fillmaparray
             if (< (indexof $maplist $chosenmap) 0) [chosenmap = ""]
          ] [] (? (= $$arg2 $arg4) "checkboxon" "checkbox")
        ]
    ] ]
]

mutsvar = [
    local g m s t
    guistayopen [ guilist [
        g = $$arg2
        m = $$arg3
        s = $$arg5
        if (< $g 0) [ guinohitfx [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" ] ] [
             if (|| (& (mutsimplied $g $m) $arg4) (& $mutslockforce $arg4)) [
                 guibutton $arg1 [ implied = @arg4 ] [] "checkboxtwo"
             ] [
                 if (ismodelocked $g (| $m $arg4) $arg4 $s) [ guinohitfx [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" ] ] [
                     t = (& $m $arg4)
                     guibutton $arg1 [
                          mutate = @arg4
                          if @t [@@arg3 = @@(mutscheck $g (- $m $arg4))] [
                              @@arg3 = @@(mutscheck $g (| $m $arg4) $arg4)
                          ]
                          fillmaparray
                     ] [] (? $t "checkboxon" "checkbox")
                 ]
             ]
        ]
    ] ]
]

mapselection = [
    guilist [
        guilist [
            local j
            j = 0
            looplist i (sublist $maplist $mapindex 8) [
                isfav = (!= (listfind j $mapfavs [result (=s $j $i)]) -1)
                guistrut 0.2
                guilist [
                    if (=s $chosenmap $i) [guibackground 0x444444] [guibackground 0x222222]
                    guilist [
                        guimerge 30 [
                            guiimage (? (= $mapcategory 2) $i (concatword "maps/" $i)) [] 1.5 1 "textures/emblem"
                            guistrut 1
                            guilist [
                                guistrut 0.5
                                guibutton (concatword (? ($isfav) "^fb" "") (? (= $mapcategory 2) (substring $i 5) $i)) []
                                guifont "little" [
                                    if (!= $mapcategory 2) [guibutton (getmapmodemuts $i) []] [guibutton "^f[0x808080]sauerbraten" []]
                                ]
                                guistrut 25 1
                            ]
                        ] [
                            if (= $chosenmode -1) [fillmaparray]
                            chosenmap = @i
                        ] [] []
                    ]
                    j = (+ $j 1)
                    guilist [
                        guispring 1
                        if ($isfav) [
                            guiimage "textures/staron" [mapfavs = (listdel $mapfavs @@i); fillmaparray] 0.75 0 "" [] 0x6666ff
                        ] [
                            guiimage "textures/star"   [mapfavs = (concat  $mapfavs @@i); fillmaparray] 0.75 0 "" [] 
                        ] 
                        if (> $mapcategory 0)[ 
                            curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
                            isprev = (!= (listfind j $curprev [result (=s $j $i)]) -1)
                            islocked = (= (listfind j (getmaplist $chosenmode $chosenmuts $allowmaps) [result (=s $j $i)]) -1)
                            if ($isprev) [
                                guinohitfx [guiimage "textures/waiting" [] 0.75 0 "" [] 0x999999 ]
                            ] [
                                if ($islocked) [guinohitfx [guiimage "textures/warning" [] 0.75 0 "" [] 0x999999 ]]
                            ]
                            if (=s $guirollovername textures/warning) [ guitooltip "this map is not in the rotation" ]
                            if (=s $guirollovername textures/waiting) [
                                guitooltip (format "recently played maps: %1" (prettylist $curprev and)) 400
                            ]
                        ]

                        if (=s $guirollovername textures/star) [guitooltip "add to favorites and move to the top" 400]
                        if (=s $guirollovername textures/staron) [guitooltip "remove from favorites" ]
                        guispring 1
                    ]
                ]
            ]
            if (=s $maplist "") [
                j = 1
                guistrut 0.2
                guimerge 35 [
                    guibackground 0x444444 
                    guiimage "textures/info" [] 1.5 1 
                    guistrut 1
                    guilist [
                        guistrut 0.5
                        guitext "no matching maps" 
                        guifont "little" [ guitext "^fdtry another search filter"]
                    ]
                    guispring
                    guiimage "textures/guiexit" [ mapsearch = "" ; mapsearchon = 0; fillmaparray] 0.5
                ]
            ]    
            if (< $j 8) [
                loop i (- 8 $j) [
                    guistrut 0.2
                    guimerge 30 [
                        guibackground 0x111111
                        guistrut 3 1
                        guistrut 35
                    ] []
                ]
            ]
        ]
        guislider mapindex 0 (max 0 (- (listlen $maplist) 8)) [] 1 1
    ]
]

parsedemo = [
    local y mn d h m months
    months = "January February March April May June July August September October November December"
    y = (substring $arg1 0 4)
    mn = (substring $arg1 4 2)
    d = (substring $arg1 6 2)
    h = (substring $arg1 8 2)
    m = (substring $arg1 10 2)
    result (concatword (at $months (- $mn 1)) " " $d ", " $y ", " $h ":" $m)
]

demoselection = [
    guilist [
        guilist [
            local j
            j = 0
            looplist i (sublist $maplist $mapindex 8) [
                isfav = (!= (listfind j $demofavs [result (=s $j $i)]) -1)
                guistrut 0.2
                guilist [
                    if (=s $chosenmap $i) [guibackground 0x444444] [guibackground 0x222222]
                    guilist [
                        guimerge 30 [
                            t_chosendemo = (demoscan (concatword "demos/" $i ".dmo"))
                            guiimage (concatword "maps/" (demoinfo $t_chosendemo 1)) [] 1.5 1 "textures/emblem"
                            guistrut 1
                            guilist [
                                guistrut 0.5
                                guibutton (concatword (? ($isfav) "^fb" "") (? (= (stringlen $i) 14) (parsedemo $i) $i)) []
                                guifont "little" [
                                    guibutton (concatword "^f[0x808080]" (gamename (demoinfo $t_chosendemo 2) (demoinfo $t_chosendemo 3) 0 32) " on " (demoinfo $t_chosendemo 1)) []
                                ]
                                guistrut 25 1
                            ]
                        ] [
                            chosenmap = @i
                            chosendemo = (demoscan (concatword "demos/" @i ".dmo"))
                            chosendemoimage = (concatword "maps/" (demoinfo $chosendemo 2))
                        ] [] []
                    ]
                    j = (+ $j 1)
                    guilist [
                        guispring 1
                        if ($isfav) [
                            guiimage "textures/staron" [mapfavs = (listdel $mapfavs @@i); fillmaparray] 0.75 0 "" [] 0x6666ff
                        ] [
                            guiimage "textures/star"   [mapfavs = (concat  $mapfavs @@i); fillmaparray] 0.75 0 "" [] 
                        ] 
                        if (=s $guirollovername textures/star) [guitooltip "add to favorites and move to the top" 400]
                        if (=s $guirollovername textures/staron) [guitooltip "remove from favorites" ]
                        guispring 1
                    ]
                ]
            ]
            if (=s $maplist "") [
                j = 1
                guistrut 0.2
                guimerge 35 [
                    guibackground 0x444444 
                    guiimage "textures/info" [] 1.5 1 
                    guistrut 1
                    guilist [
                        guistrut 0.5
                        guitext "no matching maps" 
                        guifont "little" [ guitext "^fdtry another search filter"]
                    ]
                    guispring
                    guiimage "textures/guiexit" [ mapsearch = "" ; mapsearchon = 0; fillmaparray] 0.5
                ]
            ]    
            if (< $j 8) [
                loop i (- 8 $j) [
                    guistrut 0.2
                    guimerge 30 [
                        guibackground 0x111111
                        guistrut 3 1
                        guistrut 35
                    ] []
                ]
            ]
        ]
        guislider mapindex 0 (max 0 (- (listlen $maplist) 8)) [] 1 1
    ]
]

voteselection = [
    guilist [
        guilist [
            guicheckbox "^fcdynamic sort" sortvotes; guistrut 1.5
            guicheckbox "^focleanup list" cleanvotes; guistrut 1.5
            guibutton "^fyclear vote" clearvote
            guispring
            guilist [
                guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend
                guilist [
                    guistrut 1
                    guitext (format "^fc%1 ^fwvote%2" $votenum (? (!= $votenum 1) "s"))
                    guistrut 1
                ]
            ]
        ]
        guistrut 1
        if (getvote) [
            guilist [
                guilist [
                    loop j (if (< (getvote) 8) [result (getvote)] [result 8]) [
                        i = (+ $j $voteindex)
                        guistrut 0.2
                        voteplayers = (getvote $i 0)
                        votemode = (getvote $i 1)
                        votemuts = (getvote $i 2)
                        votemap = (getvote $i 3)
                        voteself = 0
                        guilist [
                            loop j $voteplayers [if (= (getclientnum) (getvote $i 0 $j)) [voteself = 1]]
                            if (= $voteself 1) [guibackground 0x444444] [guibackground 0x222222]
                            guilist [
                                guimerge 74 [
                                    guistrut 1
                                    guicenter [
                                        guistrut 4 1
                                        votecolour = (? $voteself "^fy" "^fw")
                                        guicenter [guifont "default" [guibutton (concatword $votecolour $voteplayers)]]
                                        guicenter [guifont "little" [guibutton (format "%1vote%2" $votecolour (? (!= $voteplayers 1) "s"))]]
                                    ]
                                    guistrut 1
                                    if (!= (stringstr $votemap "base/") -1) [
                                        guiimage $votemap "" 1.5 1 "textures/emblem"
                                        echo $votemap
                                    ] [
                                        guiimage (concatword "maps/" $votemap) "" 1.5 1 "textures/emblem"
                                    ]
                                    guistrut 1
                                    guicenter [
                                        gname = (gamename $votemode $votemuts 0 32)
                                        guifont "reduced" [guibutton (format "^fy%1 ^faon ^fo%2" $gname (? (!= (stringstr $votemap "base/") -1) (substring $votemap 5) $votemap))]
                                        guilist [
                                            if (> $voteplayers 0) [
                                                guifont "reduced" [guibutton "^fwby "]
                                                guifont "little" [ 
                                                    pname = ""
                                                    pmore = 0
                                                    loop j $voteplayers [
                                                        if (|| $pmore (> (guitextwidth $pname) 60)) [ pmore = (+ $pmore 1) ] [
                                                            append pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                                        ]
                                                    ]
                                                    guibutton (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                                ]
                                            ] [guifont "reduced" [guibutton "^fano current votes"]]
                                        ]
                                    ]
                                ] [if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]] [] []
                            ]
                        ]
                    ]
                ]
                guislider voteindex 0 (max 0 (- (getvote) 8)) [] 1 1
                guistrut 24.2 1
            ]
        ] [
            guilist [
                guistrut 76
                guistrut 12 1
            ]
            guilist [
                guispring 1
                guibutton " There are no votes currently pending, ^fgsubmit^fw one yourself" [showgui maps 1]
                guispring 1
            ]
        ]
    ] [
        guivisible [ guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 2")) ]
    ]
]

initmapsel = [
    chosendemofilename = ""
    chosendemoserver = ""
    mapcategory = 0
    mapindex = 0
    chosenmap = ""
    chosendemo = -1
    chosenmode = -1
    chosenmuts = 0
    maplist = ""
    fillmaparray
]

extraoptions = [
    if (= $chosenmode 1) [
        guitext "^fbmap name"
        guistrut 0.5
        guifield chosenmap 34
    ]
    if (gspmutname $chosenmode 0) [
        guitext "^fbextra muts"
        guistrut 0.5
        guifont little [
            guiloopsplit n 3 $mutsidxgsn [
                mut = (gspmutname $chosenmode $n)
                if (stringlen $mut) [
                    mutsvar $mut chosenmode chosenmuts (<< 1 (+ $mutsidxgsp $n)) chosenmap
                ]
            ] [ guistrut 3 ]
        ]
    ]
]

fillmaparray = [ octapaks [
    maplist = ""
    case $mapcategory 0 [
        t_maplist = (getmaplist $chosenmode $chosenmuts $allowmaps)
        maplist = (listdel $t_maplist $mapfavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr $i $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ] 1 [
        t_maplist = ""
        loopfiles i maps mpz [
            t_maplist = (concat $t_maplist $i)
        ]
        t_maplist = (sortlist $t_maplist a b [<s $a $b])
        maplist = (listdel $t_maplist $allowmaps)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = $maplist
        maplist = (listdel $t_maplist $mapfavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr $i $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ] 2 [
        t_maplist = ""
        loopfiles i base ogz [
            t_maplist = (concat $t_maplist (concatword "base/" $i))
        ]
        t_maplist = (sortlist $t_maplist a b [<s $a $b])
        maplist = (listdel $t_maplist $mapfavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr (substring $i 5) $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ] () [
        t_maplist = ""
        loopfiles i demos dmo [
            t_maplist = (concat $t_maplist $i)
            t_maplist = (sortlist $t_maplist a b [>s $a $b])
        ]
        maplist = (listdel $t_maplist $demofavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr (demoinfo (demoscan (concatword "demos/" $i ".dmo")) 1) $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ]
    mapindex = (? (< (+ $mapindex 8) (listlen $maplist)) $mapindex (? (< (- (listlen $maplist) 8) 0) 0 (- (listlen $maplist) 8)))
]]

getmapmodemuts = [
    local a
    a = "^f[0x808080]"
    if (!= (listfind i $capturemaps [=s $i $arg1]) -1) [
        a = (concatword $a "ctf ")
    ]
    if (!= (listfind i $defendmaps [=s $i $arg1]) -1) [
        a = (concatword $a "dtf ")
    ]
    if (!= (listfind i $bombermaps [=s $i $arg1]) -1) [
        a = (concatword $a "bb ")
    ]
    if (!= (listfind i $holdmaps [=s $i $arg1]) -1) [
        a = (concatword $a "hold ")
    ]
    if (!= (listfind i $racemaps [=s $i $arg1]) -1) [
        a = (concatword $a "race ")
    ]
    if (!= (listfind i $multimaps [=s $i $arg1]) -1) [
        a = (concatword $a "multi ")
    ]
    if (!= (listfind i $duelmaps [=s $i $arg1]) -1) [
        a = (concatword $a "duel ")
    ]
    if (&& (!= (listfind i $allowmaps [=s $i $arg1]) -1) (=s $a "^f[0x808080]")) [
        a = (concatword $a "deathmatch ")
    ]
    if (=s $a "^f[0x808080]") [
        a = (concatword $a "edit ")
    ]
    result $a
]

octapaks [
    fillmaparray
]

newgui maps [ guistayopen [ octapaks [
    guilist [
        guilist [
            guilist [
                guispring 1
                guitext (? (= $chosenmode -1) "game selection" (gamename $chosenmode (? (= $chosenmode 0) 0 $chosenmuts) 0 32))
                guispring 1
            ]
            guilist [
                guifont little [
                    guispring 1
                    if (= $chosenmode 0) [
                        if (> (stringlen $chosenmap) 0) [
                            guitext (concatword "^f[0x808080]" (gamename (demoinfo $chosendemo 2) (demoinfo $chosendemo 3) 0 32) " on " (demoinfo $chosendemo 1))
                        ] [
                            guitext "select a demo"
                        ]
                    ] [
                        guitext (? (=s $chosenmap "") "select a mode and a map to continue" (concat "on" (? (= $mapcategory 2) (substring $chosenmap 29) $chosenmap)))
                    ]
                    guispring 1
                ]
            ]
            guilist [
                guifont little [
                    guispring 1
                    guibutton "^f[0x9030F0]random" [
                        mapcategory = 0
                        chosenmap = (at $allowmaps (rnd (listlen $allowmaps)))
                        chosenmode = (+ (rnd (- $modeidxnum $modeidxplay)) $modeidxplay)
                        chosenmuts = (mutscheck $chosenmode (rnd $mutsbitall))
                        fillmaparray
                    ]
                    guispring 1
                    guibutton "^f[0x9030F0]reset" [
                        mapcategory = 0
                        chosenmap = ""
                        chosenmode = -1
                        chosenmuts = 0
                        chosendemoimage = ""
                        fillmaparray
                    ]
                    guispring 1
                    guibutton "^f[0x9030F0]copy" [
                        mapcategory = 1
                        chosenmode = (gamemode)
                        if (< (mutators) 0) [chosenmuts = 0] [chosenmuts = (mutscheck $chosenmode (mutators))]
                        chosendemoimage = ""
                        fillmaparray
                    ]
                    guispring 1
                ]
            ]
            guilist [
                guilist [
                    guitext "^fbmode"
                    guistrut 1
                    guifont medium [
                        loop z $modeidxnum [
                            if (&& (!= $servertype 1) (= $z 0) (isonline)) [
                                if (= $chosenmode 0) [
                                    initmapsel
                                ]
                            ] [
                                modevar (at $modename $z) chosenmode chosenmuts $z chosenmap
                            ]
                        ]
                    ]
                ]
                guistrut 3
                guilist [
                    guinohitfx [
                        if (= $mapcategory -1) [
                            guiimage $chosendemoimage [] 5 1 "textures/emblem"
                        ] [
                            if (= $mapcategory 2) [
                                guiimage $chosenmap [] 5 1 "textures/emblem"
                            ] [
                                guiimage (concatword "maps/" $chosenmap) [] 5 1 "textures/emblem"
                            ]
                        ]
                    ]
                ]
            ]
            guistrut 1
            if (!= $chosenmode 0) [
                guilist [
                    guilist [
                        guitext "^fbmuts"
                        guistrut 0.5
                        guifont medium [
                            count = (- $mutsidxnum $mutsidxgsn)
                            guiloopsplit n 3 $count [
                                mutsvar (at $mutsname $n) chosenmode chosenmuts (<< 1 $n) chosenmap
                            ] [ guistrut 3 ]
                        ]
                    ]
                ]
            ]
            guistrut 1
            extraoptions
            guispring 1
            if (&& (getclientpriv (getclientnum) $masterlock) (!= $chosenmode 0)) [
                guitext "^fbserver type"
                guistrut 0.5
                guilist [
                    guiradio "offline" servertype 0
                    guistrut 1
                    guiradio "private" servertype 1
                    guistrut 1
                    guiradio "public" servertype 2
                ]
            ]
        ]
        guispring 1
        guilist [
            if (&& (getclientpriv (getclientnum) $mapslock) (!= $chosenmode 0)) [
                guilist [
                    guibutton (? (= $mapcategory 0) "available" "^f[0x808080]available") [mapcategory = 0; chosenmap = ""; fillmaparray]
                    guistrut 1
                    guibutton (? (= $mapcategory 1) "all maps" "^f[0x808080]all maps") [mapcategory = 1; chosenmap = ""; fillmaparray]
                    if (= $hasoctapaks 1) [
                        guistrut 1
                        guibutton (? (= $mapcategory 2) "sauerbraten" "^f[0x808080]sauerbraten") [mapcategory = 2; chosenmap = ""; fillmaparray]
                    ]
                ]
            ] [
                guistrut 1
            ]
            guilist [
                    guifield mapsearch 25 [
                        if (stringlen $mapsearch) [mapsearchon = 1] [mapsearchon = 0]
                        fillmaparray
                    ] -1 0 "" 0 "^fd <enter search terms>" 1
                guistrut 1.0
                guicheckbox filter  mapsearchon 1 0 [fillmaparray] 
            ]
            if (= $chosenmode 0) [
                demoselection
            ] [
                mapselection
            ]
            guistrut 0.5
            guifont super [
                guicenter [
                    if (= (stringlen $chosenmap) 0) [
                        guitext (? (isonline) "^fdvote" "^fdplay")
                    ] [
                        guibutton (? (isonline) "^fgvote" "^fgplay") [if (isonline) [showgui maps 2];start $chosenmap $chosenmode (chosenmuts);mapsearch = ""]
                    ]
                ]
            ]
        ]
    ]
    guivisible [
        cases (at $guirolloveraction 0) "chosenmode" [
            guitooltip (modedesc (at $guirolloveraction 2) $chosenmuts 3)
        ] "mutate" [
            guitooltip (mutsdesc $chosenmode (at $guirolloveraction 2) 3)
        ] "implied" [
            guitooltip "this is forced on by the current configuration"
        ] "disabled" [
            guitooltip "this is disabled by the current configuration"
        ]
        guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 1"))
    ]
    guitab "votes"
    guilist [
        voteselection
        guivisible [
            guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 2"))
        ]
    ]
]]] [
     if (= $guipasses 0) [
          initmapsel
          voteindex = 0
     ]
]
