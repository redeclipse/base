resetmapgui = 0
mapselected = 0
mutsselected = 0
modeselected = -1
lastmode = -1
lastmuts = 0

mapindex = 0
lastmode = -1
lastmuts = 0
mapnum = -1
mapcur = ""
mappth = ""
maplist = ""
mappath = ""
mapextra = ""
mapimg = ""
mapocta = 0
if (getalias mapfavs) [] [mapfavs = ""]
setpersist mapfavs 1
setcomplete mapfavs 1
if (getalias demofavs) [] [demofavs = ""]
setpersist demofavs 1
setcomplete demofavs 1
if (getalias gamefavs) [] [gamefavs = ""]
setpersist gamefavs 1
setcomplete gamefavs 1
resetmapgui = 1
mapscount = 20
searchfilter = 0

mapsmenuinit = [
    modeselected = -1
    mutsselected = 0
    mapindex = 0
    lastmode = -1
    lastmuts = 0
    mapnum = -1
    mapcur = ""
    mappth = ""
    maplist = ""
    mappath = ""
    mapextra = ""
    mapimg = ""
    mapocta = 0
    resetmapgui = 1
    nummodes = 0
    curmode = 0
    loop g (- $modeidxnum 1) [
        q = (+ $g 1)
        if (! (ismodelocked $q)) [ nummodes = (+ $nummodes 1); curmode = $q ]
    ]
    if (= $nummodes 1) [ modeselected = $curmode ]
]

mapsmenuiter = [
    if (|| [!= $lastmode $modeselected] [!= $lastmuts $mutsselected]) [ resetmapgui = 1 ]
    if (= $resetmapgui 1) [
        maprot = 0
        maplist = ""
        mappath = ""
        if (= $modeselected $modeidxdemo) [
            mutsselected = 0
            demofavs = (sortlist $demofavs a b [<s $a $b])
            filelist = (sortlist (listfiles demos dmo) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $filelist [
                    if (? $q (< (listfind xcurmap $demofavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $demofavs [stringcmp $xcurmap $lcurmap]) 0)) [
                        append maplist $lcurmap
                        append mappath [demos/@lcurmap]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            append maplist "?"
            append mappath "?"
        ] [
            mutsselected = (mutscheck $modeselected $mutsselected)
            append maplist "<random>"
            append mappath "<random>"
            mapfavs = (sortlist $mapfavs a b [<s $a $b])
            curlist = (sortlist (? (>= $modeselected $modeidxplay) (getmaplist $modeselected $mutsselected (? (isonline) (listlen (listclients 1)) 0)) $allowmaps) a b [<s $a $b])
            curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $curlist [
                    if (< (listfind pcurmap $curprev [stringcmp $pcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            append maplist $lcurmap
                            append mappath [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            wantlist = 1
            looplist lcurmap $curprev [
                if $wantlist [
                    append maplist "~"
                    append mappath "~"
                    wantlist = 0
                ]
                append maplist $lcurmap
                append mappath [maps/@lcurmap]
            ]
            wantlist = 1
            filelist = (sortlist (listfiles maps mpz) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $filelist [
                    if (< (listfind mcurmap $maplist [stringcmp $mcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "*"
                                append mappath "*"
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append mappath [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& [> $mapocta 0] [> $hasoctapaks 0]) [
                wantlist = 1
                filelist = (sortlist (listfiles base ogz) a b [<s $a $b])
                loop q 2 [
                    looplist lcurmap $filelist [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "."
                                append mappath "."
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append mappath [base/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& (= 1 $searchfilter) (> (stringlen $mapextra) 0)) [
                // apply the search filter and keep special tags $t
                t = "~*.?"
                maplist = (listfilter xcurmap $maplist [ (|| (> (stringstr $xcurmap $mapextra) -1) (> (stringstr $t $xcurmap) -1))])
                mappath = (listfilter xcurmap $mappath [ (|| (> (stringstr $xcurmap $mapextra) -1) (> (stringstr $t $xcurmap) -1))])
                // if there are no maps between two $t tags, remove the first tag
                // if no matching maps are available to vote, begin with a "-" tag
                if (> (stringstr $t (at $maplist 0)) -1) [
                    xcurmap = "-"
                    pcurmap = "-"
                ] [
                    xcurmap = ""
                    pcurmap = ""
                ]
                loop i (listlen $maplist) [
                    if (|| (= (stringstr $t (at $maplist $i)) -1) (= (stringstr $t (at $maplist (+ 1 $i))) -1)) [
                        append xcurmap (at $maplist $i)
                        append pcurmap (at $mappath $i)
                    ]
                ]
                maplist = $xcurmap
                mappath = $pcurmap
            ]
            append maplist "?"
            append mappath "?"
            mapnum = (listfind curmap $maplist [
                || [=s $curmap $mapcur] [=s [maps/@curmap] $mapcur] [
                    && [> $hasoctapaks 0] [> $mapocta 0] [=s [base/@curmap] $mapcur]
                ]
            ])
        ]
        mapcount = (listlen $maplist)
        if (! $maprot) [ maprot = $mapcount ]
        lastmode = $modeselected
        lastmuts = $mutsselected
        mapindex = 0
        resetmapgui = 0
    ]
    if (|| [< $mapnum 0] [>= $mapnum $mapcount]) [
        mapnum = (? (= $modeselected $modeidxdemo) -1 0)
        mapcur = ""
        mappth = ""
        mapdmo = ""
        mapimg = ""
    ] [
    cases (at $maplist $mapnum) "?" [
        mapcur = $mapextra
        if (= $modeselected $modeidxdemo) [
            mappth = [demos/@mapcur]
            mapdmo = (demoscan (format "%1.dmo" $mappth))
            mapimg = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 1)) "textures/emblem")
        ] [
            mappth = $mapextra
            mapimg = $mapextra
        ]
    ] "<random>" [
        mapcur = "<random>"
        mappth = "<random>"
        mapimg = "question"
    ] () [
        mapcur = (at $maplist $mapnum)
        mappth = (at $mappath $mapnum)
        if (= $modeselected $modeidxdemo) [
            mapdmo = (demoscan (format "%1.dmo" $mappth))
            mapimg = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 1)) "textures/emblem")
        ] [
            mapimg = (at $mappath $mapnum)
        ]
    ] ]
]

mutsvar = [
    local g m s t
    guistayopen [ guilist [
        g = $$arg2
        m = $$arg3
        s = $$arg5
        if (< $g 0) [ guinoskinfx [ guinocursorfx [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
            if (|| (& (mutsimplied $g $m) $arg4) (& $mutslockforce $arg4)) [
                guibutton $arg1 [ implied = @arg4 ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 "checkboxon" $guicheckboxtwocolour
            ] [
                if (ismodelocked $g (| $m $arg4) $arg4 $s) [ guinoskinfx [ guinocursorfx [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
                    t = (& $m $arg4)
                    guibutton $arg1 [
                        mutate = @arg4
                        if @t [@@arg3 = @@(mutscheck $g (- $m $arg4))] [
                            @@arg3 = @@(mutscheck $g (| $m $arg4) $arg4)
                        ]
                    ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $t "checkboxon" "") $guicheckboxcolour
                ]
            ]
        ]
    ] ]
]

modevar = [
    local g m s
    guistayopen [ guilist [
        g = $arg4
        m = $$arg3
        s = $$arg5
        if (ismodelocked $g $m 0 $s) [ guinoskinfx [ guinocursorfx [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
            guibutton $arg1 [@arg2 = @arg4] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (= $$arg2 $arg4) "checkboxon" "") $guicheckboxcolour
        ]
    ] ]
]

mapsexec = [
    sleep 1 [
        if (isconnected) [ showgui maps 2 ]
        start @arg1 @arg2 @arg3
    ]
]

mapsmenu = [
    guilist [
        guistrut 70 1
        guilist [
            guilist [
                guilist [
                    guistrut 3 1
                    guilist [
                        guistrut 38 1
                        if (< $lastmode 0) [
                            guifont "emphasis" [ guitext "game select" ]
                            guitext "please select a mode and map to continue"
                        ] [
                            gname = (gamename $modeselected $mutsselected 0 32)
                            guilist [
                                guifont "emphasis" [ guitext $gname ]
                                if (&& (stringlen $mapcur) (= $modeselected $modeidxdemo)) [
                                    dinfo = (demoscan (format "%1.dmo" $mappth))
                                    dmode = (demoinfo $dinfo 2)
                                    dmuts = (demoinfo $dinfo 3)
                                    dname = (gamename $dmode $dmuts 0 32)
                                    guicenter [ guifont "little" [ guitext (format " (^fs^fa%1^fS)" $dname) ] ]
                                ]
                            ]
                            guilist [
                                if (stringlen $mapcur) [
                                    guitext " ^faselected on ";
                                    guitext $mapcur
                                    if (= $modeselected $modeidxdemo) [
                                        dinfo = (demoscan (format "%1.dmo" $mappth))
                                        dmapname = (demoinfo $dinfo 1)
                                        guicenter [ guifont "little" [ guitext (format " (^fs^fa%1^fS)" $dmapname) ] ]
                                    ]
                                ] [ guitext "please select a map to continue" ]
                            ]
                        ]
                        guistrut 0.125
                        guilist [
                            guispring
                            guistayopen [ guibutton "^fvpick random" [
                                pickrandom = 1
                                nummodes = 0
                                cntmodes = (- $modeidxnum $modeidxplay)
                                modeselected = (+ (rnd $cntmodes) $modeidxplay)
                                while [&& [ismodelocked $modeselected] [<= $nummodes $cntmodes]] [
                                    modeselected = (+ $modeselected 1)
                                    if (>= $modeselected $modeidxnum) [ modeselected = $modeidxplay ]
                                    nummodes = (+ $nummodes 1)
                                ]
                                mutsselected = (rnd (+ $mutsbitall 1))
                                loop g $mutsidxnum [
                                    q = (<< 1 $g)
                                    if (ismodelocked $modeselected $q) [ mutsselected = (&~ $mutsselected $q) ]
                                ]
                            ] ]
                            guispring
                            if (isconnected) [
                                guistayopen [
                                    guibutton "^focopy current" [
                                        copycurrent = 1
                                        modeselected = (gamemode)
                                        if (< (mutators) 0) [ mutsselected = 0 ] [ mutsselected = (mutscheck $modeselected (mutators)) ]
                                    ]
                                ]
                                guispring
                            ]
                            guistayopen [ guibutton "^fyreset selection" [
                                resetselection = 1
                                modeselected = -1
                                mutsselected = 0
                                mapnum = -1
                                mapcur = ""
                                mappth = ""
                                mapimg = ""
                                mapdmo = ""
                            ] ]
                            guispring
                        ]
                        guistrut 0.125
                    ]
                ]
                guilist [
                    guilist [
                        guifont "emphasis" [ guitext "mode" ]
                        guistrut 0.25
                        loop z $modeidxnum [
                            modevar (at $modename $z) modeselected mutsselected $z mapcur
                        ]
                    ]
                    guistrut 3
                    guilist [
                        guistayopen [
                            guiimage $mapimg [if (stringlen $mapcur) [ mapsexec @mapcur @modeselected @mutsselected ] [ conout 1 "you have not selected a map" ] ] 5 1 "textures/emblem"
                        ]
                    ]
                ]
                guifont "emphasis" [ guitext "mutators" ]
                guistrut 0.25
                cnt = (- $mutsidxnum $mutsidxgsn)
                guiloopsplit n 3 $cnt [
                    mutsvar (at $mutsname $n) modeselected mutsselected (<< 1 $n) mapcur
                ] [ guistrut 3 ]
                if (> (stringlen (gspmutname $modeselected 0)) 0) [
                    guistrut 0.25
                    guifont "emphasis" [ guitext "mode specific" ]
                    guistrut 0.25
                    guiloopsplit n 3 $mutsidxgsn [
                        mut = (gspmutname $modeselected $n)
                        if (stringlen $mut) [
                            mutsvar $mut modeselected mutsselected (<< 1 (+ $mutsidxgsp $n)) mapcur
                        ]
                    ] [ guistrut 3 ]
                ]
            ]
            guistrut 1
            guilist [
                guilist [
                    guitext "available maps:"
                    if (> $hasoctapaks 0) [
                        guispring
                        guistayopen [
                            guibutton "show sauer maps" [
                                mapocta = (! $mapocta)
                                resetmapgui = 1
                            ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (> $mapocta 0) "checkboxon" "") $guicheckboxcolour
                        ]
                        guispring 1
                        guitext "fav"
                        guistrut 3
                    ]
                ]
                guistrut 0.25
                guilist [
                    guicontainer [1] [
                        nummaps = (- (listlen $maplist) 1)
                        guilist [
                            guilist [
                                guistrut $mapscount 1
                                mapindex = (min (max 0 (- $nummaps $mapscount)) $mapindex) //safeguard
                                mapnum = (min $mapnum $nummaps)
                                guilist [
                                    guistrut 37 1
                                    break = 0
                                    loopwhile i $mapscount [= $break 0] [
                                        q = (+ $mapindex $i)
                                        curmap = (at $maplist $q)
                                        cases $curmap "*" [
                                            guitext "maps not in the rotation:"
                                        ] "~" [
                                            guitext "maps played recently:"
                                        ] "." [
                                            guitext "maps from sauerbraten:"
                                        ] "-" [
                                            guitext "^fanone for this search filter" "textures/info"
                                        ] "?" [
                                            break = 1
                                        ] () [
                                            guilist [
                                                guiradio (stringreplace $curmap $mapextra (format "^fs^fy%1^fS" $mapextra)) mapnum $q
                                                guispring 1
                                                guistayopen [
                                                    hasmap = (>= (indexof (? (= $modeselected $modeidxdemo) $demofavs $mapfavs) $curmap) 0)
                                                    guibutton "" [
                                                        if (= $modeselected $modeidxdemo) [
                                                            demofavs = (? @@hasmap (listdel $demofavs @@curmap) (concat (listdel $demofavs @@curmap) @@curmap))
                                                        ] [
                                                            mapfavs = (? @@hasmap (listdel $mapfavs @@curmap) (concat (listdel $mapfavs @@curmap) @@curmap))
                                                        ]
                                                        resetmapgui = 1
                                                    ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $hasmap "checkboxon" "") $guicheckboxcolour
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                            guilist [
                                guitext "custom map or "
                                guicheckbox "search filter" searchfilter 1 0 [resetmapgui = 1]
                            ]
                            guilist [
                                mapextraval = $mapextra
                                mapextranum = $nummaps
                                guiradio "" mapnum $mapextranum
                                guifield mapextraval 34 [mapextra = $mapextraval; mapnum = $mapextranum; resetmapgui = 1] -1 0 "" 0 "^fd<enter map name>" $searchfilter
                            ]
                        ]
                        guislider mapindex 0 (max (- $nummaps $mapscount) 0) [] 1 1
                    ] [
                        guistrut 40.25
                        guistrut (+f $mapscount 2) 1
                    ]
                ]
            ]
        ]
        guistrut 0.25
        guilist [
            if (! (isonline)) [
                guitext "server type:"; guistrut 0.5
                guiradio "offline" servertype 0; guistrut 0.5
                guiradio "private" servertype 1; guistrut 0.5
                guiradio "public" servertype 2
            ]
            guispring 1
            guifont "default" [
                if (>= $lastmode 0) [
                    guistayopen [
                        if (getclientcount) [
                            // other players are present
                            if (&& (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto")) [
                                guibutton "^fzwyforce new game (veto)" [ mapsexec $mapcur $modeselected $mutsselected ]
                            ] [
                                guibutton "^fgsubmit this vote" [ mapsexec $mapcur $modeselected $mutsselected ]
                            ]
                        ] [
                            //either playing offline or without other players
                            guibutton "^fgstart a new game" [ mapsexec $mapcur $modeselected $mutsselected ]
                        ]
                        guistrut 2
                        guibutton "add to favourites" [
                            fav = (format "%1.%2.%3" @mapcur @modeselected @mutsselected)
                            gamefavs = (concat $fav (listdel $gamefavs $fav)) 
                        ] [] "" 0x0080ff
                        guistrut 14
                    ]
                ] [
                    guitext "^fy.. pick a mode to continue .."
                    guistrut 10
                ]
            ]
        ]
        if (&& (getclientpriv $getclientnum $masterlock) (getclientcount)) [
            guistrut 0.2
            guistayopen [ guiright [
                guibutton (concat "master mode:" (getmastermode 1)) [mastermode (! (getmastermode))]
                guistrut 13.5
            ] ]
        ]
        guivisible [
            cases (at $guirolloveraction 0) "modeselected" [
                guitooltip (modedesc (at $guirolloveraction 2) $mutsselected 3)
            ] "mutate" [
                guitooltip (mutsdesc $modeselected (at $guirolloveraction 2) 3)
            ] "implied" [
                guitooltip "this is forced on by the current configuration"
            ] "disabled" [
                guitooltip "this is disabled by the current configuration"
            ]
            guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 1"))
        ]
    ]
]

voteindex = 0
votenum = 0
votemenuinit = [ voteindex = 0 ]

votemenu = [
    guipage vote 8 74 3.2 [1] (getvote) [
        voteplayers = (getvote $i 0)
        votemode = (getvote $i 1)
        votemuts = (getvote $i 2)
        votemap = (getvote $i 3)
        voteself = 0
        loop j $voteplayers [ if (= (getclientnum) (getvote $i 0 $j)) [ voteself = 1 ] ]
    ] [1] [
        guibutton "^fwThere are no votes currently pending, ^fgsubmit ^fwone yourself" [showgui maps 1] [] "chat"
    ] [
        guilist [
            guicheckbox "^fcdynamic sort" sortvotes; guistrut 1.5
            guicheckbox "^focleanup list" cleanvotes; guistrut 1.5
            guibutton "^fyclear vote" clearvote
            guispring
            guilist [
                guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend
                guilist [
                    guistrut 1
                    guitext (format "^fc%1 ^fwgame%2 suggested" $votenum (? (!= $votenum 1) "s"))
                    guistrut 1
                ]
            ]
        ]
        guistrut 0.25
    ] [
        guimerge 72 [
            guicenter [
                guistrut 4 1
                votecolour = (? $voteself "^fy" "^fw")
                guicenter [guifont "default" [guibutton (concatword $votecolour $voteplayers)]]
                guicenter [guifont "little" [guibutton (format "%1vote%2" $votecolour (? (!= $voteplayers 1) "s"))]]
            ]
            voteimage = "textures/chat"
            votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                && [> $mapocta 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
            ]])
            if (> $votepath -1) [ voteimage = (at $mappath $votepath) ]
            guistrut 1
            guiimage $voteimage "" 1.5 1 "textures/chat"
            guistrut 1
            guicenter [
                gname = (gamename $votemode $votemuts 0 32)
                guibutton (format "^fy%1 ^favoted for on ^fo%2" $gname $votemap)
                guilist [
                    if (> $voteplayers 0) [
                        guibutton "^fwby "
                        guifont "little" [
                            pname = ""
                            pmore = 0
                            loop j $voteplayers [
                                if (|| $pmore (> (guitextwidth $pname) 1220)) [ pmore = (+ $pmore 1) ] [
                                    append pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                ]
                            ]
                            guibutton (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                        ]
                    ] [ guibutton "^fano current votes" ]
                ]
            ]
        ] [
            if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]
        ]
    ] [
        guivisible [ guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 2")) ]
    ]
]

favindex = 0
favnum = 0

favsmenu = [
    guipage fav 8 74 3.2 [1] (listlen $gamefavs) [
        favs = (at $gamefavs $i)
        favl = (stringreplace $favs "." " ")
        votemap = (at $favl 0)
        votemode = (at $favl 1)
        votemuts = (at $favl 2) 
        votebad = (ismodelocked $votemode $votemuts $votemuts $votemap)
        if (= -1 (stringstr (getmaplist $votemode $votemuts) $votemap)) [votebad = 1]
    ] [1] [
        guibutton "^fwuse the maps menu to save your favourite game modes." [showgui maps 1] [] "info"
    ] [
        guitext (format "Select a game to %1. Use the buttons to clear, edit or push entries to the top or bottom." (? $getclientcount "vote" "start"))
        guistrut 0.25
    ] [
        guistrut 0.2
        guilist [
            guibody [
                guilist [
                    voteimage = "textures/question"
                    votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                        && [> $mapocta 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
                    ]])
                    if (> $votepath -1) [ voteimage = (at $mappath $votepath) ]
                    guistrut 1
                    guiimage $voteimage [] 1.5 1 
                    guistrut 0.5
                    guicenter [
                        gname = (gamename $votemode $votemuts 0 60)
                        if $votebad [
                            guitext (format " ^fd%1 on %2" $gname $votemap)
                            guitext " ^frthis game is currently not allowed"
                        ] [
                            guibutton (format " ^fy%1 ^faon ^fo%2" $gname $votemap)
                            guilist [
                                looplist i (modetexlist $votemode $votemuts 1) [guiimage $i [] 0.75]
                            ]
                        ]
                    ]
                ]
            ] [mapsexec @favl] 
            guispring
            guilist [
                guiimage $warningtex [gamefavs = (listdel $gamefavs @favs)] 0.75 1 [] "" 0xff3333
                guiimage "textures/menu" [
                    modeselected = @votemode
                    mutsselected = @votemuts
                    mapcur = @votemap
                    showgui maps 1
                ] 0.75 1 
            ]
            guilist [
            guiimage $arrowtex [gamefavs = (concat @favs (listdel $gamefavs @favs))] 0.75 1 "textures/chat" 
            guiimage $arrowdowntex [gamefavs = (concat (listdel $gamefavs @favs) @favs)] 0.75 1 "textures/chat"
            ]
        ]
    ] [
        //guivisible [ guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 3")) ]
    ]
]


newgui maps [
    octapaks [
        mapsmenuiter
        guilist [ mapsmenu ]
        if (|| (getclientcount) (getvote)) [
            votesum = 0
            loop j $votenum [ votesum = (+ $votesum (getvote $j 0)) ]
            guitab (format "votes (%1)" $votesum) 
            guilist [ votemenu ]
        ]
        guitab (format "favourites (%1)" $favnum)
        guilist [favsmenu]
    ]
] [
    if (= $guipasses 0) [
        octapaks [ mapsmenuinit; votemenuinit ]
    ]
]
