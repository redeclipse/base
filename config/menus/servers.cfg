shownservergui = 0
sinfoindex = 0
sinfowait = 0
sinforetry = ""
sinfopass = ""
sinfonum = 0
sinfodisp = 0
sinfoplayers = 0
sinfoservers = 0
sinfonumsrv = 0
sinfouitime = 0

sinfotypes = [ "" "status" "name" "port" "qport" "desc" "mode" "muts" "map" "time" "players" "maxplayers" "ping" ]
sinfomodify = [
    sinfotlist = $arg1
    sinfotabsl = (? (> $sinfotlist 0) $sinfotlist (- 0 $sinfotlist))
    loop i (listlen $serversort) [
        sinfostype = (at $serversort $i)
        if $sinfostype [
            sinfosabsl = (? (> $sinfostype 0) $sinfostype (- 0 $sinfostype))
            if (!= $sinfotabsl $sinfosabsl) [ append sinfotlist $sinfostype ]
        ]
    ]
    serversort $sinfotlist
]
servermenuinit = [
    sinfoindex = 0
    sinfowait = 0
    sinforetry = ""
    sinfopass = ""
    sinfoplayers = 0
    sinfoservers = 0
    sinfonumsrv = 0
    sinfouitime = (getmillis 1)
    shownservergui = 0
    updateservergui = 0
    searchfilter = 0
    searchstr = ""
]
servermenuiter = [
    if (! $shownservergui) [
        maplist = ""
        mappath = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $maplist [=s $mcurmap $lcurmap]) 0) [
                append maplist $lcurmap
                append mappath [maps/@lcurmap]
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                append maplist $lcurmap
                append mappath [base/@lcurmap]
            ]
        ]
        shownservergui = 1
    ]
]
servermenu = [
    sinfopause = 0
    sinfotimer = (- (getmillis 1) $sinfouitime)
    guipage sinfo 5 96 4 [getserver] (getserver) [
        sinfostat = (getserver $i 0 0)
        sinfoname = (getserver $i 0 1)
        sinfoport = (getserver $i 0 2)
        sinfonpid = [@[sinfoname]:[@@sinfoport]]
        sinfodesc = (getserver $i 0 3)
        sinfomapn = (getserver $i 0 4)
        sinfonump = (getserver $i 0 5)
        sinfoplayers = (? $i (+ $sinfoplayers $sinfonump) $sinfonump)
        sinfoservers = (? $i (? $sinfonump (+ $sinfoservers 1) $sinfoservers) (? $sinfonump 1 0))
        sinfoping = (getserver $i 0 6)
        sinfolast = (getserver $i 0 7)
        sinfohandle = (getserver $i 0 8)
        sinfoflags = (getserver $i 0 9)
        sinfostatsflag = 0
        looplist fi $sinfoflags [
            if (=s $fi "s") [ sinfostatsflag = 1 ]
        ]
        // attrs
        sinfoattr = (getserver $i 1 -1)
        sinfogver = (getserver $i 1 0)
        sinfomode = (getserver $i 1 1)
        sinfomuts = (getserver $i 1 2)
        sinfotime = (getserver $i 1 3)
        sinfomaxp = (getserver $i 1 4)
        sinfomstr = (getserver $i 1 5)
        sinfovars = (getserver $i 1 6)
        sinfomods = (getserver $i 1 7)
        sinfoverm = (getserver $i 1 8)
        sinfovern = (getserver $i 1 9)
        sinfoverp = (getserver $i 1 10)
        sinfovers = (getserver $i 1 11)
        sinfovera = (getserver $i 1 12)
        if (< $sinfogver 227) [
            sinfobran = "?"
            sinfogmst = (getserver $i 1 13)
            sinfogmtl = (getserver $i 1 14)
        ] [
            sinfobran = (getserver $i 1 13)
            sinfogmst = (getserver $i 1 14)
            sinfogmtl = (getserver $i 1 15)
        ]
        // other
        sinfoofft = (? (>= $sinfolast 0) (div (max (- (getmillis 1) (- $sinfolast (div $sinfoping 2))) 0) 1000) 0)
        sinfoactive = (? (< $sinfoping $serverwaiting) 1 0)
        sinfonumsrv = (? $i (+ $sinfonumsrv $sinfoactive) $sinfoactive)
        searchbuffer = (concat $sinfodesc $sinfonpid $sinfomapn (gamename $sinfomode $sinfomuts 0))
        if (> $sinfonump 0 ) [
            loop j $sinfonump [
                append searchbuffer (getserver $i 2 $j) // the player name
                append searchbuffer (getserver $i 3 $j) // and handle
            ]
        ]
    ] [(|| (= $searchfilter 0)  (> (stringcasestr $searchbuffer $searchstr) -1))] [
        if $updateservergui [
            guibutton "^fwThere are no servers to display, maybe ^fgupdate ^fwthe list?" updatefrommaster [] "info"
        ] [
            sleep 1 [ updateservergui = 1; updatefrommaster; sinfouitime = (getmillis) ]
        ]
    ] [
        if (getserver) [ sleep 1 [ updateservers ] ]
        guilist [
            guibutton "^fgupdate" updatefrommaster
            guistrut 1.5
            guibutton "^frreset" clearservers
            guistrut 1.5
            guibutton "^fodisconnect" "savewarnchk disconnect"
            guistrut 1.5
            guibutton "^fmlan connect" "savewarnchk lanconnect"
            guistrut 1.5
            guicheckbox "^fcsearch lan" searchlan
            guistrut 1.5
            guicheckbox (? $pausesortservers "^fdauto sort" "^fbauto sort") autosortservers
            guistrut 1.5
            guibutton "^fysort now" sortservers
            guispring 1
            guilist [
                guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend
                guilist [
                    guistrut 1
                    guitext (format "^fc%1 ^fwplayers on ^fc%2 ^fwof ^fc%3^fa/^fc%4 ^fwserver%5" $sinfoplayers $sinfoservers $sinfonumsrv $sinfonum (? (!= $sinfonum 1) "s"))
                    guistrut 1
                ]
            ]
        ]
        guistrut 0.25
        if (> (getversion 3) (getversion 1)) [
            guilist [ guibutton "^fzoynew version released! ^fwget it now from: ^fcwww.redeclipse.net" "" ]
            guistrut 0.25
        ]
    ] [
        guimerge 88 [
            guicenter [
                guistrut 8 1
                if $sinfoactive [
                    guicenter [
                        guifont "huge" [ guibutton (format "^fw%1" $sinfonump) ]
                        guicenter [ guibutton (format "^fd/^fa%1" $sinfomaxp) ]
                    ]
                    guistrut 0.125
                    guicenter [
                        guibutton (format "^f%1%2" (? (< $sinfoping 200) "g" (? (< $sinfoping 400) "y" "r")) $sinfoping)
                        guifont "little" [ guibutton " ^fams" ]
                    ]
                    guistrut 0.125
                    guicenter [
                        sinfosecs = (? (>= $sinfolast 0) (div (- (getmillis 1) $sinfolast) 1000) -1)
                        guifont "little" [ guibutton (? (>= $sinfosecs 0) (format "^fa%1 ^fds ago" $sinfosecs) "^fdwaiting..") ]
                    ]
                ] [ guicenter [ guifont "huge" [ guicenterx [ guibutton "?" ] ] ] ]
            ]
            sinfoimage = "textures/emblem"
            if (&& [< $sinfoping $serverwaiting] [= $sinfogver (getversion 1)]) [
                sinfopath = (listfind curmap $maplist [|| [=s $curmap $sinfomapn] [=s [base/@curmap] $sinfomapn] [
                    && [> $hasoctapaks 0] [=s [base/@curmap] $sinfomapn]
                ]])
                if (> $sinfopath -1) [ sinfoimage = (at $mappath $sinfopath) ]
            ]
            guiimage $sinfoimage "" 2 1 "textures/emblem"
            guistrut 1
            guicenter [
                guilist [
                    guibutton (format "^fw%1" $sinfodesc)
                    guifont "little" [ guicenter [ guibutton (format "^fd(^fa%1^fd)" $sinfonpid) ] ]
                    guifont "little" [ guicenter [ guibutton (format "^fd[^fc%1^fd]" $sinfohandle) ] ]
                    if $sinfostatsflag [
                        guifont "little" [ guicenter [ guibutton "^fd[^fcstatistics^fd]" ] ]
                    ]
                ]
                guispring 1
                if $sinfoactive [
                    guilist [
                        guistrut 0.25
                        if (= $sinfogver (getversion 1)) [
                            guinohitfx [ guifont "default" [
                                case $sinfostat 0 [
                                    guibutton "^fgopen" [] [] "textures/servers/default"
                                ] 1 [
                                    guibutton "^fylock" [] [] "textures/servers/locked"
                                ] 2 [
                                    guibutton "^fmpriv" [] [] "textures/servers/private"
                                ] 3 [
                                    guibutton "^frfull" [] [] "textures/servers/full"
                                ] () [
                                    guibutton "^founknown" [] [] "textures/servers/unknown"
                                ]
                            ] ]
                            if (!= $sinfomode $modeidxediting) [
                                guibutton (format " ^fd[^fc%1^fd:^fw%2^fd]" (at $gamestatename $sinfogmst) (timestr (* (? $sinfonump (max (- $sinfogmtl $sinfoofft) 0) $sinfotime) 1000) 3))
                            ]
                            gname = (gamename $sinfomode $sinfomuts 0 32)
                            guifont "little" [ guicenter [ guilist [
                                guibutton (format " ^fy%1 ^faon ^fo%2" $gname $sinfomapn)
                                guibutton (format " ^fa(%1modified^fa)" (? $sinfomods (format "^fc%1%% " (precf (*f (divf $sinfomods $sinfovars) 100) 2)) "^fgun"))
                                if (>= $sinfoattr 13) [ guibutton (format " ^fa[^fc%1.%2.%3-%4%5-%6^fa]" $sinfoverm $sinfovern $sinfoverp (platname $sinfovers) $sinfovera $sinfoverb) ]
                            ] ] ]
                        ] [
                            guinohitfx [ guifont "default" [ guibutton "^foincompatible" [] [] "textures/servers/failed" ] ]
                            guibutton (concat " ^faserver is using" (? (> $sinfogver (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol")
                        ]
                    ]
                    guispring 1
                    guilist [
                        if (=s $sinfonpid $sinforetry) [
                            if (= $sinfostat 3) [ guibutton "^fd[ ^fwwaiting for slot ^fd] " ]
                            guibutton "^fwpassword ^fd= "
                            sinfopassval = $sinfopass
                            guifield sinfopassval 20 [sinfopass = $sinfopassval]
                        ] [
                            if (> $sinfonump 0) [
                                guistrut 0.25
                                sinfopnum = (getserver $i 2)
                                if (> $sinfopnum 0) [
                                    guifont "little" [
                                        pname = ""
                                        plist = ""
                                        pmore = 0
                                        loop j $sinfopnum [
                                            if (|| $pmore (>= (guitextwidth $plist) 1400)) [ pmore = (+ $pmore 1) ] [
                                                append pname (format ["%1"] (getserver $i 2 $j))
                                                plist = (prettylist $pname)
                                            ]
                                        ]
                                        guibutton (concat $plist (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                        //[] [] "" -1 -1 1900
                                    ]
                                ] [ guibutton "^faplayer info not available" ]
                            ] [ guibutton "^fano players online" ]
                        ]
                    ]
                ] [
                    guilist [
                        guistrut 0.25
                        guifont "default" [ guibutton "^founresponsive" [] [] "textures/servers/failed" ]
                        guibutton " ^faserver is not replying to queries"
                    ]
                ]
            ]
        ] [
            sinfopass = ""
            sinforetry = @(escape $sinfonpid)
            sinfowait = (! (|| [hasauthkey 1] [!= @@sinfomstr 4]))
        ] [
            sinfopass = ""
            sinforetry = @(escape $sinfonpid)
            sinfowait = (! (hasauthkey 1))
        ] [
            sinfopause = 1
            if $sinfoactive [
                sinfopnum = (getserver $i 2)
                if (> $sinfopnum 0) [
                    phover = ""
                    loop j $sinfopnum [
                        if $j [ append phover "^n" ]
                        phandle = (getserver $i 3 $j)
                        if (stringlen $phandle) [
                            append phover (format "%1 (%2)" (getserver $i 2 $j) $phandle)
                        ] [
                            append phover (format "%1" (getserver $i 2 $j))
                        ]
                    ]
                    guitooltip $phover
                ] [ guitooltip "^fano players online" ]
            ] [ guitooltip "^fano information available" ]
        ]
        if (=s $sinfonpid $sinforetry) [
            if (= $sinfowait 1) [
                if (stringlen $sinfopass) [
                    savewarnchk (format "connect %1 %2 %3" connect $sinfoname $sinfoport $sinfopass)
                    sinfopass = ""
                    sinforetry = ""
                    sinfowait = 0
                ]
            ] [
                if (|| [hasauthkey 1] [!= $sinfostat 3]) [
                    savewarnchk (format "connect %1 %2" $sinfoname $sinfoport)
                    sinfopass = ""
                    sinforetry = ""
                    sinfowait = 0
                ]
            ]
        ]
    ] [
        guistrut 0.5
        guilist [
            guicenterz [ guitext "update interval:" ]
            guistrut 0.5
            guilist [
                guistrut 87.5 1
                guilistslider serverupdateinterval "1 2 3 4 5 10 15 20 25 30 35 40 45 50 55 60"
            ]
        ]
        guistrut 0.5
        guilist [
            guicenterz [ guicheckbox "filter:" searchfilter ]
            if (=s $guirollovername "filter:") [
                guitooltip "^faFilter by player names or handles, current map, mode or mutators, server description, IP address or port."
            ]
            guistrut 0.5
            guifield searchstrval 18 [searchstr = $searchstrval; searchfilter = 1] -1 0 "" 0 "^fd <enter search terms>" 1
            guispring 1
            guicenterz [
                guitext "sort:"
                guistrut 0.5
                loop i (listlen $serversort) [
                    sinfostype = (at $serversort $i)
                    if $sinfostype [
                        sinfosabsl = (? (> $sinfostype 0) $sinfostype (- 0 $sinfostype))
                        sinfosname = (at $sinfotypes $sinfosabsl)
                        guistrut 0.5
                        guibutton (? (> $sinfostype 0) $sinfosname [-@sinfosname]) [
                            sinfomodify @sinfostype
                        ] [
                            sinfomodify (- 0 @sinfostype)
                        ] "" 0x00FFFF
                    ]
                ]
            ]
            guispring 1
            guicenterz [
                guifont "little" [
                    loop i (listlen $sinfotypes) [
                        if (&& $i [< (listfind sinfoctype $serversort [= $i (? (> $sinfoctype 0) $sinfoctype (- 0 $sinfoctype))]) 0]) [
                            sinfosname = (at $sinfotypes $i)
                            guistrut 1
                            guibutton $sinfosname [
                                sinfomodify @i
                            ] [
                                sinfomodify (- 0 @i)
                            ] "" 0x888888
                        ]
                    ]
                ]
            ]
            guispring 1
            guicenterz [ guibutton "reset" [serversortreset] [serversortreset] "" 0xFF8888 ]
            guistrut 1
        ]
        guistrut 0.5
        if (hasauthkey) [
            guilist [
                guicenterz [ guitext "user account:" ]
                guispring 1
                guicenterz [ guicheckbox (format "identify as ^fs^fc%1^fS on connect" $accountname) authconnect ]
                guispring 1
                guicenterz [ guifont "little" [ guibutton "(^fs^fredit account^fS)" [showgui profile 4] ] ]
            ]
        ]
    ]
    if (&& [! $guilayoutpass] [|| (> $sinfotimer 30000) (> $sinfonumsrv (div (* $sinfonum 3) 4))]) [ if $sinfopause [ if (! $pausesortservers) [ pausesortservers 1 ] ] [ if $pausesortservers [ pausesortservers 0 ] ] ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showservers"))
]

newgui servers [
    servermenuiter
    guilist [ servermenu ]
] [
    if (= $guipasses 0) [
        servermenuinit
    ]
]

newgui guidelines [
    guieditor doc/guidelines.txt -80 22
    textinit doc/guidelines.txt doc/guidelines.txt
    textmode 4
    guistrut 0.5
    guifont "emphasis" [
        guilist [
            guibutton "[ ^fs^foi disagree, stay offline^fS ]" [cleargui 1]
            guispring 1
            guibutton "[ ^fs^fgi agree, play online^fS ]" [connectguidelines 1; @guidelinesaction; guidelinesaction ""]
            guistrut 3
        ]
    ]
    guistatus "you must read and agree to these guidelines before playing online"
] [ if $connectguidelines [cleargui 1] ]
showservers = [ guidelinesaction "showgui servers"; showgui (? $connectguidelines servers guidelines) ]
