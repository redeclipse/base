bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

showmainplayerprev = 1
setpersist showmainplayerprev 1
setcomplete showmainplayerprev 1

newgui main [
    guitwo [
        if $showmainplayerprev [ guicenter [
            guicenter [ guiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) [showgui profilediff; showgui profile] 7.5 1 1]
            guicenter [ guitext (getplayername) ]
        ] ]
    ] [
        guilist [ guistrut 30 ]
        guicenter [ guilist [ guifont "emphasis" [
            guicenter [ guibutton "profile" "showgui profilediff; showgui profile" ]
            guistrut 0.5
            if (&& (isconnected) (> (getvote) 0)) [
                guicenter [ guibutton "show votes" "showgui maps 2" ]
                guistrut 0.5
            ]
            guicenter [ guibutton (? (isonline) "find servers" "play online") "showservers" ]
            guicenter [ guibutton (? (isconnected) "vote map/mode" "offline practice") "showgui maps 1" ]
            guistrut 0.5
            if (&& (isconnected) (= (gamemode) $modeidxediting)) [
                guicenter [ guibutton "editing" "showgui edit" ]
                guistrut 0.5
            ]
            guicenter [ guibutton "options" "showgui options" ]
            guicenter [ guibutton "variables" "showgui vars" ]
            guicenter [ guibutton "help" "showgui help" ]
            guistrut 0.5
            if (isconnected) [
                guicenter [ guibutton "disconnect" "savewarnchk disconnect" ]
                guistrut 0.5
            ]
            guicenter [ guibutton "quit" "savewarnchk quit" ]
        ] ] ]
    ]
    cases $guirolloveraction "showservers" [
        guitooltip "display the server browser for online matches"
    ] "showgui profilediff; showgui profile" [
        guitooltip "edit your name, colour, loadout and model type"
    ] "showgui edit" [
        guitooltip "access commands to use while editing a map"
    ]  "savewarnchk disconnect" [
        guitooltip "disconnect your current connection"
    ] "showgui maps 2" [
        guitooltip "display the current votes"
    ] "showgui maps 1" [
        guitooltip (format "select a map/mode combination to %1" (? (isonline) "vote for" "practice on"))
    ] "showgui options" [
        guitooltip "edit your configuration options"
    ] "showgui vars" [
        guitooltip "search for variables, commands or aliases and edit a plethora of game and client settings."
    ] "showgui help" [
        guitooltip "learn more about many aspects of the game"
    ] "savewarnchk quit" [
        guitooltip "close Red Eclipse"
    ]
    guitip
]

guimodes = [
    guicenter [ guitext (modedesc @arg1 @arg2 5) ]
    if (> $arg2 0) [
        loop i $mutsidxnum [
            mut = (<< 1 $i)
            if (&& [& $arg2 $mut] [! (& (mutsimplied $arg1) $mut)]) [
                muttxt = (mutsdesc $arg1 $mut 4)
                if (stringlen $muttxt) [ guicenter [ guitext (concatword "^fa" $muttxt) ] ]
            ]
        ]
    ]
]

newgui loading [
    guistayopen [ guinohitfx [
        guione [
            guifont "huge" [ guicenter [ guitext (? (&& (isloadingmap) (stringlen $maptitle)) $maptitle "loading") ] ]
            guifont "default" [ guicenter [ guitext (? (&& (isloadingmap) (stringlen $maptitle) (stringlen $mapauthor)) (concat "by" $mapauthor) "please wait..") ] ]
            if (&& (isloadingmap) (isconnected)) [
                if (= (isloadingmap) 2) [
                    guicenter [ guitext "^fs^fylightmapping^fS" ]
                ] [
                    gname = (gamename (gamemode) (mutators) 0 32)
                    guicenter [ guitext (format "^fs^fy%1^fS" $gname) ]
                ]
            ]
            case (isloadingmap) 2 [
                guistrut 0.5
                guicenter [ guiimage "" [] 7 0 ]
            ] 1 [
                if (isconnected) [
                    guistrut 0.5
                    guifont "little" [
                        guicenter [
                            modetexs = (modetexlist (gamemode) (mutators))
                            looplist modetex $modetexs [ guiimage $modetex [] 1 0 "textures/blank" ]
                        ]
                        guimodes (gamemode) (mutators)
                    ]
                    guistrut 0.5
                    if (stringlen $connectname) [
                        guicenter [ guitext (format "^faon: ^fw%1:[%2]" $connectname $connectport) ]
                        if (&& (isconnected) (stringlen $serverdesc)) [ guifont "little" [ guicenter [ guitext (format "^"^fs%1^fS^"" $serverdesc) ] ] ]
                    ]
                ]
            ]
        ]
        guitip
    ] ]
]
