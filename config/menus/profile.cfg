profilebuttons = [
    guilist [ guifont "emphasis" [
        guistrut 2
        guibutton "^fgok" [
            setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevlweap
            cleargui 1
        ] []
        guistrut 3
        if (needname) [
            guibutton "^fomain menu" [showgui main]
        ] [
            guibutton "^focancel" [cleargui 1]
        ]
    ] ]
]

profilepreview = [
    guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.6 1 1
]

playerprevinherit = 0
playerprevdisinherit = 0

newgui playerprev [
    guiheader "player preview"
    guicenter [ guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [cleargui 1] 15 1 1 ]
    guistrut 0.25
    guicenter [
        guilistx 2 [
            guitext "team: "
            teamneutraltex = $teamtex
            loop t 5 [
                push t (at "neutral alpha omega kappa sigma" $t) [
                    guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
                ]
            ]
            guistrut 1
            guitext "weapon: "
            loop w $weapidxnum [
                push w (at $weapname $w) [
                    guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevinherit [ playerprevinherit = 0; playerprevdisinherit = 1 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]

loadweapall = 0
loadweaps = [
    lwa = ""
    lwl = (listlen $playerprevlweap)
    lwo = (? (> $lwl $arg1) (at $playerprevlweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $playerprevlweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [ lw3 = $lwo ]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ playerprevlweap = $lwa ]
]

newgui profile [
    guibox [ profilepreview ] [
        guilist [
            guistrut 3
            guilist [
                guicenter [ guifont "emphasis" [ guitext "player name:" ] ]
                guicenter [ guifield playerprevname 24 [] -1 0 "" 0 "^fzad<enter name>" ]
                guistrut 1
                guicenter [ guifont "emphasis" [ guitext "player color:" ] ]
                guicenter [
                    guihexpreview $playerprevcolour "hex-value"
                    guistrut 1
                    guilist [
                        guirgbsliders playerprevcolour
                    ]
                ]
                guistrut 1
                guicenter [ guifont "emphasis" [ guitext "player model:" ] ]
                guistrut 0.25
                guicenter [
                    guiradio "male" playerprevmodel 0
                    guistrut 1.5
                    guiradio "female" playerprevmodel 1
                ]
            ]
        ]
    ] [ profilebuttons ]
    guitab "loadout"
    guibox [ profilepreview ] [
        guilist [
            guistrut 3
            guilist [
                guistrut 0.75
                guifont "emphasis" [
                    guicenter [ guitext "CHOOSE YOUR WEAPONS" ]
                ]
                guistrut 0.5
                guifont "little" [
                    guicenter [ guitext "you will spawn with these weapons" ]
                    guicenter [ guitext (format "you can carry ^fs^fc%1^fS %2" $maxcarry (? (= $maxcarry 1) "weapon" "weapons")) ]
                ]
                guistrut 0.5
                guilist [
                    guistrut 3
                    guilist [
                        guistrut 0.5
                        guicenter [ guicheckbox " show all weapon slots" loadweapall ]
                        guifont "little" [
                            guistrut 0.5
                            guicenter [ guitext "(if a weapon is not available, the" ]
                            guicenter [ guitext "next one in the list will be used)" ]
                            guistrut 0.5
                        ]
                    ]
                    guistrut 3
                ]
                guistrut 0.5
                guicenter [ guicheckbox " pick a loadout every new game" showloadoutmenu ]
                if (= $getplayerstate 0) [
                    guistrut 0.5
                    guicenter [ guitext "^fs^frWeapon changes will not take^fS"]
                    guicenter [ guitext "^fs^freffect until after respawn!"]
                    guistrut 0.5
                ]
            ]
            guibar
            guistrut 2
            guilist [
                guibackground
                guicenter [
                    guistayopen [
                        gdw = 0
                        gdl = (listlen $playerprevlweap)
                        loop w2 (? $loadweapall $weapidxloadout $maxcarry) [
                            w3 = (? (> $gdl $w2) (at $playerprevlweap $w2) -1)
                            hi = (mod $w2 2)
                            al = (|| (= $w3 0) (allowedweap $w3))
                            guilist [
                                guibackground (? (&& $al (< $gdw $maxcarry)) 0x303030 0x000000)
                                guistrut 0.25
                                guicenter [ guitext (format "%1%2" (? $hi "^fd" "^fw") (+ $w2 1)) ]
                                guistrut 0.5
                                guilist [
                                    if (= $w3 0) [ guibackground 0xFFFFFF 0.01 0xFFFFFF 1 1 ]
                                    guistrut 0.125
                                    guiimage [textures/question] [loadweaps @w2 0] 0.75 0 [textures/blank] [] (? (= $w3 0) 0xFFFFFF 0x808080)
                                    guistrut 0.125
                                ]
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    guilist [
                                        if (= $w3 $w4) [ guibackground 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
                                        guistrut 0.125
                                        guiimage (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [loadweaps @w2 @w4] 0.75 0 [textures/blank] [] (? (= $w3 $w4) $[@[w5]colour] 0x808080)
                                        guistrut 0.125
                                    ]
                                ]
                            ]
                            if $al [ gdw = (+ $gdw 1) ]
                        ]
                        guistrut 1
                        guilist [
                            guistrut 1.5
                            guispring 1
                            guicenter [guibutton "random" [loadweaps 0 0] [loadweaps 1 0]]
                            guispring 1
                            loop w1 $weapidxloadout [
                                w4 = (+ $w1 $weapidxoffset)
                                w5 = (at $weapname $w4)
                                guibutton (format "%1" $[@[w5]longname]) [loadweaps 0 (+ $weapidxoffset @w1)] [loadweaps 1 (+ $weapidxoffset @w1)] "" $[@[w5]colour]
                                cases $guirollovername "random" [
                                    guitooltip "You will get a random weapon each time you spawn" 1500
                                ] (format "%1" $[@[w5]longname]) [
                                  guitooltip (format "^f[%1]primary mode:^n^fw%2^n^f[%1]secondary mode:^n^fw%3" $[@[w5]colour] $[@[w5]desc1] $[@[w5]desc2]) 1500
                                ]
                                guispring 1
                            ]
                        ]
                        guistrut 1
                    ]
                ]
            ]
        ]
    guivisible [ guitip (format "press %1 to open this weapon menu at any time" (dobindsearch "showgui profile 2")) ]
    ] [ profilebuttons ]
    guitab "vanity items"
    guibox [ profilepreview ] [
        guilist [
            guistrut 3
            guilist [
                guifont "emphasis" [
                    guicenter [ guitext "CHOOSE YOUR VANITIES" ]
                ]
                guistrut 0.5
                guifont "little" [
                guicenter [ guitext "(vanities are for decoration only" ]
                guicenter [ guitext "and do not influence player movement" ]
                guicenter [ guitext "or enhance abilities in any way)" ]
                ]
                guistrut 0.5
                guicenter [
                    guilist [
                        vanityset = (listlen $playerprevvanity)
                        loop z $vanityset [
                            vaint = (at $playerprevvanity $z)
                            vainn = (findvanity $vaint)
                            if (>= $vainn 0) [
                                guistayopen [ guicenter [ guibutton (format "^fy%1" (getvanity $vainn 2)) [ playerprevvanity = (listdel $playerprevvanity @(escape $vaint)) ] ] ]
                            ]
                        ]
                        guistrut 1
                        guispring 1
                        guistrut 1
                    ]
                ]
            ]
            guistrut 1
            guilist [
                guicenter [
                    guilist [
                        guifont "little" [
                            vanitynum = (getvanity)
                            guiloopsplit z 3 $vanitynum [
                                vaint = (getvanity $z 1)
                                vainn = (indexof $playerprevvanity $vaint)
                                if (< $vainn 0) [
                                    guistayopen [ guicenter [ guibutton (format "^fa%1" (getvanity $z 2)) [ playerprevvanity = (concat $playerprevvanity @(escape $vaint)) ] ] ]
                                ]
                            ] [ guistrut 2 ]
                        ]
                    ]
                ]
            ]
        ]
    ] [ profilebuttons ]
] [
    if (= $guipasses 0) [
        if $playerprevdisinherit [ playerprevdisinherit = 0 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
        playerprevlweap = ""
        break = 0
        loopwhile i $weapidxloadout [= $break 0] [
            q = (getloadweap $i)
            if (< $q 0) [ break = 1 ] [ playerprevlweap = (? $i (concat $playerprevlweap $q) $q) ]
        ]
    ]
]

newgui useraccount [
    guiheader "user account"
    guilist [
        guistrut 2
        guilist [
            guistrut 0.5
            guifont "emphasis" [ guicenter [ guitext "RED ECLIPSE USER ACCOUNT" ] ]
            guistrut 0.5
            guicenter [ guitext "unique user accounts provide you with an established identity in the" ]
            guicenter [ guitext "community, and allow server owners to add you as a local moderator." ]
            guicenter [
                guitext "the icon before your name will also change to player status: "
                guinohitfx [ guiimage "textures/privs/player" [] 0.5 ]
            ]
            guistrut 0.5
            guicenter [ guitext "enter the account login information received in your email below:" ]
            guistrut 0.5
            guicenter [ guitext "player handle:" ]
            guicenter [ guifield accountname 48 [] ]
            guistrut 0.5
            guicenter [ guitext "private key:" ]
            guicenter [ guifield accountpass 48 [] ]
            guistrut 1
            guicenter [ guicheckbox "use my account online" authconnect ]
            guicenter [ guicheckbox "use my account offline" quickauthchecks ]
            guistrut 1
            guicenter [ guitext "new account applications and replacement accounts are available at:" ]
            guicenter [ guitext "^fghttp://redeclipse.net/apply" ]
            guistrut 1
        ]
        guistrut 2
    ]
]
