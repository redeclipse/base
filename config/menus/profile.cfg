profilebuttons = [
    guilist [ guifont "emphasis" [
        guistrut 10
        guibutton "^fgok" [
            setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevlweap
            cleargui 1
        ] []
        guistrut 3
        if (needname) [
            guibutton "^fomain menu" [showgui main]
        ] [
            guibutton "^focancel" [cleargui 1]
        ]
    ] ]
]

profilepreview = [
    guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.6 1 1
]

playerprevinherit = 0
playerprevdisinherit = 0

newgui playerprev [
    guiheader "player preview"
    guicenter [ guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [cleargui 1] 15 1 1 ]
    guistrut 0.25
    guicenter [
        guilistx 2 [
            guitext "team: "
            teamneutraltex = $teamtex
            loop t 5 [
                push t (at "neutral alpha omega kappa sigma" $t) [
                    guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
                ]
            ]
            guistrut 1
            guitext "weapon: "
            loop w $weapidxnum [
                push w (at $weapname $w) [
                    guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevinherit [ playerprevinherit = 0; playerprevdisinherit = 1 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]


newgui profile [
    guibox [ profilepreview ] [
        guicenter [ guifont "emphasis" [ guitext "player name" ] ]
        guicenter [ guifield playerprevname 24 [] -1 0 "" 0 "^fzad<enter name>" ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "colour tone" ] ]
        guicenter [
            colr = (& (>> $playerprevcolour 16) 0xFF)
            colg = (& (>> $playerprevcolour 8) 0xFF)
            colb = (& $playerprevcolour 0xFF)
            guilist [
                guibackground $playerprevcolour 1 0xFFFFFF 1 1
                guistrut 6 1
                guistrut 3
            ]
            guistrut 1
            guilist [
                loop i 3 [
                    n = [col@(at "r g b" $i)]
                    guilist [
                        guilist [
                            guistrut 30 1
                            guislider $n 0 255 [
                                playerprevcolour = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)
                            ] 0 1 (at [0xFF0000 0x00FF00 0x0000FF] $i) 1 (<< $$n (* (- 2 $i) 8))
                        ]
                        guitext (concatword "^f[" (at [0xFF0000 0x00FF00 0x0000FF] $i) "]" (at "R G B" $i))
                    ]
                ]
            ]
        ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "gender" ] ]
        guicenter [
            guiradio "male" playerprevmodel 0
            guistrut 1
            guiradio "female" playerprevmodel 1
        ]
    ] [ profilebuttons ]
    guitab "loadout"
    guibox [ profilepreview ] [
        guilist [
            guilist [ 
                guicenter [ guitext "Sort the weapons according to your preference" ]
                guistrut 0.5
                guicenter [
                    guistayopen [
                        loop i $weapidxitem [
                            guilist [
                                wi = (at $playerprevlweap $i)
                                if (< $wi 2) [
                                    wn = "random"
                                    wt = "textures/question"
                                ] [    
                                    wn = (at $weapname $wi)
                                    wt = (? (allowedweap $wi) [textures/weapons/@[wn]] [textures/warning])
                                ]
                                guiimage $wt [
                                    playerprevlweap = (concat @wi (listdel $playerprevlweap @wi))
                                ] 1.40 0 "" [
                                    playerprevlweap = (concat (listdel $playerprevlweap @wi) @wi)
                                ] $[@[wn]colour]
                                guicenter [ guifont "little" [ guitext $wn "" $[@[wn]colour]]]
                            ]    
                        ]
                    ]
                ]
                guistrut 1.5
                if (= $getplayerstate 0) [
                    guicenter [ guitext "^faNOTE: Weapon changes take effect ^fzoyafter ^farespawn!" "textures/dead"]
                    guistrut 0.5
                ]
                guifont "little" [
                    guicenter [ guitext (format "you can carry ^fs^fc%1^fS %2" $maxcarry (? (= $maxcarry 1) "weapon" "weapons")) ]
                    guicenter [ guitext "if a weapon is not available, the next one in the list will be used"]
                    guicenter [ guitext "you can right-click a weapon to move it to the end of the list" ]
                ]
                guistrut 1
                guifont "little" [
                    guicenter [ guicheckbox "pick a loadout every new game" showloadoutmenu ]
                ]
                guispring 1
            ]
        ]
    ] [ profilebuttons ]
    guitab "vanity items"
    guibox [ profilepreview ] [
        guicenter [
            guilist [
                vanityset = (listlen $playerprevvanity)
                loop z $vanityset [
                    vaint = (at $playerprevvanity $z)
                    vainn = (findvanity $vaint)
                    if (>= $vainn 0) [
                        guistayopen [ guicenter [ guibutton (format "^fy%1" (getvanity $vainn 2)) [ playerprevvanity = (listdel $playerprevvanity @(escape $vaint)) ] ] ]
                    ]
                ]
            ]
            guistrut 1
            guispring 1
            guistrut 1
            guilist [
                guifont "little" [
                    vanitynum = (getvanity)
                    guiloopsplit z 3 $vanitynum [
                        vaint = (getvanity $z 1)
                        vainn = (indexof $playerprevvanity $vaint)
                        if (< $vainn 0) [
                            guistayopen [ guicenter [ guibutton (format "^fa%1" (getvanity $z 2)) [ playerprevvanity = (concat $playerprevvanity @(escape $vaint)) ] ] ]
                        ]
                    ] [ guistrut 2 ]
                ]
            ]
        ]
    ] [ profilebuttons ]
    guitab "user account"
    guibox [ profilepreview ] [
        guicenter [ guitext "username " ]
        guicenter [ guifield accountname 48 [] ]
        guistrut 0.5
        guicenter [ guitext "private key " ]
        guicenter [ guifield accountpass 48 [] ]
        guistrut 1
        guicenter [ guicheckbox "identify on connect" authconnect ]
        guicenter [ guicheckbox "identify when playing offline" quickauthchecks ]
        guistrut 1
        guicenter [ guitext "^fyaccount applications are available at" ]
        guicenter [ guitext "^fchttp://redeclipse.net/apply" ]
        guistrut 1
    ] [ profilebuttons ]
] [
    if (= $guipasses 0) [
        if $playerprevdisinherit [ playerprevdisinherit = 0 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
        // this makes sure all weapon types and two random slots (0 and 1) are in the loadout list (but no -1 tail).
        playerprevlweap = (concat (listdel $playerloadweap -1) (listdel (loopconcat i $weapidxitem [$i]) $playerloadweap))
    ]
]
