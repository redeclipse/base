irccurident = 0
enterlobby = 0

newgui lobby [
    guiheader "lobby"
    if $enterlobby [
        if (! (ircconnected freenode) ) [
            enterlobby = 0
            cleargui 1
        ]
        ircgui freenode
        guivisible [ guitip (format "press %1 to open this chat window again at any time" (dobindsearch "showgui lobby")) ]
    ] [
        guifont "emphasis" [ guicenter [ guitext "RED ECLIPSE CHAT LOBBY" ] ]
        guistrut 0.5
        guicenter [ guitext "You are about to enter the main project chat room." ]
        guistrut 0.5
        guicenter [ guitext "From there, you can talk with other connected players or ask questions." ]
        guicenter [ guitext  (format "To access the lobby again at any time, press %1" (dobindsearch "showgui lobby"))]
        guistrut 0.5
        guicenter [ guitext "Are you sure you wish to continue?" ]
        guistrut 0.5
        guicenter [ guistayopen [
            guibutton "^fgYES, continue" [enterlobby = 1; ircaddclient freenode irc.freenode.net 6667 (getplayername); ircaddchan freenode #redeclipse ]
            guispring 1
            guibutton "^foNO, go back" [cleargui 1]
        ] ]
    ]
]

newgui irc [
    guistayopen [
        guitext "saved identities"
        guislider irccurident 0 10
        guilist [
            guilist [
                guitext "network  "
                guitext "address  "
                guitext "port     "
                guitext "nickname "
                guitext "channel  "
            ]
            guilist [
                guilist [
                    guifield ircname@irccurident -40
                    guistrut 1
                    guibutton "connect" [
                        ircaddclient $[ircname@irccurident] $[irchost@irccurident] $[ircport@irccurident] $[ircnick@irccurident]
                        ircaddchan $[ircname@irccurident] $[ircchan@irccurident]
                    ]
                ]
                guifield [irchost@irccurident] -40
                guifield [ircport@irccurident] -40
                guifield [ircnick@irccurident] -40
                guilist [
                    guifield [ircchan@irccurident] -40
                    guistrut 1
                    guibutton "join" [ ircaddchan $[ircname@irccurident] $[ircchan@irccurident] ]
                ]
            ]
        ]
    ]
    guibar
    ircgui
] [
    if (= $guipasses 0) [
        ircname0 = freenode
        irchost0 = irc.freenode.net
        ircport0 = 6667
        ircnick0 = (filter (getplayername) 1 1 0)
        ircchan0 = #redeclipse
    ]
]
