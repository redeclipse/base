
    // utilities
    entselbyindex = [entselect [(> (indexof $arg1 (entindex)) -1)]]
    setdesc "entselbyindex" "selects one or more entities using a list of indices, for example^n/entselbyindex $myentlist^n/entselbyindex [12 15 7]" "indexlist"
    setcomplete entselbyindex 1

    // main edit menu bound to F3
    newgui edit [ guistayopen [
        guifont emphasis [
            // guicenter [guieditbutton2 [toggle edit mode] [edittoggle]]
            guicenter [guibutton "map info" [showgui mapinfo]]
            guicenter [guibutton "geometry and texturing" [showgui geometry]]
            guicenter [guibutton (format "texture list %1" (dobindsearch showtexgui edit)) [cleargui ; showtexgui ]]
            guicenter [guibutton "editing options" [showgui editoptions]]
            guicenter [guibutton "environment settings" [showgui world]]
            guicenter [guibutton "texture variations" [showgui textures]]
            guicenter [guibutton "find and edit entities" [showgui ents]]
            guicenter [guibutton "configure key binds" [sleep 50 [curbindtype = 2]; showgui options_controls]]
        ]
        guistatus (format "Press %1 to show this menu as a compass" (dobindsearch [showcompass editing] edit))
    ] ]

    // this one could go to config/compass.cfg, but it is convenient to have it here for comparison
    newcompass editing $editingtex [
        //compass "^fs^fcQ^fSuit editing" Q [edittoggle]
        compass "m^fs^fca^fSp info" A [showgui mapinfo]
        compass "^fs^fcg^fSeometry and texturing" G [showgui geometry]
        compass "^fs^fct^fSexture list" T [showtexgui]
        compass "e^fs^fcd^fSiting options" D [showgui editoptions]
        compass "^fs^fcw^fSorld variables" W [showgui world]
        compass "texture ^fs^fcv^fSariations" V [showgui textures]
        compass "^fs^fcf^fSind and edit entities" F [showgui ents]
        compass "^fs^fcc^fSonfigure key binds" C [sleep 50 [curbindtype = 2]; showgui options_controls]
        // shown below:
        compass (format "^fs^fce^fSscape (toggle this compass) ^n^n%1 show this as a menu" (dobindsearch [showgui edit] edit)) E [cleargui 1]
    ]

    newgui mapinfo [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guilist [
            guinocursorfx [ guicenter [guiimage $mapname [] 9 1 "textures/emblem"]]
            guistrut 2
            guicenter [
                guilist [
                    guifield maptitle -20
                    guistrut 2
                    guitext  "map title"
                ]
                guilist [
                    guifield mapauthor -20
                    guistrut 2
                    guitext  "map author"
                ]
                guilist [
                    guifield mapname -20
                    guistrut 2
                    guitext  "file name"
                ]
                guilist [
                    guifield mapmusic -20
                    guistrut 2
                    guitext  "music"
                    guispring 1
                    guibutton "pick" [showgui music]
                ]
                guilist [
                    guifield numplayers 4
                    guitext " num"
                    guistrut 2
                    guifield maxplayers 4
                    guitext " max"
                    guistrut 2.2
                    guitext  "players"
                ]
                guistrut 0.5
                guilist [
                    guitext "switch teams at half-time:"
                    guispring 1
                    guiradio "off" mapbalance 0
                ]
                guilist [
                    guispring 1
                    guiradio "in ctf, dnc and bb" mapbalance 1
                    guispring 1
                    guiradio "in all modes" mapbalance 2
                ]
                guistrut 0.5
                guilist [
                    guitext (format "map revision: %1" $maprevision)
                    guispring 1
                    guieditbutton2 "save the map" [savemap]
                ]
                guitext (format "map file format version: %1" $mapversion)
                guistrut 0.5
                guitext (format "map size: 2^^%1 or %2 cubes = %3m" $mapsize (<< 1 $mapsize) (<< 1 (- $mapsize 3 )))
                guilist [
                    guispring 1
                    guieditbutton2 "double" [mapenlarge]
                    guispring 1
                    guieditbutton2 "shrink to non-empty volume" [shrinkmap]
                ]
                guistrut 0.5
                if (isonline) [ guieditbutton "refresh map from server" getmap ]
                guibutton "edit the config file" [notepad @mapname.cfg] []
                guibutton "reset the texture list" [showgui texreset] [] $warningtex
            ]
        ]

        guitab "waypoints and routes"
        if (= 0 $guipasses) [ idmax = 3 ; count = "" ]
        guistrut 1
        guicenter [
            guilist [
                guifont "emphasis" [guitext "waypoints"]
                guistrut 1
                guitext "serve as navigation meshes for bots" $privbottex
                guieditcheckbox "show waypoints" showwaypoints
                guieditcheckbox "drop waypoints as you move" dropwaypoints
                guistrut 0.5
                guieditbutton "^fyclear^fw all waypoints" clearwaypoints
                guieditbutton "clear waypoints in selected volume" delselwaypoints
                guistrut 0.5
                if (findfile (format "%1.wpt" $mapname)) [
                    guieditbutton "reload waypoints from file" [loadwaypoints]
                    guieditbutton "save and ^fyoverwrite^fw waypoints file" [savewaypoints]

                ] [
                    guitext "^fano waypoint file found for this map"
                    guieditbutton "save current waypoints" [savewaypoints]
                ]
                guistrut 0.5
                guitext "^faoptions for testing"
                guilist [
                    guibutton "raise or " [gamespeed (+25 $gamespeed)]
                    guibutton "lower gamespeed:" [gamespeed (-25 $gamespeed)]
                    guispring 1
                    guifield gamespeed 4
                    guitext "%"
                ]
                guilist [
                    guieditbutton "add bot" addbot
                    guispring 1
                    guieditbutton "remove bot" delbot
                ]

            ]
            guistrut 1
            guibar 5 1 0xffffff
            guistrut 1
            guilist [
                guifont "emphasis" [guitext "routes"]
                guistrut 20 1
                guistrut 1
                guitext "show players the way (optional)" $moderacetex
                guitext "^fashow routes"
                guilist [
                    guiradio "disabled" routeid -1
                    guispring 1
                    guibutton "deselect all" [entcancel]
                ]
                loop i $idmax [
                    guilist [
                        guiradio (format "id %1" $i) routeid $i
                        guispring 1
                        guibutton "select" [entcancel; entfind route @i]
                    ]
                ]
                guilist [
                    guitext "^falook up more routes "
                    guispring 1
                    guifield idmax 3
                ] 
                guistrut 0.5
                guieditcheckbox "drop route ents as you move" droproute
                guistrut 0.5
                guitext (format "^faselected entities: %1" (enthavesel))
                guibutton "^fydelete^fw all selected ents" [delent]
                guistrut 0.5
            ]
        ]

        guitab load
        local [maps whichmaps]
        if (= 0 $guipasses) [
            whichmaps = 0
            selmap = $mapname
            slider = 0
            maps = $mapname
            imax = 1
        ]
        guitext "pick a map and click the preview image to start a new editing session"
        guistrut 2
        guilist [
            guilist [
                guitext "browse maps:"
                guiradio "current map" whichmaps 0 [slider = 0; maps = "" ; selmap = $mapname;]
                guiradio "custom" whichmaps 1 [
                    slider = 0
                    maps = ""
                    imax = 0
                    loopfiles i "maps" mpz [
                        append maps $i
                    ]
                    maps = (listdel $maps $mainmaps)
                    maps = (listdel $maps $racemaps)
                ] 
                guiradio "downloaded" whichmaps 2 [
                    slider = 0
                    maps = ""
                    loopfiles i "maps" mpz [
                        if (< -1 (stringstr $i "_0x")) [append maps $i]
                    ]
                ] 
                j = 3
                looplist i [small medium large capture defend bomber race duel multi hold king] [
                    guiradio $i  whichmaps $j [slider = 0; maps = $[@@[i]maps]]
                    j = (+ 1 $j)
                ]
            ] 
            guispring 1
            imax = (listlen $maps)
            if (imax) [
                guilist [
                guiloopscrollbar j $imax 15 30 [
                    p = (at $maps $j)
                    guibutton $p [selmap = @p] 
                ]
                ]
            ] [
                guistrut 33 
            ]
            guispring 1
            guilist [
                guicenter [guiimage [maps/@selmap] [edit @selmap] 5 1 $emblemtex]
                guicenter [guitext $selmap]
                guistrut 0.5
                guicenter [guitext "^fyWARNING:"]
                guicenter [guitext "unsaved changes are lost"]
            ]
        ]
    ] ]

    newgui music [ guistayopen [
        loopfiles i "sounds/music" "ogg" [
            guilist [ 
                guibutton $i [music music/@i] [] $arrowrighttex
                guispring 1
                if $editing [
                    guibutton "" [mapmusic sounds/music/@i; cleargui 1] [] $editingtex
                ]
            ]
        ]
        guilist [
        guibutton clear [music ""] [] $warningtex
            if $editing [
                guispring 1
                guibutton "" [mapmusic ""; cleargui 1] [] $editingtex
            ]
        ]
    ] ]

    newgui editoptions [ guistayopen [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guilist [
            guilist [
                guieditcheckbox "show material volumes" showmat
                guieditcheckbox "toggle allfaces texture mode" "allfaces"
                guistrut 0.5
                guieditcheckbox "enable selection and editing of entities" entediting
                guieditcheckbox "snap ents to the grid as they are moved" entselsnap
                guistrut 0.5
                guitext "^faplace new entities:"
                i = (& $entdrop 2)
                guibitfield "on selected area" entdrop 2 
                // next checkbox is just the opposite of this bitfield
                guicheckbox "on camera position"  i 0 2 [entdrop (^ $entdrop 2)] 
                guibitfield "drop to ground" entdrop 1
                guistrut 0.5
                guilist [ 
                    guitext "threads to compute lightmaps in parallel "
                    guispring 1
                    guifield lightthreads 2 
                ]
                guilist [ 
                    guitext "precision for quicklight"
                    guispring 1
                    guifield lightprecisionquick 4 
                ]
                guilist [ 
                    guitext "adjust your float speed "
                    guispring 1
                    guifield floatspeed 6 [ floatspeed $floatspeed ]
                ]
                guistrut 0.5
                guitext (format "grid size: %1 cubes =  %2m" (<< 1 $gridpower) (<< 1 (- $gridpower 3)))
                guislider gridpower 0 (min (- $mapsize 1) 12) 
                guistrut 0.5
                guitext "undo cache size in MB"
                guislider undomegs 1 10
            ]
        ]    

        guitab view
        guieditcheckbox "show distance to selection" "crosshairdistance"
        guistrut 0.5
        guieditcheckbox "toggle outline" "outline"
        guibutton "change outline colour" [pickcolour outlinecolour] [] $editingtex
        guistrut 0.5
        guieditcheckbox "toggle fullbright" "fullbright"
        guieditcheckbox "toggle wireframe" "wireframe"
        guieditcheckbox "toggle blank geometry" "blankgeom"
        guistrut 0.5
        guieditcheckbox "toggle paste grid" showpastegrid
        guieditcheckbox "toggle cursor grid" showcursorgrid
        guieditcheckbox "toggle selection grid" showselgrid
        guistrut 0.5
        guieditcheckbox "show edit radar:" showeditradar
        guiradio "sectional compass" editradarstyle 0
        guiradio "compass-distance " editradarstyle 1
        guiradio "full hud" editradarstyle 2
        guilist [
            guiradio "minimap" editradarstyle 3
            guispring 1
            guibutton "refresh" [minimapsize @minimapsize]
            guispring 1
            guibutton "customize" [showgui world 6]
        ]
        guistrut 1
        guilist [
            guitext "radar distance (zoom out)"
            guispring 1
            guiradio "full map size" editradardist 0
        ]
        guislider editradardist 16 (<< 1 $mapsize)

        if (isonline) [
            guitab lock
            guitext "admins can restrict editing by auth rank"
            guilist [
                guitext "when the server is locked:"
                guispring 1
                guieditbutton open [mastermode 0]
                guispring 1
                guieditbutton lock [mastermode 2]
                guispring 1
            ]
            guitext (format "current master mode: %1" (getmastermode 1))
            guistrut 0.5
            guilist [
                guispring 1
                looplist v [spawn edit] [ 
                    guitext $v
                    guispring 1
                ]
            ]
            i = 0
            looplist priv [none player supporter moderator operator administrator developer founder] [
                guilist [
                    guispring 1
                    looplist v [spawneditlock editlock] [   // spawnlock connectlock varslock ... keep it simple
                        guibody [
                        	guiimage [textures/privs/@priv] [] 0.8 1 "" [] (? (>= $i $$v) 0x00ff00 0xff0000)
                        ] [@v @i] [saycommand /@v @i] [guitooltip [/@@v @@i]]
                        guispring 1
                    ]
                ]
                i = (+ 1 $i)
            ]
        ]
    ] ]

    prefabcopy = 1

    newgui geometry [ guistayopen [
        guistrut 0.5
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guieditbutton "undo" [undo; passthroughsel 0]
        guieditbutton "redo" redo
        guistrut 0.5
        guieditbutton "pull cube" [universaldelta -1]
        guieditbutton "push cube" [universaldelta 1]
        guieditbutton "push / pull faces " [domodifier 2]
        guieditbutton "push / pull corners" [domodifier 3]
        guistrut 0.5
        guieditbutton "deselect" "cancelsel"
        guistrut 0.5
        guieditbutton "delete selection (ents first)" editdel
        guieditbutton "copy" "editcopy"
        guieditbutton "paste" "editpaste"
        guieditbutton "^fdcut and paste on release" "editcut"
        guistrut 0.5
        guieditbutton "flip" "editflip"
        guieditbutton "^fdrotate via mouse wheel" [domodifier 4]
        guistrut 0.5
        guieditbutton "toggle heightmap mode" [hmapedit (! $hmapedit); blendpaintmode 0] 
        guieditbutton "^fdcycle heightmap brushes via mouse wheel" [domodifier 9]
        guistrut 0.5
        guitext (format "copy/paste is scaled by the grid size: %1 cubes =  %2m" (<< 1 $gridpower) (<< 1 (- $gridpower 3)))
        guislider gridpower 0 (min (- $mapsize 1) 12) 
        guieditbutton "^fdchange via mouse wheel" [domodifier 1]

        guitab "prefabs"
        if (= 0 $guipasses) [
            prefabs = ""; slider = 0; pnew = "tmp"
            loopfiles i "prefab" "obr" [append prefabs $i] 
        ]
        guistrut 1
        guilist [
            guieditbutton "save the selected geometry as" [saveprefab @pnew]
            guistrut 1
            guifield pnew 10
        ]
        imax = (listlen $prefabs)
        if (imax) [
            guilist [
                guitext "^fapick a prefab to "
                guiradio (format "paste with %1 " (dobindsearch editpaste edit)) prefabcopy 1
                guiradio " insert on the map" prefabcopy 0
            ]
            guistrut 1
            guilist [
                guilist [
                    guiloopscrollbar j $imax 15 15 [
                        p = (at $prefabs $j)
                        guibutton $p [
                            copyprefab @p
                            if $prefabcopy pastehilight paste
                            cleargui
                        ]
                    ]
                ]
                guinocursorfx [guiprefabpreview $guirollovername -1 [] 7.5 1 []]
            ]
        ] [
            guitext "no obr files found in home/prefab"
        ]
        guistrut 1
        guitext (format "prefabs are scaled by the grid size: %1 cubes =  %2m" (<< 1 $gridpower) (<< 1 (- $gridpower 3)))
        guislider gridpower 0 (min (- $mapsize 1) 12) 

        guitab texturing
        guistrut 1
        guicenter [guitext "apply textures to the selected geometry"]
        guistrut 1
        guibody [
                guibutton "view the texture list"
                guistrut 1
                guispring 1 
                guibutton (dobindsearch showtexgui edit)
        ] [cleargui ; showtexgui ] [saycommand /showtexgui] [guitooltip showtexgui]    
        guieditbutton "^fdcycle textures via mouse wheel" [domodifier 6]
        guieditbutton "grab texture from selected face" gettex
        guieditbutton "repeat last texture change across the entire map" [allfaces 0; replace]
        guieditbutton "repeat last texture change inside selection" [allfaces 0; replaceinsel]
        guieditbutton "create texture variations" [showgui textures]
        guieditbutton "create a blendmap" [showgui textures 6] 
        guistrut 1
        guicenter [guitext "apply lighting to the textured geometry"]
        guistrut 1
        guieditbutton "create some light entities" [showgui lights]
        guieditbutton "optimize the geometry" remip
        guieditbutton "patch existing lightmap" [fullbright 0; patchlight]
        guieditbutton "quick lightmap calculation" [quicklight]
        guieditbutton "full lightmap calculation" [fullbright 0; calclight 1]
        guistrut 0.5
        guitext "^falight precision - larger values imply less detail:"
        guicenter [
            guicenter [guibutton "finer" [lightprecision (div $lightprecision 2)]]
            guistrut 1
            guifield lightprecision 4 [] (case (div $lightprecision 32) 0 [
                result 0xff0000 ] 1 [ result 0xff6000 ] 2 [
                result 0xffa000 ] 3 [ result 0xffff00 ] 4 [
                result 0x80ff00 ] 5 [ result 0x00ff00 ] () [ result 0x008000 ])
            guistrut 1
            guicenter [guibutton "coarser" [lightprecision (* $lightprecision 2)]]
        ]
        guistrut 0.5
        guilist [
            guitext "see also:"
            guispring
            guibutton "lightmap variables" [showgui world 2]
            guispring
            guibutton "editing options" [showgui editoptions 1]
        ]
    ] ]

    newgui world [ guistayopen [
        if (= 0 $guipasses) [coastval = 1.0]
        guistatus "some buttons have tooltips and alt-actions for console commands"
        guicenter [guitext "world variables control the map specific environment"]
        guistrut 1
        guieditbutton "apply and customize material volumes" [showgui materials] 
        guibutton "advanced search for world ^fgvariables" [sleep 500 [varflags = 8; vartypes = 7; varsmore = 1]; showgui vars]
        guibutton "advanced search for world ^fgaliases" [sleep 500 [varflags = 8; vartypes = 16; varsmore = 1]; showgui vars]
        guieditbutton "^fyRESET ALL^fw world variables" [showgui resetworldvars]
        guistrut 1
        guitext "game physics:"
        guistrut 0.5
        guilist [ 
            guifield gravity 15
            guistrut 1
            guitext "gravity"
        ]
        guilist [ 
            guifield floorcoast 15
            guistrut 1
            guitext "floor coast (slipperiness)"
        ]
        guilist [ 
            guifield coastval 15 [sleep 50 [vcoastscale @coastval]]
            guistrut 1
            guitext "scale coast on selected texture"
        ]
        guilist [ 
            guifield aircoast 15
            guistrut 1
            guitext "air coast"
        ]
        guilist [ 
            guifield liquidcoast 15
            guistrut 1
            guitext "liquid coast"
        ]
        guilist [ 
            guifield liquidspeed 15
            guistrut 1
            guitext "liquid speed"
            guispring 1
            guibutton "^fyreset^fa these physics settings" [
                looplist i [gravity floorcoast aircoast liquidcoast liquidspeed] [
                    do [@i (getfvardef @i)]
                ]
            ]
        ]
        guistrut 0.5

        guitab lighting
        guicenter [guitext "Lighting uses precomputed lightmaps"]
        guilist [
            guicenter [guitext "lightprecision: "]
            guistrut 3
            guicenter [guibutton "finer" [lightprecision (div $lightprecision 2)]]
            guistrut 1
            guifield lightprecision 4 [] (case (div $lightprecision 32) 0 [
                result 0xff0000 ] 1 [ result 0xff6000 ] 2 [
                result 0xffa000 ] 3 [ result 0xffff00 ] 4 [
                result 0x80ff00 ] 5 [ result 0x00ff00 ] () [ result 0x008000 ])
            guistrut 1
            guicenter [guibutton "coarser" [lightprecision (* $lightprecision 2)]]
        ]
        guitext "^falarger values imply less detail, smaller mpz files, faster calculations"
        guistrut 1.5
        guilist [
            guilist [
                guieditbutton "create some light ents" [showgui lights]
                guieditbutton "patch lightmap " [fullbright 0; patchlight]
                guieditbutton "quick calculation" [quicklight]
                guieditbutton "full calculation" [fullbright 0; calclight 1]
                guistrut 1
                guistrut 30 1
                guitext "colours (0 = disabled)"
                guilist [ 
                    guitext "^fascale lightmap"
                    guispring 1
                    guitext "colour   " "" $lightmapcolour
                    guibutton "pick" [pickcolour lightmapcolour] [] $editingtex 
                ]
                guilist [ 
                    guitext "^faambient lighting"
                    guispring 1
                    guitext "colour   " "" $ambient
                    guibutton "pick" [pickcolour ambient] [] $editingtex 
                ]
                guilist [ 
                    guitext "^faactor shadows"
                    guispring 1
                    guitext "colour   " "" $shadowmapambient
                    guibutton "pick" [pickcolour shadowmapambient] [] $editingtex
                ]
                guilist [ 
                    guitext "^faimplied sunlight"
                    guispring 1
                    guitext "colour   " "" $skylight
                    guibutton "pick" [pickcolour skylight] [] $editingtex 
                ]
            ]
            guispring 1
            guilist [
                guitext "^faadvanced options:"
                guitext "^fause with caution"
                guilist [ 
                    guifield lightlod 3
                    guistrut 0.3
                    guitext "^falevel of detail"
                ]
                guilist [ 
                    guifield lighterror 3
                    guistrut 0.3
                    guitext "^faerror"
                ]
                guilist [ 
                    guifield lightcompress 3
                    guistrut 0.3
                    guitext "^facompress"
                ]
                guilist [ 
                    guifield blurlms 3
                    guistrut 0.3
                    guitext "^fablurr"
                ]
                guistrut 1
                guilist [ 
                    guibutton "^fyreset all" [
                        looplist i [lightmapcolour ambient shadowmapambient skylight lighterror lightcompress blurlms lmaa] [
                            do [@i (getsvardef @i)]
                        ]
                    ]
                ]
            ]
            guistrut 2
        ]

        guitab fog
        guicenter [guitext "customize background and fog colours"]
        guistrut 1
        guilist [
            guitext "fog: blends to full opacity at distance   "
            guifield fog 8
            guispring 1
            guitext "colour   " "" $fogcolour
            guibutton "pick" [pickcolour fogcolour] [] $editingtex
        ]
        guistrut 1
        guilist [
            guitext "sky background:"
            guispring 1
            guicheckbox "glare" skybgglare
            guistrut 6
            guitext "colour   " "" $skybgcolour
            guibutton "pick" [pickcolour skybgcolour] [] $editingtex
        ]
        guitext "   shown when no skybox is present, useful for transparent skyboxes"
        guistrut 1.5
        guilist [
            guitext "fog dome: a background colour gradient for the sky"
            guispring 1
            guitext "colour   " "" $fogdomecolour
            guibutton "pick" [pickcolour fogdomecolour] [] $editingtex
        ]
        guistrut 0.5
        guilist [
            guistrut 8
            guifield fogdomeheight 5
            guistrut 1
            guitext "lower sky height for a max blend value of"
            guispring 1
            guifield fogdomemax 5
        ]
        guistrut 0.5
        guilist [
            guistrut 8
            guifield fogdomeclip 5
            guistrut 1
            guitext "upper sky height for a min blend value of"
            guispring 1
            guifield fogdomemin 5
        ]
        guistrut 0.5
        guilist [
            guistrut 8
            guieditcheckbox "cap (close) the fog dome at the bottom" fogdomecap
        ]
        guistrut 0.5
        guieditbutton "edit fog and colour properties of water and lava materials" [showgui materials]

        guitab sky
        guicenter [guitext "create a background scenery using skyboxes and cloud layers"]
        guistrut 1
        if (concatword $skybox $cloudbox $cloudlayer $envlayer) [
            guilist [
                guispring 1
                looplist j [colour blend spin yaw glare] [
                    guitext (tabify $j 2)
                ]
            ]
        ] [
            guiright [guitext "pick a skybox a or layer to show more options"]
        ]
        guistrut 0.3
        looplist i [sky cloud cloudlayer envlayer] [
            b = "box"
            if (< -1 (stringstr $i layer)) [b = ""]
            guilist [
                guibutton [@i@b] [showgui [@@i@@b]] [saycommand /showgui [@@i@@b]] $editingtex
                guispring 1
                if $[@i@b] [
                    guibutton "pick" [pickcolour @@[i]colour] [] $editingtex $[@[i]colour]
                    guistrut 2
                    guifield [@[i]blend] 5
                    guistrut 1
                    looplist j [spin yaw] [
                        // one exception: spin/yawclouds, not cloud
                        if (=s $i cloud) [
                            guifield [@[j]clouds] 5
                        ] [
                            guifield [@j@i] 5
                        ]
                        guistrut 1
                    ]
                    guicheckbox " " [@[i]glare] 
                    guistrut 3.5
                    guiimage $guiexittex [[@@i@@b] ""] 0.5
                ] [
                    guistrut 1.25 1
                ]
            ]
        ]
        guistrut 0.7
        if (concatword $cloudlayer $envlayer) [
            guilist [
                guispring 1
                looplist j [fade height scrollx scrolly scale subdiv] [ // skipped: offset 
                    guitext (tabify $j 2)
                ]
            ]
        ] [
            guiright [guitext "pick a cloud or env layer to show more options"]
        ]
        guistrut 0.3
        looplist i [cloud env] [ 
            guilist [
                guibutton $i [showgui @[i]layer]  [saycommand /showgui @[i]layer] $editingtex
                guispring 1
                if $[@[i]layer] [
                    looplist j [fade height scrollx scrolly scale subdiv] [
                        guifield [@i@j] 5
                        guistrut 1
                    ]
                ] [
                    guistrut 1.25 1
                ]
            ]
        ]
        guistrut 0.5
        guilist [
            guitext "examples with quick sky lighting:"
            guispring 1
            guibutton "night" [ambient 40 40 40; skylight 70 80 90; skybox skyboxes/stars; skycolour 255 255 255; cloudlayer skyboxes/clouds01; cloudlayercolour 50 60 100; cloudlayerblend 0.3; cloudscrollx 0.005; fogcolour 12 10 13; fog 2000; calclight -1]
            guispring 1
            guibutton "morning" [ambient 60 35 40; skylight 170 140 145; skybox freezurbern/harmony; skycolour 255 160 180; cloudlayer skyboxes/clouds03; cloudlayercolour 255 125 170; cloudlayerblend 0.2; cloudscrollx 0.01; fogcolour 129 79 59; fog 2000; calclight -1]
            guispring 1
            guibutton "evening" [ambient 50 30 10; skylight 180 100 70; skybox skyboxes/sunsetflat; skycolour 255 255 255; cloudlayer skyboxes/clouds01; cloudlayercolour 255 100 50; cloudlayerblend 0.2; cloudscrollx 0.005; fogcolour 40 20 5; fog 2000; calclight -1]
        ]
        guistrut 0.5
        guibutton "^fyreset all^fw skybox and layer variables, but keep the textures" [
            looplist i [sky cloud envlayer cloudlayer] [ 
                looplist j [colour blend glare] [
                    do [@i@j (getsvardef @i@j)]
                ]
            ]
            looplist i [spin yaw] [
                looplist j [sky clouds cloudlayer envlayer] [ 
                    do [@i@j (getsvardef @i@j)]
                ]
            ]
            looplist i [cloud env] [ 
                looplist j [fade height scrollx scrolly scale subdiv] [
                    do [@i@j (getsvardef @i@j)]
                ]
            ]
        ]

        guitab grass
        guicenter [guitext "pick a grass type to apply on all surfaces with"]
        guicenter [guitext "the same texture as the current selection"]
        guistrut 1
        guilist [
            guilist [
                guilooplistsplit i 2 [
                    textures/grass
                    nobiax/grass01
                    nobiax/grass02
                    nobiax/grass03
                    nobiax/grassbush
                    luckystrike/grass_lucky
                    luckystrike/grass_lucky_alt
                    luckystrike/grass_lucky_bright
                    luckystrike/grass_lucky_green
                ] [
                    //guiimage $i ... non-square images are not aligned correctly atm
                    guifont huge [guieditbutton (format "^f(%1)" $i)  [setgrass @i] ]
                    guistrut 0.2
                ]
                guieditbutton "^fyclear^fa from texture" [setgrass ""]
            ]
            guispring 1
            guilist [
                guistrut 22 1
                guilist [
                    guitext "colour" "" $grasscolour
                    guispring 1
                    guibutton "pick" [pickcolour grasscolour] [] $editingtex
                ]
                guilist [
                    guifield grassblend 8
                    guispring 1
                    guitext "blend"
                ]
                guilist [
                    guifield grassheight 8
                    guispring 1
                    guitext "height"
                ]
                guilist [
                    guifield grassscale 8 
                    guispring 1
                    guitext "scale down"
                ]
                guilist [
                    guifield grassanimmillis 8
                    guispring 1
                    guitext "animmillis"
                ]
                guilist [
                    guifield grassanimscale 8
                    guispring 1
                    guitext "animscale"
                ]
                guiright [ guibutton "^fyreset" [
                    looplist i [colour blend height scale animmillis animscale] [
                        do [grass@i (getfvardef grass@i)]
                    ]
                ] ]
            ]
        ]
        guistrut 1
        guicenter [guibutton "Tip: Looks better with texture blending" [showgui textures 6]]

        guitab misc
        guicenter [guitext "edit various world settings"]
        guistrut 1 
        guilist [
            guilist [
                guilist [
                    guitext "minimap settings: "
                    guispring 1
                    guieditbutton2 "^fgrefresh" [minimapsize @minimapsize]
                ]
                guilist [ 
                    guitext "^fabackground"
                    guistrut 1
                    guitext "colour   " "" $minimapcolour
                    guibutton "pick" [pickcolour minimapcolour] [] $editingtex
                ]
                guilist [ 
                    guitext "^fadrawn at height"
                    guistrut 1
                    guifield minimapheight 8
                    guispring 1
                ]
                guibutton "set height to current position" [minimapheight (at $getcampos 2)]
                guistrut 0.2
                guicheckbox "minimap clip" minimapclip
            ]
            guispring 1
            guilist [ 
                guitext "sky texture:" "textures/sky"
                guicheckbox "apply sunlight" skytexturelight
                guicheckbox "invisible (needs reload)" skytexture 0 1 [if $skytexture [
                    echo skytexture 1: skybox
                ] [
                    echo skytexture 0: invisible - takes affect after saving and loading]
                ]
                guistrut 1
                guitext "hurt material damage:"
                guilist [
                    guifield hurtdamage 2
                    guitext " hp every "
                    guifield hurtdelay 4
                    guitext " ms"
                ]

            ]
            guispring 1
        ]
        guistrut 1
        guilist [
            guilist [
                guitext "point aliases:"
                guinocursorfx [guiimage $pointtex [] 2.5]
            ]
            guistrut 2
            guilist [
                guilooplistsplit i 2 [1 2 3 4 5 6 7 8] [
                    do [guifield point_@i -20]
                ]
            ]
        ]
    ] ]

    newgui resetworldvars [
        guitext "^fyCaution:^fw A reset of all map specific variables cannot be undone."
        guitext "Therefore, it is recommended to save your map first."
        guitext "Are you sure you wish to continue?"
        guistrut 1
        guilist [
            guieditbutton "^fyYes, reset these vars" [resetworldvars]
            guispring 1
            guibutton "^fgNo, get me out of here" [cleargui 1]
        ]
    ]

    cloudlist = [ 
        skyboxes/clouds01
        skyboxes/clouds02
        skyboxes/clouds03
        wicked/wickedcloudlayer1
        wicked/wickedcloudlayer2
    ]
    loopfiles i "torley/desat" png [
        if (= -1 (stringstr $i "_")) [
            append cloudlist [torley/desat/@i]
        ]
    ]

    newgui cloudlayer [ guistayopen [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        guilist [
            guilist [
                guiloopscrollbar j (listlen $cloudlist) 10 40 [
                    p = (at $cloudlist $j)
                    if (=s $p $cloudlayer) [
                        guibutton $p [cloudlayer ""] [saycommand /cloudlayer ""] "" 0xffff00
                    ] [
                        guibutton $p [cloudlayer @p] [saycommand /cloudlayer @p] 
                    ]
                ]
            ]
            guinocursorfx [ guiimage (? $guirollovername $guirollovername $cloudlayer) [] 5 1] 
        ]
    ] ]
    // same thing for envlayer
    newgui envlayer [ guistayopen [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        guilist [
            guilist [
                guiloopscrollbar j (listlen $cloudlist) 10 40 [
                    p = (at $cloudlist $j)
                    if (=s $p $envlayer) [
                        guibutton $p [envlayer ""] [saycommand /envlayer ""] "" 0xffff00
                    ] [
                        guibutton $p [envlayer @p] [saycommand /envlayer @p] 
                    ]
                ]
            ]
            guinocursorfx [ guiimage (? $guirollovername $guirollovername $envlayer) [] 5 1] 
        ]
    ] ]

    findskyboxes = [
        skyboxes = ""
        loopfiles dir data "" [
            looplist ext "png jpg" [
                loopfiles i $dir $ext [
                    if (> (stringstr $i "_up") -1) [
                        ibox = (format "%1/%2" $dir $i)
                        append skyboxes (substring $ibox 0 (stringstr $ibox "_"))
                    ]
                ]
            ]
        ]
    ] 
    
    findskyboxes
    skyboxprev = 1
    cloudradio = 0
    
    newgui skyboxes [ guistayopen [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        if (= 0 $guipasses) [
            slider = 0
            imax = (listlen $skyboxes)
        ] 
        if (imax) [
            slidermax = (max (- $imax 10) 0)
            guilist [
                guilist [
                    guistrut 1
                    guitext "pick a texture set"
                    guistrut 0.5
                    guiradio " as skybox" cloudradio 0 
                    guiradio " as cloudbox" cloudradio 1 
                    guistrut 1
                    guicheckbox "show a preview" skyboxprev
                    guistrut 0.5
                    guitext "cloudboxes are clipped"
                    guilist [
                        guitext "at height level "
                        guifield cloudclip 4
                    ]
                ]
                guistrut 10
                guilist [
                    guistrut 25 1
                    loop i (min 10 $imax) [
                        j = (+ $i $slider)
                        p = (at $skyboxes $j)
                        if $cloudradio [
                            guibutton $p [cloudbox @p] [] (format "../%1%2_%3" "<thumbnail>" $p "ft")
                        ] [
                            guibutton $p [skybox @p] [] (format "../%1%2_%3" "<thumbnail>" $p "ft")
                        ]
                    ]
                ]
                if (slidermax) [guislider slider 0 $slidermax [] 1 1]
                guispring 
                p = $guirollovername
                if $p [] [
                    // no mouse-over preview: Show current sky or cloud box
                    if $cloudradio [p = $cloudbox] [p = $skybox]
                ]
                if $skyboxprev [guinocursorfx [ guiimage (format "%1%2_%3" "<thumbnail:128>" $p "up") [] 5 0 "textures/sky"]]
            ]
            if $skyboxprev [
                guilist [
                    looplist k "ft lf bk rt" [
                        guinocursorfx [ guiimage (format "%1%2_%3" "<thumbnail:128>" $p $k) [] 5 0 "textures/sky"]
                    ]    
                ]
                guilist [
                    guilist [
                        guistrut 2
                        if $skybox [
                            guibutton (format "the current skybox is %1 ^fy(clear)" $skybox ) [skybox ""]
                        ] [
                            guitext "no skybox is currently in use"
                        ]
                        guistrut 2
                        if $cloudbox [
                            guibutton (format "the current cloudbox is %1 ^fy(clear)" $cloudbox ) [cloudbox ""]
                        ] [
                            guitext "no cloudbox is currently in use"
                        ]
                        guistrut 2
                        guitext "Tip: A preview is shown as you move the mouse over the above list"
                    ]
                    guispring 
                    guinocursorfx [ guiimage (format "%1%2_%3" "<thumbnail:128>" $p "dn") [] 5 0 "textures/sky"]
                ]
            ]
        ] [
            guitext "no skyboxes found - this should not happen."
        ]
    ]  ]

    newgui skybox [
        cloudradio = 0
        cleargui 1
        showgui skyboxes
    ]

    newgui cloudbox [
        cloudradio = 1
        cleargui 1
        showgui skyboxes
    ]

    newgui textures [ guistayopen [
        if (getseltex) [
            guistatus "some buttons have tooltips and alt-actions for console commands"
            if (= $guipasses 0) [varpix = 32 ; vara = 0.5; vara2 = 0.0]
            guistrut 0.5
            guicenter [guitext "v-commands create variations of the currently selected texture"]
            guicenter [
                guieditbutton2 "reset variation on selected texture" [vreset]
                guispring 1
                guibutton "^fyclear^fw unused textures"  [showgui texreset]
            ]
            guistrut 0.5
            guilist [
                guispring 1
                guibox2 -1 0xffffff [
                    guitext "rotation or flip:"
                    guiright [guieditbutton2 "90 degree"  [vrotate 1]]
                    guiright [guieditbutton2 "180 degree"  [vrotate 2]]
                    guiright [guieditbutton2 "270 degree"  [vrotate 3]]
                    guiright [guieditbutton2 "left to right" [vrotate 4]]
                    guiright [guieditbutton2 "top to bottom" [vrotate 5]]
                    guiright [guieditbutton2 "reset"  [vrotate 0]]
                ]
                guispring 1
                guibox2 -1 0xffffff [
                    guitext "texture offset:"
                    guieditbutton "move up" [vdelta voffset 0 (* -1 @varpix)] [] $arrowtex
                    guieditbutton "move down" [vdelta voffset 0  @varpix] [] $arrowdowntex
                    guieditbutton "move left" [vdelta voffset (* -1 @varpix) 0] [] $arrowlefttex
                    guieditbutton "move right" [vdelta voffset @varpix 0] [] $arrowrighttex
                    guilist [
                        guitext "^faby "
                        guifield varpix 3
                        guitext " ^fapixels"
                    ]
                    guieditbutton "reset offset" [voffset 0 0] [] 
                ]
                guispring 1
                guibox2 -1 0xffffff [
                    guitext "scale:"
                    guieditbutton "800.0%" [vscale 8]
                    guieditbutton "400.0%" [vscale 4]
                    guieditbutton "200.0%" [vscale 2]
                    guieditbutton "100.0%" [vscale 1]
                    guieditbutton " 50.0%" [vscale 0.5]
                    guieditbutton " 25.0%" [vscale 0.25]
                    guieditbutton " 12.5%" [vscale 0.125]
                ]
                guispring 1
                guibox2 -1 0xffffff [
                    guitext "scrolling:"
                    guieditbutton "+horizontal" [vdelta vscroll 0.1 0]
                    guieditbutton "-horizontal" [vdelta vscroll -0.1 0]
                    guieditbutton "+vertical" [vdelta vscroll 0 0.1]
                    guieditbutton "-vertical" [vdelta vscroll 0 -0.1]
                    guieditbutton "stop moving" [vscroll 0 0]
                ]
                guispring 1
            ]
            guistrut 1
            guicenter [ 
                guibox2 -1 0xffffff [
                    guitext "customize the opacity when inside alpha material:" 
                    guilist [
                        guifield vara 3 
                        guitext " front side"
                        guistrut 3
                        guifield vara2 3 
                        guitext " back side"
                        guistrut 3
                        guieditbutton2 "apply" [valpha @vara @vara2]
                    ]
                ]
            ]

            guitab colour
            if (= $guipasses 0) [hex = 0xffffff; colr = 1.0; colg = 1.0; colb = 1.0]
            guilist [
                guistrut 2
                guiimage (gettexname $getseltex) [vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255)] 2.5 1 "" [] $hex 
                guicenter [guitext " = "]
                guiimage (gettexname $getseltex) [vcolour 1 1 1] 2.5 1 
                guicenter [guitext " * "]
                guihexpreview $hex multiplier
                guistrut 2
                guilist [
                    guirgbsliders hex
                ]
            ]
            guistrut 0.5
            guilist [
                guieditbutton2 "click the preview to apply the texture" [vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255)]
                guispring
                guieditbutton2 "or click here to multiply the current colour" [vdelta vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255)]
            ]
            guistrut 1.5
            guipalette 0.6

            guitab palette
            if (= 0 $guipasses) [tb = 0; wb = 0]
            guistrut 1
            guicenter [guitext "palettes allow to colourize a texture dynamically"]
            guistrut 1
    	    guilist [
    	        guilist [
                    guitext "disabled"
    	            guiimage $warningtex [vpalette 0 0] 1.5 1 
    	        ]
                guispring 1
    	        guilist [
                    guitext "flashing colours"
    	            guilist [
    	                guiimage $burningtex [vpalette 0 1] 1.5 1 
    	                guiimage $modekaboomtex [vpalette 0 2] 1.5 1 
    	                guiimage $bombtex [vpalette 0 3] 1.5 1 
    	                guiimage $shockingtex [vpalette 0 4] 1.5 1 
    	            ]
                    guistrut 1
    	        ]
                guispring 1
    	        guilist [
                    guitext "team colour variables"
    	            guilist [
    	                guiimage $modeffatex [vpalette 1 0] 1.5 1 
                        i = 1
                        looplist p [alpha omega kappa sigma ] [
    	                    guiimage $[team@[p]tex] [vpalette 1 (+ @i $tb)] 1.5 1 "" [] $[team@[p]colour]
                            i = (+ 1 $i)
    	                ]
    	            ]
                    guicheckbox "only applied in team games" tb 0 5
    	        ]
    	    ]
            guistrut 1
            guitext "weapon colour variables"
    	    guilist [
    	        loop i $weapidxnum [
                    w = (at $weapname $i)
    	            if (!=s $w "melee") [guiimage $[@[w]tex] [vpalette 2 (+ @i $wb)] 1.5 1 "" [] $[@[w]colour]]
    	        ]
    	    ]
            guicheckbox "only applied if the weapon is allowed to spawn" wb 0 $weapidxnum

            guitab shaders
            if (= $guipasses 0) [
                par1 = 0.05
                par2 = 0.0
                pt = 1.0
            ]
            guistrut 0.5
            guicenter [ guitext "certain textures support additional shader settings"]
            guicenter [ guitext "pick a colour and set the corresponding shader parameter"]
            guicenter [ guibutton "tip: Use black to disable a shader" [hex = 0x000000]]
            guistrut 0.5
            guicenter [
                guibox -1 0xffffff [
                    guistrut 1
                    guihexpreview $hex multiplier
                    guistrut 1
                    guicenter [
                        guirgbsliders hex
                    ]
                    guistrut 3
                    guilist [
                        guieditbutton "specular (gloss)" [vdelta vshaderparam specscale @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                        guieditbutton "glow effect (shine)" [vdelta vshaderparam glowcolor @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                        guieditbutton "pulsing glow effect" [vdelta vshaderparam pulseglowcolor @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                        guieditbutton "envmap (reflection)" [vdelta vshaderparam envscale @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ] 
                    ]
                    guistrut 1
                ]
            ]
            guistrut 0.5
            guicenter [
                guibox2 -1 0xffffff [
                    guilist [
                        guifield pt 4
                        guistrut 1
                        guitext "Hz" 
                        guistrut 3
                        guieditbutton2 "set pulse speed" [vdelta vshaderparam pulsespeed @pt]
                        guistrut 3
                        guieditbutton2 "set pulseglow speed" [vdelta vshaderparam pulseglowspeed @pt]
                    ]
                ] 
            ]
            guistrut 0.5
            guicenter [
                guibox2 -1 0xffffff [
                    guitext "parallax deforms the texture for a pseudo-3d-effect based on the normal map:" 
                    guistrut 0.5
                    guilist [
                        guifield par1 12
                        guistrut 1
                        guitext "scaling factor " 
                        guispring 1
                        guifield par2 12
                        guistrut 1
                        guitext "offset" 
                        guispring 1
                        guieditbutton2 "^fgapply" [vdelta vshaderparam parallaxscale @par1 @par2]  
                        guispring 1
                    ]
                    guilist [
                        guitext "some examples:"
                        guispring 1
                        guibutton "moderate"   [par1 = 0.02; par2 = 0.0]
                        guispring 1
                        guibutton "strong"     [par1 = 0.05; par2 = 0.0]
                        guispring 1
                        guibutton "inverted"   [par1 = -0.02; par2 = 0.0]
                        guispring 1
                        guibutton "soft blob"  [par1 = 0.00; par2 = -0.02]
                        guispring 1
                        guibutton "fish eye"   [par1 = 0.00; par2 = 2.0]
                        guispring 1
                    ]
                ]
            ]

            guitab blend
            if (= 0 $guipasses) [slotnum = 1]
            guistrut 1
            guicenter [guitext "a blendmap creates smooth transitions on two-layer textures"]
            guistrut 1
            guilist [
            guibutton "first look up a texture index in the texture list:  #" [cleargui; showtexgui]
                guistrut 1
                guifield slotnum 4
            ]
            guieditbutton "add the index as texture layer to the current selection" [vlayer @slotnum]
            guistrut 1
            guieditbutton "reset texture layer on selection" [vlayer ""]
            guistrut 1
            guibar
            guilist [
                guilist [
                    guitext "cycle blend paint mode:"
                    guiradio "off" blendpaintmode 0
                    guiradio "overwrite" blendpaintmode 1
                    guiradio "merge" blendpaintmode 2
                    guiradio "max opacity" blendpaintmode 3
                    guiradio "inverted merge" blendpaintmode 4
                    guiradio "erase" blendpaintmode 5
                ]
                guispring 1
                guibutton (dobindsearch [hmapedit 0;
    blendpaintmode (? (= $blendpaintmode (getvarmax blendpaintmode)) 0 (+ $blendpaintmode 1))
    echo (concatword "^fgblend mode:^fw " (at $paintmodes $blendpaintmode) " (^fc" $blendpaintmode "^fw)")] edit)
                guispring 1
                guilist [
                    guistrut 30 1
                    guitext "blendmap paint brush:"
                    guitext (getblendbrushname (curblendbrush))
                    if ($blendpaintmode) [
                        guieditbutton "cycle" [universaldelta -1] 
                        guieditbutton "quit blendmap paint mode" [hmapedit 0; blendpaintmode 0]
                    ] [    
                        guitext "enable blendmap mode first"
                        guistrut 1
                    ]     
                    guistrut 1
                    guieditbutton "generate preview" showblendmap
                    guieditbutton "^foclear^fw the entire blendmap" clearblendmap
                ]
            ]

            guitab "new"
            guistrut 0.5
            if (= $guipasses 0) [
                texs = (loopconcat i 5 [result (gettexname $getseltex $i)])
                tn = (listlen $texs)
                loop i $tn [
                    t = (at $texs $i)
                    do [s@i = (substring $t (+ (stringstr $t "_") 1) 1)]
                ]
                s0 = "c"
                looplist i [bump env specmap parallax pulse glow] [
                    do [s@i = 0]
                ]
            ]
            guicenter [guitext "this tab is an experimental feature"]
            guistrut 1
            guicenter [guitext "it allows to create a new texture slot using the chosen shader settings"]
            guicenter [guitext "and the components of the currently selected texture"]
            guicenter [guitext "for example, this allows to add pulseglow to a texture with glow"]
            guistrut 1
            guicenter [
                guibox2 -1 0xffffff [
                    texs = (loopconcat i 5 [result (gettexname $getseltex $i)])
                    if (stringlen $texs) [
                        guilist [
                            guibutton "add slot:" [
                                t = ""
                                looplist i [bump env specmap parallax pulse glow] [
                                    if $[s@i] [t = (concatword $t $i)]
                                ]
                                setshader (concatword $t "world")
                                echo setshader (concatword $t "world")
                                loop i $tn [
                                    texture $[s@i] (at $texs $i) 0 0 0 0.5 
                                    echo texture $[s@i] (at $texs $i) 0 0 0 0.5 
                                ]
                            ]
                            guistrut 2
                            guilist [
                                looplist i [bump env specmap parallax pulse glow] [
                                    guicheckbox $i [s@i] 1 0 
                                ]
                            ]
                            guistrut 2
                            guilist [
                                loop i $tn [
                                    guilist [
                                        guifield [s@i] 1
                                        guistrut 1
                                        t = (at $texs $i)
                                        guitext $t [../@t]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ] [
            guifont "emphasis" [guicenter [guitext "no current selection"]]
            guistrut 0.5
            guicenter [guitext "select some texture before opening this menu"]
        ]
    ] ]

    slider = 0
    efuitype = 0
    efuitypename = "*"
    
    efuireset = [
        efuiattrs = "*"
        loop i (? $efuitype (getentattr $efuitype) 12) [
            [efuiattr@i] = "*"
        ]
    ]
    efuireset
    
    groupedit = 0
    newgui ents [
        guistatus "Please note the selection info at the right side of the screen" 
        guistayopen [
            guistrut 0.5
            guicenter [
                guieditbutton "create lights" [showgui lights] 
                guispring 1
                guieditbutton "create sound" [showgui sounds] 
                guispring 1
                guieditbutton "create mapmodel" [showgui mapmodels] 
            ]
            guicenter [guitext "quick creation of basic entities:"]
            guistrut 0.5
            guilist [
                loop i 5 [
                    wp = (at $weapname (+ 2 $i))
                    guiimage $[@[wp]tex] [newent weapon (+ 2 @i)] 2 1 [] "" $[@[wp]colour]
                ]
            ]
            guilist [
                loop i 5 [
                    wp = (at $weapname (+ 7 $i))
                    guiimage $[@[wp]tex] [newent weapon (+ 7 @i)] 2 1 [] "" $[@[wp]colour]
                ]
            ]
            guilist [ 
                guiimage $flagtex [newent affinity ] 2 1 [] "" 
                guiimage $flagtex [newent affinity 1] 2 1 [] "" $teamalphacolour
                guiimage $flagtex [newent affinity 2] 2 1 [] "" $teamomegacolour
                guiimage $flagtex [newent affinity 3] 2 1 [] "" $teamkappacolour
                guiimage $flagtex [newent affinity 4] 2 1 [] "" $teamsigmacolour
            ]
            guilist [ 
                guiimage $playertex [newent playerstart ] 2 1 [] "" 
                guiimage $playertex [newent playerstart 1] 2 1 [] "" $teamalphacolour
                guiimage $playertex [newent playerstart 2] 2 1 [] "" $teamomegacolour
                guiimage $playertex [newent playerstart 3] 2 1 [] "" $teamkappacolour
                guiimage $playertex [newent playerstart 4] 2 1 [] "" $teamsigmacolour
            ]
            guilist [
                guiimage $arrowtex [newent pusher] 2 1
                guiimage $shockingtex [newent teleport] 2 1
                guiimage $burningtex [newent particles] 2 1
                guiimage $modeonslaughttex [newent actor] 2 1
                guiimage $moderacetex [newent checkpoint] 2 1
            ]

            guitab edit
            if (= 0 (enthavesel)) [
                groupedit = 0
                guicenter [
                    slider = 0
                    guilist [
                        guistrut 2 
                        guitext "no entities are currently selected"
                        guistrut 4
                        guitext "Use the other tabs to create a new entity"
                        guitext "or to find entities with specific attributes."
                        guistrut 1
                        guitext "You can also use below buttons to select:"
                        guistrut 1
                        guieditbutton " the previous group of ents," [entselbyindex $entindexlist]
                        guistrut 0.5
                        guieditbutton " all entities on this map" [entfind]
                        guistrut 0.5
                        guieditbutton " or all in the selected volume" [entfindinsel]
                        guistrut 1
                    ]
                ]
            ]
            if (> (enthavesel) 1) [
                if $groupedit [
                    guistrut 1
                ] [
                    guitext (format "selected entities: %1" $enthavesel)
                    guilist [
                        guitext "deselect:"
                        guispring 1
                        guieditbutton2 "ents" [entcancel] [] 
                        guispring 1
                        guieditbutton2 "all" [cancelsel] [] 
                    ]
                    guieditbutton "cycle entity links  ->  <-  <-->  -" [entlink]
                    guilist [
                        guitext "^faview:"
                        guispring 1
                        guieditbutton "next ent" [entautoview 1] 
                        guispring 1
                        guieditbutton "previous ent" [entautoview -1] 
                        guispring 1
                        guitext (dobindsearch [domodifier 10; onrelease entautoview] edit)
                    ]
                    guistrut 0.5
                    guitext "click below to select and edit an entity"
                    guistrut 0.5
                ]
                entgetlist = ""
                entindexlist = ""
                imax = 0
                entloop [
                    append entindexlist (entindex)
                    append entgetlist (format "[(%1)^t %2]" (entindex) (entget))
                    imax = (+ $imax 1) 
                ]
                guiloopscrollbar j $imax 15 30 [
                    guibutton (at $entgetlist $j) [
                        entcancel
                        entselect [(= (at $entindexlist @@j) (entindex))]
                        slider = 0
                    ]
                ]
                guistrut 0.5
                guicheckbox (format "^f%1 edit multiple ents: use with caution!" (? $groupedit zyw d)) groupedit 
                guistrut 0.5
                guibutton "  save the list for use with /entselbyindex" [saycommand "/myentlist = $entindexlist; setpersist myentlist 1"]
            ]    
            if (|| $groupedit (= 1 (enthavesel))) [
                if (> $enthavesel 1) [
                    guitext "Warning: entity attributes may differ" $waitingtex
                ] [
                    //guibutton "reselect matching ents (find)" [entfind @efuitypename @efuiattrs] [] 
                    entl = (getentity $entindex 2)
                    guilist [
                        guitext (format "linked entities: %1 " $entl)
                        if $entl [
                            guibutton "(select)" [
                                id = $entindex
                                loop i $entl [ entselbyindex (getentity $id 2 $i)]
                            ]
                        ]
                    ]
                    guilist [
                        guitext "deselect:"
                        guispring 1
                        guieditbutton2 "ents" [entcancel] [] 
                        guispring 1
                        guieditbutton2 "all" [cancelsel] [] 
                    ]
                ]
                guistrut 1
                itype = (indexof $enttypelist (enttype))
                guitext (format "selected: %1 (index %2)" (enttype) (entindex))
                loop iattr (getentattr $itype) [
                    a = (getentattr $itype $iattr (entattr 0)) 
                    if (|| (stringlen $a) (= $iattr 0)) [
                        guilist [
                            guifont emphasis [
                            guibutton "+" [entprop @iattr 1]
                            guistrut 1
                            guibutton "-" [entprop @iattr -1]
                            guistrut 1
                            ]
                            field@iattr = (entattr $iattr)
                            guifield field@iattr -10 [entattr @iattr (field@iattr)]
                            guistrut 1
                            guitext $a
                            guispring 1
                            if (=s $a flags) [
                                guibutton pick [pickattr = @iattr; showgui entflags] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a yaw) [
                                guibutton pick [pickattr = @iattr; showcompass entyaw] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a pitch) [
                                guibutton pick [pickattr = @iattr; showcompass entpitch] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a modes) [
                                guibutton pick [pickattr = @iattr; showgui entmodes] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a muts) [
                                guibutton pick [pickattr = @iattr; showgui entmuts] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a colour) [
                                guibutton pick [pickattr = @iattr; showgui entcolour] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a green) [
                                guibutton pick [pickattr = @iattr; showgui entcolours] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a palette) [
                                guibutton pick [pickattr = @iattr; showgui entpalette] -1 $editingtex
                                guistrut 1
                            ]
                            if (=s $a dir) [
                                guibutton pick [showgui particledir] -1 $editingtex
                                guistrut 1
                            ]
                            if (< $iattr 12) [
                                guitext (dobindsearch (concat domodifier (+ 11 $iattr)) edit)
                            ]
                        ]    
                    ]    
                ]
                guistrut 1
                guieditbutton "edit selected ents via console" selentedit
                guilist [
                    guitext "quick-edit: mousewheel and "
                    guitext (dobindsearch [domodifier 11] edit)
                    guitext (dobindsearch [domodifier 12] edit)
                    guitext (dobindsearch [domodifier 13] edit)
                    guitext " etc."
                ]
            ] 

            guitab "find"
            guicenter [guitext "advanced search and replacement"]
            guistrut 0.5
            guilist [
                guilist [
                    guiradio (tabify "any type" 3) efuitype 0 [efuitypename = "*"; efuireset]
                    loop i (- (getentinfo) 2) [
                        type = (getentinfo (+ $i 1))
                        guiradio (tabify $type 3) efuitype (+ $i 1) [efuitypename = @type; efuireset]
                    ]
                ]
                guistrut 1
                guilist [
                    if (efuitype) [
                        if (=s $efuitypename particles) [guitext "attributes differ per type:"]
                        loop i (getentattr $efuitype) [
                            a = (getentattr $efuitype $i $efuiattr0) 
                            if (|| (stringlen $a) (= $i 0)) [
                                guilist [
                                    guifield [efuiattr@i] 11  
                                    guistrut 1
                                    guitext $a
                                ]
                            ]
                        ]
                    ] [
                        loop i 12 [
                            guilist [
                                guifield [efuiattr@i] 11  
                                guistrut 1
                                guitext (format "attribute %1" (+ $i 1))
                            ]
                        ]
                    ]
                    guistrut 25 1
                    if (< 0 $enthavesel) [
                        guicenter [ guibutton "pick from selected entity" [
                            efuitypename = $enttype
                            efuitype = (indexof $enttypelist $enttype)
                            efuireset
                            loop i 12 [
                                [efuiattr@i] = (entattr $i) 
                            ]
                        ] ]
                    ]
                ]
            ]
            guistrut 0.5
            guicenter [
                guilist [
                    if (efuitype) [
                        efuiattrs = (loopconcat i (getentattr $efuitype) [result $[efuiattr@i]])
                    ] [efuiattrs = "*"]
                    guieditbutton "find and select all matching entities" [entcancel; entfind @efuitypename @efuiattrs] [] 
                    guieditbutton "add them to the current selection" [entfind @efuitypename @efuiattrs] [] 
                    guieditbutton "search matches in the selected volume" [entcancel; entfindinsel @efuitypename @efuiattrs] []
                    if (efuitype) [
                        guieditbutton "create a new ent with these attributes" [newent @efuitypename @efuiattrs]  
                    ] [
                        guistrut 1
                    ]
                    if (< 0 $enthavesel) [
                        guibutton "^fyreplace all^fw selected entities" [
                            i = 0
                            if (!=s $efuitypename "any") [enttype $efuitypename]
                            looplist j $efuiattrs [
                                if (!=s $j "*") [entattr @i @j] 
                                i = (+ $i 1)
                            ]
                        ]
                    ] [guistrut 1]
                ]
            ]
        ]
    ]

    newgui entflags [ guistayopen [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        guitext (format "pick flags for attribute %1 of the selected %2" $pickattr (enttype)) "" -1 0 500
        guistrut 0.5
        if (= 0 $guipasses) [
            flags = (entattr $pickattr)
        ]
        if (=s $enttype "mapmodel") [
            i = 1
            looplist a [hide no-clip no-shadow no-dyn-shadow] [
                guibitfield $a flags $i [sleep 50 [entattr @pickattr @flags]]
                i = (<< $i 1)
            ]
            guistrut 1
            guitext "Tip: Link the mapmodel with a trigger to make the flags mode-specific or switchable" "" -1 0 500
        ]
        if (=s $enttype "sound") [
            i = 1
            looplist a [no-atten no-delay no-cull no-pan no-dist no-quiet clamped] [
                guibitfield $a flags $i [sleep 50 [entattr @pickattr @flags]]
                i = (<< $i 1)
            ]
        ]
        if (=s $enttype "lightfx") [
            i = 1
            looplist a [rnd-min rnd-max +dynlight +flicker +pulse +glow] [
                guibitfield $a flags $i [sleep 50 [entattr @pickattr @flags]]
                i = (<< $i 1)
            ]
        ]
        if (=s $enttype "weapon") [
            guibitfield "forced" flags 1 [sleep 50 [entattr @pickattr @flags]]
        ]

    ]]

    newcompass entyaw $editingtex [
        i = 0
        loop i 8 [
            a = (format "%1 degree" (* 45 $i))
            compass $a [entattr @pickattr @a]
        ]
    ]

    newcompass entpitch $editingtex [
        i = 0
        loop i 5 [
            a = (format "%1 degree" (- 90 (* 45 $i)))
            compass $a [entattr @pickattr @a]
        ]
    ]

    newgui entmodes [ guistayopen [
        if (= 0 $guipasses) [
            modes = (entattr $pickattr)
            if (< $modes 0) [s = -1; modes = (abs $modes)] [s = 1]
        ]
        guicheckbox "not-" s -1 1 [sleep 50 [entattr @pickattr (* @s @modes)]]
        i = 1
        loop j (- $modeidxnum 2) [
            m = (at $modename (+ $j 2))
            guibitfield $m modes $i [sleep 50 [entattr @pickattr (* @s @modes)]]
            i = (<< $i 1)
        ]
        guilist [
            guibutton "^fyclear" [entattr @pickattr 0; modes = 0]
            guispring 1
            guibutton "^fgdone" [cleargui 1]
        ]
    ] ]

    newgui entmuts [ guistayopen [
        guilist [
            guilist [
                if (= 0 $guipasses) [
                    muts = (entattr $pickattr)
                    if (< $muts 0) [s = -1; muts = (abs $muts)] [s = 1]
                ]
                guicheckbox "not-" s -1 1 [sleep 50 [entattr @pickattr (* @s @muts)]]
                i = 1
                looplist m (concat $mutsname gsp1 gsp2 gsp3) [
                    guibitfield $m muts $i [sleep 50 [entattr @pickattr (* @s @muts)]]
                    i = (<< $i 1)
                ]
            ]
            guistrut 2
            guilist [
                loop i 3 [
                    guistrut 1
                    guitext (format "gsp%1:" (+ 1 $i))
                    loop j $modeidxnum [
                        m = (gspmutname $j $i)
                        if (stringlen $m) [guitext (format "%1-%2" $m  (at $modename $j))]
                    ]
                ]
            ]
        ]
        guistrut 0.5
        guilist [
            guibutton "^fyclear" [entattr @pickattr 0; muts = 0]
            guispring 1
            guibutton "^fgdone" [cleargui 1]
        ]
    ] ]

    setdir = [entattr 1 (+ $offset (* 32 $i) (* 3 $shape) $axis)]
    newgui particledir [ guistayopen [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        if (= 0 $guipasses) [
            t = (entattr 0)
            hex = (entattr 3)
            dir = (mod (entattr 1) 64)
            if (> $dir 32) [
                i = 1
                dir = (- $dir 32)
    
            ] [
                i = 0
            ] 
            axis = (mod $dir 3)
            shape = (div $dir 3)
            offset = 256
        ]
        guilist [
            guilist [
                guitext "type"
                guistrut 0.5
                looplist j [4 7 8 9 10 11 12 13] [
                    guiradio (getentattr 5 0 $j) t $j [entattr 0 @j]
                ]
            ]
            guistrut 2
            guilist [
                guitext "shape"
                guistrut 0.5
                guiradio circle shape 0 setdir
                guiradio cylinder shape 1 setdir
                guiradio cone-shell shape 2 setdir
                guiradio cone-shell-2 shape 3 setdir
                guiradio plane-volume shape 4 setdir
                guiradio line-volume shape 5 setdir
                guiradio line-volume-2 shape 6 setdir
                guiradio sphere shape 7 setdir
                guiradio plane-flat shape 8 setdir
            ]
            guistrut 2
            guilist [
                guitext "direction"
                guistrut 0.5
                guiradio x-axis axis 0 setdir
                guiradio y-axis axis 1 setdir
                guiradio z-axis axis 2 setdir
                guistrut 1
                guicheckbox inverted i 1 0 setdir
            ]
            //guistrut 2
            //guilist [
            //    guitext "test offset"
            //    loop j 8 [
            //        guiradio (* 64 $j) offset (* 64 $j) setdir
            //    ]
            //]
        ]
    ] ]

    newgui entcolour [ guistayopen [
        if (= $guipasses 0) [hex = (entattr $pickattr)]
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        guitext (format "pick a colour for attribute %1 of the selected %2" $pickattr (enttype))
        guistrut 1
        guilist [
            guihexpreview $hex hex-value
            guistrut 1
            guilist [guirgbsliders hex]
            guistrut 2
            guibutton "apply this colour" [entattr @pickattr @hex] -1 $editingtex
        ]
        guipalette 0.6
    ] ]

    newgui entcolours [ guistayopen [
        if (= $guipasses 0) [
            hex = (+ (<< (entattr (- $pickattr 1)) 16) ( << (entattr $pickattr) 8) (entattr (+ $pickattr 1)))
        ]
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        guitext (format "pick a colour for attributes %1, %2 and %3 of the selected %4" (- $pickattr 1) $pickattr (+ $pickattr 1) (enttype))
        guistrut 1
        guilist [
            guihexpreview $hex hex-value
            guistrut 1
            guilist [guirgbsliders hex]
            guistrut 2
            guibutton "apply this colour" [
                entattr (- $pickattr 1) @colr
                entattr $pickattr  @colg
                entattr (+ $pickattr 1) @colb
            ] -1 $editingtex
        ]
        guipalette 0.6
    ] ]


    newgui entpalette [ 
        if (= 0 $guipasses) [tb = 0; wb = 0; p = (+ $pickattr 1)]
        guistrut 1
        guitext (format "pick a palette for attributes %1 and %2 of the selected %3" $pickattr (+ $pickattr 1) (enttype))
        guistrut 1
    	guilist [
    	    guilist [
                guitext "disabled"
    	        guiimage $warningtex [entattr $pickattr 0; entattr $p 0] 1.5 1 
    	    ]
            guispring 1
    	    guilist [
                guitext "flashing colours"
    	        guilist [
    	            guiimage $burningtex [entattr $pickattr 0; entattr $p  1] 1.5 1 
    	            guiimage $modekaboomtex [entattr $pickattr 0; entattr $p 2] 1.5 1 
    	            guiimage $bombtex [entattr $pickattr 0; entattr $p 3] 1.5 1 
    	            guiimage $shockingtex [entattr $pickattr 0; entattr $p 4] 1.5 1 
    	        ]
                guistrut 1
    	    ]
            guispring 1
    	    guilist [
                guitext "team colour variables"
    	        guilist [
    	            guiimage $modeffatex [entattr $pickattr 1; entattr $p 0] 1.5 1 
                    i = 1
                    looplist p [alpha omega kappa sigma ] [
    	                guiimage $[team@[p]tex] [entattr $pickattr 1; entattr $p (+ @i $tb)] 1.5 1 "" [] $[team@[p]colour]
                        i = (+ 1 $i)
    	            ]
    	        ]
                guicheckbox "only applied in team games" tb 0 5
    	    ]
    	]
        guistrut 1
        guitext "weapon colour variables"
    	guilist [
    	    loop i $weapidxnum [
                w = (at $weapname $i)
    	        guiimage $[@[w]tex] [entattr $pickattr 2 ; entattr $p (+ @i $wb)] 1.5 1 "" [] $[@[w]colour]
    	    ]
    	]
        guicheckbox "only applied if the weapon is allowed to spawn" wb 0 $weapidxnum
    ] 

    newgui sounds [ guistayopen [
        if (= 0 $guipasses) [
            mapsounds = ""
            imax = (getsound 1)
            loop i $imax [
                append mapsounds (getsound 1 $i 3)
            ] 
        ] 
        if (imax) [
            guitext "^fapick a sound entity to create"
            guistrut 1
            guiloopscrollbar j $imax 15 40 [
                p = (at $mapsounds $j)
                guieditbutton $p [newent sound @j] 
            ]
        ] [
            guitext "no sounds found for this map configuration"
        ]
    ] ]

    newgui soundtest [ guistayopen [
        if (= 0 $guipasses) [
            sounds = ""
            imax = (getsound 0)
            loop i $imax [
                append sounds (getsound 0 $i 3)
            ] 
        ] 
        if (imax) [
            guibutton "test the /sound command"
            guibutton "show sound debugger" [showgui cursounds]
            guistrut 1
            guiloopscrollbar j $imax 15 30 [
                p = (format "%1^t%2" $j (at $sounds $j))
                guibutton $p [sound @j] [] $arrowrighttex
            ]
        ] [
            guitext "could not load sound data"
        ] 
    ] ]

    mapmodelprevs = 1
    newgui mapmodels [ guistayopen [
        if (= 0 $guipasses) [
            slider = 0
            imax = (mapmodelindex)
        ] 
        if (imax) [
            guitext "^fapick a mapmodel entity to create"
            guistrut 0.5
            guicheckbox "show a preview" mapmodelprevs
            guistrut 0.5
            guilist [
                guilist [
                    guiloopscrollbar j $imax 15 25 [
                        p = (mapmodelindex $j)
                        guibutton $p [newent mapmodel @j] 
                    ]
                ]
                if $mapmodelprevs [guinocursorfx [guimodelpreview $guirollovername "mapmodel" [] 7.5 1]]
            ]
        ] [
            guitext "no mapmodels found for this map configuration"
        ]
    ] ]

    newgui lights [ guistayopen [
        if (= 0 $guipasses) [ hex = 0xffffff; rad = 64 ]
        guistrut 1
        guicenter [guitext "pick a colour for creating light or sunlight presets"]
        guistrut 1
        guilist [
            guispring 1
            guilist [
                guilist [
                    guihexpreview $hex colour
                ]
                guistrut 1
                guieditbutton "create lightfx" [newent lightfx]
                guieditbutton "link ents" [entlink]
            ]
            guistrut 1
            guilist [
                guirgbsliders hex
                guistrut 1
                guieditbutton "^tquick lightmap calculation" [quicklight]
                guieditbutton "^tfull lightmap calculation" [fullbright 0; calclight 1]
            ]
            guistrut 1
            guilist [
                guitext "create entitites:"
                guieditbutton "tiny light" [newent light 32 @colr @colg @colb]
                guieditbutton "small light" [newent light 64 @colr @colg @colb]
                guieditbutton "medium light" [newent light 128 @colr @colg @colb]
                guieditbutton "large light" [newent light 256 @colr @colg @colb]
                guieditbutton "huge light" [newent light 512 @colr @colg @colb]
            ]
            guistrut 2
            guilist [
                guistrut 1
                guieditbutton "sunlight top" [newent sunlight 0 0 @colr @colg @colb 45]
                guieditbutton "sunlight north" [newent sunlight 0 30 @colr @colg @colb 45]
                guieditbutton "sunlight east" [newent sunlight 90 30 @colr @colg @colb 45]
                guieditbutton "sunlight south" [newent sunlight 180 30 @colr @colg @colb 45]
                guieditbutton "sunlight west" [newent sunlight 270 30 @colr @colg @colb 45]
            ]
        ]
        guistrut 1
        guipalette 0.6
        guistatus [Tip: Create lightfx first and then lights to link them quickly]
    ] ]

    newgui serverpriv [
        guistayopen [
            guiradio   "veto server"               openservertype 0   [serveropen 3]
            guiradio   "open server"               openservertype 1   [serveropen 1]
            guiradio   "coop server"               openservertype 2   [serveropen 2]
            guitext " "
            guibutton   "claim privileges"            "setpriv 1"
            guibutton   "relinquish privileges"         "setpriv 0"
            guitext " "
            guibutton   "administrator password"      "saycommand [/adminpass ]"
        ]
    ]

    newgui texreset [
        guilist [
            guilist [
            guieditbutton "^fyCLEAR ^fwall unused textures from map cfg" texturecull
            guieditbutton "^fyCLEAR ^fwunused textures and load the default packages" texturerehash
            guieditbutton "^frCLEAR ^fwall textures from map cfg" texturereset
            ]
        ]
    ]

    matmenu = [
        guieditcheckbox "show material volumes" showmat
        guistrut 1
        looplist mat [air alpha water lava clip noclip aiclip death hurt ladder] [
            guieditbutton $mat [editmat @mat]
        ]
        guistrut 1
        guibar
        guistrut 0.5
        guicenter [guitext "Note: Use of glass is not recommended"]
        guicenter [guitext "create transparent geometry with alpha"]
    ]

    newgui materials [
        @matmenu
        guitab "^f(textures/editing)"
        guicenter [guitext "customize colour, fog and specular"]
        guistrut 0.5
        local var
        looplist i "water lava glass" [
            looplist j ["" 2 3 4] [
                var = [@[i]@[j]]
                guilist [
                    guistrut 1.2 1
                    guibutton (tabify $var 2) $var
                    guibutton "colour" [ pickcolour @[var]colour ] [
                        saycommand /pickcolour @[var]colour
                    ] $editingtex $[@[var]colour]
                    if (!=s "glass" $i) [
                        guistrut 1
                        guifield @[var]fog 4
                    ]
                    if (=s "water" $i) [
                        guistrut 1
                        guifield @[var]spec 4
                    ]
                ]
            ]
        ]
        guitab "^f(textures/dead)"
        guicenter [guitext "customize obituaries"]
        guistrut 1
        looplist i [ fall death hurt water water2 water3 water4 lava lava2 lava3 lava4] [
            guilist [
                guitext $i
                guispring 1
                    guifield obit@i -22
            ]
        ]
    ]

