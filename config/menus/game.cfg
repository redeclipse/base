newgui team [
    guione [
        guifont "default" [ guicenter [ guitext "choose team" ] ]
        guistrut 1
        if (isspectator (getclientnum)) [
            guicenter [ guibutton (format "^fw^f(%1) unspectate" $playertex) [spectator 0] ]
        ] [
            guicenter [ guibutton (format "^fw^f(%1) spectate" $spectatortex) [spectator 1] ]
        ]
        if (&& [>= (gamemode) $modeidxstart] [! (& (mutators) $mutsbitcoop)] (|| [! (& (mutators) $mutsbitffa)] [& (mutators) $mutsbitmulti])) [
            guistrut 1
            loop t (? (& (mutators) $mutsbitmulti) 4 2) [
                push t (at "alpha omega kappa sigma" $t) [
                    guicenter [ guibutton (format "^f[%1]^f(%2) %3" $[team@[t]colour] $[team@[t]tex] $[team@[t]name]) [team @t] ]
                ]
            ]
        ]
        guistrut 1
        guifont "default" [ guicenter [ guibutton "^focancel" [cleargui 1] [] "action" ] ]
    ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui team"))
]

cnsel = (getclientnum)
weaptex = "textures/question"
loop i $weapidxloadout [append (format "textures/weapons/%1" $i)]
showbots = 0
client_tab = [
    guilist [
        guilist [
            guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
            guistrut 0.2
            guilist [
                guistrut 0.2
                guicheckbox "include bots" showbots
                guistrut 0.6
            ]
            guistrut 0.5
            if (= $showbots 1) [
                cnlist = (listclients 1)
            ] [
                cnlist = (listdel (listclients 1) (listclients 0 2))
            ]
                loop i 5 [
                    looplist cn ($cnlist)  [
                        if (= (getclientteam $cn) $i) [guilist [guistrut 0.2; guiradio (getclientname $cn 1) cnsel $cn; guistrut 0.4]]
                    ]
                ]
            guistrut 0.3
        ]

        clempty = (! (getclientpresence $cnsel))
        clname = (? $clempty "empty" (getclientname $cnsel))
        clcname = (? $clempty "empty" (getclientname $cnsel 1))
        clmodel = (? $clempty 0 (getclientmodel $cnsel))
        clcolour = (? $clempty 0 (getclientcolour $cnsel))
        clteam = (? $clempty 0 (getclientteam $cnsel))
        clweap = (? $clempty 0 (getclientweapselect $cnsel))
        clvanity = (? $clempty "" (getclientvanity $cnsel))
        cltab = (format "\f[%1]%2" (? $clempty 0x111111 $clcolour) $cnsel)
        guistrut 2
        guilist [
            guifont play [guitext $clcname]
            guinohitfx [ guiplayerpreview $clmodel $clcolour $clteam $clweap $clvanity [] 7.5 1 1]
            row = 0
            itemp = $maxcarry    //prepare while; itemp: remaining weapons
            while [> $itemp 0] [
                guicenter [ guilist [ guilist [
                    loop i (if (> $itemp 4) [result 4] [result $itemp]) [
                        wi = (getclientloadweap $cnsel (+ $i (* $row 4)))
                        if (> $wi 0) [
                            wi = (at $weapname $wi)
                            guinohitfx [guiimage textures/weapons/@[wi] [] 1.85 1 "" [] $[@[wi]colour] ]
                        ] [
                            guiimage textures/question [] 1.85 1 "" [] 0xaaaaaa
                        ]
                    ]
                ] ] ]
                row = (+ $row 1)
                itemp = (- $itemp 4)
            ]
        ]
        guistrut 1
        guilist [
            guistrut 0.6

            guilist [ guibackground [0x202020] $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1; guistrut 0.5; guilist [
                guistrut 0.2
                guilist [ guitext "Handle" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclienthandle $cnsel)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no handle" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "IP" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclienthost $cnsel)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "Clientnumber" [] [0xDD9999]; guistrut 0.4 ]
                content = $cnsel
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "RE version" [] [0xDD9999]; guistrut 0.4 ]
                content = (stringreplace (loopconcat i 3 [getclientversion $cnsel $i]) " " ".")
                guilist [ guistrut 1; if $content [guitext $content [] -1 -1 600] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "Game version" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclientversion $cnsel 3)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "Platform/ Architecture" [] [0xDD9999]; guistrut 0.4 ]
                content = (
                    case (getclientversion $cnsel 4
                        ) 0 [ result "Windows"
                        ] 1 [ result "OS X"
                        ] 2 [ result "Unix-like"
                    ]
                )
                content = (concat $content (concatword (getclientversion $cnsel 5) "bit"))
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "OpenGL version/ OpenGL shader version" [] [0xDD9999]; guistrut 0.4 ]
                content = (concatword (getclientversion $cnsel 6) "/ " (getclientversion $cnsel 7) )
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "CRC" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclientversion $cnsel 8)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "GPU vendor" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclientversion $cnsel 9)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "GPU" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclientversion $cnsel 10)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "GPU version" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclientversion $cnsel 11)
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guistrut 0.2
            ] ]
        ]
        guistrut 2
        guilist [
            guilist [ guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1; guistrut 0.5; guilist [
                guilist [ guitext "Chat" [] [0x9999DD]; guistrut 0.4 ]
                guilist [ guistrut 0.7; guibutton "Paste name to chat" [saycommand  (getclientname $cnsel)] [] "textures/chat"; guistrut 2] 
                guilist [ guistrut 0.7; guibutton "whisper   " [saycommand  /whisper $cnsel  ] [] "textures/chat" ]
                if (! (& (mutators) $mutsbitffa)) [
                    guitext "Change team" [] [0x9999DD]
                    guilist [ guistrut 0.7; guibutton (format "^f[%1]^f(%2) " $teamalphacolour $teamalphatex) [saycommand /setteam $cnsel alpha] ]
                    guilist [ guistrut 0.7; guibutton (format "^f[%1]^f(%2) " $teamomegacolour $teamomegatex) [saycommand /setteam $cnsel omega] ]
                    if (& (mutators) $mutsbitmulti )[
                        guilist [ guistrut 0.7; guibutton (format "^f[%1]^f(%2) " $teamkappacolour $teamkappatex) [saycommand /setteam $cnsel kappa] ]
                        guilist [ guistrut 0.7; guibutton (format "^f[%1]^f(%2) " $teamsigmacolour $teamsigmatex) [saycommand /setteam $cnsel sigma] ]
                    ]
                ]
                guitext "Moderation" [] [0x9999DD]
                if (! (& (mutators) $mutsbitffa)) [
                    guilist [ guistrut 0.7; guibutton  "limit" [saycommand /limit $cnsel] [] $warningtex ]
                ]
                guilist [ guistrut 0.7; guibutton "mute!   " [saycommand /mute $cnsel ] [] $chattex ]
                guilist [ guistrut 0.7; guibutton "kick!   " [saycommand /kick $cnsel ] [] $meleetex ]
                guilist [ guistrut 0.7; guibutton "ban!    " [saycommand /ban $cnsel ] [] $warningtex ]
                guistrut 0.3
            ] ]
            guistrut 0.8
            guilist [ guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1; guistrut 0.5; guilist [
                guistrut 0.2
                guilist [ guitext "Playermodel" [] [0xDD9999]; guistrut 0.4 ]
                content = (getclientmodel $cnsel)
                content = (
                    case (getclientmodel $cnsel
                        ) 0 [ result "Male"
                        ] 1 [ result "Female"
                    ]
                )
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                guilist [ guitext "Colour" [] [0xDD9999]; guistrut 0.4 ]
                content = (hexcolour (getclientcolour $cnsel))
                guilist [ guistrut 1; if $content [guitext $content] [guitext "no information available" [] [0xABABAB]]; guistrut 0.4 ]
                
                guilist [ guitext "Vanity items" [] [0xDD9999]; guistrut 0.4 ]
                content = $clvanity
                if $content [ //when there are vanities
                    if (> (listlen $content) 7) [ //when there are more then 7 vanities use a guislider
                        guilist [
                            guistrut 1
                            guilist [
                                loopcount = 0
                                looplist i (sublist $content $vanityindex 7) [ //lists all vanities
                                    guistrut
                                    guitext $i
                                    loopcount = (+ $loopcount 1)
                                    if (= $loopcount 7) [
                                        guistrut 0.4
                                    ]
                                ]
                            ]
                            guispring 1
                            guislider vanityindex 0 (- (listlen $content) 7) [] 1 1
                        ]
                        guistrut 0.2
                    ][ //less or 9 vanities
                        looplist i $content [
                            guilist  [
                                guistrut 1
                                guitext $i [] -1 -1 1000
                            ]
                        ]
                        guistrut 0.4
                    ]
                ][ //no vanity
                    guilist [
                        guistrut 1
                        guitext "no vanities" [] [0xABABAB]
                    ]
                    guistrut 0.4
                ]
                guistrut 20.4 1    //making so width as the box upper
            ] ]
        ]
    ]
]
overview_tab = [
guicenter [ guilist [
    guistrut 0.4
    guibackground $guifieldbgcolour $guifieldbgblend $guifieldbgcolour $guifieldbgblend 1
    guistrut 0.4
        guilist [
            guistrut 0.4
            loop icolumn 6 [
                guilist [
                    switch = 0
                    loop i 5 [
                        looplist cn (listdel (listclients 1) (listclients 0 2))  [
                            if (= (getclientteam $cn) $i) [
                                if (= $switch 0) [rowcolour = "0x303030"; switch = 1] [rowcolour = "0x252525"; switch = 0]
                                case ($icolumn
                                    ) 1 [
                                        guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                            content = (getclientname $cn 1)
                                            guistrut 0.1; if $content [guitext $content] [guitext "no information avaible" [] [0x444444]]; guistrut 0.1
                                        ] ]
                                    ] 2 [
                                        guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                            content = (getclienthandle $cn)
                                            guistrut 0.1; if $content [guitext $content] [guitext "no handle" [] [0x444444]]; guistrut 0.1
                                        ] ]    
                                    ] 3 [
                                        guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                            content = (getclienthost $cn)
                                            guistrut 0.1; if $content [guitext $content] [guitext "no information avaible" [] [0x444444]]; guistrut 0.1
                                        ] ]
                                    ] 4 [
                                        guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                            content = (stringreplace (loopconcat i 3 [getclientversion $cn $i]) " " ".")
                                            guistrut 0.1; if $content [guitext $content] [guitext "no information avaible" [] [0x444444]]; guistrut 0.1
                                        ] ]
                                    ] 5 [
                                        guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                            content = (
                                                case (getclientversion $cn 4
                                                    ) 0 [ result "Windows"
                                                    ] 1 [ result "OS X"
                                                    ] 2 [ result "Unix-like"
                                                ]
                                            )
                                            content = (concat $content (concatword (getclientversion $cn 5) "bit"))
                                            guistrut 0.1; if $content [guitext $content] [guitext "no information avaible" [] [0x444444]]; guistrut 0.1
                                        ] ]
                                    ] 6 [
                                    loop i $maxcarry [
                                        wi = (getclientloadweap $cn $i)
                                        if (> $wi 0) [
                                            guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                                guistrut 0.1;
                                                wi = (at $weapname $wi)
                                                guinohitfx [guiimage textures/weapons/@[wi] [] 0.6 0 "" [] $[@[wi]colour] ]
                                                guistrut 0.1
                                            ] ]
                                        ] [                                    
                                            guilist [ guistrut 0.4; guibackground $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; guistrut 0.4; guilist [
                                                guistrut 0.1;
                                                guiimage textures/question [] 0.6 0 "" [] 0xaaaaaa
                                                guistrut 0.1
                                            ] ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
            guistrut 1.4
        ]
    ] ]
]
newgui clients [
    guiheader "Clients"
        [client_tab]
    guitab "Overview"
        [overview_tab]
]
