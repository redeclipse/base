varsearchstr = ""
varscount = 30
varindex = 0
varnum = -1
vartypes = 0
varnotypes = 56
varflags = 0
varnoflags = 128

newgui vars [
    numvars = (getvarinfo -1 $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
    if (>= $varnum $numvars) [ varnum = -1 ]
    guilist [
        guifield varsearchstrval 32 [varsearchstr = $varsearchstrval; varnum = -1; varindex = 0] -1 0 "" 0 "^fd<enter search terms>" 1
        guispring 1
        guilist [
            guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend
            guilist [
                guistrut 1
                guitext (format "^fg%1 ^fa%2 found" $numvars (? (= $numvars 1) "variable" "variables"))
                guistrut 1
            ]
        ]
    ]
    guistrut 0.5
    guilist [
        guilistx 2 [
            guifont "little" [
                guicontainer [1] [
                    guilist [
                        guilist [
                            //guistrut $varscount 1
                            varindex = (min (max 0 (- $numvars $varscount)) $varindex) //safeguard
                            varnum = (min $varnum $numvars)
                            guilist [
                                guistrut 39 1
                                break = 0
                                loopwhile i $varscount [= $break 0] [
                                    q = (+ $varindex $i)
                                    if (< $q $numvars) [
                                        curvar = (getvarinfo $q $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
                                        hilvar = (strreplace $curvar $varsearchstr (format "^fs^fy%1^fS" $varsearchstr))
                                        guilist [ guiradio $hilvar varnum $q ]
                                    ] [ guistrut 1 ]
                                ]
                            ]
                        ]
                    ]
                    guislider varindex 0 (max (- $numvars $varscount) 0) [] 1 1
                ] [
                    guistrut 43.25
                    //guistrut (+f $varscount 1) 1
                ]
            ]
        ]
        guistrut 1
        guilist [
            guistrut 59 1
            if (&& (>= $varnum 0) (< $varnum $numvars) (> $numvars 0)) [
                scurvar = (getvarinfo $varnum $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
                scurvartype = (getvartype $scurvar)
                guifont "emphasis" [ guitext $scurvar ]
                guilist [
                    case $scurvartype 0 [
                        guitext "^fainteger"
                        guispring 1
                        guitext (format "^famin: ^fw%1" (getvarmin $scurvar))
                        guispring 1
                        guitext (format "^famax: ^fw%1" (getvarmax $scurvar))
                        guispring 1
                        guitext (format "^fadefault: ^fw%1" (getvardef $scurvar 1))
                        if (= 0xffffff (getvarmax $scurvar)) [
                            guispring 1
                            guitext "preview: " "" $$scurvar 
                            guilist [
                                guibackground 0xcccccc
                                guitext (hexcolour $$scurvar) "" $$scurvar 
                            ]
                        ] 
                    ] 1 [
                        guitext "^fafloat"
                        guispring 1
                        guitext (format "^famin: ^fw%1" (getfvarmin $scurvar))
                        guispring 1
                        guitext (format "^famax: ^fw%1" (getfvarmax $scurvar))
                        guispring 1
                        guitext (format "^fadefault: ^fw%1" (getfvardef $scurvar 1))
                    ] 2 [
                        guitext "^fastring"
                    ] 3 [
                        guitext "^facommand"
                    ] 4 [
                        guitext "^faalias"
                    ] 5 [
                        guitext "^falocal"
                    ] () [
                        guitext "^fa???"
                    ]
                ]
                if (= $scurvartype 2) [
                    guistrut 0.25
                    guilist [
                        guitext "^fadefault"
                        guispring 1
                        guieditor [@[scurvar]_vardef] -58 8 4 -1 0 "" (getsvardef $scurvar 1)
                    ]
                ]
                guistrut 0.25
                guilist [
                    guitext "value"
                    guispring 1
                    [@[scurvar]_varval] = $$scurvar
                    guifield [@[scurvar]_varval] -58 [@scurvar $[@@[scurvar]_varval]] -1 0 "" (? (|| (= $scurvartype 2) (= $scurvartype 4)) 8 1)
                ]
                scurusage = (getvarusage $scurvar)
                if (strlen $scurusage) [
                    guistrut 0.25
                    guilist [
                        guitext "^fausage"
                        guispring 1
                        guitext $scurusage "" -1 -1 1220
                    ]
                ]
                scurdesc = (getvardesc $scurvar)
                if (strlen $scurdesc) [
                    guistrut 0.25
                    guilist [
                        guitext "^fainfo"
                        guispring 1
                        guitext $scurdesc "" -1 -1 1220
                    ]
                ]
            ] [
                guifont "emphasis" [ guitext "no variable selected" ]
                guifont "little" [
                    guitext "^fause the list on the left to pick a variable" point
                    guitext "^fause the search field at the top to narrow results" point
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        varindex = 0
        varnum = -1
    ]
]

cursoundnames = [ "xv" "cv" "cp" "" "xr" "mr" "" "" "" "ss" "" "" "" "" "" "m" "" "c" "cn" ]
cursoundwidth = [ 4 4 4 0 4 4 0 0 0 3 0 0 0 0 0 2 0 2 3 ]
newgui cursounds [
    cursoundnum = (getcursound)
    guilist [
        guilist [ guistrut 3 1 ]
        cursoundprop = (listlen $cursoundnames)
        loop j $cursoundprop [
            cursoundw = (at $cursoundwidth $j)
            if $cursoundw [
                cursoundval = (at $cursoundnames $j)
                guilist [
                    guistrut $cursoundw 1
                    guicenter [ guitext (format "^fy%1" $cursoundval) ]
                ]
            ]
        ]
        guilist [ guistrut 30 1 ]
    ]
    loop i $cursoundnum [
        guilist [
            guilist [
                guistrut 3 1
                guilist [ guitext (format "^fg%1." $i) ]
            ]
            cursoundplay = (getcursound $i 14)
            if $cursoundplay [ 
                cursoundprop = (getcursound $i)
                loop j $cursoundprop [
                    cursoundw = (at $cursoundwidth $j)
                    if $cursoundw [
                        cursoundval = (getcursound $i $j)
                        guilist [
                            guistrut $cursoundw 1
                            guicenter [ guitext $cursoundval ]
                        ]
                    ]
                ]
                guilist [
                    guistrut 30 1
                    guilist [
                        guispring
                        guitext (getsound (getcursound $i 15) (getcursound $i 9) 3)
                    ]
                ]
            ] [
                guispring
                guitext "-"
            ]
        ]
    ]
]
