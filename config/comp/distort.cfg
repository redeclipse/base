compositedistort = [
    shader $SHADER_DEFAULT (concatword "compositedistort" $arg1) [
        attribute vec4 vvertex, vcolor;
        attribute vec2 vtexcoord0;
        uniform mat4 hudmatrix;
        varying vec2 texcoord0;
        varying vec4 colorscale;

        void main(void)
        {
            gl_Position = hudmatrix * vvertex;
            texcoord0 = vtexcoord0;
            colorscale = vcolor;
        }
    ] [
        @(? (< (strstr $arg1 "flare") 0) [
            uniform sampler2D tex0;
        ])
        uniform vec3 params; // frequency amplitude speed
        uniform vec2 factor; // xfactor yfactor
        uniform vec3 tweaks; // feather-x feather-y noise
        uniform vec3 sparkle; // frequency intensity speed
        uniform vec4 curmillis;
        varying vec2 texcoord0;
        varying vec4 colorscale;

        fragdata(0) vec4 fragcolor;

        float rand(vec2 co) { return fract(sin(dot(co.xy, vec2(12.9898, 78.233)))*43758.5453); }

        void main()
        {
            @(if (>= (strstr $arg1 "flare") 0) [result [
                vec4 diffuse = vec4(0.55 - distance(texcoord0.y, 0.5));
            ]] [result [
                vec2 ncoord = texcoord0;

                if(params.x != 0.0 && params.y != 0.0 && params.z != 0.0)
                {
                    vec2 pulse = sin(curmillis.x * params.z - params.x * texcoord0);
                    float dist = 2.0 * length(texcoord0.y - 0.5);

                    ncoord = mix(texcoord0 + params.y * (pulse.yx * factor), texcoord0, dist);
                }

                vec4 diffuse = texture2D(tex0, ncoord);
            ]])

            if(diffuse.a <= 0.0) discard;

            if(tweaks.x != 0.0)
            {
                float offset = texcoord0.x < tweaks.x ? texcoord0.x : 1.0 - texcoord0.x;
                if(offset < tweaks.x) diffuse *= offset / tweaks.x;
            }

            if(tweaks.y != 0.0)
            {
                float offset = texcoord0.y < tweaks.y ? texcoord0.y : 1.0 - texcoord0.y;
                if(offset < tweaks.y) diffuse *= offset / tweaks.y;
            }

            if(tweaks.z != 0.0)
            {
                float noise = rand(vec2(texcoord0.x - (rand(vec2(curmillis.x)) - 0.5) / 8.0, texcoord0.y + (rand(vec2(curmillis.x)) - 0.5) / 16.0));

                @(caseif (>= (strstr $arg1 "flare") 0) [result [
                    float blend = 1.0 - distance(texcoord0.y, 0.5);
                ]] (>= (strstr $arg1 "part") 0) [result [
                    float blend = diffuse.r;
                ]] () [result [
                    float blend = diffuse.a;
                ]])
                diffuse.rgb = mix(diffuse.rgb, vec3(noise), blend * tweaks.z);
            }

            if(sparkle.x != 0.0 && sparkle.y != 0.0 && sparkle.z != 0.0)
            {
                float noise = rand(vec2(texcoord0.x - (rand(vec2(curmillis.x)) - 0.5) / 8.0, texcoord0.y + (rand(vec2(curmillis.x)) - 0.5) / 16.0));
                if(noise > sparkle.y) diffuse = mix(diffuse, vec4(noise * sparkle.z), 1.0 - diffuse.a);
            }

            if(diffuse.a <= 0.0) discard;

            @(if (>= (strstr $arg1 "colour") 0) [result [
                fragcolor = diffuse * colorscale;
            ]] [result [
                fragcolor.rg = diffuse.ra * colorscale.ra;
            ]])
        }
    ] 1

    newui (concatword "distort" $arg1) $SURFACE_COMPOSITE [
        @(localargs params [64.0 0.05 8.0])
        @(localargs factor [0.125 0.5])
        @(localargs tweaks [0.0 0.0 0.125])
        @(localargs sparkle [20.0 0.5 1.0])
        @(localdef tex "textures/icon")
        execid uiarg1
        uirender @(concatword "compositedistort" $arg1) 1 1 [
            uirenderparam tweaks $tweaks1 $tweaks2 $tweaks3
            uirenderparam sparkle $sparkle1 $sparkle2 $sparkle3
            uirenderparam params $params1 $params2 $params3
            uirenderparam factor $factor1 $factor2
            @(if (< (strstr $arg1 "flare") 0) [result [
                uirendertex $tex
            ]])
        ]
    ]

    newui (concatword "distort" $arg1 "light") $SURFACE_COMPOSITE [
        @(localargs params [64.0 0.05 8.0])
        @(localargs factor [0.025 0.1])
        @(localargs tweaks [0.0 0.0 0.125])
        @(localargs sparkle [20.0 0.5 1.0])
        @(localdef tex "textures/icon")
        execid uiarg1
        uirender @(concatword "compositedistort" $arg1) 1 1 [
            uirenderparam tweaks $tweaks1 $tweaks2 $tweaks3
            uirenderparam sparkle $sparkle1 $sparkle2 $sparkle3
            uirenderparam params $params1 $params2 $params3
            uirenderparam factor $factor1 $factor2
            @(if (< (strstr $arg1 "flare") 0) [result [
                uirendertex $tex
            ]])
        ]
    ]

    newui (concatword "distort" $arg1 "tape") $SURFACE_COMPOSITE [
        @(localargs params [0.0 0.0 0.0])
        @(localargs factor [0.0 1.0])
        @(localargs tweaks [0.0 0.0 0.125])
        @(localargs sparkle [20.0 0.5 1.0])
        @(localdef tex "textures/icon")
        execid uiarg1
        uirender @(concatword "compositedistort" $arg1) 1 1 [
            uirenderparam tweaks $tweaks1 $tweaks2 $tweaks3
            uirenderparam sparkle $sparkle1 $sparkle2 $sparkle3
            uirenderparam params $params1 $params2 $params3
            uirenderparam factor $factor1 $factor2
            @(if (< (strstr $arg1 "flare") 0) [result [
                uirendertex $tex
            ]])
        ]
    ]
]

compositedistort
compositedistort "part"
compositedistort "flare"
compositedistort "colour"
compositedistort "colourpart"
compositedistort "colourflare"
