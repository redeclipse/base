newui scoreboard $SURFACE_COMPOSITE [
    @(localdef teamid 0)
    @(localdef fontname "dotmatrix/outline")
    @(localargs params [6.0 10.0 12.0])
    execid uiarg1
    if $teamid [
        params1 = (*f $params1 2.0)
        params2 = (*f $params2 2.0)
        params3 = (*f $params3 2.0)
    ]
    uistyle clampxy
    refreshscoreboard
    uifont $fontname [ uivlist 0 [
        uistyle clampxy
        uistyle centermiddle

        if (! $teamid) [ uifill 1.0 0.5 [
            uistyle centermiddle
            uivlist 0 [
                uistyle clampxy
                uistyle centermiddle
                uitext (gamename $gamemode $mutators 0 64) $params2 [uistyle centermiddle; uitextlimit 0.9]
                uitext (concat "[" (timestr (? $gametimeremain $gametimeremain $gametimeelapsed) $showtimestyle) "]") $params2 [uistyle centermiddle; uitextlimit 0.9]
            ]
        ] ]

        uihlist 0 [
            uistyle clampxy
            uistyle centermiddle
            local scoreboard_speedrun; scoreboard_speedrun = (= $gamemode $G_SPEEDRUN)
            local scoreboard_speedruntime; scoreboard_speedruntime = (&& $scoreboard_speedrun (! (hasmut $G_M_GSP1)))

            if (hasmut $G_M_FFA) [
                if $teamid [
                    uiimage $icontex 0xFFFFFF 0 1.0 1.0 [
                        uistyle centermiddle
                        uicolouradd 0x888888
                        uicolourblend 0.75
                    ]
                ] [
                    local scoreboard_count; scoreboard_count = (numscoregroups)
                    local scoreboard_output; scoreboard_output = 0
                    loopscoreboard scoreboard_player 0 [
                        if (< $scoreboard_output 2) [
                            local scoreboard_team; scoreboard_team = (getclientteam $scoreboard_player)
                            local scoreboard_colour1; scoreboard_colour1 = (modcolour (getclientcolour $scoreboard_player $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix) 0.4)
                            local scoreboard_colour2; scoreboard_colour2 = (modcolour (getteamcolour $scoreboard_team) 0.2)

                            uivlist 0 [
                                uistyle centermiddle

                                uitext (getclientname $scoreboard_player) $params1 [
                                    uistyle centermiddle
                                    uitextlimit 0.9
                                    uicolourset $scoreboard_colour1
                                ]

                                if $scoreboard_speedruntime [
                                    local scoreboard_cptime; scoreboard_cptime = (getclientcptime $scoreboard_player)
                                    uitext (? (> $scoreboard_cptime 0) (timestr $scoreboard_cptime $scorespeedrunstyle) "-") $params3 [
                                        uistyle centermiddle
                                        uitextlimit 0.9
                                    ]
                                ] [
                                    uitext (getclientpoints $scoreboard_player) $params3 [
                                        uistyle centermiddle
                                        uitextlimit 0.9
                                    ]
                                ]
                            ]
                            scoreboard_output = (+ $scoreboard_output 1)
                        ]
                    ]
                ]
            ] [
                loop scoreboard_id 2 [
                    scoreboard_check = (+ $scoreboard_id $T_ALPHA)

                    if (|| (! $teamid) (= $teamid $scoreboard_check)) [
                        loopscoregroups scoreboard_curgroup [] [] [
                            local scoreboard_team; scoreboard_team = (getscoreteam $scoreboard_curgroup)
                            if (= $scoreboard_check $scoreboard_team) [
                                local scoreboard_teamid; scoreboard_teamid = (at $T_NAMES $scoreboard_team)
                                local scoreboard_colour; scoreboard_colour = (getteamcolour $scoreboard_team)
                                local scoreboard_colour1; scoreboard_colour1 = (modcolour $scoreboard_colour 0.2)
                                local scoreboard_colour2; scoreboard_colour2 = (modcolour $scoreboard_colour 0.4)

                                uivlist 0 [
                                    uistyle centermiddle

                                    uitext $[team@[scoreboard_teamid]name] $params1 [
                                        uistyle centermiddle
                                        uitextlimit 0.9
                                        uicolourset $scoreboard_colour1
                                    ]

                                    local scoreboard_total; scoreboard_total = (getscoretotal $scoreboard_curgroup)
                                    if $scoreboard_speedruntime [
                                        uitext (? (> $scoreboard_total 0) (timestr $scoreboard_total $scorespeedrunstyle) "-") $params3 [
                                            uistyle centermiddle
                                            uitextlimit 0.9
                                        ]
                                    ] [
                                        uitext $scoreboard_total $params3 [
                                            uistyle centermiddle
                                            uitextlimit 0.9
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ] ]
]
