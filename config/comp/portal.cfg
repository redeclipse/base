shader $SHADER_DEFAULT compositeportal [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform mat4 hudmatrix;
    varying vec2 texcoord0;
    varying vec4 colorscale;

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        colorscale = vcolor;
    }
] [
    uniform vec4 curmillis;
    uniform vec4 size;   // xradius yradius blendstart lightenstart
    uniform vec4 params; // scale nsoffset nsscale iters
    uniform vec4 shade;  // colscale coloffset blendns blendnc
    uniform vec3 scroll; // xscroll yscroll
    uniform vec4 minmax; // blendmin blendmax colormin colormax
    varying vec2 texcoord0;
    varying vec4 colorscale;

    fragdata(0) vec4 fragcolor;

    const mat2 portalmat = mat2(1.6, 1.2, -1.2, 1.6);

    vec2 hash(vec2 p)
    {
        vec2 coord = vec2(dot(p, vec2(127.1, 311.7)), dot(p, vec2(269.5, 183.3)));
        return -1.0 + 2.0 * fract(sin(coord) * 43758.5453123);
    }

    float noise(vec2 p)
    {
        const float K1 = 0.366025404;
        const float K2 = 0.211324865;
        vec2 i = floor(p + (p.x + p.y) * K1);
        vec2 a = p - i + (i.x + i.y) * K2;
        vec2 o = (a.x > a.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
        vec2 b = a - o + K2;
        vec2 c = a - 1.0 + 2.0 * K2;
        vec3 h = max(0.5 - vec3(dot(a, a), dot(b, b), dot(c, c)), 0.0);
        vec3 n = h * h * h * h * vec3(dot(a, hash(i + 0.0)), dot(b, hash(i + o)), dot(c, hash(i + 1.0)));
        return dot(n, vec3(70.0));
    }

    float shape(vec2 p, float scale, float seed, float time, float wstart, float wamp, bool absolute)
    {
        float total = 0.0;
        vec2 coord = p * scale;
        coord -= (seed - time) * scroll.xy;
        float weight = wstart;
        for(int i = 0; i < params.w; i++)
        {
            total += absolute ? abs(weight * noise(coord)) : weight*noise(coord);
            coord = portalmat * coord + time * scroll.xy;
            weight *= wamp;
        }
        return total;
    }

    float fbm(vec2 p)
    {
        float total = 0.0, amplitude = 0.1;
        for(int i = 0; i < params.w; i++)
        {
            total += noise(p) * amplitude;
            p = portalmat * p;
            amplitude *= 0.4;
        }
        return total;
    }

    void main()
    {
        float dist = length((texcoord0 - vec2(0.5)) * size.xy) * 2.0;
        float blend = 1.0 - smoothstep(size.z, 1.0, dist);
        float lighten = smoothstep(size.w, 1.0, dist);

        float portalseed = fbm(texcoord0 * params.x * 0.5);
        float portalrs = shape(texcoord0, params.x, portalseed, curmillis.x + scroll.z, 0.8, 0.7, true);
        float portalns = shape(texcoord0, params.x, portalseed, curmillis.x + scroll.z, 0.7, 0.6, false);
        portalns *= portalrs + portalns;
        float portalnc = shape(texcoord0, params.x * 2.0, portalseed, (curmillis.x + scroll.z) * 2.0, 0.4, 0.6, false);
        float portalrc = shape(texcoord0, params.x * 3.0, portalseed, (curmillis.x + scroll.z) * 3.0, 0.4, 0.6, true);
        portalnc += portalrc;
        portalns = params.y + params.z * portalns * portalrs;

        float portalmix = clamp(shade.w + (portalns + portalnc) * shade.z, minmax.x, minmax.y) * blend;
        float portalcmb = clamp(shade.y * lighten + shade.x * portalnc, minmax.z, minmax.w);

        fragcolor.rg = vec2(portalcmb, portalmix) * colorscale.ra;
    }
]

newui portal $SURFACE_COMPOSITE [
    @(localargs size [1.5 1.0 0.5 0.125])
    @(localargs params [1.0 0.25 2.0 2.0])
    @(localargs shade [0.3 0.9 0.3 0.9])
    @(localargs scroll [0.0 0.1 1.0])
    @(localargs minmax [0.2 1.0 0.2 1.0])
    execid uiarg1
    uirender compositeportal 1.0 1.0 [
        uirenderparam size $size1 $size2 $size3 $size4
        uirenderparam params $params1 $params2 $params3 $params4
        uirenderparam shade $shade1 $shade2 $shade3 $shade4
        uirenderparam scroll $scroll1 $scroll2 $scroll3
        uirenderparam minmax $minmax1 $minmax2 $minmax3 $minmax4
    ]
]
