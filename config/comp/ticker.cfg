newui ticker $SURFACE_COMPOSITE [
    @(localdef speed 10000)
    @(localdef fontsize 5.0)
    @(localdef fontname "dotmatrix/outline")
    execid uiarg1

    local ticker_mod; ticker_mod = (divf (mod $lastmillis $speed) $speed)
    local ticker_idx; ticker_idx = (floor (divf (mod $lastmillis (mul $speed 4)) $speed))

    refreshscoreboard
    local ticker_race; ticker_race = (= $gamemode $G_RACE)
    local ticker_racetime; ticker_racetime = (&& $ticker_race (! (& $mutators (<< 1 $G_M_GSP1))))

    uifont $fontname [ uispace 0.01 0.01 [ uiclip 0.99 0.99 0 0 [
        uivlist 0 [ loop i 8 [
            local j; j = (mod (+ $i $ticker_idx) 4)
            uifill 6.0 0.12375 [ uihlist 0 [
                uispace 0.495
                case $j 0 [
                    uitext (concatword "Playing ^fs^fy" (gamename $gamemode $mutators 0 32) "^fS on ^fs^fo" (? (!=s $maptitle "") $maptitle (filename $mapname)) "^fS") $fontsize
                ] 1 [
                    uitext (? $gametimeremain (concatword "Match time remaining: ^fs^fg" (timestr $gametimeremain $showtimestyle) "^fS") (concatword "Time elapsed: ^fs^fg" (timestr $gametimeelapsed $showtimestyle) "^fS")) $fontsize
                ] 2 [
                    local ticker_place1; ticker_place1 = -1
                    local ticker_place2; ticker_place2 = -1

                    if (& $mutators (<< 1 $G_M_FFA)) [
                        loopscoreboard ticker_player 0 [
                            caseif (< $ticker_place1 0) [
                                ticker_place1 = $ticker_player
                            ] (< $ticker_place2 0) [
                                ticker_place2 = $ticker_player
                            ]
                        ]

                        caseif (< $ticker_place1 0) [
                            uitext "Waiting for players..." $fontsize
                        ] (< $ticker_place2 0) [
                            if $ticker_racetime [
                                local ticker_total1; ticker_total1 = (getclientcptime $ticker_place1)
                                if (> $ticker_total1 0) [
                                    uitext (concatword "Best lap for " (getclientname $ticker_place1) " is ^fs^fc" (timestr $ticker_total1 $scoreracestyle) "^fS") $fontsize
                                ] [
                                    uitext (concatword "Current lap for " (getclientname $ticker_player) " is ^fs^fc" (timestr (getclientcpmillis $ticker_place1) $scoreracestyle) "^fS") $fontsize
                                ]
                            ] [
                                uitext (concatword (getclientname $ticker_place1) " has a score of ^fs^fc" (getclientpoints $ticker_place1) "^fS") $fontsize
                            ]
                        ] (>= $ticker_place2 0) [
                            if $ticker_racetime [
                                local ticker_total1; ticker_total1 = (getclientcptime $ticker_place1)
                                if (> $ticker_total1 0) [
                                    local ticker_total2; ticker_total2 = (getclientcptime $ticker_place2)
                                    if (> $ticker_total2 0) [
                                        if (= $ticker_total1 $ticker_total2) [
                                            uitext (concatword (getclientname $ticker_place1) " and " (getclientname $ticker_place2) " are tied with a lap time of ^fs^fc" (timestr $ticker_total1 $scoreracestyle) "^fS") $fontsize
                                        ] [
                                            uitext (concatword (getclientname $ticker_place1) " is ahead of " (getclientname $ticker_place2) " by ^fs^fc" (timestr (- $ticker_total2 $ticker_total1) $scoreracestyle) "^fS") $fontsize
                                        ]
                                    ] [
                                        uitext "On your marks.. get set.. GO!" $fontsize
                                    ]
                                ] [
                                    uitext (concatword "Current lap for " (getclientname $ticker_place1) " is ^fs^fc" (timestr (getclientcpmillis $ticker_place1) $scoreracestyle) "^fS") $fontsize
                                ]
                            ] [
                                local ticker_total1; ticker_total1 = (getclientpoints $ticker_place1)
                                local ticker_total2; ticker_total2 = (getclientpoints $ticker_place2)
                                if (= $ticker_total1 $ticker_total2) [
                                    uitext (concatword (getclientname $ticker_place1) " and " (getclientname $ticker_place2) " are tied with a score of ^fs^fc" $ticker_total1 "^fS " (? (= $ticker_total1 1) "point" "points")) $fontsize
                                ] [
                                    local ticker_total; ticker_total = (- $ticker_total1 $ticker_total2)
                                    uitext (concatword (getclientname $ticker_place1) " is ahead of " (getclientname $ticker_place2) " by ^fs^fc" $ticker_total "^fS " (? (= $ticker_total 1) "point" "points")) $fontsize
                                ]
                            ]
                        ]
                    ] [
                        loopscoregroups ticker_curgroup [] [] [
                            if (getscoreteam $ticker_curgroup) [
                                caseif (< $ticker_place1 0) [
                                    ticker_place1 = $ticker_curgroup
                                ] (< $ticker_place2 0) [
                                    ticker_place2 = $ticker_curgroup
                                ]
                            ]
                        ]

                        caseif (< $ticker_place1 0) [
                            uitext "Waiting for players..." $fontsize
                        ] (< $ticker_place2 0) [
                            local ticker_team1; ticker_team1 = (getscoreteam $ticker_place1)
                            local ticker_total; ticker_total = (getscoretotal $ticker_place1)
                            if $ticker_racetime [
                                uitext (concatword "Best lap for " (getteamname $ticker_team1) " is ^fs^fc" (timestr $ticker_total $scoreracestyle) "^fS") $fontsize
                            ] [
                                uitext (concatword (getteamname $ticker_team1) " has a score of ^fs^fc" $ticker_total "^fS") $fontsize
                            ]
                        ] (>= $ticker_place2 0) [
                            local ticker_team1; ticker_team1 = (getscoreteam $ticker_place1)
                            local ticker_total1; ticker_total1 = (getscoretotal $ticker_place1)
                            local ticker_team2; ticker_team2 = (getscoreteam $ticker_place2)
                            local ticker_total2; ticker_total2 = (getscoretotal $ticker_place2)

                            if $ticker_racetime [
                                if (> $ticker_total1 0) [
                                    if (> $ticker_total2 0) [
                                        if (= $ticker_total1 $ticker_total2) [
                                            uitext (concatword (getteamname $ticker_team1) " and " (getteamname $ticker_team2) " are tied with a lap time of ^fs^fc" (timestr $ticker_total1 $scoreracestyle) "^fS") $fontsize
                                        ] [
                                            uitext (concatword (getteamname $ticker_team1) " is ahead of " (getteamname $ticker_team2) " by ^fs^fc" (timestr (- $ticker_total2 $ticker_total1) $scoreracestyle) "^fS") $fontsize
                                        ]
                                    ] [
                                        uitext (concatword "Best lap for " (getteamname $ticker_team1) " is ^fs^fc" (timestr $ticker_total1 $scoreracestyle) "^fS") $fontsize
                                    ]
                                ] [
                                    uitext "On your marks.. get set.. GO!" $fontsize
                                ]
                            ] [
                                if (= $ticker_total1 $ticker_total2) [
                                    uitext (concatword (getteamname $ticker_team1) " and " (getteamname $ticker_team2) " are tied with a score of ^fs^fc" $ticker_total1 "^fS " (? (= $ticker_total1 1) "point" "points")) $fontsize
                                ] [
                                    local ticker_total; ticker_total = (- $ticker_total1 $ticker_total2)
                                    uitext (concatword (getteamname $ticker_team1) " is ahead of " (getteamname $ticker_team2) " by ^fs^fc" $ticker_total "^fS " (? (= $ticker_total 1) "point" "points")) $fontsize
                                ]
                            ]
                        ]
                    ]
                ] 3 [
                    uitext $[tip_id_@(mrnd $speed $tip_num)] $fontsize
                ]
                uispace 0.495
            ] ]
        ] ]
        uiclipoffsetx (*f $ticker_mod $uiclipvirtw)
    ] ] ]
]

newui tickertv $SURFACE_COMPOSITE [
    uiclip 0.99 0.3155625 0.015 0.0255 [
        uiimage "<comp:1>ticker" 0xFFFFFFFF 0 1.5 2.55
    ]
]