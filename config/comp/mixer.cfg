shader $SHADER_DEFAULT "mixerconv" [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform mat4 hudmatrix;
    varying vec2 texcoord0;
    varying vec4 colorscale;

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        colorscale = vcolor;
    }
] [
    uniform sampler2D tex0;
    uniform float blurradius;
    uniform vec4 matsplit;
    varying vec2 texcoord0;
    varying vec4 colorscale;

    fragdata(0) vec4 fragcolor;

    const vec3 material1 = vec3(1.0, 0.0, 0.0);
    const vec3 material2 = vec3(0.0, 1.0, 0.0);
    const vec3 material3 = vec3(0.0, 0.0, 1.0);

    void main()
    {
        vec4 pattern = texture2D(tex0, texcoord0), diffuse = vec4(0.0);

        if(blurradius > 0.0)
        {
            pattern += texture2D(tex0, texcoord0 + vec2(0.0,            -blurradius));
            pattern += texture2D(tex0, texcoord0 + vec2(-blurradius,    0.0));

            pattern += texture2D(tex0, texcoord0 + vec2(0.0,            blurradius));
            pattern += texture2D(tex0, texcoord0 + vec2(blurradius,     0.0));

            pattern += texture2D(tex0, texcoord0 + vec2(-blurradius,    -blurradius));
            pattern += texture2D(tex0, texcoord0 + vec2(blurradius,     blurradius));

            pattern += texture2D(tex0, texcoord0 + vec2(-blurradius,    blurradius));
            pattern += texture2D(tex0, texcoord0 + vec2(blurradius,     -blurradius));

            pattern *= 0.11111112;
        }

        if(matsplit.x > 0.0)
        {
            if(pattern.r < matsplit.x)
                diffuse.rgb = mix(material3, material1, (matsplit.x - pattern.r) * matsplit.z); // 0   -> x   = 1 -> 0
            else if(pattern.r > matsplit.y)
                diffuse.rgb = mix(material3, material2, (pattern.r - matsplit.y) * matsplit.z); // y   -> 1   = 0 -> 1
            else diffuse.rgb = material3;
        }
        else diffuse.rgb = mix(material1, material2, smoothstep(0.0, 1.0, pattern.r));

        fragcolor = colorscale - diffuse;
    }
]

newui "mixerconv" $SURFACE_COMPOSITE [
    @(localdef split 0.0)
    @(localdef blur 0.0)
    @(localdef tex "<grey>textures/notexture")

    execid uiarg1

    local _matsplit1 _matsplit2 _matsplit3 _matsplit4
    if (>f $split 0.0) [
        _matsplit1 = (clampf $split 0.0 0.5)
        _matsplit2 = (-f 1.0 $_matsplit1)
        _matsplit3 = (divf 1.0 $_matsplit1)
        _matsplit4 = (divf 1.0 (*f (-f $_matsplit2 $_matsplit1) 0.5))
    ] [
        _matsplit1 = 0
        _matsplit2 = 0
        _matsplit3 = 0
        _matsplit4 = 0
    ]

    uirender "mixerconv" 1 1 [
        uirenderparam matsplit $_matsplit1 $_matsplit2 $_matsplit3 $_matsplit4
        uirenderparam blurradius $blur
        uirendertex $tex
    ]
]

shader $SHADER_DEFAULT "mixerview" [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform mat4 hudmatrix;
    varying vec2 texcoord0;
    varying vec4 colorscale;

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        colorscale = vcolor;
    }
] [
    uniform sampler2D tex0;
    uniform vec3 material1, material2, material3, material4;
    varying vec2 texcoord0;
    varying vec4 colorscale;

    fragdata(0) vec4 fragcolor;

    void main()
    {
        vec4 mixer = texture2D(tex0, texcoord0);
        vec3 mixerbuf = vec3(0.0);

        mixerbuf = mix(material1, mixerbuf, mixer.r);
        mixerbuf = mix(material2, mixerbuf, mixer.g);
        mixerbuf = mix(material3, mixerbuf, mixer.b);
        mixerbuf = mix(material4, mixerbuf, mixer.a);

        fragcolor = vec4(mixerbuf, 1.0) * colorscale;
    }
]
