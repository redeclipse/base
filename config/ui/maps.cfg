defsvarp mapfavs ""
defsvarp gamefavs ""

// cache these on start
ui_maps_cache = [
    ui_maps_cache_filelist = (listfiles maps mpz)
    looplist ui_maps_cache_lcurmap $ui_maps_cache_filelist [mapcscan [maps/@[ui_maps_cache_lcurmap]]]
    mapcsort
    ui_maps_cache_filelist = (listfiles demos dmo)
    looplist ui_maps_cache_lcurmap $ui_maps_cache_filelist [demoscan [demos/@[ui_maps_cache_lcurmap].dmo]]
    demosort
]
ui_maps_cache

ui_maps_init = [
    ui_maps_muts = 0
    ui_maps_mode = -1
    ui_maps_lastmode = -1
    ui_maps_lastmuts = 0
    ui_maps_reset = 1
    ui_maps_num = -1
    ui_maps_cur = ""
    ui_maps_file = ""
    ui_maps_list = ""
    ui_maps_paths = ""
    ui_maps_extra = ""
    ui_maps_img = $emblemtex
    ui_maps_modeimg = ""
    ui_maps_filter = 1
    //sizes for the UI to break maps up into recommended/other
    ui_maps_smallsize = (ceil (*f $sv_smallmapmax .66)) //take the "recommended" size to be two-thirds of the maximum server small map playercount (default 4)
    ui_maps_mediumsize = (ceil (*f $sv_mediummapmax .66))//ditto for medium maps (default 8)

    // Detect if only one mode is usable and preselect it if that is the case.
    // First, loop through all modes and count how many are unlocked.
    ui_maps_last_possible_mode = -1
    ui_maps_possible_modes = 0
    loop z $modeidxnum [
        // If unlocked, record as a candidate to preselect and add to count.
        if (! (ismodelocked $z)) [
            ui_maps_possible_modes = (+ $ui_maps_possible_modes 1)
            ui_maps_last_possible_mode = $z
        ]
    ]
    // If there is exactly one unlocked mode, preselect it.
    if (= $ui_maps_possible_modes 1) [
        ui_maps_mode = $ui_maps_last_possible_mode
    ]
]
//initialize vars
ui_maps_init
//maps list generator for the right-side hlist in maps menu
ui_maps_iter = [
    if (|| [!= $ui_maps_lastmode $ui_maps_mode] [!= $ui_maps_lastmuts $ui_maps_muts]) [ui_maps_reset = 1] //don't bother with the big code below unless one of the modes/muts have been changed
    if (= $ui_maps_reset 1) [
        ui_maps_cache //get maps list
        ui_maps_rot = 0
        ui_maps_list = ""
        ui_maps_paths = ""
        if (= $ui_maps_mode $modeidxdemo) [ //handling demos (which don't allow selecting a map)
            ui_maps_muts = 0
            ui_maps_filelist = (sortlist (listfiles demos dmo) a b [<s $a $b])
            looplist ui_maps_lcurmap $ui_maps_filelist [
                append ui_maps_list $ui_maps_lcurmap
                append ui_maps_paths [demos/@ui_maps_lcurmap]
            ]
            ui_maps_rot = (listlen $ui_maps_list)
            append ui_maps_list "?"
            append ui_maps_paths "?"
        ] [
            ui_maps_muts = (mutscheck $ui_maps_mode $ui_maps_muts)
            append ui_maps_list "<random>" //start the list off with random entry (will be reordered later if player is online and in an non-race/edit mode: see below)
            append ui_maps_paths "<random>"
            mapfavs = (sortmaps $mapfavs)
            ui_maps_curlist = (sortmaps (? (>= $ui_maps_mode $modeidxplay) (getmaplist $ui_maps_mode $ui_maps_muts (? (isonline) (listlen (listclients 1)) 0)) $allowmaps))
            ui_maps_curprev = (sortmaps (sublist $previousmaps 0 $maphistory))
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_curlist [
                    if (< (listfind ui_maps_pcurmap $ui_maps_curprev [=s $ui_maps_pcurmap $ui_maps_lcurmap]) 0) [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [=s $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [=s $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_paths [maps/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            // map recommendation reorder: moves wrong size maps into their own section "other maps"
            // if online, and current gamemode isn't race/edit (which doesn't care about map size), seperate current list into size-appropriate and not size-appropriate lists
            if (&& (isonline) (&& (!= $ui_maps_mode $modeidxediting) (!= $ui_maps_mode $modeidxrace)) ) [ //NOTing both inputs of an AND results in a NOR (want to reorder if the mode is neither race nor edit)
                caseif (>= (numhumansonline) $ui_maps_mediumsize) [ //if bigger than medium cap, recommend large
                    ui_maps_recommended = (listintersect $ui_maps_list $largemaps) //create recommended list as intersection of mode and size lists
                    ui_maps_other = (listdel $ui_maps_list $largemaps) //create other list from those rejected by size in maps_recommended
                    ui_maps_list = (concat "+" $ui_maps_recommended "?" $ui_maps_other)
                    ui_maps_paths = (concat "+" (looplistconcat i $ui_maps_recommended [result (concatword "maps/" $i)]) "?" (looplistconcat i $ui_maps_other [result (concatword "maps/" $i)] ))
                ] (>= (numhumansonline) $ui_maps_smallsize) [ //recommend medium
                    ui_maps_recommended = (listintersect $ui_maps_list $mediummaps)
                    ui_maps_other = (listdel $ui_maps_list $mediummaps)
                    ui_maps_list = (concat "+" $ui_maps_recommended "?" $ui_maps_other)
                    ui_maps_paths = (concat "+" (looplistconcat i $ui_maps_recommended [result (concatword "maps/" $i)]) "?" (looplistconcat i $ui_maps_other [result (concatword "maps/" $i)] ))
                ] () [ //else small
                    ui_maps_recommended = (listintersect $ui_maps_list $smallmaps)
                    ui_maps_other = (listdel $ui_maps_list $smallmaps)
                    ui_maps_list = (concat "+" $ui_maps_recommended "?" $ui_maps_other)
                    ui_maps_paths = (concat "+" (looplistconcat i $ui_maps_recommended [result (concatword "maps/" $i)]) "?" (looplistconcat i $ui_maps_other [result (concatword "maps/" $i)] ))
                ]
            ]
            //end of map recommendation reorder
            //now that recommended/other are done, continue with "~": recently played maps
            ui_maps_rot = (listlen $ui_maps_list)
            ui_maps_wantlist = 1
            looplist ui_maps_lcurmap $ui_maps_curprev [
                if $ui_maps_wantlist [
                    append ui_maps_list "~"
                    append ui_maps_paths "~"
                    ui_maps_wantlist = 0
                ]
                append ui_maps_list $ui_maps_lcurmap
                append ui_maps_paths [maps/@ui_maps_lcurmap]
            ]
            //now do "*": non-rotation maps
            ui_maps_wantlist = 1
            ui_maps_filelist = (sortmaps (listfiles maps mpz))
            loop q 2 [
                looplist ui_maps_lcurmap $ui_maps_filelist [
                    if (< (listfind ui_maps_mcurmap $ui_maps_list [=s $ui_maps_mcurmap $ui_maps_lcurmap]) 0) [
                        if (? $q (< (listfind ui_maps_xcurmap $mapfavs [=s $ui_maps_xcurmap $ui_maps_lcurmap]) 0) (>= (listfind ui_maps_xcurmap $mapfavs [=s $ui_maps_xcurmap $ui_maps_lcurmap]) 0)) [
                            if $ui_maps_wantlist [
                                append ui_maps_list "*"
                                append ui_maps_paths "*"
                                ui_maps_wantlist = 0
                            ]
                            append ui_maps_list $ui_maps_lcurmap
                            append ui_maps_paths [maps/@ui_maps_lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& (= $ui_maps_filter 1) (!=s $ui_maps_extra "")) [
                // apply the search filter and keep special tags $t
                ui_maps_t = "~*.?"
                ui_maps_list = (listfilter ui_maps_xcurmap $ui_maps_list [(|| (> (strstr $ui_maps_xcurmap $ui_maps_extra) -1) (> (strstr $ui_maps_t $ui_maps_xcurmap) -1))])
                ui_maps_paths = (listfilter ui_maps_xcurmap $ui_maps_paths [(|| (> (strstr $ui_maps_xcurmap $ui_maps_extra) -1) (> (strstr $ui_maps_t $ui_maps_xcurmap) -1))])
                ui_maps_xcurmap = ""
                ui_maps_pcurmap = ""
                loop i (listlen $ui_maps_list) [
                    if (|| (= (strstr $ui_maps_t (at $ui_maps_list $i)) -1) (= (strstr $ui_maps_t (at $ui_maps_list (+ 1 $i))) -1)) [
                        append ui_maps_xcurmap (at $ui_maps_list $i)
                        append ui_maps_pcurmap (at $ui_maps_paths $i)
                    ]
                ]
                ui_maps_list = $ui_maps_xcurmap
                ui_maps_paths = $ui_maps_pcurmap
            ]
            append ui_maps_list "?"
            append ui_maps_paths "?"
            ui_maps_num = (listfind ui_maps_curmap $ui_maps_list [
                || [=s $ui_maps_curmap $ui_maps_cur] [=s [maps/@ui_maps_curmap] $ui_maps_cur]
            ])
        ]
        ui_maps_count = (listlen $ui_maps_list)
        if (! $ui_maps_rot) [ui_maps_rot = $ui_maps_count]
        ui_maps_lastmode = $ui_maps_mode
        ui_maps_lastmuts = $ui_maps_muts
        ui_maps_nummaps = (- $ui_maps_count 1)
        ui_maps_reset = 2
    ]
    if (= $ui_maps_reset 2) [
        if (|| [< $ui_maps_num 0] [>= $ui_maps_num $ui_maps_count]) [
            ui_maps_num = -1
            ui_maps_cur = ""
            ui_maps_file = ""
            ui_maps_dmo = ""
            ui_maps_img = $emblemtex
        ] [
            cases (at $ui_maps_list $ui_maps_num) "?" [
                ui_maps_cur = $ui_maps_extra
                if (= $ui_maps_mode $modeidxdemo) [
                    ui_maps_file = [demos/@ui_maps_cur]
                    ui_maps_dmo = (demoscan (format "%1.dmo" $ui_maps_img))
                    ui_maps_img = (? (>= $ui_maps_dmo 0) (format "maps/%1" (demoinfo $ui_maps_dmo 1)) $emblemtex)
                ] [
                    ui_maps_file = $ui_maps_extra
                    ui_maps_img = [maps/@ui_maps_extra]
                ]
            ] "<random>" [
                ui_maps_cur = "<random>"
                ui_maps_file = "<random>"
                ui_maps_img = $questiontex
            ] () [
                ui_maps_cur = (at $ui_maps_list $ui_maps_num)
                ui_maps_file = (at $ui_maps_paths $ui_maps_num)
                if (= $ui_maps_mode $modeidxdemo) [
                    ui_maps_dmo = (demoscan (format "%1.dmo" $ui_maps_file))
                    ui_maps_img = (? (>= $ui_maps_dmo 0) (format "maps/%1" (demoinfo $ui_maps_dmo 1)) $emblemtex)
                ] [
                    ui_maps_img = (at $ui_maps_paths $ui_maps_num)
                ]
            ]
        ]
        if (< $ui_maps_mode 0) [
            ui_maps_modeimg = $questiontex
        ] [
            if (&& (!=s $ui_maps_cur "") (= $ui_maps_mode $modeidxdemo)) [
                ui_maps_dinfo = (demoscan (format "%1.dmo" $ui_maps_file))
                ui_maps_dmode = (demoinfo $ui_maps_dinfo 2)
                ui_maps_dmuts = (demoinfo $ui_maps_dinfo 3)
                ui_maps_dname = (gamename $ui_maps_dmode $ui_maps_dmuts 0 30)
                ui_maps_modeimg = (modetexlist $ui_maps_dmode $ui_maps_dmuts -1)
            ] [ui_maps_modeimg = (modetexlist $ui_maps_mode $ui_maps_muts -1)]
        ]
        ui_maps_reset = 0
    ]
]
//end of maplist iter

//generate mode/muts tooltips
loop i $modeidxnum [
    [ui_tip_maps_mode_@[i]] = [
        uihlist 0 [
            uiimage (modetexlist @@i 0 -1) $ui_default 0 $ui_imgtip $ui_imgtip [uiimageshadow $ui_menu_s]
            uispace $ui_padtiny
            uitext (concatword (modedesc @@i 0 3) ".") $ui_texttip
        ]
    ]
    loop j $mutsidxnum [
        k = (<< 1 $j)
        [ui_tip_maps_muts_@[i]_@[k]] = [
            uihlist 0 [
                uiimage (at (modetexlist @@i @@k 1) -1) $ui_default 0 $ui_imgtip $ui_imgtip [uiimageshadow $ui_menu_s]
                uispace $ui_padtiny
                uitext (concatword (mutsdesc @@i @@k 3) ".") $ui_texttip
            ]
        ]
    ]
]

ui_maps_mutsvar = [
    [ui_maps_muts_disable_@arg4] = (|| (< $$arg2 0) (& (mutsimplied $$arg2 $$arg3) $arg4) (& $mutslockforce $arg4) (ismodelocked $$arg2 (| $$arg3 $arg4) $arg4 $$arg5))
    uicheckbox $arg1 (? (|| (& $$arg3 $arg4) (& (mutsimplied $$arg2 $$arg3) $arg4) (& $mutslockforce $arg4)) 1 0) $ui_checksize [
        if (& $$arg3 $arg4) [
            $arg3 = (mutscheck $$arg2 (&~ $$arg3 $arg4))
        ] [
            $arg3 = (mutscheck $$arg2 (| $$arg3 $arg4) $arg4)
        ]
    ] 0 0 0 [uialign -1] [maps_muts_@[@[arg2]]_@[arg4]] $[ui_maps_muts_disable_@arg4]
]

ui_maps_modevar = [
    [ui_maps_mode_disable_@arg4] = (ismodelocked $arg4 $$arg3 0 $$arg5)
    uiradio $arg1 (= $$arg2 $arg4) $ui_checksize [$arg2 = $arg4] 0 0 [
        if (= $[ui_maps_mode_disable_@@arg4] 0) [
            uitooltip [maps_mode_@@@arg4]
        ]
        uialign -1
    ] $[ui_maps_mode_disable_@arg4]
]


ui_maps_exec = [
    start $arg1 $arg2 $arg3
    if (|| (! (getclientcount)) [&& (=s (getmastermode 1) "veto") (getclientpriv $getclientnum $vetolock)]) [uiclose "maps"] [uiopen "votes"]
]

uimenu "maps" "Select a Map and Mode" "<grey>textures/icons/maps" [
    ui_maps_iter
    uivlist $ui_padbutton [
        uihlist $ui_padbutton [
            uialign -1 -1
            uivlist $ui_padbutton [
                uialign -1 -1
                uihlist $ui_padbutton [
                    uialign -1 -1
                    uiimage $ui_maps_modeimg $ui_default 0 $ui_doubleh $ui_doubleh [uiimagealttex $questiontex; uiimageshadow $ui_menu_s]
                    uivlist 0 [
                        uialign -1 0
                        if (< $ui_maps_lastmode 0) [
                            uitextleft "Game Select" $ui_text
                            uitextleft "Please select a map and mode to continue." $ui_textsmall
                        ] [
                            ui_maps_gname = (gamename $ui_maps_mode $ui_maps_muts 0 36)
                            if (&& (!=s $ui_maps_cur "") (= $ui_maps_mode $modeidxdemo)) [
                                ui_maps_gname = (concat $ui_maps_dname "(demo)")
                            ]
                            uitextleft (format "^fy%1" $ui_maps_gname) $ui_text
                            if (!=s $ui_maps_cur "") [
                                if (= $ui_maps_mode $modeidxdemo) [
                                    ui_maps_dinfo = (demoscan (format "%1.dmo" $ui_maps_file))
                                    ui_maps_dmapname = (getmaptitle (demoinfo $ui_maps_dinfo 1))
                                    uitextleft (format "^farecorded on ^fo%1" $ui_maps_dmapname) $ui_textsmall
                                ] [
                                    uitextleft (format "^faselected on ^fo%1" (getmaptitle $ui_maps_cur)) $ui_textsmall
                                ]
                            ] [uitextleft "Please select a map to continue." $ui_textsmall]
                        ]
                    ]
                ]
                uihlist $ui_padbutton [
                    uialign -1 -1
                    uivlist 0 [
                        uialign -1 -1
                        uitextleft "Mode:" $ui_text
                        loop z $modeidxnum [
                            ui_maps_modevar (at $modename $z) ui_maps_mode ui_maps_muts $z ui_maps_cur
                        ]
                    ]
                    uivlist 0 [
                        uialign -1 -1
                        uiimgbutton (? (!=s $ui_maps_img "") $ui_maps_img $emblemtex) 0.16 0.16 [
                            ui_maps_exec $ui_maps_cur $ui_maps_mode $ui_maps_muts
                        ] 0 [] (|| (< $ui_maps_mode 0) (=s $ui_maps_cur "")) $ui_padbutton [
                            ui_maps_fav = (format "%1.%2.%3" $ui_maps_cur $ui_maps_mode $ui_maps_muts)
                            gamefavs = (concat $ui_maps_fav (listdel $gamefavs $ui_maps_fav))
                        ] $ui_default [] 1
                    ]
                ]
                uivlist $ui_padbutton [
                    uialign -1 -1
                    uitextleft "Mutators:" $ui_text
                    uigrid 3 0 0 [
                        uialign -1 -1
                        loop n $mutsidxnormal [
                            uifill 0.125 0 [uialign -1 -1; ui_maps_mutsvar (at $mutsname $n) ui_maps_mode ui_maps_muts (<< 1 $n) ui_maps_cur]
                        ]
                        if (!=s (gspmutname $ui_maps_mode 0) "") [
                            loop n $mutsidxgsn [
                                ui_maps_curmut = (gspmutname $ui_maps_mode $n)
                                if (!=s $ui_maps_curmut "") [
                                    uifill 0.125 0 [uialign -1 -1; ui_maps_mutsvar $ui_maps_curmut ui_maps_mode ui_maps_muts (<< 1 (+ $mutsidxgsp $n)) ui_maps_cur; uispace $ui_padbutton]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
            uivlist 0 [
                uihlist $ui_padsmall [
                    uiclamp 1 1 1 1
                    uialign -1 -1
                    uiscroll 0.34 0.36 [
                        uihlist 0 [uispace 0.1395]
                        uialign -1 -1
                        uivlist $ui_padtiny [
                            uiclamp 1 1 1 1
                            uialign -1 -1
                            looplistn ui_maps_curmap ui_maps_curiter $ui_maps_list [
                                cases $ui_maps_curmap "*" [
                                    uitextleft "^faMaps not in the rotation" $ui_textsmall
                                    uibar 1 0
                                ] "~" [
                                    uitextleft "^faMaps played recently" $ui_textsmall
                                    uibar 1 0
                                ] "?" [
                                    uitextleft "^faOther maps" $ui_textsmall
                                    uibar 1 0
                                ] "+" [
                                    uitextleft "^faRecommended maps" $ui_textsmall
                                    uibar 1 0
                                ] () [
                                    uihlist 0 [
                                        uiclamp 1 1 1 1
                                        uialign -1
                                        ui_maps_formatted = $ui_maps_curmap
                                        if (= $ui_maps_mode $modeidxdemo) [
                                            ui_maps_dcur = (at $ui_maps_paths $ui_maps_curiter)
                                            ui_maps_dnum = (demoscan [@[ui_maps_dcur].dmo])
                                            if (>= $ui_maps_dnum 0) [
                                                ui_maps_dmap = (demoinfo $ui_maps_dnum 1)
                                                ui_maps_dtitle = (getmaptitle $ui_maps_dmap)
                                                ui_maps_dauthor = (getmapauthor $ui_maps_dmap)
                                                ui_maps_ddesc = (getmapdesc $ui_maps_dmap)
                                                ui_maps_dmode = (demoinfo $ui_maps_dnum 2)
                                                ui_maps_dmuts = (demoinfo $ui_maps_dnum 3)
                                                ui_maps_gname = (gamename $ui_maps_dmode $ui_maps_dmuts 0 36)
                                                [ui_tip_maps_@ui_maps_curmap] = [
                                                    uivlist 0 [
                                                        uispace $ui_buttonzs 0
                                                        uitext (concatword "^fs^fy" [@@@ui_maps_gname] "^fS") $ui_texttip [uitextlimit 1]
                                                        uitext "on" $ui_texttiptiny [uitextwrap $ui_tipwrap]
                                                        uitext [@@@ui_maps_dtitle] $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                        if (!=s [@@@ui_maps_dauthor] "") [uitext (concat "by" [@@@@ui_maps_dauthor]) $ui_texttipsmall [uitextwrap $ui_tipwrap]]
                                                        uispace $ui_padsmall $ui_padsmall [
                                                            uiimage (getmapfile [@@@@ui_maps_dmap]) $ui_default 0 $ui_imglist $ui_imglist [uiimagealttex "textures/nothumb"; uioutline $ui_line 0 0 [uiclamp- 1 1 1 1]]
                                                        ]
                                                        uitext (? (=s [@@@ui_maps_ddesc] "") "This map has no description." [@@@ui_maps_ddesc]) $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                        uitext (concatword "^fs^fA[^fS " @@ui_maps_curmap " ^fs^fA]^fS") $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                    ]
                                                ]
                                            ]
                                        ] [
                                            ui_maps_formatted = (getmaptitle $ui_maps_curmap)
                                            ui_maps_author = (getmapauthor $ui_maps_curmap)
                                            ui_maps_desc = (getmapdesc $ui_maps_curmap)
                                            if (=s $ui_maps_curmap "<random>") [
                                                [ui_tip_maps_@ui_maps_curmap] = [
                                                    uitext "This will pick a random map." $ui_texttip
                                                ]
                                            ] [
                                                [ui_tip_maps_@ui_maps_curmap] = [
                                                    uivlist 0 [
                                                        uispace $ui_buttonzs 0
                                                        uitext [@@@ui_maps_formatted] $ui_texttip [uitextwrap $ui_tipwrap]
                                                        if (!=s [@@@ui_maps_author] "") [uitext (concat "by" [@@@@ui_maps_author]) $ui_texttipsmall [uitextwrap $ui_tipwrap]]
                                                        uispace $ui_padsmall $ui_padsmall [
                                                            uiimage (at $ui_maps_paths @@@ui_maps_curiter) $ui_default 0 $ui_imglist $ui_imglist [uiimagealttex "textures/nothumb"; uioutline $ui_line 0 0 [uiclamp 1 1 1 1]]
                                                        ]
                                                        uitext (? (=s [@@@ui_maps_desc] "") "This map has no description." [@@@ui_maps_desc]) $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                        uitext (concatword "^fs^fA[^fS " @@ui_maps_curmap " ^fs^fA]^fS") $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                    ]
                                                ]
                                            ]
                                        ]
                                        if $ui_maps_filter [ui_maps_formatted = (strcasereplace $ui_maps_formatted $ui_maps_extra (format "^fs^fy%1^fS" $ui_maps_extra))]
                                        uiradio $ui_maps_formatted (= $ui_maps_num $ui_maps_curiter) $ui_radiosize [ui_maps_num = @ui_maps_curiter; ui_maps_reset = 2] 0 0 [
                                            uialign -1
                                            uitooltip [maps_@@ui_maps_curmap]
                                        ]
                                        if (!= $ui_maps_mode $modeidxdemo) [
                                            ui_maps_hasmap = (>= (indexof $mapfavs $ui_maps_curmap) 0)
                                            uicheckbox "" (!= $ui_maps_hasmap 0) $ui_checksize [
                                                mapfavs = (? @ui_maps_hasmap (listdel $mapfavs @ui_maps_curmap) (concat (listdel $mapfavs @ui_maps_curmap) @ui_maps_curmap))
                                                ui_maps_reset = 1
                                            ] $colouryellow 0 $ui_default [uialign 1]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uivscroll $ui_slidersize 0.36
                ]
                //uihlist 0 [
                //    uialign -1 -1
                //    uispace $ui_padsmall 0 [uitext "custom map or" $ui_textsmall]
                //    uicheckbox "search filter:" $ui_maps_filter $ui_checksize [ui_maps_reset = 1; ui_maps_filter = (? $ui_maps_filter 0 1)] $colourred 0 $ui_default [uialign -1] 0 $ui_textsmall
                //]
				uispace 0 0.0025
                uihlist 0 [
                    uialign -1 -1
                    ui_maps_extraval = $ui_maps_extra
                    ui_maps_extranum = $ui_maps_nummaps
                    //uiradio "" (= $ui_maps_num $ui_maps_extranum) $ui_radiosize [ui_maps_num = $ui_maps_extranum; ui_maps_reset = 2] 0 0 [uialign -1]
                    //uispace $ui_padtiny
                    uiinput ui_maps_extraval 16 [
                        ui_maps_extra = $ui_maps_extraval
                        if $ui_maps_filter [ui_maps_reset = 1] [ui_maps_num = $ui_maps_extranum]
                        ui_maps_reset = $ui_maps_filter
                    ] 1 $ui_maps_filter (? $ui_maps_filter "[click here to enter search text]" "[click here to enter map name]")
                ]
            ]
        ]
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw (? $ui_maps_mode (? (getclientcount) (? (&& (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto")) "Force (VETO)" "Submit Vote") "Start Game") "Start Demo") [
                ui_maps_exec $ui_maps_cur $ui_maps_mode $ui_maps_muts
            ] (|| (< $ui_maps_mode 0) (=s $ui_maps_cur "")) $colourgreen
            uibuttonw "Reset" [ui_maps_init; ui_maps_reset = 1] 0 $colourorange
            uibuttonw "Cancel" [uiclose "maps"] 0 $colourred
            uibuttonw "Show Votes" [uiopen "votes"] (! $isonline) $colouryellow
        ]
    ]
] [ui_maps_init]

uimenu "votes" "Current Votes" "<grey>textures/icons/vote" [
    uivlist $ui_padbutton [
        uialign -1 -1
        ui_votes_num = (getvote)
        uihlist $ui_padbutton [
            uiclamp 1 1 1 1
            uialign -1 0
            uihlist $ui_padbutton [
                uialign -1 0
                uicheckbox "Dynamic Sort" (!= $sortvotes 0) $ui_checksize [sortvotes (! $sortvotes)] $colourred 0 $ui_default [uialign -1]
                uicheckbox "Cleanup List" (!= $cleanvotes 0) $ui_checksize [cleanvotes (! $cleanvotes)] $colourred 0 $ui_default [uialign -1]
                uibuttonz "Clear Vote" [clearvote] 0 $colourred [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Show Maps" [uiopen "maps"] 0 $colourorange [] $ui_buttonzh $ui_text $ui_padsmall
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uitextright (format "^fc%1 ^fwgame%2 suggested" $ui_votes_num (? (!= $ui_votes_num 1) "s"))
            ]
        ]
        uispace $ui_padtiny
        uihlist 0 [
            uiclamp 1 1 1 1
            uialign -1 -1
            uihlist $ui_padsmall [
                uiclamp 1 1 1 1
                uialign -1 -1
                uiscroll 1 0.35 [
                    uialign -1 -1
                    uivlist 0 [
                        uihlist 0 [uispace 0.35]
                        uialign -1 -1
                        loopvotes ui_votes_cur [
                            ui_votes_players = (getvote $ui_votes_cur 0)
                            ui_votes_mode = (getvote $ui_votes_cur 1)
                            ui_votes_muts = (getvote $ui_votes_cur 2)
                            ui_votes_map = (getvote $ui_votes_cur 3)
                            ui_votes_maptitle = (getmaptitle $ui_votes_map)
                            ui_votes_self = 0
                            ui_votes_gname = (gamename $ui_votes_mode $ui_votes_muts 0 48)
                            [ui_tip_votes_@ui_votes_cur] = [
                                if (> @ui_votes_players 0) [
                                    uitext "Players currently supporting this vote." $ui_texttip
                                    ui_votes_pname = ""
                                    loop j @@ui_votes_players [
                                        append ui_votes_pname (format ["%1"] (getclientname (getvote @@@ui_votes_cur 0 $j)))
                                    ]
                                    uitext (prettylist $ui_votes_pname) $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                ] [uitext "No players supporting this vote." $ui_tipwrap $ui_texttip]
                            ]
                            loop j $ui_votes_players [if (= (getclientnum) (getvote $ui_votes_cur 0 $j)) [ui_votes_self = 1]]
                            ui_votes_img = $emblemtex
                            if (!=s $ui_votes_map "") [
                                if (=s $ui_votes_map "<random>") [ui_votes_img = $questiontex] [ui_votes_img = (getmapfile $ui_votes_map)]
                            ]
                            uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 (? $ui_votes_self $colouryellow $ui_line) $colourred $colourpink [
                                uirelease [if (= [@@ui_votes_self] 1) [clearvote] [start [@@ui_votes_map] [@@ui_votes_mode] [@@ui_votes_muts]]]
                                uialtrelease [uiopen maps; ui_maps_mode = [@@ui_votes_mode]; ui_maps_muts = [@@ui_votes_muts]; ui_maps_cur = [@@ui_votes_map]]
                                uitooltip [votes_@ui_votes_cur]
                                uialign -1 0
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uialign -1 0
                                    uihlist 0 [
                                        uialign -1 0
                                        uispace $ui_padsmall $ui_padsmall [
                                            uialign -1 0
                                            uivlist 0 [
                                                uispace 0.025
                                                uitext $ui_votes_players $ui_textlarge
                                                uitext (? (= $ui_votes_players 1) "vote" "votes") $ui_textsmall
                                            ]
                                        ]
                                        uispace $ui_padsmall $ui_padsmall [
                                            uiimage $ui_votes_img $ui_default 0 $ui_imgbut $ui_imgbut [uiimagealttex "textures/nothumb"; uioutline $ui_line; uiclamp- 1 1 1 1]
                                        ]
                                        uispace $ui_padsmall $ui_padtiny [
                                            uialign -1 0
                                            uivlist 0 [
                                                uialign -1 0
                                                uitextleft (format "^fy%1 ^favoted for on ^fo%2" $ui_votes_gname $ui_votes_maptitle)
                                                uihlist 0 [
                                                    uialign -1 0
                                                    if (> $ui_votes_players 0) [
                                                        uitextleft "^fwby: "
                                                        ui_votes_pname = ""
                                                        ui_votes_pmore = 0
                                                        loop j $ui_votes_players [
                                                            if (|| $ui_votes_pmore (> (strlen (stripcolors $ui_votes_pname)) 128)) [ui_votes_pmore = (+ $ui_votes_pmore 1)] [
                                                                append ui_votes_pname (format ["%1"] (getclientname (getvote $ui_votes_cur 0 $j)))
                                                            ]
                                                        ]
                                                        uitext (concat (prettylist $ui_votes_pname) (? $ui_votes_pmore (concat "and^fy" $ui_votes_pmore "^fwmore"))) $ui_textsmall [uitextwrap 0.55; uitextalign -1; uialign -1 0]
                                                    ] [uitextleft "^faNo current votes."]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1] 1
                        ] [
                            uialign -1 0
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmall [
                                uialign -1 0
                                uivlist $ui_padsmall [
                                    uitextleft "There are no current votes."
                                    if (! $isonline) [
                                        uitextleft "Votes only appear in multiplayer matches."
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                uivscroll $ui_slidersize 0.35
                uialign- 1 -1
            ]
        ]
    ]
]
