defvarp showmainplayerprev 0 1 1

ui_tip_main_profile = [uitext "Changes your name and other player settings." $ui_texttip]
ui_tip_main_maps = [uitext "Select a map and mode to start playing on." $ui_texttip]
ui_tip_main_servers = [uitext "Select a server to connect to and play on." $ui_texttip]
ui_tip_main_disconnect = [uitext "Disconnect from the current server." $ui_texttip]
ui_tip_main_settings = [uitext "Change settings such as graphics, audio, input, etc." $ui_texttip]
ui_tip_main_help = [uitext "Get help with playing the game and operating the environment." $ui_texttip]
ui_tip_main_credits = [uitext "Show the people who made this game possible." $ui_texttip]
ui_tip_main_quit = [uitext "Quits the game." $ui_texttip]

uimenu "main" "Main Menu" "textures/icon" [
    uihlist $ui_padbutton [
        if $showmainplayerprev [
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel (getplayermodel)
                playerpreviewpattern (getplayerpattern)
                playerpreviewcolour (getplayercolour -1)
                playerpreviewteam (getplayerteam 1)
                playerpreviewweap (weapselect)
                playerpreviewvanity (getplayervanity)
            ] [ui_playerprev_easteregg]
        ]
        uivlist 0 [
            uiclamp 1 1 1 1
            uialign 0 -1
            uivlist 0 [
                uialign 0 -1
                uipad 0 0 0 $ui_padsmall [uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh]
                ui_main_version = (concatword "v" $versionstring " (" $versionrelease ")")
                if $versionbuild [ui_main_version = (concatword $ui_main_version " [" $versionbranch ":" $versionbuild "]")]
                uitext $ui_main_version $ui_textsmall
            ]
            uivlist $ui_padbutton [
                uialign 0 -1
                uibuttonm "Profile Setup" "textures/icons/player" [uiopen "profile"] 0 main_profile
                uibuttonm "Select Map/Mode" "textures/icons/maps" [uiopen "maps"] 0 main_maps
                uibuttonm "Server Browser" "textures/servers/list" [uiopen "servers"] 0 main_servers
                uibuttonm "Disconnect" "textures/servers/disconnect" [disconnect] (= (isconnected) 0) main_disconnect
                uibuttonm "Settings" "textures/icons/settings" [uiopen "settings"] 0 main_settings
                uibuttonm "Help and Support" $questiontex [uiopen "help"] 0 main_help
                uibuttonm "Quit" "textures/icons/warning" [quit] 0 main_quit
            ]
        ]
    ]
] [] [] [
    ui_menu_title_main = (? (isconnected) "Main Menu" (concat "Welcome to" $versionname))
] [] [] [= (isconnected) 0]
