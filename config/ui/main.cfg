defvarp showmainplayerprev 0 1 1

ui_tip_main_profile = [uitext "Changes your name and other player settings." $ui_texttip]
ui_tip_main_maps = [uitext "Select a map and mode to start playing on." $ui_texttip]
ui_tip_main_servers = [uitext "Select a server to connect to and play on." $ui_texttip]
ui_tip_main_disconnect = [uitext "Disconnect from the current server." $ui_texttip]
ui_tip_main_settings = [uitext "Change settings such as graphics, audio, input, etc." $ui_texttip]
ui_tip_main_help = [uitext "Get help with playing the game and operating the environment." $ui_texttip]
ui_tip_main_credits = [uitext "Show the people who made this game possible." $ui_texttip]
ui_tip_main_quit = [uitext "Quits the game." $ui_texttip]

ui_contents_main = [
    local iseditmode
    iseditmode = (&& (= $gamemode $modeidxediting) (isconnected))
    uialign 0 -1
    uibuttonm "Play Online" "textures/servers/list" [uiopen "servers"] 0 main_servers
    uibuttonm (? (isconnected) "Vote Map/Mode" "Offline Practice") "textures/icons/maps" [uiopen "maps"] 0 main_maps
    uibuttonm "Parkour Challenges" "textures/modes/race" [uiopen "challengemenu"] 0 main_sp
    uifill 0 $ui_padwin
    uibuttonm "Profile Setup" "textures/icons/player" [uiopen "profile"] 0 main_profile
    uibuttonm "Settings" "textures/icons/settings" [uiopen "settings"] 0 main_settings
    uibuttonm "Help and Support" $questiontex [uiopen "help"] 0 main_help
    if $isconnected [
        uibuttonm "Disconnect" "textures/servers/disconnect" [
            if $iseditmode [uiopen "disconnectwarn"] disconnect
        ] 0 main_disconnect
    ]
    uibuttonm "Quit" "textures/icons/warning" [
        if $iseditmode [uiopen "quitwarn"] quit
    ] 0 main_quit
]

uimenu "main" "Main Menu" "textures/icon" [
    uihlist $ui_padlarge [
        if $showmainplayerprev [
            uiplayerpreview 1 1 $ui_mainw $ui_mainh [
                uiplayerpreviewmodel (getplayermodel)
                uiplayerpreviewpattern (getplayerpattern)
                uiplayerpreviewcolour (getplayercolour -1)
                uiplayerpreviewteam (getplayerteam 1)
                uiplayerpreviewweapselect (weapselect)
                uiplayerpreviewvanity (getplayervanity)
                uiplayerpreviewweapselect $weapidxmelee
            ] [
                uipreviewinteractive 1
                uialtrelease [uipreviewresetoffset]
                if (! (isconnected)) [
                    uipreviewskycol (modcolour (skewcolour $backgroundcolour $colourwhite 0.4) 0.2)
                    uipreviewsuncol (skewcolour $backgroundcolour $colourwhite 0.5)
                    uipreviewsundir 0 1 2
                    uipreviewexcol (modcolour $backgroundcolour 0.4)
                    uipreviewexdir 0 0 -2
                    uipreviewyaw 140
                ]
                ui_playerprev_easteregg
            ]
        ]
        uivlist (*f $ui_padlarge 3) [
            uialign 0 0
            uivlist 0 [
                uiimage "textures/logo" $ui_default 0 $ui_timgw $ui_timgh [
                    uiimageshadow $ui_menu_s
                    ui_main_version = (concatword "v" $versionstring " (" $versionrelease ")")
                    if $versionbuild [ui_main_version = (concatword $ui_main_version " [" $versionbranch ":" $versionbuild "]")]
                    uitext $ui_main_version $ui_textsmall [uialign 0 1]
                ]
            ]
            uivlist $ui_padbutton [
                ui_contents_main
            ]
        ]
    ]
]

uimenu "file" "Main Menu" "textures/icon" [
    uivlist $ui_padbutton [
        ui_contents_main
    ]
] [] [] [] [] []

uioverlay "progress" "Please Wait" "textures/icons/waiting" [
    uiexclusive 1
    uiallowinput 0
    ui_progress_type = (getprogresstype)
    ui_progress_title = (getprogresstitle)
    if $ui_progress_type [
        if (! (getprogresswait)) [uiexclusive 1]
        uiblurskin 0.3 0 $ui_menu_a [
            uispace $ui_padwin $ui_padwin [
                uivlist 0 [
                    if (getloadedmap) [
                        uitext (concatword "^f<bold>" (getmaptitle $mapname)) $ui_textlarge
                        if (!=s (getmapauthor $mapname) "") [
                            uitext (concat "by" (getmapauthor $mapname)) $ui_textbig
                        ]
                        uispace 0 $ui_padbutton
                        uitext (format "^f<bold>^fs^fy%1^fS" (gamename (gamemode) (mutators) 0 32)) $ui_text
                        ui_loading_modetexs = (modetexlist (gamemode) (mutators))
                        looplist ui_loading_modetex $ui_loading_modetexs [uiimage $ui_loading_modetex $ui_default 0 $ui_imgbut $ui_imgbut [uiimageshadow $ui_menu_s]]
                        uitext (modedesc (gamemode) (mutators) 5) $ui_textstatus
                        if (> (mutators) 0) [
                            loop i $mutsidxnum [
                                ui_loading_mut = (<< 1 $i)
                                if (&& [& (mutators) $ui_loading_mut] [! (& (mutsimplied (gamemode)) $ui_loading_mut)]) [
                                    ui_loading_muttxt = (mutsdesc (gamemode) $ui_loading_mut 4)
                                    if (!=s $ui_loading_muttxt "") [uitext (concatword "^fa" $ui_loading_muttxt) $ui_textstatus]
                                ]
                            ]
                        ]
                    ] [uitext "^f<bold>Loading.." $ui_textlarge]
                    if (isconnected) [
                        uispace 0 $ui_padbutton
                        if (!=s $connectname "") [
                            uitext (format "^faOn: ^fw%1:[%2]" $connectname $connectport) $ui_textstatus
                            if (!=s $serverdesc "") [uitext (format "^"^fs%1^fS^"" $serverdesc) $ui_textstatus]
                        ] [uitext (format "^faPlaying Offline" $connectname $connectport) $ui_textstatus]
                    ]
                ]
            ]
        ]
        uispace $ui_padwin $ui_padwin [
            uialign 0 1
            uioffset 0 -0.4 [
                uiblurskin 0 0 $ui_menu_a [
                    uispace $ui_padwin $ui_padnormal [uitext (tipshow) $ui_text]
                ]
            ]
        ]
    ] [uiexclusive 0]
    if (!=s $ui_progress_title "") [
        uispace $ui_padwin $ui_padwin [
            uialign -1 1
            uihlist 0 [
                uialign -1 1
                uiblurskin 0 0 $ui_menu_a [
                    uispace $ui_padsmall $ui_padsmall [
                        if (>f $progressamt 0) [
                            uihmetera $progressamt $ui_buttonzs $ui_buttonzz $colourdarkred $ui_menu $ui_disa [@colourred] [] -1
                        ] [
                            uihthroba (divf (mod (getmillis -1) 1000) 1000) $ui_buttonzs $ui_buttonzz $colourdarkred $ui_menu $ui_disa [@colourred]
                        ]
                    ]
                ]
                uispace $ui_padsmall
                uitext $ui_progress_title $ui_text [uitextalign -1; uialign -1 0]
            ]
        ]
    ]
] [uitoggle]
