loadweapall = 1
setpersist loadweapall 1

ui_profile_loadweaps = [
    lwa = ""
    lwl = (listlen $ui_profile_prev_loadweap)
    lwo = (? (> $lwl $arg1) (at $ui_profile_prev_loadweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $ui_profile_prev_loadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [lw3 = $lwo]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ui_profile_prev_loadweap = $lwa]
    ui_profile_prev_weap = $arg2
]

ui_profile_randweaps = [
    rwa = ""
    rwl = (listlen $ui_profile_prev_randweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $ui_profile_prev_randweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ui_profile_prev_randweap = $rwa]
]

// these names should match the types in config/vanities.cfg
ui_profile_vanity_name = [chest eyes scalp forehead ears face back [right foot] [left foot] [right shoulder] [left shoulder]]
ui_profile_vanity_names = [Chest Eyes Scalp Forehead Ears Face Back [Right Foot] [Left Foot] [Right Shoulder] [Left Shoulder]]
ui_profile_vanity_type = -1
ui_profile_vanity_origsel = -1
ui_profile_vanity_origprev = ""
ui_profile_vanity_sel = -1

uitoolmenu "vanity" "Choose Vanity Item" "textures/question" [
    uivlist $ui_padbutton [
        uitext (format "Which vanity item for your ^fs^fy%1^fS?" (at $ui_profile_vanity_name $ui_profile_vanity_type))
        uichecktext "nothing" (= $ui_profile_vanity_sel -1) $ui_buttonw $ui_buttonh [
            ui_profile_vanity_sel = -1
            loop j (getvanity) [
                if (= (getvanity $j 0) $ui_profile_vanity_type) [
                    ui_profile_prev_vanity = (listdel $ui_profile_prev_vanity (getvanity $j 1))
                ]
            ]
        ] $ui_on3 $ui_default
        loop i (getvanity) [
            if (= (getvanity $i 0) $ui_profile_vanity_type) [
                uichecktext (getvanity $i 2) (= $ui_profile_vanity_sel $i) $ui_buttonw $ui_buttonh [
                    ui_profile_vanity_sel = $i
                    loop j (getvanity) [
                        if (= (getvanity $j 0) $ui_profile_vanity_type) [
                            ui_profile_prev_vanity = (listdel $ui_profile_prev_vanity (getvanity $j 1))
                        ]
                    ]
                    append ui_profile_prev_vanity (getvanity $i 1)
                ] $ui_on3 $ui_default
            ]
        ]
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "OK" [uiclose "vanity"] 0 $ui_ok
            uibuttonw "Reset" [ui_profile_vanity_sel = $ui_profile_vanity_origsel; ui_profile_prev_vanity = $ui_profile_vanity_origprev] 0 $ui_change
            uibuttonw "Cancel" [ui_profile_vanity_sel = $ui_profile_vanity_origsel; ui_profile_prev_vanity = $ui_profile_vanity_origprev; uiclose "vanity"] 0 $ui_cancel
        ]
    ]
] [
    if (= $ui_profile_vanity_type -1) [uiclose "vanity"] [ui_profile_vanity_origsel = $ui_profile_vanity_sel; ui_profile_vanity_origprev = $ui_profile_prev_vanity]
] [] [] [ui_profile_vanity_sel = $ui_profile_vanity_origsel; ui_profile_prev_vanity = $ui_profile_vanity_origprev; uiclose "vanity"]

uitoolmenu "welcome" "Welcome to Red Eclipse" "textures/hud/alert" [
    uivlist $ui_padbutton [
        uitext "To get ready for action, please set up your player profile!"
        uispace 0 $ui_padbutton
        uitext "Use the this menu to choose a name, a profile colour and a pair of loadout weapons." 0.8
        uitext "To learn more about the weapons and many other aspects of the game, you can consult the help menus." 0.8
        uispace 0 $ui_padtiny
        uitext (format "You can press %1 for help, and %2 to open the menu (or close the current one)." (dobindsearch [uiopen help]) (dobindsearch [togglemainmenu]))
        uispace 0 $ui_padtiny
        uitext "During a match, this allows you to quickly look up rules and tips for the current game mode." 0.8
        uitext "Most of this information is also available on the Red Eclipse wiki, ^fs^fchttp://redeclipse.net/wiki/^fS." 0.8
        uitext "Once you have set up your profile with the ^fs^fgOK^fS button, you can start playing offline or online." 0.8
        uitext "Learning by doing or watching experienced players, both are great ways to get you started." 0.8
        uispace 0 $ui_padtiny
        uitext (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))
        uispace 0 $ui_padtiny
        uitext "Use the mouse wheel to follow the view of different players while spectating." 0.8
        uispace 0 $ui_padtiny
        uitext (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))
        uispace 0 $ui_padtiny
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "OK, Thanks!" [uiclose "welcome"] 0 $ui_ok
        ]
    ]
]

ui_profile_prev_reset = [
    ui_profile_prev_team = (getplayerteam 1)
    ui_profile_prev_weap = (weapselect)
    ui_profile_prev_name = (getplayername)
    ui_profile_prev_colour = (getplayercolour -1)
    ui_profile_prev_model = (getplayermodel)
    ui_profile_prev_vanity = (getplayervanity)
    ui_profile_prev_loadweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getloadweap $i)
        if (< $q 0) [break = 1] [ui_profile_prev_loadweap = (? $i (concat $ui_profile_prev_loadweap $q) $q)]
    ]
    ui_profile_prev_randweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getrandweap $i)
        ui_profile_prev_randweap = (? $i (concat $ui_profile_prev_randweap $q) $q)
    ]
    ui_profile_cur_name = $ui_profile_prev_name
    ui_profile_cur_colour = $ui_profile_prev_colour
    ui_profile_cur_model = $ui_profile_prev_model
    ui_profile_cur_vanity = $ui_profile_prev_vanity
    ui_profile_cur_loadweap = $ui_profile_prev_loadweap
    ui_profile_cur_randweap = $ui_profile_prev_randweap
]

ui_profilesave_reset = 0
uitoolmenu "profilesave" "Save Player Profile?" "textures/hud/alert" [
    uivlist 0 [
        skip = 0
        uitext "There are unsaved changes to your player profile!"
        uitext "Do you want to save these settings?"
        uispace 0 $ui_padbutton
        uitable 0 $ui_padtiny [
            looplist i [name colour model vanity loadweap randweap] [
                if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [
                    uitablerow [
                        uihlist $ui_padbutton [uitext $i 0.8]
                        uispace $ui_padtiny
                        uihlist $ui_padbutton [uitext "=" 0.8]
                        uispace $ui_padtiny
                        uihlist $ui_padbutton [uitext $[ui_profile_prev_@i] 0.8]
                    ]
                ]
            ]
        ]
        uispace 0 $ui_padbutton
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "Save" [
                ui_profilesave_reset = 1
                setinfo $ui_profile_prev_name $ui_profile_prev_colour $ui_profile_prev_model $ui_profile_prev_vanity $ui_profile_prev_loadweap $ui_profile_prev_randweap
                uiclose "profilesave"
                uiclose "profile"
            ] 0 $ui_ok
            uibuttonw "Modify" [uiclose "profilesave"] 0 $ui_change
            uibuttonw "Discard" [
                ui_profilesave_reset = 1
                uiclose "profilesave"
                uiclose "profile"
            ] 0 $ui_cancel
        ]
    ]
] [
    ui_profilesave_reset = 0
] [
    if $ui_profilesave_reset [ui_profile_prev_reset]
] [] [uiclose "profilesave"; uiclose "profile"]

ui_tip_profile_name = [uitext "Click here to change your display name." $ui_texttip 0]
ui_tip_profile_model_male = [uitext "Click here to change your display model to ^fs^fymale^fS." $ui_texttip 0]
ui_tip_profile_model_female = [uitext "Click here to change your display model to ^fs^fyfemale^fS." $ui_texttip 0]

uimenu "profile" "Player Profile Setup" "textures/player" [
    uihlist $ui_padbutton [
        uivlist $ui_padbutton [uiplayerpreview $ui_profile_prev_model $ui_profile_prev_colour $ui_profile_prev_team $ui_profile_prev_weap $ui_profile_prev_vanity 1 1 0.4 0.5]
        uivlist $ui_padbutton [
            uihlist $ui_padbutton [
                uiclamp 1 1 1 1
                uialign 0 -1
                uitable 0 0 [
                    uialign 0 -1
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [uitext "Name"]
                            ]
                        ] [uiclamp 1 1 1 1]
                        uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [uiinput ui_profile_prev_name 24 [] 1 "" "^fzad<enter name>" [uitooltip profile_name] ]
                            ]
                        ] [uiclamp 1 1 1 1]
                    ]
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [uitext "Model"]
                            ]
                        ] [uiclamp 1 1 1 1]
                        uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [
                                    uiradio "Male" (= $ui_profile_prev_model 0) $ui_radiosize [ui_profile_prev_model = 0] 0 0 [uitooltip profile_model_male]
                                    uiradio "Female" (= $ui_profile_prev_model 1) $ui_radiosize [ui_profile_prev_model = 1] 0 0 [uitooltip profile_model_female]
                                ]
                            ]
                        ] [uiclamp 1 1 1 1]
                    ]
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [uitext "Colour"]
                            ]
                        ] [uiclamp 1 1 1 1]
                        uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [
                                    uihexpreview $ui_profile_prev_colour "hex-value"
                                    uirgbsliders ui_profile_prev_colour
                                ]
                            ]
                        ] [uiclamp 1 1 1 1]
                    ]
                    uitablerow [
                        uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padbutton $ui_padsmall [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [uitext "Loadout"]
                            ]
                        ] [uiclamp 1 1 1 1]
                        uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmall [
                                uiclamp 1 1 1 1
                                uivlist $ui_padmini [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padmini [
                                        gdl = (listlen $ui_profile_prev_loadweap)
                                        gdw = 0
                                        uispace $ui_padsmall 0 [
                                            uiclamp 1 1 1 1
                                            uivlist 0 [
                                                uiclamp 1 1
                                                uialign 0 0
                                                uitext "slot" $ui_textbig
                                                uitarget $ui_default $ui_titlesize $ui_titlesize [
                                                    uiclamp 1 1 1 1
                                                    uispace $ui_padbutton 0 [uitext "^farandom"]
                                                ]
                                                loop w1 $weapidxloadout [
                                                    w4 = (+ $w1 $weapidxoffset)
                                                    w5 = (at $weapname $w4)
                                                    uitarget $ui_default $ui_titlesize $ui_titlesize [
                                                        uiclamp 1 1 1 1
                                                        uispace $ui_padbutton 0 [uitext (format "^f[%1]%2" (? (allowedweap $w4) $[@[w5]colour] 0x444444) $w5)]
                                                    ]
                                                ]
                                                uispace 0 0.0035
                                            ]
                                        ]
                                        loop w2 (? $loadweapall $weapidxloadout $playermaxcarry) [
                                            w3 = (? (> $gdl $w2) (at $ui_profile_prev_loadweap $w2) -1)
                                            hi = (mod $w2 2)
                                            al = (|| (= $w3 0) (allowedweap $w3))
                                            uivlist 0 [
                                                uialign 0 0
                                                uispace $ui_padsmall 0 [uitext (+ $w2 1) $ui_textbig]
                                                uispace $ui_padsmall 0 [
                                                    uicheckbutton "textures/question" (= $w3 0) $ui_titlesize [ui_profile_loadweaps $w2 0] $ui_default $ui_line [
                                                        [ui_tip_profile_loadout_@[w2]_0] = [
                                                            uitext (format "Click here to select ^fs^farandom weapons^fS for loadout slot ^fs^fy#%1^fS." @w2) $ui_texttip 0
                                                            uiwraptext "This will pick a random weapon every time you spawn." $ui_tipwrap $ui_texttipsmall 0
                                                        ]
                                                        uitooltip [profile_loadout_@[w2]_0]
                                                    ]
                                                ]
                                                loop w1 $weapidxloadout [
                                                    w4 = (+ $w1 $weapidxoffset)
                                                    w5 = (at $weapname $w4)
                                                    uispace $ui_padsmall 0 [
                                                        uicheckbutton (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) (= $w3 $w4) $ui_titlesize [ui_profile_loadweaps $w2 $w4] $[@[w5]colour] $ui_line [
                                                            [ui_tip_profile_loadout_@[w2]_@[w4]] = [
                                                                uitext (format "Click here to select the ^fs^f[%1]%2^fS for loadout slot ^fs^fy#%3^fS." $[@@[w5]colour] @w5 (+ @w2 1)) $ui_texttip 0
                                                                uiwraptext (format "The primary attack %1." $[@@[w5]desc1]) $ui_tipwrap $ui_texttipsmall 0
                                                                uiwraptext (format "The secondary attack %1." $[@@[w5]desc2]) $ui_tipwrap $ui_texttipsmall 0
                                                            ]
                                                            uitooltip [profile_loadout_@[w2]_@[w4]]
                                                        ]
                                                    ]
                                                ]
                                                uispace 0 0.0035
                                            ]
                                            if $al [gdw = (+ $gdw 1)]
                                        ]
                                    ]
                                    uispace 0 $ui_padsmall [uicheckbox "Show all slots" (= $loadweapall 1) $ui_checksize [loadweapall = (! $loadweapall)] $ui_on1 $ui_on2 $ui_default]
                                ]
                            ]
                        ] [uiclamp 1 1 1 1]
                    ]
                ]
                uivlist $ui_padbutton [
                    uiclamp 1 1 1 1
                    uialign 0 -1
                    uitable 0 0 [
                        uialign 0 -1
                        uitableheader [] [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Vanity Items" $ui_textbig]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        loop i (listlen $ui_profile_vanity_name) [
                            r = ""
                            loop j (getvanity) [
                                if (= (getvanity $j 0) $i) [
                                    if (< -1 (indexof  $ui_profile_prev_vanity (getvanity $j 1))) [
                                        r = (getvanity $j 2)
                                    ]
                                ]
                            ]
                            uitablerow [
                                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_line $ui_line $ui_line [
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [uitext (at $ui_profile_vanity_names $i)]
                                    ]
                                ] [uiclamp 1 1 1 1]
                                uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $ui_hover $ui_hold [
                                    [ui_tip_profile_vanity_@[i]] = [uitext (format "Click here change the vanity item for your ^fs^fy%1^fS." (at $ui_profile_vanity_name @i)) $ui_texttip]
                                    uitooltip [profile_vanity_@[i]]
                                    uirelease [
                                        ui_profile_vanity_sel = -1
                                        ui_profile_vanity_type = $i
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $i) [
                                                if (< -1 (indexof  $ui_profile_prev_vanity (getvanity $j 1))) [ui_profile_vanity_sel = $j]
                                            ]
                                        ]
                                        uiopen "vanity" 0
                                    ]
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [
                                            uitext $r
                                        ]
                                    ]
                                ] [uiclamp 1 1 1 1]
                            ]
                        ]
                    ]
                    uihlist $ui_padbutton [
                        uialign 0 1
                        uibuttonw "OK" [setinfo $ui_profile_prev_name $ui_profile_prev_colour $ui_profile_prev_model $ui_profile_prev_vanity $ui_profile_prev_loadweap $ui_profile_prev_randweap; uiclose "profile"] 0 $ui_ok
                        uibuttonw "Reset" [ui_profile_prev_reset] 0 $ui_change
                        uibuttonw "Cancel" [
                            haschanges = 0
                            looplist i [name colour model vanity loadweap randweap] [
                                if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [haschanges = 1]
                            ]
                            if $haschanges [uiopen "profilesave" 0] [uiclose "profile"]
                        ] 0 $ui_cancel
                    ]
                ]
            ]
        ]
    ]
] [
    ui_profile_prev_reset
    if (needname) [uiopen "welcome" 0]
] [] [] [] [
    haschanges = 0
    looplist i [name colour model vanity loadweap randweap] [
        if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [haschanges = 1]
    ]
    if $haschanges [uiopen "profilesave" 0] [uiclose "profile"]
]
