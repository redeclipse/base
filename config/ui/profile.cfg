ui_profile_loadweaps = [
    lwa = ""
    lwl = (listlen $ui_profile_prev_loadweap)
    lwo = (? (> $lwl $arg1) (at $ui_profile_prev_loadweap $arg1) -1)
    loop lw2 $weapcarrydefault [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $ui_profile_prev_loadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [lw3 = $lwo]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ui_profile_prev_loadweap = $lwa]
    ui_profile_prev_weap = $arg2
]

ui_profile_randweaps = [
    rwa = ""
    rwl = (listlen $ui_profile_prev_randweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $ui_profile_prev_randweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ui_profile_prev_randweap = $rwa]
]

// these names should match the types in config/vanities.cfg
ui_profile_vanity_type = -1
ui_profile_vanity_sel = -1

uimenu "vanity" "Choose Vanity Item" $questiontex [
    uivlist $ui_padbutton [
        uitext (format "Which vanity item for your ^fs^fy%1^fS?" (at $vanitynames $ui_profile_vanity_type))
        uichecktext "[nothing]" (= $ui_profile_vanity_sel -1) $ui_buttonw $ui_buttonh [
            ui_profile_vanity_sel = -1
            loop j (getvanity) [
                if (= (getvanity $j 0) $ui_profile_vanity_type) [
                    ui_profile_prev_vanity = (listdel $ui_profile_prev_vanity (getvanity $j 1))
                ]
            ]
            uiclose "vanity"
        ] $colouryellow $ui_default
        loop i (getvanity) [
            if (= (getvanity $i 0) $ui_profile_vanity_type) [
                uichecktext (getvanity $i 2) (= $ui_profile_vanity_sel $i) $ui_buttonw $ui_buttonh [
                    ui_profile_vanity_sel = $i
                    loop j (getvanity) [
                        if (= (getvanity $j 0) $ui_profile_vanity_type) [
                            ui_profile_prev_vanity = (listdel $ui_profile_prev_vanity (getvanity $j 1))
                        ]
                    ]
                    append ui_profile_prev_vanity (getvanity $i 1)
                    uiclose "vanity"
                ] $colouryellow $ui_default
            ]
        ]
    ]
] [] [] [] 2 [] 0 8

ui_profile_loadout_type = -1
ui_profile_loadout_sel = -1

uimenu "loadout" "Choose Loadout Weapon" $questiontex [
    uivlist $ui_padbutton [
        uitext (format "Which weapon for loadout slot ^fs^fy#%1^fS?" (+ $ui_profile_loadout_type 1))
        uichecktextbutton "Random" $questiontex (= $ui_profile_loadout_sel 0) $ui_buttonw $ui_buttonh [
            ui_profile_loadout_sel = 0
            ui_profile_loadweaps $ui_profile_loadout_type $ui_profile_loadout_sel
            uiclose "loadout"
        ] $colourwhite $ui_default
        loop i $weapidxloadout [
            ui_profile_loadout_idx = (+ $i $weapidxoffset)
            ui_profile_loadout_weap = (at $weapname $ui_profile_loadout_idx)
            uichecktextbutton $[@[ui_profile_loadout_weap]longname] [textures/weapons/@ui_profile_loadout_weap] (= $ui_profile_loadout_sel $ui_profile_loadout_idx) $ui_buttonw $ui_buttonh [
                ui_profile_loadout_sel = $ui_profile_loadout_idx
                ui_profile_loadweaps $ui_profile_loadout_type $ui_profile_loadout_sel
                uiclose "loadout"
            ] $[@[ui_profile_loadout_weap]colour] $ui_default
        ]
    ]
] [] [] [] 2 [] 0 8

ui_profile_pattern_sel = -1

loop i $numpatterns [
    ui_profile_pattern_tex = (format "<crop:0,0,512,512><mix:255,0><thumbnail:128>%1" (getpattern $i 0))
    ui_profile_pattern_id = (getpattern $i 1)
    ui_profile_pattern_name = (getpattern $i 2)
    [ui_tip_profile_pattern_@[ui_profile_pattern_id]] = [
        uispace $ui_padsmall $ui_padsmall [
            uivlist $ui_padtiny [
                uicolour (getteamcolour $ui_profile_prev_team) 0 0 [
                    uiimage @@@@ui_profile_pattern_tex $ui_profile_prev_colour 0 (*f $ui_imglist 2) (*f $ui_imglist 2) [uiimagealttex "textures/nothumb"; uioutline $ui_line 0 0 [uiclamp 1 1 1 1]]
                ]
                uitext @@@ui_profile_pattern_name $ui_textstatus
            ]
        ]
    ]
]

uimenu "pattern" "Choose Player Pattern" $questiontex [
    uivlist $ui_padbutton [
        uitext "Which pattern would you like to use?"
        ui_profile_pattern_split = (div $numpatterns 2)
        ui_profile_pattern_split = (- $numpatterns $ui_profile_pattern_split)
        ui_profile_pattern_inc = 0
        uihlist $ui_padbutton [
            loop j 2 [
                uivlist $ui_padbutton [
                    uialign 0 -1
                    loop i $ui_profile_pattern_split [
                        ui_profile_pattern_id = (getpattern $ui_profile_pattern_inc 1)
                        ui_profile_pattern_name = (getpattern $ui_profile_pattern_inc 2)
                        uichecktext $ui_profile_pattern_name (= $ui_profile_pattern_sel $ui_profile_pattern_inc) $ui_buttonw $ui_buttonh [
                            ui_profile_pattern_sel = @ui_profile_pattern_inc
                            ui_profile_prev_pattern = $ui_profile_pattern_sel
                            uiclose "pattern"
                        ] $colouryellow $ui_default [uitooltip [profile_pattern_@[ui_profile_pattern_id]]]
                        ui_profile_pattern_inc = (+ $ui_profile_pattern_inc 1)
                    ]
                ]
                ui_profile_pattern_split = (- $numpatterns $ui_profile_pattern_inc)
            ]
        ]
    ]
] [] [] [] 2 [] 0 8

//arg1 key to highlight
ui_profile_welcome_keyboard = [
    uivlist $ui_padnormal [
        uihlist $ui_padsmall [
            uivlist $ui_padsmall [
                loop j $ui_binds_@[keyboardlayout]_rows [ //loops through keyboard rows
                    uihlist $ui_padsmall [ //hlist since the keyboard config is defined in rows
                        loop i (listlen $[ui_binds_@[keyboardlayout]_@j]) [ //loop through items in each configured row list
                            ui_binds_temp_key = (sublist $[ui_binds_@[keyboardlayout]_@j] $i 1)
                            //key rendering
                            if (& (!=s "_" $ui_binds_temp_key) (!=s "." $ui_binds_temp_key) ) [// "_" and "." are spacers;if found, do not attempt to uiimage and instead uifill 1 key width
                                if (< (strlen $ui_binds_temp_key) 3) [ //if the key is short (length <3 {includes alphanumeric chars}, it can automatically be rendered at normal size without checking (layouts with nonstandard alphanumeric keys will require this to be changed or omitted)
                                    uiimage (concatword "<grey>textures/keys/" $ui_binds_temp_key) (if (=s (listintersect $arg1 $ui_binds_temp_key) $ui_binds_temp_key) [result $colourred] [result $colourwhite]) 0 $ui_checksize $ui_checksize
                                ] [
                                    if (!= -1 (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] ) ) [ //if the key is found to be long make it larger
                                        l = (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] )
                                        uiimage (concatword "<grey>textures/keys/" $ui_binds_temp_key) (if (=s (listintersect $arg1 $ui_binds_temp_key) $ui_binds_temp_key) [result $colourred] [result $colourwhite]) 0 ( *f (sublist $ui_binds_@[keyboardlayout]_keyscale $l 1) $ui_checksize) $ui_checksize
                                    ] [ //else display standard size
                                        uiimage (concatword "<grey>textures/keys/" $ui_binds_temp_key) (if (=s (listintersect $arg1 $ui_binds_temp_key) $ui_binds_temp_key) [result $colourred] [result $colourwhite]) 0 $ui_checksize $ui_checksize
                                    ]
                                ]
                            ] [ //else fill in with the appropriate space
                                if (=s "_" (sublist $[ui_binds_@[keyboardlayout]_@j] $i 1)) [ //if "_", insert a full space
                                    uifill $ui_checksize $ui_checksize
                                ] [ //else insert a half space
                                    uifill (divf $ui_checksize 2) $ui_checksize
                                ]
                            ]
                        ]
                    ]
                ]
            ]
            uivlist $ui_padsmall [ //column for numpad vertical keys
                loop i (listlen $[ui_binds_@[keyboardlayout]_vert]) [
                    ui_binds_temp_key = (sublist $ui_binds_@[keyboardlayout]_vert $i 1)
                    if (& (!=s "_" $ui_binds_temp_key) (!=s "." $ui_binds_temp_key) ) [ // "_" and "." are spacers; does not attempt to uiimage and instead uifill 1 key width (for "_") and 1/2 key width (for ".")
                        if (!= -1 (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] ) ) [ //if the key is found to be long make it larger
                            l = (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key] )
                            uiimage (concatword "<grey>textures/keys/" $ui_binds_temp_key) (if (=s (listintersect $arg1 $ui_binds_temp_key) $ui_binds_temp_key) [result $colourred] [result $colourwhite]) 0 $ui_checksize ( *f (sublist $ui_binds_@[keyboardlayout]_keyscale $l 1) $ui_checksize)
                        ] [ //else display standard size
                            uiimage (concatword "<grey>textures/keys/" $ui_binds_temp_key) (if (=s (listintersect $arg1 $ui_binds_temp_key) $ui_binds_temp_key) [result $colourred] [result $colourwhite]) 0 $ui_checksize $ui_checksize
                        ]
                    ] [ //else fill in with the appropriate space
                        if (=s "_" (sublist $ui_binds_@[keyboardlayout]_vert $i 1)) [ //if "_", insert a full space
                            uifill $ui_checksize $ui_checksize
                        ] [ //else insert a half-space
                            uifill (divf $ui_checksize 2) $ui_checksize
                        ]
                    ]
                ]
            ]
        ]
        //mouse
        uihlist $ui_padsmall [
            uiimage "<grey>textures/keys/mouse1 (if (=s (listintersect $arg1 "mouse1") "mouse1") [result $colourred] [result $colourwhite]) 0 $ui_checksize $ui_checksize
            uiimage "<grey>textures/keys/mouse1 (if (=s (listintersect $arg1 "mouse2") "mouse1") [result $colourred] [result $colourwhite]) 0 $ui_checksize $ui_checksize
        ]
    ]
]

ui_profile_welcome = 1
ui_profile_welcome_tab = 0
ui_profile_welcome_max = 7
uimenu "welcome" "Welcome to Red Eclipse 2" $alerttex [
    uifill .6 .85
    uivlist $ui_padbutton [
        uialign 0 -1
        uifill .6 0
        case $ui_profile_welcome_tab 0 [
            uitext "Welcome to Red Eclipse ^f02^f7."
            uispace 0 $ui_padnormal
            uitext "Before you can play, you need set up your profile."
            uitext "These are local settings and this is not an account creation process."
            uitext "To get started, press the ^f0Next^f7 button."
            uitext "You can go back with the ^f3Back^f7 button to review previous information."
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 1 [
            uitext "First, you need an ingame name."
            uitext "This will be your handle for online play."
            uispace 0 $ui_padnormal
            uihlist 0 [
                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [uitext "Name" $ui_text [uicolourset (? (=s $ui_profile_prev_name "") (getclientrescolour (getplayercn) $pulseidxwarn) $colourwhite)]]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [
                            uiinput ui_profile_prev_name 16 [] 1 0 "[click here to set a name]" (? (=s $ui_profile_prev_name "") (getclientrescolour (getplayercn) $pulseidxwarn) $colourgrey) profile_name
                        ]
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 2 [
            uitext "Which player model would you like?"
            uispace 0 $ui_padnormal
            uihlist 0 [
                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [uitext "Model"]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [
                            uiradio "Male" (= $ui_profile_prev_model 0) $ui_radiosize [ui_profile_prev_model = 0] 0 0 [uitooltip welcome_model_male]
                            uiradio "Female" (= $ui_profile_prev_model 1) $ui_radiosize [ui_profile_prev_model = 1] 0 0 [uitooltip welcome_model_female]
                        ]
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 3 [
            uitext "Now, select a player colour."
            uitext "Selecting black (0x000000) will use the weapon palette colour."
            uihlist 0 [
                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [uitext "Colour"]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [
                            uihexpreview $ui_profile_prev_colour $ui_main0
                            uirgbsliders ui_profile_prev_colour
                        ]
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 4 [
            uitext "You can select a different colour pattern for your player model."
            uitext "This changes the arrangement of the player and team colours."
            uispace 0 $ui_padnormal
            uihlist 0 [
                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [uitext "Pattern"]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
                            uitooltip [profile_pattern]
                            uicursorset
                            uirelease [
                                ui_profile_pattern_sel = $ui_profile_prev_pattern
                                uiopen "pattern" 0
                            ]
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmaller [
                                uiclamp 1 1 1 1
                                uihlist $ui_padbutton [
                                    uitext (getpattern $ui_profile_prev_pattern 2)
                                ]
                            ]
                        ] [uiclamp 1 1 1 1] 1
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 5 [
            uitext "Select your main loadout weapons."
            uitext "You can change your spawn loadout at any time with [,] in game."
            uihlist 0 [
                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [uitext "Loadout"]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                    uiclamp 1 1 1 1
                    uispace $ui_padsmall $ui_padsmall [
                        uiclamp 1 1 1 1
                        uivlist $ui_padmini [
                            uiclamp 1 1 1 1
                            uihlist $ui_padmini [
                                ui_profile_loadweaplen = (listlen $ui_profile_prev_loadweap)
                                loop ui_profile_loadweapw2 $weapcarrydefault [
                                    ui_profile_loadweapw3 = (? (> $ui_profile_loadweaplen $ui_profile_loadweapw2) (at $ui_profile_prev_loadweap $ui_profile_loadweapw2) -1)
                                    uivlist 0 [
                                        uialign 0 0
                                        uispace $ui_padtiny $ui_padtiny [
                                            [ui_tip_profile_loadout_@[ui_profile_loadweapw2]] = [
                                                uitext (format "Click here to select a weapon for loadout slot ^fs^fy#%1^fS." @(+ $ui_profile_loadweapw2 1)) $ui_texttip
                                            ]
                                            if (<= $ui_profile_loadweapw3 0) [
                                                uivlist -0.025 [ //note that negative padding is to put the uitext over the button
                                                    uicheckbutton $questiontex (= $ui_profile_loadweapw3 0) $ui_hugesize [
                                                        ui_profile_loadout_sel = 0
                                                        ui_profile_loadout_type = $ui_profile_loadweapw2
                                                        uiopen "loadout" 0
                                                    ] $ui_default $ui_line [
                                                        uitooltip [profile_loadout_@[ui_profile_loadweapw2]]
                                                    ]
                                                    uitext "random"
                                                    uifill 0 0.035 //padding to stop the ui background from ending early
                                                ]
                                            ] [
                                                ui_profile_loadweapw4 = (at $weapname $ui_profile_loadweapw3)
                                                uivlist -.025 [ //also a negative padding here to put the uitext over the button
                                                    uicheckbutton [textures/weapons/@ui_profile_loadweapw4] 1 $ui_hugesize [
                                                        ui_profile_loadout_sel = $ui_profile_loadweapw3
                                                        ui_profile_loadout_type = $ui_profile_loadweapw2
                                                        uiopen "loadout" 0
                                                    ] $[@[ui_profile_loadweapw4]colour] $ui_line [
                                                        uitooltip [profile_loadout_@[ui_profile_loadweapw2]]
                                                    ]
                                                    uitext $ui_profile_loadweapw4
                                                    uifill 0 0.035 //padding to stop the ui background from ending early
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 6 [
            uitext "Select your random weapon filter."
            uitext "This allows you to conditionally enable certain weapons when"
            uitext "you opt to have one or both weapons set to ^"Random^""
            uispace 0 $ui_padnormal
            uitext "You can skip this page if you did not set any random weapon slots."
            uihlist 0 [
                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                    uiclamp 1 1 1 1
                    uispace $ui_padbutton $ui_padsmall [
                        uiclamp 1 1 1 1
                        uihlist $ui_padbutton [uitext "Random Filter"]
                    ]
                ] [uiclamp 1 1 1 1]
                uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                    uiclamp 1 1 1 1
                    uispace $ui_padsmall $ui_padsmall [
                        uiclamp 1 1 1 1
                        uivlist $ui_padmini [
                            uiclamp 1 1 1 1
                            uihlist $ui_padmini [
                                gdr = (listlen $ui_profile_prev_randweap)
                                loop ui_profile_loadweapw1 $weapidxloadout [
                                    ui_profile_loadweapw4 = (+ $ui_profile_loadweapw1 $weapidxoffset)
                                    ui_profile_loadweapw5 = (at $weapname $ui_profile_loadweapw4)
                                    ui_profile_loadweapw3 = (? (> $gdr $ui_profile_loadweapw1) (at $ui_profile_prev_randweap $ui_profile_loadweapw1) 1)
                                    uispace $ui_padtiny $ui_padtiny [
                                        uicheckbutton [textures/weapons/@ui_profile_loadweapw5] $ui_profile_loadweapw3 $ui_titlesize [
                                            rwv = (? (> (listlen $ui_profile_prev_randweap) @ui_profile_loadweapw1) (at $ui_profile_prev_randweap @ui_profile_loadweapw1) 1)
                                            ui_profile_randweaps @ui_profile_loadweapw1 (! $rwv)
                                        ] $[@[ui_profile_loadweapw5]colour] $ui_line [
                                            [ui_tip_profile_randweap_@[ui_profile_loadweapw4]] = [
                                                uitext (format "Click here to ^fa^fa%1^fS the ^fs^f[%2]%3^fS from the filter." (? @ui_profile_loadweapw3 "remove" "add") $[@@[ui_profile_loadweapw5]colour] $[@@[ui_profile_loadweapw5]longname]) $ui_texttip
                                                uitext "This will be used when you pick ^fs^fyRandom^fS for any of the weapon slots in the above ^fs^fyLoadout^fS section." $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                            ]
                                            uitooltip [profile_randweap_@[ui_profile_loadweapw4]]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ] [uiclamp 1 1 1 1]
            ]
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ] 7 [
            uitext "You've successfully created your game profile."
            uispace 0 $ui_padnormal
            uitext "To change your profile settings later,
            uitext "enter the ^f2Profile^f7 menu from the top of the main menu."
            uiplayerpreview 1 1 $ui_prevw $ui_prevh [
                playerpreviewmodel $ui_profile_prev_model
                playerpreviewpattern $ui_profile_prev_pattern
                playerpreviewcolour $ui_profile_prev_colour
                playerpreviewteam $ui_profile_prev_team
                playerpreviewweapselect $ui_profile_prev_weap
                playerpreviewvanity $ui_profile_prev_vanity
            ] [ui_playerprev_easteregg]
            uitext (getclientname)
        ]
        uialign 0 1
        uitextright (format "%1/%2" (+ $ui_profile_welcome_tab 1) (+ $ui_profile_welcome_max 1))
        if (= $ui_profile_welcome_max $ui_profile_welcome_tab) [
            uihlist $ui_padbutton [
                uialign 0 1
                uibuttonw "Back" [ui_profile_welcome_tab = (- $ui_profile_welcome_tab 1)] 0 $colourred
                uibuttonw "Close" [uiclose welcome; ui_profile_welcome = 0] 0 $colourgreen
            ]
        ] [
            uihlist $ui_padbutton [
                uialign 0 1
                if (!= $ui_profile_welcome_tab 0) [
                    uibuttonw "Back" [ui_profile_commit; ui_profile_welcome_tab = (- $ui_profile_welcome_tab 1)] 0 $colourred
                ]
                uibuttonw "Next" [ui_profile_commit; ui_profile_welcome_tab = (+ $ui_profile_welcome_tab 1)] 0 $colourgreen
            ]
        ]
    ]
    uialign 0 -1
] [
    uiclose profile //supress profile popup for incomplete profile
    ui_profile_prev_reset
    ui_profile_tab = 0
    ui_profile_weap = (at $ui_profile_prev_loadweap 0)
    if (>= $ui_profile_weap 0) [ui_profile_prev_weap = $ui_profile_weap]
    if (&& $ui_profile_welcome (=s $ui_profile_prev_name "")) [
        if (!=s $steamusername "") [ui_profile_prev_name = $steamusername]
        uiopen "welcome" 0
    ]
] [if $ui_profile_welcome [uiopen welcome] [uiopen controls]] [] 1

ui_profile_controls = 1
ui_profile_controls_tab = 0
ui_profile_controls_max = 10

uimenu "controls" "Controls Overview" $alerttex [
    uifill .7 .5
    uivlist $ui_padbutton [
        uifill .7 0
        case $ui_profile_controls_tab 0 [
            ui_profile_welcome_keyboard "w a s d left right up down q 1 2 3 4 e r t y lshift lctrl z x v b tab backquote escape f space"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "Red Eclipse has a lot of important keys to be aware of."
                    uitext "In this tutorial, you will learn to use them to be successful ingame."
                    uifill 0 $ui_padnormal
                    uitext "For a full menu explaining all keybinds"
                    uitext "turn on the advanced view in the keybinds menu."
                ] uialign -1 0
            ]
        ] 1 [
            ui_profile_welcome_keyboard "w a s d left right up down"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "The player can be moved around with the WASD and arrow keys."
                    uitext "Since most of the keybinds are proximate to the WASD keys,"
                    uitext "it is recommended that you use them instead of the arrow keys."
                ] uialign -1 0
            ]
        ] 2 [
            ui_profile_welcome_keyboard "space"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "To jump, press the spacebar."
                    uitext "Additional jump boosts can be carried out with additional presses."
                    uitext 0 $ui_padnormal
                    uitext "Jump boosts have their direction based upon directional keys pressed:"
                    uitextleft "* A/Left: Leftwards"
                    uitextleft "* D/Right: Rightwards"
                    uitextleft "* W/Up: Forwards"
                    uitextleft "* S/Down: Backwards"
                    uitextleft "* L Shift: Downwards"
                    uitextleft "* None: Upwards"
                ] uialign -1 0
            ]
        ] 3 [
            ui_profile_welcome_keyboard "q"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "To perform special actions, like parkour, use the Q key."
                    uifill 0 $ui_padnormal
                    uitext "The Q key can be used for the following:"
                    uitextleft "* Wall running: Look parallel to a wall after hitting it midair and press Q to run on it"
                    uitextleft "* Wall climbing: Look upwards and press Q while adjacent to a wall to climb it"
                    uitextleft "* Melee attacks: Press Q while midair to kick nearby opponents"
                ] uialign -1 0
            ]
        ] 4 [
            ui_profile_welcome_keyboard "lctrl lshift"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "To reduce movement speed and slightly improve accuracy, use the lctrl key to walk."
                    uitext "To reduce one's profile and improve accuracy, use the left shift key to crouch."
                    uifill 0 $ui_padnormal
                    uitext "The left shift key also can be used to perform impulse slides"
                    uitext "To slide, land from a jump while pressing the left shift key."
                    uitext "Starting a jump while in the process of a slide allows for larger jumps."
                ] uialign -1 0
            ]
        ] 5 [
            ui_profile_welcome_keyboard "mouse1 mouse2 r"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "The mouse buttons fire the weapons' primary and secondary fires."
                    uitext "Each weapon has two fire modes."
                    uifill 0 $ui_padnormal
                    uitext "The R key can be used to reload the weapon"
                    uitext "Automatic reloading can be enabled in the settings menu if desired."
                ] uialign -1 0
            ]
        ] 6 [
            ui_profile_welcome_keyboard "1 2 3"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "You selected weapons earlier in the profile setup."
                    uitext "In game, these can be quickly selected by the keys numbered 1, 2, and 3."
                    uitext "The pistol takes key 1, and the two main weapons take 2 and 3 respectively."
                    uitext 0 $ui_padnormal
                    uitext "These can be toggled through by the scroll wheel as well."
                ] uialign -1 0
            ]
        ] 7 [
            ui_profile_welcome_keyboard "4 5"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "Upon picking up explosives and mines, they will appear in slots 4 and 5."
                    uifill 0 $ui_padnormal
                    uitext "These items will also appear by using the scroll wheel."
                ] uialign -1 0
            ]
        ] 8 [
            ui_profile_welcome_keyboard "e f z"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "The E, F, and Z keys are used to interact with objects in game."
                    uifill 0 $ui_padnormal
                    uitext "* The E key picks up items such as ammo and explosives (mines and grenades)."
                    uitextleft "* The E key also is used to interact with ingame triggers."
                    uitextleft "* The F key picks up the flag in CTF from your own base."
                    uitextleft "* The Z key drops items, such as grenades and mines."
                ] uialign -1 0
            ]
        ] 9 [
            ui_profile_welcome_keyboard "backquote tab"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "The backquote key opens the command line for the game."
                    uifill 0 $ui_padnormal
                    uitext "The tab key opens the scoreboard."
                    uitext "Your score and those of the other players is listed here."
                ] uialign -1 0
            ]
        ] 10 [
            ui_profile_welcome_keyboard "x v b"
            uihlist 0 [
                uifill 0 .25
                uivlist 0 [
                    uitext "The X, V, and B keys control the voice compasses."
                    uitextleft "* The X key opens the team compass, for sending quick messages to teammates."
                    uitextleft "* The V key opens the voice compass, for broadcasting quick messages to all."
                    uitextleft "* The B key, when applicable, opens the bot command compass menu."
                ] uialign -1 0
            ]
        ]
        uialign 0 1
        uitextright (format "%1/%2" (+ $ui_profile_controls_tab 1) (+ $ui_profile_controls_max 1))
        if (= $ui_profile_controls_max $ui_profile_controls_tab) [
            uihlist $ui_padbutton [
                uialign 0 1
                uibuttonw "Back" [ui_profile_controls_tab = (- $ui_profile_controls_tab 1)] 0 $colourred
                uibuttonw "Close" [uiclose controls; ui_profile_controls = 0] 0 $colourgreen
            ]
        ] [
            uihlist $ui_padbutton [
                uialign 0 1
                if (!= $ui_profile_controls_tab 0) [
                    uibuttonw "Back" [ui_profile_controls_tab = (- $ui_profile_controls_tab 1)] 0 $colourred
                ]
                uibuttonw "Next" [ui_profile_controls_tab = (+ $ui_profile_controls_tab 1)] 0 $colourgreen
            ]
        ]
    ]
    uialign 0 1
] [] [if $ui_profile_controls [uiopen controls]] [] 1

ui_profile_prev_reset = [
    ui_profile_prev_team = (getplayerteam 1)
    ui_profile_prev_weap = (weapselect)
    ui_profile_prev_name = (getplayername)
    ui_profile_prev_colour = (getplayercolour -1)
    ui_profile_prev_model = (getplayermodel)
    ui_profile_prev_pattern = (getplayerpattern)
    ui_profile_prev_vanity = (getplayervanity)
    ui_profile_prev_loadweap = ""
    break = 0
    loopwhile i $weapcarrydefault [= $break 0] [
        q = (getloadweap $i)
        if (< $q 0) [break = 1] [ui_profile_prev_loadweap = (? $i (concat $ui_profile_prev_loadweap $q) $q)]
    ]
    ui_profile_prev_randweap = ""
    break = 0
    loopwhile i $weapidxloadout [= $break 0] [
        q = (getrandweap $i)
        ui_profile_prev_randweap = (? $i (concat $ui_profile_prev_randweap $q) $q)
    ]
    ui_profile_cur_name = $ui_profile_prev_name
    ui_profile_cur_colour = $ui_profile_prev_colour
    ui_profile_cur_model = $ui_profile_prev_model
    ui_profile_cur_pattern = $ui_profile_prev_pattern
    ui_profile_cur_vanity = $ui_profile_prev_vanity
    ui_profile_cur_loadweap = $ui_profile_prev_loadweap
    ui_profile_cur_randweap = $ui_profile_prev_randweap
]

ui_profile_commit = [
    if (=s $ui_profile_prev_loadweap "") [ui_profile_prev_loadweap = "0 0"]
    playername $ui_profile_prev_name
    playercolour $ui_profile_prev_colour
    playermodel $ui_profile_prev_model
    playerpattern $ui_profile_prev_pattern
    playervanity $ui_profile_prev_vanity
    playerloadweap $ui_profile_prev_loadweap
    playerrandweap $ui_profile_prev_randweap
]

ui_profilesave_reset = 0
uimenu "profilesave" "Save Player Profile" $alerttex [
    uivlist 0 [
        uitext "There are changes to your player profile!"
        uitext "Do you want to save these settings?"
        uispace 0 $ui_padbutton
        looplist ui_profilesave_setting [name colour model pattern vanity loadweap randweap] [
            if (!=s $[ui_profile_cur_@ui_profilesave_setting] $[ui_profile_prev_@ui_profilesave_setting]) [
                uispace $ui_padtiny 0 [uitext (format "^fy%1" $ui_profilesave_setting) $ui_textsmall]
                uispace $ui_padtiny $ui_padtiny [uitext $[ui_profile_prev_@ui_profilesave_setting] $ui_textsmall [uitextwrap 0.35]]
            ]
        ]
        uispace 0 $ui_padbutton
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "Save" [
                ui_profilesave_reset = 1
                ui_profile_commit
                uiclose "profilesave"
                uiclose "profile"
            ] 0 $colourgreen
            uibuttonw "Modify" [uiclose "profilesave"] 0 $colourorange
            uibuttonw "Discard" [
                ui_profilesave_reset = 1
                uiclose "profilesave"
                uiclose "profile"
            ] 0 $colourred
        ]
    ]
] [
    ui_profilesave_reset = 0
] [
    if $ui_profilesave_reset [ui_profile_prev_reset]
] [] 1 [uiclose "profilesave"; uiclose "profile"]

ui_tip_profile_name = [uitext "Click here to change your display name.^nThis is required in order to play." $ui_texttip]
ui_tip_profile_model_male = [uitext "Click here to change your display model to ^fs^fyMale^fS.^nYou can see how you look on the left." $ui_texttip]
ui_tip_profile_model_female = [uitext "Click here to change your display model to ^fs^fyFemale^fS.^nYou can see how you look on the left." $ui_texttip]
ui_tip_profile_pattern = [
    ui_profile_pattern_tex = (format "<crop:0,0,512,512><mix:255,0><thumbnail:128>%1" (getpattern $ui_profile_prev_pattern 0))
    ui_profile_pattern_name = (getpattern $ui_profile_prev_pattern 2)
    uispace $ui_padsmall $ui_padsmall [
        uivlist $ui_padsmaller [
            uitext (format "Your current pattern is: %1" $ui_profile_pattern_name) $ui_texttip
            uicolour (getteamcolour $ui_profile_prev_team) 0 0 [
                uiimage $ui_profile_pattern_tex $ui_profile_prev_colour 0 (*f $ui_imglist 2) (*f $ui_imglist 2) [uiimagealttex "textures/nothumb"; uioutline $ui_line 0 0 [uiclamp 1 1 1 1]]
            ]
            uitext "Click to change your display pattern." $ui_texttip
        ]
    ]
]

ui_profile_tab = 0
uimenu "profile" "Player Profile Setup" "<grey>textures/icons/player" [
    uihlist $ui_padbutton [
        uiplayerpreview 1 1 $ui_prevw $ui_prevh [
            playerpreviewmodel $ui_profile_prev_model
            playerpreviewpattern $ui_profile_prev_pattern
            playerpreviewcolour $ui_profile_prev_colour
            playerpreviewteam $ui_profile_prev_team
            playerpreviewweapselect $ui_profile_prev_weap
            playerpreviewvanity $ui_profile_prev_vanity
        ] [ui_playerprev_easteregg]
        uivlist 0 [
            uiclamp 1 1 1 1
            uihtab 0.5 0.45 .25 $ui_tabh ui_profile_tab [ // three magic numbers: .5 (x dimension of region) .45 (y dimension of region .25 (half of x dimension of region (so the two tabs fill the top of the region)
                0 "Settings" []
                1 "Vanity Items" []
            ] [
                case $ui_profile_tab 0 [
                    uitable 0 0 [
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Name" $ui_text [uicolourset (? (=s $ui_profile_prev_name "") (getclientrescolour (getplayercn) $pulseidxwarn) $colourwhite)]]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [
                                        uiinput ui_profile_prev_name 16 [] 1 0 "[click here to set a name]" (? (=s $ui_profile_prev_name "") (getclientrescolour (getplayercn) $pulseidxwarn) $colourgrey) profile_name
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Model"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [
                                        uiradio "Male" (= $ui_profile_prev_model 0) $ui_radiosize [ui_profile_prev_model = 0] 0 0 [uitooltip profile_model_male]
                                        uiradio "Female" (= $ui_profile_prev_model 1) $ui_radiosize [ui_profile_prev_model = 1] 0 0 [uitooltip profile_model_female]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Pattern"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
                                        uitooltip [profile_pattern]
                                        uicursorset
                                        uirelease [
                                            ui_profile_pattern_sel = $ui_profile_prev_pattern
                                            uiopen "pattern" 0
                                        ]
                                        uiclamp 1 1 1 1
                                        uispace $ui_padsmall $ui_padsmaller [
                                            uiclamp 1 1 1 1
                                            uihlist $ui_padbutton [
                                                uitext (getpattern $ui_profile_prev_pattern 2)
                                            ]
                                        ]
                                    ] [uiclamp 1 1 1 1] 1
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Colour"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [
                                        uihexpreview $ui_profile_prev_colour $ui_main0
                                        uirgbsliders ui_profile_prev_colour
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Loadout"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padsmall $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uivlist $ui_padmini [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padmini [
                                            ui_profile_loadweaplen = (listlen $ui_profile_prev_loadweap)
                                            loop ui_profile_loadweapw2 $weapcarrydefault [
                                                ui_profile_loadweapw3 = (? (> $ui_profile_loadweaplen $ui_profile_loadweapw2) (at $ui_profile_prev_loadweap $ui_profile_loadweapw2) -1)
                                                uivlist 0 [
                                                    uialign 0 0
                                                    uispace $ui_padtiny $ui_padtiny [
                                                        [ui_tip_profile_loadout_@[ui_profile_loadweapw2]] = [
                                                            uitext (format "Click here to select a weapon for loadout slot ^fs^fy#%1^fS." @(+ $ui_profile_loadweapw2 1)) $ui_texttip
                                                        ]
                                                        if (<= $ui_profile_loadweapw3 0) [
                                                            uivlist -0.025 [ //note that negative padding is to put the uitext over the button
                                                                uicheckbutton $questiontex (= $ui_profile_loadweapw3 0) $ui_hugesize [
                                                                    ui_profile_loadout_sel = 0
                                                                    ui_profile_loadout_type = $ui_profile_loadweapw2
                                                                    uiopen "loadout" 0
                                                                ] $ui_default $ui_line [
                                                                    uitooltip [profile_loadout_@[ui_profile_loadweapw2]]
                                                                ]
                                                                uitext "random"
                                                                uifill 0 0.035 //padding to stop the ui background from ending early
                                                            ]
                                                        ] [
                                                            ui_profile_loadweapw4 = (at $weapname $ui_profile_loadweapw3)
                                                            uivlist -.025 [ //also a negative padding here to put the uitext over the button
                                                                uicheckbutton [textures/weapons/@ui_profile_loadweapw4] 1 $ui_hugesize [
                                                                    ui_profile_loadout_sel = $ui_profile_loadweapw3
                                                                    ui_profile_loadout_type = $ui_profile_loadweapw2
                                                                    uiopen "loadout" 0
                                                                ] $[@[ui_profile_loadweapw4]colour] $ui_line [
                                                                    uitooltip [profile_loadout_@[ui_profile_loadweapw2]]
                                                                ]
                                                                uitext $ui_profile_loadweapw4
                                                                uifill 0 0.035 //padding to stop the ui background from ending early
                                                            ]
                                                        ]
                                                    ]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Random Filter"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padsmall $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uivlist $ui_padmini [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padmini [
                                            gdr = (listlen $ui_profile_prev_randweap)
                                            loop ui_profile_loadweapw1 $weapidxloadout [
                                                ui_profile_loadweapw4 = (+ $ui_profile_loadweapw1 $weapidxoffset)
                                                ui_profile_loadweapw5 = (at $weapname $ui_profile_loadweapw4)
                                                ui_profile_loadweapw3 = (? (> $gdr $ui_profile_loadweapw1) (at $ui_profile_prev_randweap $ui_profile_loadweapw1) 1)
                                                uispace $ui_padtiny $ui_padtiny [
                                                    uicheckbutton [textures/weapons/@ui_profile_loadweapw5] $ui_profile_loadweapw3 $ui_titlesize [
                                                        rwv = (? (> (listlen $ui_profile_prev_randweap) @ui_profile_loadweapw1) (at $ui_profile_prev_randweap @ui_profile_loadweapw1) 1)
                                                        ui_profile_randweaps @ui_profile_loadweapw1 (! $rwv)
                                                    ] $[@[ui_profile_loadweapw5]colour] $ui_line [
                                                        [ui_tip_profile_randweap_@[ui_profile_loadweapw4]] = [
                                                            uitext (format "Click here to ^fa^fa%1^fS the ^fs^f[%2]%3^fS from the filter." (? @ui_profile_loadweapw3 "remove" "add") $[@@[ui_profile_loadweapw5]colour] $[@@[ui_profile_loadweapw5]longname]) $ui_texttip
                                                            uitext "This will be used when you pick ^fs^fyRandom^fS for any of the weapon slots in the above ^fs^fyLoadout^fS section." $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                                        ]
                                                        uitooltip [profile_randweap_@[ui_profile_loadweapw4]]
                                                    ]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                        uitablerow [
                            uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uihlist $ui_padbutton [uitext "Options"]
                                ]
                            ] [uiclamp 1 1 1 1]
                            uiskinborder 0 0 $ui_main1 $ui_main1 $ui_main1 $ui_main0 $ui_main0 $ui_main0 [
                                uiclamp 1 1 1 1
                                uispace $ui_padsmall $ui_padsmall [
                                    uiclamp 1 1 1 1
                                    uivlist $ui_padmini [
                                        uiclamp 1 1 1 1
                                        uihlist 0 [
                                            uispace $ui_padtiny $ui_padsmall [uicheckbox "Show this menu on map start" (= $showloadoutmenu 1) $ui_checksize [showloadoutmenu = (! $showloadoutmenu)] $colourred $colourmagenta $ui_default]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1]
                        ]
                    ]
                ] 1 [
                    uitable 0 0 [
                        loop i (listlen $vanitynames) [
                            r = ""
                            loop j (getvanity) [
                                if (= (getvanity $j 0) $i) [
                                    if (< -1 (indexof  $ui_profile_prev_vanity (getvanity $j 1))) [
                                        r = (getvanity $j 2)
                                    ]
                                ]
                            ]
                            uitablerow [
                                uiskinborder 0 0 $ui_main2 $ui_main2 $ui_main2 $ui_main1 $ui_main1 $ui_main1 [
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [uitext (at $vanitynames $i)]
                                    ]
                                ] [uiclamp 1 1 1 1]
                                uiskinborder 0.25 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
                                    [ui_tip_profile_vanity_@[i]] = [uitext (format "Click here change the vanity item for your ^fs^fy%1^fS." (at $vanitynames @i)) $ui_texttip]
                                    uitooltip [profile_vanity_@[i]]
                                    uicursorset
                                    uirelease [
                                        ui_profile_vanity_sel = -1
                                        ui_profile_vanity_type = $i
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $i) [
                                                if (< -1 (indexof  $ui_profile_prev_vanity (getvanity $j 1))) [ui_profile_vanity_sel = $j]
                                            ]
                                        ]
                                        uiopen "vanity" 0
                                    ]
                                    uiclamp 1 1 1 1
                                    uispace $ui_padbutton $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uihlist $ui_padbutton [
                                            if (=s $r "") [uitext "^fa[click here to select a vanity item]" $ui_texttiny] [uitext $r]
                                        ]
                                    ]
                                ] [uiclamp 1 1 1 1] 1
                            ]
                        ]
                    ]
                ]
            ]
            uispace 0 $ui_padsmall
            uihlist $ui_padbutton [
                uialign 0 1
                uibuttonw "OK" [ui_profile_commit; uiclose "profile"] (=s $ui_profile_prev_name "") $colourgreen
                uibuttonw "Reset" [ui_profile_prev_reset] 0 $colourorange
                uibuttonw "Cancel" [
                    ui_profile_haschanges = 0
                    looplist i [name colour model pattern vanity loadweap randweap] [
                        if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [ui_profile_haschanges = 1]
                    ]
                    if $ui_profile_haschanges [uiopen "profilesave" 0] [uiclose "profile"]
                ] (needname) $colourred
            ]
        ]
    ]
] [
    ui_profile_prev_reset
    ui_profile_tab = 0
    ui_profile_weap = (at $ui_profile_prev_loadweap 0)
    if (>= $ui_profile_weap 0) [ui_profile_prev_weap = $ui_profile_weap]
    if (&& $ui_profile_welcome (=s $ui_profile_prev_name "")) [
        if (!=s $steamusername "") [ui_profile_prev_name = $steamusername]
        uiopen "welcome" 0
    ]
] [] [] [] [
    ui_profile_haschanges = 0
    looplist i [name colour model pattern vanity loadweap randweap] [
        if (!=s $[ui_profile_cur_@i] $[ui_profile_prev_@i]) [ui_profile_haschanges = 1]
    ]
    if $ui_profile_haschanges [uiopen "profilesave" 0] [uiclose "profile"]
] [needname]
