ui_tool_edit_bind_text_width = 0.4
ui_tool_edit_bind_field_width = 0.2

tool_bind_search_query = ""

defvarp toolfreevaluescroll 0 0 1

// 1:<action id> 2:<query>
tool_action_filter = [
    > (strcasestr (at $$arg1 $TA_PROP_SHORT_DESC) $arg2) -1
]

// 1:<control> 2:<query> 3:<category>
tool_control_filter = [
    local _match _qtoken _tokens
    _match  = (|| [
        > (strcasestr (at $arg1 $TC_PROP_DESC) $arg2 -1)
    ] [
        > (strcasestr $arg3 $arg2) -1
    ])

    _qtoken = ""
    _tokens = []

    if $_match [] [
        // tokenized search, must include all
        _tokens = (listunion (at $arg1 $TC_PROP_DESC) (at $arg1 $TC_PROP_TAGS))
        _match  = 1
        loopwhile i (listlen $arg2) [$_match] [
            _qtoken = (at $arg2 $i)
            if (< (listfind _token $_tokens [> (strcasestr $_token $_qtoken) -1]) 0) [
                // token not found
                _match = 0
            ]
        ]
    ]

    result $_match
]

// 1:<action id>
ui_tool_edit_cat_bind = [
    local _action_type _desc
    _action_type = (at $$arg1 $TA_PROP_TYPE)
    _desc = (at $$arg1 $TA_PROP_LONG_DESC)

    uihlist 0 [
        uitarget $ui_tool_edit_bind_text_width 0 [
            uitext (at $$arg1 $TA_PROP_SHORT_DESC) $ui_tool_text_size_s
            uiprev [uialign -1]
            ui_tool_tip [
                p_tip_simple = [@_desc]
            ]
        ]

        if (= $_action_type $TA_TYPE_SCROLL) [
            uitarget 0 0 [
                uiimage (concatword $textkeyprefix "mouse4") $ui_tool_accent_colour 0 (*f $ui_tool_icon_size_m 0.8) $ui_tool_icon_size_m
                ui_tool_tip [
                    p_tip_simple = "Mouse scroll action"
                ]
            ]
        ]
    ]

    uihlist 0 [
        uistyle clampx
        ui_tool_keyinput [tb_@arg1] [
            p_var_update = 0
            p_bind_command = toolbind
            p_bind_action = @arg1
            p_width = $ui_tool_edit_bind_field_width
        ]
        ui_tool_button [
            p_label = "X"
            p_tip_simple = "Clear bind"
            p_on_click = [
                toolunbind @arg1
            ]
        ]
        uiprev [uistyle clampx]
    ]
    uiprev [uialign 1]

    uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]
    uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]
]

// 1:<category>
ui_tool_edit_cat_bindings = [
    local _num_actions _action
    _num_actions = (listlen $[tool_actions_@arg1])

    uigrid 2 $ui_tool_elem_space_l 0 [
        uistyle clampx
        loop i $_num_actions [
            _action = (at $[tool_actions_@arg1] $i)

            if (|| (=s tool_bind_search_query "") [tool_action_filter $_action $tool_bind_search_query]) [
                ui_tool_edit_cat_bind $_action
            ]
        ]
    ]
]

ui_tool_edit_binding_presets = [
    uivlist 0 [
        uistyle clampx

        ui_tool_button [
            p_label = "Legacy"
            p_tip_simple = "Provides old-style, legacy editor key binds"
            p_on_click = [
                toolpanel_close_this
                exec "config/tool/binds/legacy.cfg"
            ]
        ]

        ui_tool_button [
            p_label = "Default"
            p_tip_simple = "Provides new editor key binds, tailored for the overhauled workflow"
            p_on_click = [
                toolpanel_close_this
                exec "config/tool/binds/default.cfg"
            ]
        ]

        uipropchild [uistyle clampx]
    ]
]

# ui_tool_edit_bindings = [
    local _num_cats _cat
    _num_cats = (listlen $tool_action_categories)

    uivlist $ui_tool_elem_space_l [
        ui_tool_button [
            p_label = "Presets"
            p_on_click = [
                toolpanel_open tool_edit_binding_presets popup [
                    p_position = (uicursorpos)
                ]
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        uihlist 0 [
            uitext "Search: " $ui_tool_text_size_s
            uiinput tool_bind_search_query 32 [] $ui_toolpanel_text_size 0 "[search for action here]"
        ]

        uipropchild [uistyle clampx]

        ui_tool_vscrollarea [
            uivlist 0 [
                uistyle clampx
                loop i $_num_cats [
                    _cat = (at $tool_action_categories $i)
                    uifill 0 $ui_tool_elem_space_l
                    ui_tool_collapsegroup [tool_bind_group_@_cat] [
                        ui_tool_edit_cat_bindings $_cat
                    ] [
                        p_label = $_cat
                        p_force_open = @(!=s $tool_bind_search_query "")
                    ]
                ]
            ]
        ] [
            p_height = 0.5
            p_id     = #(tool_get_id)
        ]
    ]
]

# ui_tool_edit_settings = [
    uivlist $ui_toolpanel_elem_space [
        uistyle clampx
        ui_tool_var_input "Undo size (MB):" undomegs 8 [
            p_id = #(tool_get_id)
        ]
        #(tool_edit_register_control "Undo size" "buffer")

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        ui_tool_var_input "Float speed:" floatspeed 50 [
            p_immediate = 1
            p_id        = #(tool_get_id)
        ]
        #(tool_edit_register_control "Float speed" "flying flight")

        ui_tool_var_input "Fast float speed:" shiftfloatspeed 50 [
            p_immediate = 1
            p_id        = #(tool_get_id)
        ]
        #(tool_edit_register_control "Fast float speed" "flying flight shift")

        ui_tool_var_input "Float coast:" floatcoast 1 [
            p_immediate = 1
            p_id        = #(tool_get_id)
        ]
        #(tool_edit_register_control "Float coast" "flying flight drag air")

        ui_tool_var_input "Edit FOV:" editfov 10 [
            p_immediate = 1
            p_id        = #(tool_get_id)
        ]
        #(tool_edit_register_control "Edit FOV" "field vision")

        ui_tool_checkbox nompedit [
            p_label      = "Lock non-multiplayer functionality"
            p_tip_simple = "Disables edit functions which cannot be synchronized in multiplayer"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Lock non-multiplayer functionality" "multiplayer synchronization")

        ui_tool_var_input "Entity sel. box size" entselradius 1 [
            p_immediate = 1
            p_id        = #(tool_get_id)
        ]
        #(tool_edit_register_control "Entity sel. box size" "selection bounding")

        ui_tool_checkbox entmoveselect [
            p_label      = "Add entities to selection on grab"
            p_tip_simple = "Adds hovered entities to move selection with already having entities selected"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Add entities to selection on grab" "move hovered")

        ui_tool_checkbox enteditingonpanel [
            p_label      = "Entity edit only with panel open"
            p_tip_simple = "Requires the entity panel to be open to allow for entity editing"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Entity edit only with panel open" "editing ui lock")

        ui_tool_checkbox passthroughentcancel [
            p_label      = "Cancel entity selection in passthrough"
            p_tip_simple = "Deselects entities when engaging passthrough mode"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Cancel entity selection in passthrough" "selection")

        ui_tool_checkbox selectionoffset [
            p_label      = "Offset selection grid"
            p_tip_simple = "Offsets the selection grid from the surfaces"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Offset selection grid" "selection")

        ui_tool_checkbox texeditdefaultautoapply [
            p_label      = "Default auto-apply during texture edit"
            p_tip_simple = "Automatically enables auto-apply for the duration of editing texture variants"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Default auto-apply during texture edit")

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        ui_tool_var_input "Edit UI scale:" uitoolscale 10 [
            p_val_format = i
            p_get        = [ toint (round (*f $arg1 100)) ]
            p_set        = [ divf $arg1 100 ]
            p_val_text   = [ concatword $arg1 "^%" ]
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Edit UI scale" "size")

        ui_tool_checkbox toolinfoactionsound [
            p_label      = "Edit info notification sound"
            p_tip_simple = "Play a notification sound whenever edit information bar is shown"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Edit info notification sound" "information message chime popup")

        ui_tool_checkbox toolinfoextendedstatus [
            p_label      = "Show extended render status"
            p_tip_simple = "Display extended renderer information in the status bar"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Show extended render status" "information rendering 3d resources")

        ui_tool_var_input "Edit Info length" toolinfoactionlength 1 [
            p_val_format = f
            p_get        = [ divf $arg1 1000 ]
            p_set        = [ toint (round (*f $arg1 1000)) ]
            p_val_text   = [ concat $arg1 "s" ]
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Edit Info length" "information message popup")

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        ui_tool_checkbox toolfreevaluescroll [
            p_label      = "Free value scrolling"
            p_tip_simple = "When enabled, allows scrolling of values on supported widgets without holding the shift key.^nCan get in the way of scrolling displayed contents, use at your discretion."
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Free value scrolling" "widget control")

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        ui_tool_checkbox autoshowblendmap [
            p_label      = "Auto-prepare blendmap painting"
            p_tip_simple = "Automatically prepares map geometry when entering blendmap painting mode"
            p_id         = #(tool_get_id)
        ]
        #(tool_edit_register_control "Auto-prepare blendmap painting")

        ui_tool_checkbox disableblendpaintonuiclose [
            p_label      = "Disable blend paining when exiting painting panel"
            p_tip_simple = "Automatically disables blend painting mode when closing the blendmap painting panel"
            p_id         = #(tool_get_id)
            #(tool_edit_register_control "Disable blend paining when exiting painting panel" "blendmap ui")
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        #(ui_tool_get_action_button ta_edit_bindings)
        uiprev [uistyle clampx]

        uipropchild [uialign -1 -1]
    ]
]

TOOL_SEARCH_MAX_RESULTS = 20
TOOL_SEARCH_PER_FRAME   = 8

tool_search_proc_active = 0

tool_serach_clear_results = [
    tool_search_did_nav              = 0
    tool_search_results_actions      = []
    tool_search_results_controls     = []
    tool_search_results_num_actions  = 0
    tool_search_results_num_controls = 0
    tool_search_nav_index            = 0
    tool_search_has_more_results     = 0
    tool_search_total_results        = 0

    tool_search_proc_actions         = 0
    tool_search_proc_controls        = 0
    tool_search_proc_control_cats    = 0
]

tool_search_init = [
    tool_search_query = ""
    tool_serach_clear_results

]
tool_search_init

tool_search_begin = [
    tool_serach_clear_results

    if $tool_search_proc_active [] [
        tool_search_proc_active = 1
        tool_proc_add tool_search_proc
    ]
]

tool_search_shift_nav = [
    if (&& $tool_search_did_nav [>= $tool_search_nav_index $tool_search_results_num_actions]) [
        tool_search_nav_index = (+ $tool_search_nav_index 1)
    ]
]

tool_search_proc = [
    local _done_actions _done_controls _loop _iters_actions _iters_controls _control_cat _num_controls _controls_var _control
    _done_actions   = 1
    _done_controls  = 1
    _loop           = 1
    _iters_actions  = (min $tool_num_actions (+ $tool_search_proc_actions $TOOL_SEARCH_PER_FRAME))
    _iters_controls = 0

    if (!=s $tool_search_query "") [
        if (< $tool_search_total_results (+ $TOOL_SEARCH_MAX_RESULTS 1)) [
            if (< $tool_search_proc_actions $tool_num_actions) [
                _done_actions = 0
            ]

            if (< $tool_search_proc_control_cats $tool_num_control_categories) [
                _done_controls = 0
            ]
        ]

        while [&& [
            < $tool_search_proc_actions $_iters_actions
        ] [
            < $tool_search_total_results (+ $TOOL_SEARCH_MAX_RESULTS 1)
        ]] [
            local _action
            _action = (at $tool_actions $tool_search_proc_actions)

            if (tool_action_filter $_action $tool_search_query) [
                if (< $tool_search_total_results $TOOL_SEARCH_MAX_RESULTS) [
                    tool_search_shift_nav
                    append tool_search_results_actions $_action
                    tool_search_results_num_actions = (+ $tool_search_results_num_actions 1)
                ]
                tool_search_total_results = (+ 1 $tool_search_total_results)
            ]

            tool_search_proc_actions = (+ $tool_search_proc_actions 1)
        ]

        _loop = (! $_done_controls)
        while [$_loop] [
            _control_cat  = (at $tool_control_categories $tool_search_proc_control_cats)
            _num_controls = $[tool_num_controls_@_control_cat]
            _controls_var = [tool_controls_@_control_cat]
            _control      = (at $$_controls_var $tool_search_proc_controls)

            if (tool_control_filter $_control $tool_search_query $_control_cat) [
                if (< $tool_search_total_results $TOOL_SEARCH_MAX_RESULTS) [
                    append tool_search_results_controls [[@@tool_search_proc_control_cats @@tool_search_proc_controls]]
                    tool_search_results_num_controls = (+ $tool_search_results_num_controls 1)
                ]
                tool_search_total_results = (+ 1 $tool_search_total_results)
            ]

            tool_search_proc_controls = (+ $tool_search_proc_controls 1)

            if (>= $tool_search_proc_controls $_num_controls) [
                // finished searching through all controls in this category
                tool_search_proc_controls     = 0
                tool_search_proc_control_cats = (+ $tool_search_proc_control_cats 1)
            ]

            _iters_controls = (+ $_iters_controls 1)

            if (|| [
                >= $_iters_controls $TOOL_SEARCH_PER_FRAME
            ] [
                // We want at least one more than max, to know if there are more results
                > $tool_search_total_results $TOOL_SEARCH_MAX_RESULTS
            ] [
                >= $tool_search_proc_control_cats $tool_num_control_categories
            ]) [
                _loop = 0
            ]
        ]
    ]

    if (&& $_done_actions $_done_controls) [
        tool_search_proc_active = 0
        tool_proc_remove tool_search_proc

        tool_search_has_more_results = (> $tool_search_total_results $TOOL_SEARCH_MAX_RESULTS)
        tool_search_total_results    = (min $tool_search_total_results $TOOL_SEARCH_MAX_RESULTS)
    ]
]

ui_tool_search_input = [
    local _icon
    _icon = (? $tool_search_proc_active $uiloadtex "<grey>textures/icons/edit/find")

    uihlist $ui_toolpanel_elem_space [
        uistyle clampx
        uiimage $_icon $ui_tool_accent_colour 0 $ui_tool_icon_size_m $ui_tool_icon_size_m
        ui_tool_textinput tool_search_query 32 [
            p_label     = ""
            p_prompt    = "[Search for action here]"
            p_on_change = [
                tool_search_begin
            ]
            p_focus     = (&& $tool_search_focus $ui_freecursor)
            p_immediate = 0
        ]

        if $tool_search_focus [
            tool_search_focus = 0
        ]
    ]
]

# ui_tool_search_list = [
    if $toolpanel_this_isinit [
        tool_search_nav_index = -1
    ]

    loop i $tool_search_results_num_actions [
        local _action _action_type _cat _label _keys _bind_info
        _action      = (at $tool_search_results_actions $i)
        _action_type = (at $$_action $TA_PROP_TYPE)
        _cat         = (at $$_action $TA_PROP_CATEGORY)
        _label       = (at $$_action $TA_PROP_SHORT_DESC)
        _keys        = $[tb_@_action]
        _bind_info   = (prettybindinfo (at $_keys 0) (at $_keys 1))

        if (= $i $tool_search_nav_index) uisethighlight

        ui_tool_button [
            p_children = [
                uihlist $ui_tool_elem_space_s [
                    if (= $_action_type $TA_TYPE_SCROLL) [
                        uiimage (concatword $textkeyprefix "mouse5") $ui_tool_accent_colour 0 (*f $ui_tool_icon_size_s 0.8) $ui_tool_icon_size_s
                        _bind_info = (concatword "+" $_bind_info)
                    ]
                    if @_bind_info [
                        uicolourtext [@_bind_info] $ui_tool_warn_colour $ui_tool_text_size_xs
                        uitriangle $ui_tool_dark_accent_colour $ui_tool_separator_size $ui_tool_separator_size -90
                    ]
                    uicolourtext @_cat $ui_tool_dark_accent_colour $ui_tool_text_size_xs
                ]
                uiprev [uialign 1]
            ]
            p_label       = [@_label]
            p_label_size  = $ui_tool_text_size_xs_unscaled
            p_label_align = -1
            p_tip_action  = $_action
            p_on_click    = [
                toolpanel_close tool_search
                tool_do_action @_action $TA_CTX_SEARCH
            ]
        ]
        uiprev [uistyle clampx]
    ]


    loop i $tool_search_results_num_controls [
        local _result _control_cat _control_idx _controls_var _control _label
        _result       = (at $tool_search_results_controls $i)
        _control_cat  = (at $tool_control_categories (at $_result 0))
        _control_idx  = (at $_result 1)
        _controls_var = [tool_controls_@_control_cat]

        _control      = (at $$_controls_var $_control_idx)
        _label        = (at $_control $TC_PROP_DESC)

        if (= (+ $i $tool_search_results_num_actions) $tool_search_nav_index) uisethighlight

        ui_tool_button [
            p_children    = [
                uihlist $ui_tool_elem_space_s [
                    uicolourtext "Go to" $ui_tool_warn_colour $ui_tool_text_size_xs
                    uitriangle $ui_tool_dark_accent_colour $ui_tool_separator_size $ui_tool_separator_size -90
                    uicolourtext @_control_cat $ui_tool_dark_accent_colour $ui_tool_text_size_xs
                ]
                uiprev [uialign 1]
            ]
            p_label       = [@_label]
            p_label_size  = $ui_tool_text_size_xs_unscaled
            p_label_align = -1
            p_on_click    = [
                toolpanel_close tool_search
                tool_goto_control @_control_idx @_control_cat
            ]
        ]
        uiprev [uistyle clampx]
    ]

    if $tool_search_has_more_results [
        local _txt
        _txt = (concat "Search limited to" $TOOL_SEARCH_MAX_RESULTS "results")
        uifill 0 $ui_tool_elem_space_l
        uicolourtext $_txt $ui_tool_dark_accent_colour $ui_tool_text_size_s
    ]
]

tool_search_nav_confirm = [
    local _index _result _control_cat _control_idx
    _index       = $tool_search_nav_index
    _control_idx = -1

    if (< $_index $tool_search_results_num_actions) [
        toolpanel_close tool_search
        tool_do_action (at $tool_search_results_actions $_index) $TA_CTX_SEARCH
    ] [
        _index = (- $_index $tool_search_results_num_actions)
        if (< $_index $tool_search_results_num_controls) [
            _result      = (at $tool_search_results_controls $_index)
            _control_idx = (at $_result 1)
            _control_cat = (at $tool_control_categories (at $_result 0))

            toolpanel_close tool_search
            tool_goto_control $_control_idx $_control_cat
        ]
    ]
]

// 1:<type> 2:<param>
ui_tool_search_nav = [
    case $arg1 @TOOLPANEL_NAV_V [
        local _prev_nav
        _prev_nav             = $tool_search_nav_index
        tool_search_nav_index = (- $tool_search_nav_index $arg2)
        tool_search_nav_index = (clamp $tool_search_nav_index 0 (- $tool_search_total_results 1))

        if (!= $_prev_nav $tool_search_nav_index) [
            tool_search_did_nav = 1
        ]
    ] @TOOLPANEL_NAV_ENTER [
        tool_search_nav_confirm
    ] @TOOLPANEL_NAV_ESCAPE [
        toolpanel_close tool_search
    ]
]

ui_tool_search = [
    uivlist 0 [
        uistyle clampx
        ui_tool_search_input
        uifill 0 $ui_toolpanel_elem_space
        uiline $ui_tool_accent_colour 0 0 [ uistyle clampx ]
        uifill 0 $ui_toolpanel_elem_space
        ui_tool_search_list
    ]
]
