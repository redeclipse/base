ui_tool_edit_bind_text_width = 0.4
ui_tool_edit_bind_field_width = 0.2

// 1:<category>
ui_tool_edit_cat_bindings = [
    local num_actions action_idx action action_type
    num_actions = (listlen $[tool_actions_@arg1])

    uigrid 2 $ui_toolview_base_elem_space 0 [
        uiclamp 1 1
        loop i $num_actions [
            action_idx = (at $[tool_actions_@arg1] $i)
            action = (at $tool_actions $action_idx)
            action_type = @(tool_action_prop [$action] type)

            uihlist 0 [
                uitarget $ui_tool_edit_bind_text_width 0 [
                    uitext @(tool_action_prop [$action] short_desc) $ui_toolview_small_text_size
                    uialign- -1
                    ui_tool_tip [
                        uit_tip_simple = @(tool_action_prop [$action] long_desc)
                    ]
                ]

                if (= $action_type $TOOL_ACTION_SCROLL) [
                    uitarget 0 0 [
                        uiimage "textures/keys/mouse5" $ui_toolview_accent_colour 0 (*f $ui_toolview_base_icon_size 0.8) $ui_toolview_base_icon_size
                        ui_tool_tip [
                            uit_tip_simple = "Mouse scroll action"
                        ]
                    ]
                ]
            ]

            uihlist 0 [
                uiclamp 1 1
                ui_tool_keyinput [tb_@action] [
                    uit_var_update = 0
                    uit_bind_command = toolbind
                    uit_bind_action = @action
                    uit_width = $ui_tool_edit_bind_field_width
                ]
                ui_tool_button [
                    uit_label = "X"
                    uit_tip_simple = "Clear bind"
                    uit_on_click = [
                        toolunbind @action
                    ]
                ]
                uiclamp- 1 1
            ]
            uialign- 1

            uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
            uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        ]
    ]
]

ui_tool_edit_bindings = [
    local num_cats cat
    num_cats = (listlen $tool_actions_categories)

    ui_tool_vscrollarea [
        uivlist 0 [
            uiclamp 1 1
            loop i $num_cats [
                cat = (at $tool_actions_categories $i)
                uifill 0 $ui_toolview_base_elem_space
                ui_tool_collapsegroup [tool_bind_group_@cat] [
                    ui_tool_edit_cat_bindings $cat
                ] [
                    uit_label = $cat
                ]
            ]
        ]
    ] [
        uit_height = 0.5
    ]
]

ui_tool_edit_settings = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_var_input "Undo size (MB):" undomegs 8

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_var_input "Float speed:" floatspeed 50 [
            uit_immediate = 1
        ]

        ui_tool_var_input "Fast float speed:" shiftfloatspeed 50 [
            uit_immediate = 1
        ]

        ui_tool_var_input "Float coast:" floatcoast 1 [
            uit_immediate = 1
        ]

        ui_tool_var_input "Edit FOV:" editfov 10 [
            uit_immediate = 1
        ]

        ui_tool_checkbox nompedit [
            uit_label = "Lock non-multiplayer functionality"
            uit_tip_simple = "Disables edit functions which cannot be synchronized in multiplayer"
        ]

        ui_tool_checkbox entmodifierdrag [
            uit_label = "Entity modifier drag"
            uit_tip_simple = "Requires G key to be held to drag entities if enabled"
        ]

        ui_tool_checkbox passthroughentcancel [
            uit_label = "Cancel entity selection in passthrough"
            uit_tip_simple = "Deselects entities when engaging passthrough mode"
        ]

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_var_input "Edit UI text scale:" uitooltextscale 0.1

        ui_tool_checkbox toolinfoactionsound [
            uit_label = "Edit info notification sound"
            uit_tip_simple = "Play a notification sound whenever edit information bar is shown"
        ]

        ui_tool_checkbox toolinfoextendedstatus [
            uit_label = "Show extended render status"
            uit_tip_simple = "Display extended renderer information in the status bar"
        ]

        ui_tool_var_input "Edit Info length (ms)" toolinfoactionlength 1000

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_checkbox autoshowblendmap [
            uit_label = "Auto-prepare blendmap painting"
            uit_tip_simple = "Automatically prepares world geometry when entering blendmap painting mode"
        ]

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_button [
            uit_label = @(tool_action_prop ta_edit_bindings short_desc)
            uit_tip_action = ta_edit_bindings
            uit_on_click = [
                tool_do_action ta_edit_bindings
            ]
        ]
        uiclamp- 1 1

        uialign* -1 -1
    ]
]

tool_search_max_results = 10

tool_search_query = ""
tool_search_results = []
tool_search_num_actions = 0
tool_search_action_idx = 0
tool_search_has_more_results = 0

tool_search_init = [
    tool_search_query = ""
    tool_search_results = []
    tool_search_num_actions = 0
    tool_search_has_more_results = 0
    tool_search_action_idx = 0
]

tool_search_proc = [
    tool_search_num_actions = 0
    tool_search_action_idx = 0
    tool_search_has_more_results = 0
    tool_search_results = []

    if (strlen $arg1) [
        loopwhile i $tool_actions_num (< $tool_search_num_actions (+ $tool_search_max_results 1)) [
            local action
            action = (at $tool_actions $i)

            if (> (strcasestr @@(tool_action_prop [$action] short_desc) $arg1) -1) [
                append tool_search_results $action
                tool_search_num_actions = (+ 1 $tool_search_num_actions)
            ]
        ]
    ]

    tool_search_has_more_results = (> $tool_search_num_actions $tool_search_max_results)
    tool_search_num_actions = (clamp $tool_search_num_actions 0 $tool_search_max_results)
]

ui_tool_search_input = [
    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uiimage "textures/icons/action" $ui_toolview_accent_colour 0 $ui_toolview_base_icon_size $ui_toolview_base_icon_size
        uiinput tool_search_query 32 [
            tool_search_proc $tool_search_query
        ] $ui_toolpanel_text_size 0 "[search for action here]" 0 "" [
            if (&& $toolpanel_init $ui_freecursor) uieditorsetfocus
        ]
    ]
]

ui_tool_search_list = [
    if $toolpanel_init [
        tool_search_action_idx = -1
    ]

    loop i $tool_search_num_actions [
        local action action_type cat label keys bind_info
        action = (at $tool_search_results $i)
        action_type = @(tool_action_prop [$action] type)
        cat = @(tool_action_prop [$action] category)
        label = @(tool_action_prop [$action] short_desc)
        keys = $[tb_@action]
        bind_info = (prettybindinfo (at $keys 0) (at $keys 1))

        if (= $i $tool_search_action_idx) uisethighlight
        ui_tool_button [
            uit_children = [
                uihlist $ui_toolview_small_elem_space [
                    if (= $action_type $TOOL_ACTION_SCROLL) [
                        uiimage "textures/keys/mouse5" $ui_toolview_accent_colour 0 (*f $ui_toolview_small_icon_size 0.8) $ui_toolview_small_icon_size
                        bind_info = (concatword "+" $bind_info)
                    ]
                    if @bind_info [
                        uicolourtext [@bind_info] $ui_toolview_warn_colour $ui_toolview_tiny_text_size
                        uitriangle $ui_toolview_dark_accent_colour $ui_toolview_separator_icon_size $ui_toolview_separator_icon_size -90
                    ]
                    uicolourtext @cat $ui_toolview_dark_accent_colour $ui_toolview_tiny_text_size
                ]
                uialign- 1
            ]
            uit_label = [@label]
            uit_label_align = -1
            uit_tip_action = $action
            uit_on_click = [
                toolpanel_hide tool_search
                tool_do_action @action
            ]
        ]
        uiclamp- 1 1
    ]

    if $tool_search_has_more_results [
        local txt
        txt = (concat "Search limited to" $tool_search_max_results "results")
        uifill 0 $ui_toolview_base_elem_space
        uicolourtext $txt $ui_toolview_dark_accent_colour $ui_toolview_small_text_size
    ]
]

tool_search_nav = [
    tool_search_action_idx = (- $tool_search_action_idx $arg2)
    tool_search_action_idx = (clamp $tool_search_action_idx 0 (- $tool_search_num_actions 1))

    if $arg3 [
        tool_do_action (at $tool_search_results $tool_search_action_idx)
        toolpanel_hide tool_search
    ]
]

ui_tool_search = [
    uivlist 0 [
        uiclamp 1 1
        ui_tool_search_input
        uifill 0 $ui_toolpanel_elem_space
        uiline $ui_toolview_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolpanel_elem_space
        ui_tool_search_list
    ]
]

ui_tool_main_menu = [
    uivlist $ui_padbutton [
        ui_contents_main
    ]
]
