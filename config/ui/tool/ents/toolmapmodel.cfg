ui_tool_ent_mapmodel = [
    uitext Mapmodel

    ui_tool_ent_param_group "Model" [
        ui_tool_modelselect @@(tool_ent_attr mapmodel type) [
            @@@(ui_tool_ent_attr_props mapmodel type)
            uit_align = 1
        ]
    ]

    ui_tool_ent_param_group "Dimensions" [
        uigrid 2 0 0 [
            ui_tool_numinput @@@(tool_ent_attr mapmodel yaw) 0 360 1 [
                @@@@(ui_tool_ent_attr_props mapmodel yaw)
                uit_label = "Yaw"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Yaw from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr mapmodel yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change mapmodel $tool_ent_mapmodel_idx_yaw
                    ]
                ]
            ]

            ui_tool_numinput @@@(tool_ent_attr mapmodel pitch) -180 180 1 [
                @@@@(ui_tool_ent_attr_props mapmodel pitch)
                uit_label = "Pitch"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Pitch from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr mapmodel pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change mapmodel $tool_ent_mapmodel_idx_pitch
                    ]
                ]
            ]

            ui_tool_numinput @@@(tool_ent_attr mapmodel roll) -180 180 1 [
                @@@@(ui_tool_ent_attr_props mapmodel roll)
                uit_label = "Roll"
                uit_val_format = i
                uit_circular = 1
            ]
        ]

        uifill 0 $ui_toolview_base_elem_space

        ui_tool_numinput @@(tool_ent_attr mapmodel scale) 0 10000 10 [
            @@@(ui_tool_ent_attr_props mapmodel scale)
            uit_label = "Scale"
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Spin" [
        ui_tool_numinput @@(tool_ent_attr mapmodel spinyaw) 0 5000 10 [
            @@@(ui_tool_ent_attr_props mapmodel spinyaw)
            uit_label = "Yaw"
            uit_val_format = i
        ]

        ui_tool_numinput @@(tool_ent_attr mapmodel spinpitch) 0 5000 10 [
            @@@(ui_tool_ent_attr_props mapmodel spinpitch)
            uit_label = "Pitch"
            uit_val_format = i
        ]

        ui_tool_numinput @@(tool_ent_attr mapmodel spinroll) 0 5000 10 [
            @@@(ui_tool_ent_attr_props mapmodel spinroll)
            uit_label = "Roll"
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Colour" [
        ui_tool_colour tool_ent_attr_colour [
            @@@(ui_tool_ent_attr_props mapmodel colour)
        ]

        uifill 0 $ui_toolview_base_elem_space

        ui_tool_numinput tool_ent_attr_blend_val 1 100 1 [
            @@@(ui_tool_ent_attr_props mapmodel blend)
            uit_label = "Blend ^%"
            uit_val_format = i
        ]
    ]

    @(ui_tool_ent_flags_group mapmodel $tool_ent_mapmodel_flags [
        "Hide on trigger"
        "No collision"
        "No shadow"
    ])

    ui_tool_ent_param_group "Animation" [
        ui_tool_searchselect @@(tool_ent_attr mapmodel anim) [
            @@@(ui_tool_ent_attr_props mapmodel anim)
            uit_label = "Type"
            uit_list = $tool_mapmodel_anims
        ]

        ui_tool_numinput @@(tool_ent_attr mapmodel aspeed) 0 1000 1 [
            @@@(ui_tool_ent_attr_props mapmodel aspeed)
            uit_label = "Speed"
            uit_val_format = i
        ]

        ui_tool_numinput @@(tool_ent_attr mapmodel aoffset) 0 1000 1 [
            @@@(ui_tool_ent_attr_props mapmodel aoffset)
            uit_label = "Offset on rail"
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "LOD offset" [
        ui_tool_numinput @@(tool_ent_attr mapmodel lodoff) -10000 10000 100 [
            @@@(ui_tool_ent_attr_props mapmodel lodoff)
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Shadow distance" [
        uihlist 0 [
            ui_tool_numinput @@(tool_ent_attr mapmodel shadowdist) 0 10000 100 [
                @@@(ui_tool_ent_attr_props mapmodel shadowdist)
                uit_val_format = i
            ]
            ui_tool_button [
                uit_icon = @(tool_action_prop ta_ent_autoshadowdist icon)
                uit_tip_action = ta_ent_autoshadowdist
                uit_on_click = [
                    tool_do_action ta_ent_autoshadowdist
                ]
                uit_disabled = $tool_ent_delta_edit_on
            ]
        ]
    ]

    @(ui_tool_ent_palette_group mapmodel)
    @(ui_tool_ent_variant_group mapmodel 1)
]
