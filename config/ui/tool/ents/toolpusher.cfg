ui_tool_ent_pusher = [
    uitext Pusher

    ui_tool_ent_param_group "Direction" [
        uigrid 2 0 0 [
            ui_tool_numinput @@@(tool_ent_attr pusher yaw) 0 360 1 [
                @@@@(ui_tool_ent_attr_props pusher yaw)
                uit_label = "Yaw"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Yaw from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr pusher yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change pusher $tool_ent_pusher_idx_yaw
                    ]
                ]
            ]

            ui_tool_numinput @@@(tool_ent_attr pusher pitch) -90 90 1 [
                @@@@(ui_tool_ent_attr_props pusher pitch)
                uit_label = "Pitch"
                uit_val_format = i
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Pitch from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr pusher pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change pusher $tool_ent_pusher_idx_pitch
                    ]
                ]
            ]
        ]
    ]

    ui_tool_ent_param_group "Force" [
        ui_tool_numinput @@(tool_ent_attr pusher force) -10000 10000 50 [
            @@@(ui_tool_ent_attr_props pusher force)
            uit_val_format = i
        ]

        ui_tool_numinput @@(tool_ent_attr pusher sdelay) 0 10000 100 [
            @@@(ui_tool_ent_attr_props pusher sdelay)
            uit_val_format = i
            uit_label = "Stagger time (ms)"
        ]
    ]

    ui_tool_ent_param_group "Radius" [
        ui_tool_numinput @@(tool_ent_attr pusher maxrad) 0 1000 1 [
            @@@(ui_tool_ent_attr_props pusher maxrad)
            uit_label = "Outer radius"
            uit_val_format = i
        ]

        ui_tool_numinput @@(tool_ent_attr pusher minrad) 0 1000 1 [
            @@@(ui_tool_ent_attr_props pusher minrad)
            uit_label = "Inner radius"
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Type" [
        ui_tool_dropdown @@(tool_ent_attr pusher type) [
            "Conditional"
            "Add"
            "Redirect"
            "Absolute"
        ] [
            @@@(ui_tool_ent_attr_props pusher type)
        ]
    ]

    @(ui_tool_ent_gamemode_group pusher)
    @(ui_tool_ent_variant_group pusher)
]
