# ui_tool_ent_pusher = [
    ui_tool_ent_param_group "Direction" [
        uigrid 2 0 0 [
            ui_tool_numinput #(tool_ent_attr pusher yaw) 0 360 1 [
                #(ui_tool_ent_attr_props pusher yaw [] 1)
                p_label = "Yaw"
                p_val_format = i
                p_circular = 1
            ]

            ui_tool_button [
                p_icon = "<grey>textures/icons/camera"
                p_icon_size = 0.015
                p_icon_align = 0
                p_tip_simple = "Yaw from camera"
                p_on_click = [
                    tool_param_set #(tool_ent_attr pusher yaw) (toint (round $camerayaw)) [
                        tool_ent_attr_change pusher $tool_ent_pusher_idx_yaw
                    ]
                ]
                p_disabled = $tool_ent_delta_edit_on
            ]

            ui_tool_numinput #(tool_ent_attr pusher pitch) -90 90 1 [
                #(ui_tool_ent_attr_props pusher pitch [] 1)
                p_label = "Pitch"
                p_val_format = i
            ]

            ui_tool_button [
                p_icon = "<grey>textures/icons/camera"
                p_icon_size = 0.015
                p_icon_align = 0
                p_tip_simple = "Pitch from camera"
                p_on_click = [
                    tool_param_set #(tool_ent_attr pusher pitch) (toint (round $camerapitch)) [
                        tool_ent_attr_change pusher $tool_ent_pusher_idx_pitch
                    ]
                ]
                p_disabled = $tool_ent_delta_edit_on
            ]
        ]
    ]

    ui_tool_ent_param_group "Force" [
        ui_tool_numinput #(tool_ent_attr pusher force) -10000 10000 50 [
            #(ui_tool_ent_attr_props pusher force [] 1)
            p_val_format = i
        ]

        ui_tool_numinput #(tool_ent_attr pusher sdelay) 0 10000 100 [
            #(ui_tool_ent_attr_props pusher sdelay [] 1)
            p_val_format = i
            p_label = "Stagger time (ms)"
        ]
    ]

    ui_tool_ent_param_group "Radius" [
        ui_tool_numinput #(tool_ent_attr pusher maxrad) 0 1000 1 [
            #(ui_tool_ent_attr_props pusher maxrad [] 1)
            p_label = "Outer radius"
            p_val_format = i
        ]

        ui_tool_numinput #(tool_ent_attr pusher minrad) 0 1000 1 [
            #(ui_tool_ent_attr_props pusher minrad [] 1)
            p_label = "Inner radius"
            p_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Type" [
        ui_tool_dropdown #(tool_ent_attr pusher type) [
            "Conditional"
            "Add"
            "Redirect"
            "Absolute"
        ] [
            #(ui_tool_ent_attr_props pusher type)
        ]
    ]

    @(ui_tool_ent_gamemode_group pusher)
    @(ui_tool_ent_variant_group pusher)
]
