ui_tool_ent_playerstart = [
    uitext Playerstart

    ui_tool_ent_param_group "Team" [
        ui_tool_dropdown @@(tool_ent_attr playerstart team) $tool_team_names [
            @@@(ui_tool_ent_attr_props playerstart team)
        ]
    ]

    ui_tool_ent_param_group "Direction" [
        uigrid 2 0 0 [
            ui_tool_numinput @@@(tool_ent_attr playerstart yaw) 0 360 1 [
                @@@@(ui_tool_ent_attr_props playerstart yaw)
                uit_label = "Yaw"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Yaw from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr playerstart yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change playerstart $tool_ent_playerstart_idx_yaw
                    ]
                ]
            ]

            ui_tool_numinput @@@(tool_ent_attr playerstart pitch) -90 90 1 [
                @@@@(ui_tool_ent_attr_props playerstart pitch)
                uit_label = "Pitch"
                uit_val_format = i
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Pitch from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr playerstart pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change playerstart $tool_ent_playerstart_idx_pitch
                    ]
                ]
            ]
        ]
    ]

    @(ui_tool_ent_gamemode_group playerstart)
    @(ui_tool_ent_variant_group playerstart)

    ui_tool_ent_param_group "ID" [
        ui_tool_numinput @@(tool_ent_attr playerstart id) 0 1000 1 [
            @@@(ui_tool_ent_attr_props playerstart id)
            uit_val_format = i
        ]
    ]
]
