# ui_tool_ent_soundenv [
    ui_tool_ent_param_group "Type" [
        ui_tool_searchselect #(tool_ent_attr soundenv type) [
            #(ui_tool_ent_attr_props soundenv type)
            p_num_items_func = [ + (soundenvinfo -1) 1]
            p_item_func = [
                if $arg1 [
                    soundenvinfo (- $arg1 1)
                ] [
                    result "<None>"
                ]
            ]
            p_value = "Browse"
        ]

        local _picked
        if $#(tool_ent_attr soundenv type) [
            _picked = (soundenvinfo (- $#(tool_ent_attr soundenv type) 1))
        ] [
            _picked = "<None>"
        ]

        uitext $_picked $ui_tool_text_size_xs
        uialign- 1
    ]

    ui_tool_ent_param_group "Size" [
        ui_tool_numinput #(tool_ent_attr soundenv width) 0 999999 1 [
            #(ui_tool_ent_attr_props soundenv width [] 1)
            p_val_format = i
            p_label = "X"
        ]

        ui_tool_numinput #(tool_ent_attr soundenv length) 0 999999 1 [
            #(ui_tool_ent_attr_props soundenv length [] 1)
            p_val_format = i
            p_label = "Y"
        ]

        ui_tool_numinput #(tool_ent_attr soundenv height) 0 999999 1 [
            #(ui_tool_ent_attr_props soundenv height [] 1)
            p_val_format = i
            p_label = "Z"
        ]
    ]

    ui_tool_ent_param_group "Fade groups" [
        local _fadevalattr _fadevalattrname _fadeval
        _fadevalattr     = ""
        _fadevalattrname = ""
        _fadeval         = 0

        tool_ent_soundenv_add_influence = 0
        ui_tool_dropdown tool_ent_soundenv_add_influence [
            "<Add influence>"
            #(loopconcat i 16 [result (+ $i 1)])
        ] [
            p_width     = $ui_tool_button_width_m_unscaled
            p_filter    = [= (tool_ent_soundenv_get_fade (- $arg1 1)) 0]
            p_on_change = [
                tool_ent_soundenv_set_fade (- $tool_ent_soundenv_add_influence 1) 100
            ]
        ]
        uialign- 1

        loop i 16 [
            _fadevalattr     = (concatword fadevals (+ (div $i 4) 1))
            _fadevalattrname = (tool_ent_attr soundenv $_fadevalattr)
            _fadeval         = (tool_get_byte $$_fadevalattrname (mod $i 4))

            if $_fadeval [
                uihlist 0 [
                    ui_tool_numinput $_fadevalattrname 1 100 1 [
                        @(ui_tool_ent_attr_props soundenv $_fadevalattr)
                        p_label      = [@@@(concatword "Group #" (+ $i 1))]
                        p_val_format = i
                        p_get        = [tool_get_byte $arg1 (mod @@@i 4)]
                        p_set        = [tool_set_byte $@@_fadevalattrname $arg1 (mod @@@i 4)]
                        p_id         = @i
                        p_val_text   = [concatword $arg1 "^%"]
                        p_tip_simple = "Fade strength"
                    ]

                    ui_tool_button [
                        #ui_tool_square_button_s
                        p_icon       = "textures/hud/exit"
                        p_tip_simple = "Remove influence"
                        p_on_click   = [
                            @@_fadevalattrname = (tool_set_byte $@@_fadevalattrname 0 (mod @@@i 4))
                            tool_ent_force_attr soundenv @@_fadevalattr
                        ]
                    ]
                ]
            ]
        ]
    ]
]
