ui_tool_ent_checkpoint = [
    uitext Checkpoint

    ui_tool_ent_param_group "Radius" [
        ui_tool_numinput @@(tool_ent_attr checkpoint radius) 0 1000 1 [
            @@@(ui_tool_ent_attr_props checkpoint radius)
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Direction" [
        uigrid 2 0 0 [
            ui_tool_numinput @@@(tool_ent_attr checkpoint yaw) 0 360 1 [
                @@@@(ui_tool_ent_attr_props checkpoint yaw)
                uit_label = "Yaw"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Yaw from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr checkpoint yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change checkpoint $tool_ent_checkpoint_idx_yaw
                    ]
                ]
            ]

            ui_tool_numinput @@@(tool_ent_attr checkpoint pitch) -90 90 1 [
                @@@@(ui_tool_ent_attr_props checkpoint pitch)
                uit_label = "Pitch"
                uit_val_format = i
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Pitch from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr checkpoint pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change checkpoint $tool_ent_checkpoint_idx_pitch
                    ]
                ]
            ]
        ]
    ]

    ui_tool_ent_param_group "Type" [
        ui_tool_dropdown @@(tool_ent_attr checkpoint type) [
            "Respawn"
            "Start"
            "Finish"
            "Last"
        ] [
            @@@(ui_tool_ent_attr_props checkpoint type)
        ]
    ]

    @(ui_tool_ent_gamemode_group checkpoint)
    @(ui_tool_ent_variant_group checkpoint)

    ui_tool_ent_param_group "ID" [
        ui_tool_numinput @@(tool_ent_attr checkpoint id) 0 1000 1 [
            @@@(ui_tool_ent_attr_props checkpoint id)
            uit_val_format = i
        ]
    ]
]
