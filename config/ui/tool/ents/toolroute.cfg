# ui_tool_ent_route [
    ui_tool_ent_param_group "Route" [
        ui_tool_numinput #(tool_ent_attr route num) 0 1000 1 [
            #(ui_tool_ent_attr_props route num [] 1)
            p_val_format = i
        ]

        if $routenames [
            ui_tool_dropdown #(tool_ent_attr route num) $routenames [
                #(ui_tool_ent_attr_props route num)
                p_label = "From list"
            ]
        ]
    ]

    ui_tool_ent_param_group "Direction" [
        uigrid 2 0 0 [
            ui_tool_numinput #(tool_ent_attr route yaw) 0 360 1 [
                #(ui_tool_ent_attr_props route yaw [] 1)
                p_label = "Yaw"
                p_val_format = i
                p_circular = 1
            ]

            ui_tool_button [
                p_icon = "textures/icons/camera"
                p_icon_size = 0.015
                p_icon_align = 0
                p_tip_simple = "Yaw from camera"
                p_on_click = [
                    tool_param_set #(tool_ent_attr route yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change route $tool_ent_route_idx_yaw
                    ]
                ]
                p_disabled = $tool_ent_delta_edit_on
            ]

            ui_tool_numinput #(tool_ent_attr route pitch) -90 90 1 [
                #(ui_tool_ent_attr_props route pitch [] 1)
                p_label = "Pitch"
                p_val_format = i
            ]

            ui_tool_button [
                p_icon = "textures/icons/camera"
                p_icon_size = 0.015
                p_icon_align = 0
                p_tip_simple = "Pitch from camera"
                p_on_click = [
                    tool_param_set #(tool_ent_attr route pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change route $tool_ent_route_idx_pitch
                    ]
                ]
                p_disabled = $tool_ent_delta_edit_on
            ]
        ]
    ]

    local _action_names
    _action_names = [
        "Primary fire"
        "Secondary fire"
        "Reload"
        "Use"
        "Jump"
        "Walk"
        "Crouch"
        "Special"
        "Drop"
        "Affinity"
        "Dash"
    ]

    ui_tool_ent_param_group "Actions" [
        local _attr_mode

        uigrid 2 0 0 [
            loop i (listlen $tool_ent_route_actions) [
                _attr_mode = (at $tool_ent_route_actions $i)
                ui_tool_compactswitch [tool_ent_attr_action_@_attr_mode] [
                    #(ui_tool_ent_attr_props route action)
                    p_label = [@(at $_action_names $i)]
                ]
                uiclamp- 1 1
            ]
        ]

        uifill 0 $ui_tool_elem_space_l

        ui_tool_dropdown tool_ent_attr_move [
            "Backward"
            "None"
            "Forward"
        ] [
            #(ui_tool_ent_attr_props route move)
            p_label = "Move"
            p_reset_val = 1
        ]

        ui_tool_dropdown tool_ent_attr_strafe [
            "Left"
            "None"
            "Right"
        ] [
            #(ui_tool_ent_attr_props route strafe)
            p_label = "Strafe"
            p_reset_val = 1
        ]
    ]
]
