ui_tool_ent_route = [
    uitext Route

    ui_tool_ent_param_group "Route" [
        ui_tool_numinput @@(tool_ent_attr route num) 0 1000 1 [
            @@@(ui_tool_ent_attr_props route num)
            uit_val_format = i
        ]

        if $routenames [
            ui_tool_dropdown @@@(tool_ent_attr route num) $routenames [
                @@@@(ui_tool_ent_attr_props route num)
                uit_label = "From list"
            ]
        ]
    ]

    ui_tool_ent_param_group "Direction" [
        uigrid 2 0 0 [
            ui_tool_numinput @@@(tool_ent_attr route yaw) 0 360 1 [
                @@@@(ui_tool_ent_attr_props route yaw)
                uit_label = "Yaw"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Yaw from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr route yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change route $tool_ent_route_idx_yaw
                    ]
                ]
            ]

            ui_tool_numinput @@@(tool_ent_attr route pitch) -90 90 1 [
                @@@@(ui_tool_ent_attr_props route pitch)
                uit_label = "Pitch"
                uit_val_format = i
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Pitch from camera"
                uit_on_click = [
                    tool_param_set @@@@@(tool_ent_attr route pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change route $tool_ent_route_idx_pitch
                    ]
                ]
            ]
        ]
    ]

    local action_names
    action_names = [
        "Primary fire"
        "Secondary fire"
        "Reload"
        "Use"
        "Jump"
        "Walk"
        "Crouch"
        "Special"
        "Drop"
        "Affinity"
        "Dash"
    ]

    ui_tool_ent_param_group "Actions" [
        local attr_mode

        uigrid 2 0 0 [
            loop i (listlen $tool_ent_route_actions) [
                attr_mode = (at $tool_ent_route_actions $i)
                ui_tool_compactswitch [tool_ent_attr_action_@attr_mode] [
                    @@@@@(ui_tool_ent_attr_props route action)
                    uit_label = [@(at $action_names $i)]
                ]
                uiclamp- 1 1
            ]
        ]

        uifill 0 $ui_toolview_base_elem_space

        ui_tool_dropdown tool_ent_attr_move [
            "Backward"
            "None"
            "Forward"
        ] [
            @@@(ui_tool_ent_attr_props route move)
            uit_label = "Move"
            uit_reset_val = 1
        ]

        ui_tool_dropdown tool_ent_attr_strafe [
            "Left"
            "None"
            "Right"
        ] [
            @@@(ui_tool_ent_attr_props route strafe)
            uit_label = "Strafe"
            uit_reset_val = 1
        ]
    ]
]
