# ui_tool_ent_sound [
    ui_tool_compactswitch mapsoundautomute [
        p_label = "Mute others"
        p_tip_simple = "Mute other mapsounds that are not selected"
        p_label_size = $ui_tool_text_size_s_unscaled
    ]
    uiclamp- 1 1

    #ui_tool_divider

    ui_tool_ent_param_group "Sound" [
        ui_tool_searchselect #(tool_ent_attr sound sound) [
            #(ui_tool_ent_attr_props sound sound)
            p_num_items_func = [mapsoundinfo -1]
            p_item_func = [mapsoundinfo $arg1]
            p_value = "Browse"
        ]

        uitext (mapsoundinfo $#(tool_ent_attr sound sound)) $ui_tool_text_size_xs
    ]

    ui_tool_ent_param_group "Parameters" [
        ui_tool_numinput #(tool_ent_attr sound gain) 0 1000 1 [
            #(ui_tool_ent_attr_props sound gain [] 1)
            p_label = "Gain ^%"
            p_val_format = i
        ]

        ui_tool_numinput #(tool_ent_attr sound pitch) 0 1000 1 [
            #(ui_tool_ent_attr_props sound pitch [] 1)
            p_label = "Pitch ^%"
            p_val_format = i
        ]

        ui_tool_numinput #(tool_ent_attr sound offset) 0 10 0.1 [
            #(ui_tool_ent_attr_props sound offset [] 1)
            p_label = "Offset"
            p_val_format = f
            p_get = [ divf $arg1 1000 ]
            p_set = [ toint (round (*f $arg1 1000)) ]
            p_val_text = [ concatword $arg1 "s" ]
        ]

        ui_tool_numinput #(tool_ent_attr sound groupid) 0 32 1 [
            #(ui_tool_ent_attr_props sound groupid [] 1)
            p_label = "Fade group"
            p_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Spread" [
        ui_tool_numinput #(tool_ent_attr sound rolloff) 0 1000 10 [
            #(ui_tool_ent_attr_props sound rolloff [] 1)
            p_label = "Attenuation ^%"
            p_val_format = i
        ]

        uifill 0 $ui_tool_elem_space_m

        ui_tool_collapsegroup tool_soundent_adv [
            ui_tool_numinput #(tool_ent_attr sound refdist) 0 1000 1 [
                #(ui_tool_ent_attr_props sound refdist [] 1)
                p_label = "Ref. distance"
                p_val_format = i
            ]
            ui_tool_numinput #(tool_ent_attr sound maxdist) 0 100000 100 [
                #(ui_tool_ent_attr_props sound maxdist [] 1)
                p_label = "Distance clamp"
                p_val_format = i
            ]
        ] [
            p_label = "Advanced"
        ]
    ]

    @(ui_tool_ent_flags_group sound $tool_ent_sound_flags [
        "No attenuation"
        "No delay"
        "Priority"
        "No panning"
        "No distance"
        "No environment"
        "Clamp"
    ])

    @(ui_tool_ent_gamemode_group sound)
    @(ui_tool_ent_variant_group sound)
]
