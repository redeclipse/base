ui_tooltex_preview_size = 0.1
ui_tooltex_preview_small_size = 0.06

ui_tool_tex_options = [
    uigrid 2 0 0 [
        uiclamp 1 1 1 1

        ui_tool_checkbox tool_tex_autoapply [
            p_label = "Auto apply"
        ]

        ui_tool_checkbox allfaces [
            p_label = "All cube faces"
        ]

        uialign* -1 -1
    ]
]

ui_tip_tex_variant_slot = [
    uitext (format "Source for variant: #%1" $tool_tex_variantsrc) $ui_tool_text_size_s
    if (tool_tex_can_focus) [
        uitext "Click to focus" $ui_tool_text_size_xs
    ]
]

# ui_tool_cur_tex [
    local _buttons_disabled _tex
    _tex = (? $tool_tex_edit $tool_tex_cur_edit $tool_tex_cur)
    _buttons_disabled = (|| (< $_tex 0) $tool_tex_edit)

    ui_tool_texinfo $_tex
    uialign- -1 -1

    uihlist 0 [
        uiclamp 1 1
        uihlist 0 [
            uivslotview $_tex $ui_tooltex_preview_size $ui_tooltex_preview_size
            uifill $ui_tool_elem_space_s
            if (istexvariant $_tex) [
                uifill 0 0 [
                    uivslotview $tool_tex_variantsrc $ui_tooltex_preview_small_size $ui_tooltex_preview_small_size
                    uitooltip tex_variant_slot
                    uialign* -1 -1

                    uirelease [
                        tool_focus_tex $tool_tex_variantsrc
                    ]
                ]
            ] [
                uifill $ui_tooltex_preview_small_size
            ]
            uialign* -1 -1
        ]

        ui_tool_button [
            p_label = "Misc"
            p_width = 0.05
            p_height = 0.05
            p_on_click = [
                toolpanel_open_menu [
                    p_width = 0.2
                    p_item_actions = [
                        ta_tex_replace
                        ta_tex_paint
                        ta_tex_cull
                        ta_newslot
                    ]
                    p_item_dividers = [1]
                ]
            ]
        ]
        uialign- 1 -1

        uivlist 0 [
            uihlist 0 [
                uiclamp 1 1 1 1
                #(ui_tool_get_action_button ta_tex_apply [
                    p_disabled = [@_buttons_disabled]
                    p_icon_align = 0
                    p_height = 0.05
                    p_width = 0.1
                ] [
                    p_override_label = 1
                ])
            ]
            uihlist 0 [
                uiclamp 1 1 1 1
                #(ui_tool_get_action_button ta_tex_editvariant [
                    p_icon_align = 0
                    p_height = 0.05
                    p_width = 0.05
                ] [
                    p_override_label = 1
                ])
                #(ui_tool_get_action_button ta_tex_get [
                    p_icon_align = 0
                    p_height = 0.05
                    p_width = 0.05
                ] [
                    p_override_label = 1
                ])
            ]
        ]
        uialign- 1 -1
    ]
]

// 1:<param idx> 2:<disabled>
ui_tool_tex_param_props = [
    result [
        p_disabled = @(|| $tool_tex_props_disabled $arg2)
        p_label_size = $ui_tool_text_size_xs
        p_can_reset = 1
        p_on_change = [
            tool_tex_param_change @@arg1 $tool_tex_autoapply
        ]
    ]
]

// 1:<param idx>
ui_tool_tex_shader_param_props = [
    result [
        p_disabled = @(|| $tool_tex_props_disabled (! (tool_tex_accepts_param $arg1)))
        p_label_size = $ui_tool_text_size_xs
        p_can_reset = 1
        p_on_change = [
            tool_tex_shader_param_change @@arg1 $tool_tex_autoapply
        ]
    ]
]

// 1:<text> 2:<fields> 3:<disabled>
ui_tool_tex_param_group = [
    local _disabled _col
    _disabled = (|| $tool_tex_props_disabled $arg3)

    _col = (? $_disabled $ui_tool_dark_colour $ui_tool_accent_colour)

    uihlist 0 [
        uiclamp 1 1
        uicolourtext $arg1 $_col $ui_tool_text_size_s
        uivlist 0 [
            @arg2
        ]
    ]
    uifill 0 $ui_tool_elem_space_s
    uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uifill 0 $ui_tool_elem_space_s
]

// 1:<text> 2:<param idx> 3:<fields>
ui_tool_tex_shader_param_group = [
    local _disabled _accepts_param
    _accepts_param = (tool_tex_accepts_param $arg2)
    _disabled = (|| $tool_tex_props_disabled (! $_accepts_param))

    if $_disabled [] [
        uihlist 0 [
            uiclamp 1 1
            uicolourtext $arg1 $ui_tool_accent_colour $ui_tool_text_size_s
            uivlist 0 [
                @arg3
            ]
        ]
        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s
    ]
]

ui_tool_tex_param_mp_warning = [
    if (isonline) [
        local _warn_text

        if $nompedit [
            _warn_text = "Parameter not available in multiplayer"
        ] [
            _warn_text = "Parameter will cause desynchronization"
        ]

        uispace 0 $ui_tool_elem_space_s [
            uicolourtext $_warn_text $ui_tool_warn_colour $ui_tool_text_size_xs
        ]
    ]
]

ui_tool_tex_dim_params = [
    ui_tool_tex_param_group "Scale" [
        ui_tool_numinput tool_tex_vscale 0.125 8 0 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vscale)
            p_reset_val = $tool_tex_vscale_default
            p_tip_simple = "Scale multiplier"
        ]
    ]

    local _step _tex_w _tex_h
    _step = (divf (pow 2 $gridpower) $tool_tex_vscale)

    if (listhas [1 3 6 7] $tool_tex_vrotate) [
        _tex_w = (getvteximgheight $tool_tex_cur_edit)
        _tex_h = (getvteximgwidth $tool_tex_cur_edit)
    ] [
        _tex_w = (getvteximgwidth $tool_tex_cur_edit)
        _tex_h = (getvteximgheight $tool_tex_cur_edit)
    ]

    ui_tool_tex_param_group "Offset" [
        ui_tool_numinput tool_tex_voffsetx 0 $_tex_w $_step [
            @(ui_tool_tex_param_props $tool_tex_param_idx_voffset)
            p_val_format = i
            p_reset_val = $tool_tex_voffsetx_default
            p_tip_simple = "X offset in world units"
            p_circular = 1
        ]
        ui_tool_numinput tool_tex_voffsety 0 $_tex_h $_step [
            @(ui_tool_tex_param_props $tool_tex_param_idx_voffset)
            p_val_format = i
            p_reset_val = $tool_tex_voffsety_default
            p_tip_simple = "Y offset in world units"
            p_circular = 1
        ]
    ]

    ui_tool_tex_param_group "Scroll" [
        ui_tool_numinput tool_tex_vscrollx -10 10 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vscroll)
            p_label = "X"
            p_reset_val = $tool_tex_vscrollx_default
            p_tip_simple = "X scroll animation speed"
        ]
        ui_tool_numinput tool_tex_vscrolly -10 10 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vscroll)
            p_label = "Y"
            p_reset_val = $tool_tex_voffsety_default
            p_tip_simple = "Y scroll animation speed"
        ]
    ]

    ui_tool_tex_param_group "Rotate" [
        ui_tool_dropdown tool_tex_vrotate [
            "0 degrees"
            "90 degrees"
            "180 degrees"
            "270 degrees"
            "H-flipped"
            "V-flipped"
            "90 degrees, H-flipped"
            "90 degrees, V-flipped"
        ] [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vrotate)
            p_reset_val = $tool_tex_vrotate_default
            p_tip_simple = "Predefined angle transformations"
        ]
        uialign- 1

        ui_tool_numinput tool_tex_vangle_nudge -8 8 1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vangle)
            p_label = "Angle nudge"
            p_val_format = i
            p_reset_val = $tool_tex_vangle_nudge_default
            p_tip_simple = "Slope steps to fit the angle in"
        ]
    ]
]

ui_tool_tex_blend_params = [
    ui_tool_tex_param_group "Layer" [
        ui_tool_tex_param_mp_warning
        ui_tool_texselect tool_tex_vlayer [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vlayer (isonline))
            p_reset_val = $tool_tex_vlayer_default
            p_align = 1
            p_sky_tex = 0
            p_tip_simple = "Texture to blend with when using a blendmap"
        ]
    ] (isonline)

    // Unknown engine feature, no idea how this works, hide away
    // ui_tool_tex_param_group "Detail" [
    //     ui_tool_tex_param_mp_warning
    //     ui_tool_texselect tool_tex_vdetail [
    //         @(ui_tool_tex_param_props $tool_tex_param_idx_vdetail (isonline))
    //         p_reset_val = $tool_tex_vdetail_default
    //         p_align = 1
    //         p_sky_tex = 0
    //     ]
    // ] (isonline)
]

ui_tool_tex_colour_params = [
    ui_tool_tex_param_group "Colour" [
        ui_tool_colour tool_tex_vcolour [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vcolour)
            p_max_val = 10
            p_reset_val = $tool_tex_vcolour_default
            p_tip_simple = "Colour multiplier"
        ]
    ]

    ui_tool_tex_param_group "Palette" [
        ui_tool_dropdown tool_tex_vpalette_set [
            "Pulse"
            "Team"
            "Weapon"
        ] [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vpalette)
            p_reset_val = $tool_tex_vpalette_set_default
            p_label = "Type"
            p_tip_simple = "Colour palette set"
        ]

        ui_tool_dropdown tool_tex_vpalette_id (at $tool_palette_ids $tool_tex_vpalette_set) [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vpalette)
            p_reset_val = $tool_tex_vpalette_set_default
            p_label = "Id"
            p_tip_simple = "Palette colour"
        ]

        uifill 0 $ui_tool_elem_space_m

        ui_tool_checkbox tool_tex_vpalette_enforce [
            p_label = "Enforce"
            p_label_size = $ui_tool_text_size_xs
            @(ui_tool_tex_param_props $tool_tex_param_idx_vpalette)
            p_tip_simple = "Force palette to show regardless of game mode settings"
        ]
        uialign- 1
    ]

    ui_tool_tex_param_group "Alpha" [
        ui_tool_numinput tool_tex_valpha_front 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_valpha)
            p_label = "Front"
            p_reset_val = $tool_tex_valpha_front_default
            p_tip_simple = "Transparency of the faces facing the camera when in the alpha material"
        ]
        ui_tool_numinput tool_tex_valpha_back 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_valpha)
            p_label = "Back"
            p_reset_val = $tool_tex_valpha_back_default
            p_tip_simple = "Transparency of the faces facing away from the camera when in the alpha material"
        ]
    ]

    ui_tool_tex_param_group "Alpha refract" [
        local _refract_col_dis
        _refract_col_dis = (|| $tool_tex_props_disabled (=f $tool_tex_vrefract_strength))

        ui_tool_numinput tool_tex_vrefract_strength 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vrefract)
            p_label = "Strength"
            p_reset_val = $tool_tex_vrefract_strength_default
            p_tip_simple = "Refraction strength when in the alpha material"
        ]

        uifill 0 $ui_tool_elem_space_m

        ui_tool_colour tool_tex_vrefract_colour [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vrefract $_refract_col_dis)
            p_reset_val = $tool_tex_vrefract_colour_default
            p_tip_simple = "Refraction colour multiplier when in the alpha material"
        ]
    ]

    ui_tool_tex_param_group "Alpha shadow" [
        local _shadow_alpha_dis
        _shadow_alpha_dis = (|| $tool_tex_props_disabled (! $tool_tex_vshadow_en))

        ui_tool_numinput tool_tex_vshadow_alpha 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vshadow $_shadow_alpha_dis)
            p_label = "Strength"
            p_reset_val = $tool_tex_vshadow_alpha_default
            p_tip_simple = "Transparency shadow strength multiplier when in the alpha material"
        ]

        uifill 0 $ui_tool_elem_space_m

        ui_tool_checkbox tool_tex_vshadow_en [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vshadow)
            p_label = "Enable shadow"
            p_tip_simple = "Enable transparency shadow when in the alpha material"
        ]
        uialign- 1
    ]
]

ui_tool_tex_shader_params = [
    ui_tool_tex_shader_param_group "Glow" $tool_tex_shader_param_idx_glowcolor [
        ui_tool_colour tool_tex_glowcolor [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_glowcolor)
            p_max_val = 10
            p_reset_val = $tool_tex_glowcolor_default
            p_tip_simple = "Glow colour multiplier"
        ]
    ]

    ui_tool_tex_shader_param_group "Pulse glow" $tool_tex_shader_param_idx_pulseglowcolor [
        ui_tool_colour tool_tex_pulseglowcolor [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_pulseglowcolor)
            p_max_val = 10
            p_reset_val = $tool_tex_pulseglowcolor_default
            p_tip_simple = "Glow pulse glow colour multiplier"
        ]
    ]

    ui_tool_tex_shader_param_group "Pulse speed" $tool_tex_shader_param_idx_pulseglowspeed [
        ui_tool_numinput tool_tex_pulseglowspeed_val 0 60 0.01 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_pulseglowspeed)
            p_reset_val = $tool_tex_pulseglowspeed_val_default
            p_label = "Hz"
            p_tip_simple = "Glow pulse frequency"
        ]
    ]

    ui_tool_tex_shader_param_group "Pulse offset" $tool_tex_shader_param_idx_pulseglowoffset [
        ui_tool_numinput tool_tex_pulseglowoffset_val 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_pulseglowoffset)
            p_reset_val = $tool_tex_pulseglowoffset_val_default
            p_tip_simple = "Glow pulse offset in time"
        ]
    ]

    ui_tool_tex_shader_param_group "Gloss" $tool_tex_shader_param_idx_gloss [
        ui_tool_numinput tool_tex_gloss_val 0 2 1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_gloss)
            p_reset_val = $tool_tex_gloss_val_default
            p_val_format = i
            p_tip_simple = "Specular gloss, inverse of roughness"
        ]
    ]

    ui_tool_tex_shader_param_group "Specularity" $tool_tex_shader_param_idx_specscale [
        ui_tool_numinput tool_tex_specscale_val 0 10 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_specscale)
            p_reset_val = $tool_tex_specscale_val_default
            p_tip_simple = "Specularity multiplier"
        ]
    ]

    ui_tool_tex_shader_param_group "Normal scale" $tool_tex_shader_param_idx_normalscale [
        ui_tool_numinput tool_tex_normalscale_val -10 10 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_normalscale)
            p_reset_val = $tool_tex_normalscale_val_default
            p_tip_simple = "Normal map multiplier"
        ]
    ]

    ui_tool_tex_shader_param_group "Reflect min" $tool_tex_shader_param_idx_envmin [
        ui_tool_colour tool_tex_envmin [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_envmin)
            p_max_val = 10
            p_reset_val = $tool_tex_envmin_default
            p_tip_simple = "Minimum reflection colour"
        ]
    ]

    ui_tool_tex_shader_param_group "Reflect scale" $tool_tex_shader_param_idx_envscale [
        ui_tool_colour tool_tex_envscale [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_envscale)
            p_max_val = 10
            p_reset_val = $tool_tex_envscale_default
            p_tip_simple = "Reflection colour multiplier"
        ]
    ]

    ui_tool_tex_shader_param_group "Triplanar bias" $tool_tex_shader_param_idx_triplanarbias [
        ui_tool_numinput tool_tex_triplanarbias_x 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_triplanarbias)
            p_reset_val = $tool_tex_triplanarbias_x_default
            p_label = "X"
            p_tip_simple = "X plane bias"
        ]
        ui_tool_numinput tool_tex_triplanarbias_y 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_triplanarbias)
            p_reset_val = $tool_tex_triplanarbias_y_default
            p_label = "Y"
            p_tip_simple = "Y plane bias"
        ]
        ui_tool_numinput tool_tex_triplanarbias_z 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_triplanarbias)
            p_reset_val = $tool_tex_triplanarbias_z_default
            p_label = "Z"
            p_tip_simple = "Z plane bias"
        ]
    ]

    ui_tool_tex_shader_param_group "Parallax" $tool_tex_shader_param_idx_parallaxscale [
        ui_tool_numinput tool_tex_parallaxscale_low -1 1 0.001 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_parallaxscale)
            p_reset_val = $tool_tex_parallaxscale_low_default
            p_label = "Mul."
            p_tip_simple = "Parallax height multiplier"
        ]
        ui_tool_numinput tool_tex_parallaxscale_high -1 1 0.001 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_parallaxscale)
            p_reset_val = $tool_tex_parallaxscale_high_default
            p_label = "Offset"
            p_tip_simple = "Parallax height offset"
        ]
    ]
]

ui_tool_tex_other_params = [
    ui_tool_tex_param_group "Player coast scale" [
        ui_tool_numinput tool_tex_vcoastscale 0 1000 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vcoastscale)
            p_reset_val = $tool_tex_vcoastscale_default
            p_tip_simple = "Inverse of friction"
        ]
    ]
]

ui_tool_tex_params = [
    tool_tex_props_disabled = (|| [! (hassel)] [< $tool_tex_cur_edit 0])

    ui_tool_collapsegroup tool_tex_params_dim [
        @@ui_tool_tex_dim_params
    ] [
        p_label = "Transform"
        p_id    = #(tool_get_id)
    ]
    #(tool_tex_register_control "Texture transform" "scale size rotation rotate offset scroll angle")

    ui_tool_collapsegroup tool_tex_params_blend [
        @@ui_tool_tex_blend_params
    ] [
        p_label = "Blending"
        p_id    = #(tool_get_id)
    ]
    #(tool_tex_register_control "Texture blending" "blendmap")

    ui_tool_collapsegroup tool_tex_params_col [
        @@ui_tool_tex_colour_params
    ] [
        p_label = "Colour"
        p_id    = #(tool_get_id)
    ]
    #(tool_tex_register_control "Texture colouring" "coloring alpha transparency palette")

    ui_tool_collapsegroup tool_tex_params_shader [
        @@ui_tool_tex_shader_params
    ] [
        p_label = "Shader parameters"
        p_id    = #(tool_get_id)
    ]
    #(tool_tex_register_control "Texture shader parameters" "shading gloss specularity reflections normalmap")

    ui_tool_collapsegroup tool_tex_params_other [
        @@ui_tool_tex_other_params
    ] [
        p_label = "Other"
        p_id    = #(tool_get_id)
    ]
    #(tool_tex_register_control "Other texture parameters" "player coast scale drag friction")
]

# ui_tool_tex_edit [
    tool_tex_params_sync

    uihlist 0 [
        ui_tool_button [
            p_disabled = $tool_tex_autoapply
            p_icon = "textures/icons/attack"
            p_icon_align = 0
            p_tip_simple = "Apply changes"
            p_width = 0.08
            p_height = 0.05
            p_on_click = [
                tool_tex_commit_all_params
            ]
        ]
    ]

    ui_tool_vscrollarea [
        uivlist 0 [
            uiclamp 1 1
            @@@ui_tool_tex_params
        ]
    ] [
        p_height = 0.45
        p_id     = #(tool_get_id)
    ]

    if $tool_tex_autoapply [
        tool_tex_commit_all_params
    ]
]

// 1:<slot>
# tool_tex_list_menu [
    local _cull_group_text
    _cull_group_text = "Cull group"

    if (gettexgroup $arg1) [
        _cull_group_text = (format "Cull group (%1)" (gettexgroup $arg1))
    ]

    toolpanel_open_menu [
        p_width = 0.25
        p_nav_enable = 0
        p_item_names = [
            "Edit slot"
            "Remove slot"
            "Cull from here"
            [#1 _cull_group_text]
        ]
        p_tips = @(? (texhasvariants $arg1) [["Unable to remove slots with variants"]] [])
        p_on_select = [
            do (at [
                [
                    tool_tex_editslot (getslottex (getvindex #1 arg1))
                ] [
                    tool_confirm_prompt [#1(format "Do you want to remove slot #%1?" $arg1)] [
                        tool_tex_remove #1 arg1
                    ] [
                        p_noundo_warn = 1
                    ]
                ] [
                    tool_confirm_prompt [#1(format "Do you want to cull slots starting from #%1?" $arg1)] [
                        tool_tex_cull_from #1 arg1
                    ] [
                        p_noundo_warn = 1
                    ]
                ] [
                    tool_confirm_prompt [#1(format "Do you want to cull slot group '%1'?" (gettexgroup $arg1))] [
                        tool_tex_cull_group #1 arg1
                    ] [
                        p_noundo_warn = 1
                    ]
                ]
            ] $arg1)
        ]
        p_disabled = [
            case $arg1 1 [
                texhasvariants #1 arg1
            ] 3 [
                =s (gettexgroup #1 arg1)
            ]
        ]
    ]
]

# ui_tool_tex_list [
    ui_tool_texlist tool_tex_cur [
        p_on_select = [
            if $tool_tex_autoapply [
                tool_do_action ta_tex_apply
            ]
        ]
        p_on_doubleclick = [
            tool_do_action ta_tex_apply
        ]
        p_on_rightclick = [
            if (> $arg1 1) [
                tool_tex_list_menu $arg1
            ]
        ]
        p_sel_area = 0.45
    ]
    tool_tex_variantsrc = (getslottex (getvindex $tool_tex_cur))
]

ui_tool_textures_on_close = [
    tool_tex_on_close
]

ui_tool_textures = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1 1 1
        ui_tool_cur_tex
        ui_tool_tex_options
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        do $(at [ui_tool_tex_list ui_tool_tex_edit] $tool_tex_edit)
    ]
]

ui_tool_texreplace = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1

        ui_tool_checkbox tool_tex_replace_sel [
            p_label = "Limit to selection"
        ]
        uialign- -1

        uihlist 0 [
            uiclamp 1 1

            uitext "Old texture" $ui_tool_text_size_s

            uihlist $ui_toolpanel_elem_space [
                ui_tool_texselect tool_tex_cur [
                    p_align = 1
                ]
                uialign- 1

                ui_tool_button [
                    p_icon = "textures/icons/arrow"
                    p_icon_align = 0
                    p_width = 0.03
                    p_tip_simple = "Get from selection"
                    p_on_click = [
                        tool_focus_tex (getseltex 1)
                    ]
                ]
                uiclamp- 0 0 1 1
            ]
        ]

        ui_tool_button [
            p_label = "Swap"
            p_width = 0.1
            p_on_click = [
                local _swap_tmp
                _swap_tmp = $tool_tex_cur
                tool_focus_tex $tool_tex_replace
                tool_tex_replace = $_swap_tmp
            ]
        ]
        uialign- 1

        uihlist 0 [
            uiclamp 1 1

            uitext "New texture" $ui_tool_text_size_s

            uihlist $ui_toolpanel_elem_space [
                ui_tool_texselect tool_tex_replace [
                    p_align = 1
                ]
                uialign- 1

                ui_tool_button [
                    p_icon = "textures/icons/arrow"
                    p_icon_align = 0
                    p_width = 0.03
                    p_tip_simple = "Get from selection"
                    p_on_click = [
                        tool_tex_replace = (getseltex 1)
                    ]
                ]
                uiclamp- 0 0 1 1
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        uihlist 0 [
            ui_tool_button [
                p_disabled = (|| [< $tool_tex_cur 0] [< $tool_tex_replace])
                p_icon = "textures/icons/action"
                p_icon_align = 0
                p_width = 0.05
                p_height = 0.05
                p_tip_simple = "Replace"
                p_on_click = [
                    if $tool_tex_replace_sel [
                        replacetexsel $tool_tex_replace $tool_tex_cur
                    ] [
                        replacetex $tool_tex_replace $tool_tex_cur
                    ]
                ]
            ]
            ui_tool_button [
                p_icon = "textures/icons/warning"
                p_icon_align = 0
                p_width = 0.05
                p_height = 0.05
                p_tip_simple = "Close"
                p_on_click = [
                    toolpanel_close tool_texreplace
                ]
            ]
        ]

        @(ui_tool_undowarn)
    ]
]

defvarp disableblendpaintonuiclose 0 1 1

ui_tool_texpaint_on_close = [
    if $disableblendpaintonuiclose [
        blendpaintmode 0
    ]
]

ui_tool_texpaint = [
    uivlist $ui_toolpanel_elem_space [
        ui_tool_dropdown blendpaintmode [
            "Off"
            "Overwrite"
            "Merge"
            "Inverted erase"
            "Inverted merge"
            "Erase"
        ] [
            p_label = "Paint mode"
        ]

        ui_tool_imagelist blendbrush $tool_tex_blend_brushes [
            p_sel_cols = 6
            p_sel_size = 0.05
            p_sel_area = 0.3
            p_path = "blendbrush"
            p_allow_custom = 0
            p_get = [
                getblendbrushname $arg1
            ]
        ]
    ]
]

ui_tool_texeditslot_on_open = [
    local _texcount _textype _shadername _shaderidx
    _texcount   = (getvtexcount $tool_tex_cur)
    _shadername = (getvshadername $tool_tex_cur)

    // Clear all slot textures
    loop i 8 [
        [tool_texeditslot_tex@i] = ""
    ]

    loop i $_texcount [
        _textype = (getvtextype $tool_tex_cur $i)
        [tool_texeditslot_tex@_textype] = (fixpathslashes (getvtexname $tool_tex_cur $i))
    ]

    _shaderidx = (listfind i $worldshaders [
        =s $_shadername (at $i 0)
    ])

    tool_texeditslot_hints = (at $worldshaders $_shaderidx 1)

    tool_tex_editslot_checkhints 1
]

# ui_tool_texeditslot [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1

        ui_tool_vscrollarea [
            uivlist $ui_toolpanel_elem_space [
                uiclamp 1 1

                loop i 8 [
                    local _label _reqhint _mergeinfo
                    _label = (at [
                        "Diffuse"
                        "Normal"
                        "Glow"
                        "Reflection"
                        "Specularity"
                        "Height"
                        "Alpha"
                        "Displacement"
                    ] $i)

                    _reqhint = (substr " ngeSpmv" $i 1)

                    ui_tool_fileselect [tool_texeditslot_tex@i] [
                        p_text_width = (uiwidth 0.12)
                        p_label      = $_label
                        p_file_type  = $TOOL_FILE_IMAGE
                        p_on_change  = [ tool_tex_editslot_checkhints ]
                        @(if $i [
                            result [
                                p_disabled      = (< (strstr $tool_texeditslot_allowedhints $_reqhint) 0)
                                p_disabled_text = "Not allowed in this combination"
                                p_dir           = (filedir (tool_path_striptexcmds $tool_texeditslot_tex0))
                            ]
                        ] [
                            result [
                                p_can_deselect = 0
                            ]
                        ])
                    ]
                    uiclamp- 1 1

                    _mergeinfo = ""

                    case $i 4 [
                        if $tool_texeditslot_specfromdiffuse [
                            _mergeinfo = "Specular from diffuse alpha available"
                        ]
                    ] 5 [
                        if $tool_texeditslot_heightfromnormal [
                            _mergeinfo = "Heightmap from normalmap alpha available"
                        ]
                    ]

                    uipad 0 0 0 0.002 [
                        uicolourtext $_mergeinfo $ui_tool_sel_accent_colour $ui_tool_text_size_xs
                    ]
                    uialign- 1 -1
                ]
            ]
        ] [
            p_height = 0.4
        ]

        #ui_tool_divider

        uigrid 4 0 0 [
            if (> (strstr $tool_texeditslot_allowedhints "T") 0) [
                ui_tool_compactswitch tool_texeditslot_hints [
                    p_label     = "Triplanar"
                    p_on_change = [ tool_tex_editslot_checkhints 1 ]
                    p_get       = [ >= (strstr $arg1 "T") 0 ]
                    p_set       = [
                        if $arg1 [
                            concatword $tool_texeditslot_hints "T"
                        ] [
                            strreplace $tool_texeditslot_hints "T" ""
                        ]
                    ]
                ]
            ]

            if (&& [
                    =s $[tool_texeditslot_tex@TEX_ENVMAP] ""
                ] [
                    > (strstr $tool_texeditslot_allowedhints "e") 0
                ] [
                    > (strstr $tool_texeditslot_allowedhints "r") 0
                ]) [
                ui_tool_compactswitch tool_texeditslot_hints [
                    p_label     = "Reflection"
                    p_on_change = [ tool_tex_editslot_checkhints 1 ]
                    p_get       = [ >= (strstr $arg1 "e") 0 ]
                    p_set       = [
                        if $arg1 [
                            if (>= (strstr $tool_texeditslot_hints "S") 0) [
                                concatword $tool_texeditslot_hints "erR"
                            ] [
                                concatword $tool_texeditslot_hints "er"
                            ]
                        ] [
                            local _result
                            _result = $tool_texeditslot_hints
                            _result = (strreplace $_result "e" "")
                            _result = (strreplace $_result "r" "")

                            strreplace $_result "R"
                        ]
                    ]
                ]
            ]

            if (&& [
                    =s $[tool_texeditslot_tex@TEX_SPEC] ""
                ] [
                    > (strstr $tool_texeditslot_allowedhints "s") 0
                ]) [
                ui_tool_compactswitch tool_texeditslot_hints [
                    p_label     = "Specularity"
                    p_on_change = [ tool_tex_editslot_checkhints 1 ]
                    p_get       = [ >= (strstr $arg1 "s") 0 ]
                    p_set       = [
                        if $arg1 [
                            concatword $tool_texeditslot_hints "s"
                        ] [
                            strreplace (strreplace $tool_texeditslot_hints "s" "") "S" ""
                        ]
                    ]
                ]
            ]

            if (&& [
                    !=s $[tool_texeditslot_tex@TEX_GLOW] ""
                ] [
                    > (strstr $tool_texeditslot_allowedhints "G") 0
                ]) [
                ui_tool_compactswitch tool_texeditslot_hints [
                    p_label     = "Pulse glow"
                    p_on_change = [ tool_tex_editslot_checkhints 1 ]
                    p_get       = [ >= (strstr $arg1 "G") 0 ]
                    p_set       = [
                        if $arg1 [
                            concatword $tool_texeditslot_hints "G"
                        ] [
                            strreplace $tool_texeditslot_hints "G" ""
                        ]
                    ]
                ]
            ]
        ]

        #ui_tool_divider

        uifill 0 0 [
            ui_tool_switch tool_tex_editslot_apply_rescale [
                p_label      = "Compensate scale"
                p_tip_simple = "Rescale the texture in the world when its size changes"
            ]
        ]

        uihlist $ui_toolpanel_elem_space [
            ui_tool_button [
                p_label    = "Apply"
                p_on_click = [
                    tool_tex_editslot_apply
                ]
                p_width  = 0.1
                p_height = 0.03
            ]

            ui_tool_button [
                p_label    = "Close"
                p_on_click = [
                    tool_do_action ta_textures
                ]
                p_width  = 0.1
                p_height = 0.03
            ]
        ]
    ]
]
