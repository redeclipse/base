ui_tooltex_preview_size = 0.1
ui_tooltex_preview_small_size = 0.06

ui_tool_undowarn = [
    result [
        uicolourtext "Undo for this operation is not supported" $ui_toolview_warn_colour $ui_toolview_small_text_size
    ]
]

ui_tool_tex_options = [
    uigrid 2 0 0 [
        uiclamp 1 1 1 1

        ui_tool_checkbox tool_tex_autoapply [
            uit_label = "Auto apply"
        ]

        ui_tool_checkbox allfaces [
            uit_label = "All cube faces"
        ]

        uialign* -1 -1
    ]
]

ui_tip_tex_variant_slot = [
    uitext (format "Source for variant: #%1" $tool_tex_variantsrc) $ui_toolview_small_text_size
    if (tool_tex_can_focus) [
        uitext "Click to focus" $ui_toolview_tiny_text_size
    ]
]

ui_tool_tex_menu = [
    uivlist 0 [
        uiclamp 1 1

        uihover [
            menu_click_hide = 1
        ]

        ui_tool_button [
            @@@(ui_tool_action_button ta_tex_cull 1 0 -1)
        ]

        ui_tool_button [
            @@@(ui_tool_action_button ta_tex_replace 1 0 -1)
        ]

        ui_tool_button [
            @@@(ui_tool_action_button ta_tex_paint 1 0 -1)
        ]

        uiclamp* 1 1
    ]
]

ui_tool_cur_tex = [
    local buttons_disabled
    buttons_disabled = (|| (< $tool_tex_cur 0) $tool_tex_edit)

    ui_tool_texinfo $tool_tex_cur
    uialign- -1 -1

    uihlist 0 [
        uiclamp 1 1
        uihlist 0 [
            uivslotview $tool_tex_cur $ui_tooltex_preview_size $ui_tooltex_preview_size
            uifill $ui_toolview_small_elem_space
            if (istexvariant $tool_tex_cur) [
                uifill 0 0 [
                    uivslotview $tool_tex_variantsrc $ui_tooltex_preview_small_size $ui_tooltex_preview_small_size
                    uitooltip tex_variant_slot
                    uialign* -1 -1

                    uirelease [
                        tool_focus_tex $tool_tex_variantsrc
                    ]
                ]
            ] [
                uifill $ui_tooltex_preview_small_size
            ]
            uialign* -1 -1
        ]

        ui_tool_button [
            uit_label = "Menu"
            uit_width = 0.05
            uit_height = 0.05
            uit_on_click = [
                toolpanel_show tool_tex_menu "" menu [
                    uit_position = (uicursorpos)
                ]
            ]
        ]
        uialign- 1 -1

        uivlist 0 [
            uihlist 0 [
                uiclamp 1 1 1 1
                ui_tool_button [
                    uit_disabled = @buttons_disabled
                    uit_icon_align = 0
                    uit_height = 0.05
                    uit_width = 0.1
                    uit_icon = @(tool_action_prop ta_tex_apply icon)
                    uit_tip_action = ta_tex_apply
                    uit_on_click = [
                        tool_do_action ta_tex_apply
                    ]
                ]
            ]
            uihlist 0 [
                uiclamp 1 1 1 1
                ui_tool_button [
                    uit_icon = @(tool_action_prop ta_tex_editvariant icon)
                    uit_icon_align = 0
                    uit_tip_action = ta_tex_editvariant
                    uit_height = 0.05
                    uit_width = 0.05
                    uit_on_click = [
                        tool_do_action ta_tex_editvariant
                    ]
                ]
                ui_tool_button [
                    uit_disabled = @buttons_disabled
                    uit_icon = @(tool_action_prop ta_tex_get icon)
                    uit_icon_align = 0
                    uit_tip_action = ta_tex_get
                    uit_height = 0.05
                    uit_width = 0.05
                    uit_on_click = [
                        tool_do_action ta_tex_get
                    ]
                ]
            ]
        ]
        uialign- 1 -1
    ]
]

// 1:<param idx> 2:<disabled>
ui_tool_tex_param_props = [
    result [
        uit_disabled = @(|| $tool_tex_props_disabled $arg2)
        uit_label_size = $ui_toolview_tiny_text_size
        uit_can_reset = 1
        uit_on_change = [
            tool_tex_param_change @@arg1 $tool_tex_autoapply
        ]
    ]
]

// 1:<param idx>
ui_tool_tex_shader_param_props = [
    result [
        uit_disabled = @(|| $tool_tex_props_disabled (! (tool_tex_accepts_param $arg1)))
        uit_label_size = $ui_toolview_tiny_text_size
        uit_can_reset = 1
        uit_on_change = [
            tool_tex_shader_param_change @@arg1 $tool_tex_autoapply
        ]
    ]
]

// 1:<text> 2:<fields> 3:<disabled>
ui_tool_tex_param_group = [
    local disabled
    disabled = (|| $tool_tex_props_disabled $arg3)

    local colour
    colour = (? $disabled $ui_toolview_dark_colour $ui_toolview_accent_colour)

    uihlist 0 [
        uiclamp 1 1
        uicolourtext $arg1 $colour $ui_toolview_small_text_size
        uivlist 0 [
            @arg2
        ]
    ]
    uifill 0 $ui_toolview_small_elem_space
    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uifill 0 $ui_toolview_small_elem_space
]

// 1:<text> 2:<param idx> 3:<fields>
ui_tool_tex_shader_param_group = [
    local disabled accepts_param
    accepts_param = (tool_tex_accepts_param $arg2)
    disabled = (|| $tool_tex_props_disabled (! $accepts_param))

    if $disabled [] [
        uihlist 0 [
            uiclamp 1 1
            uicolourtext $arg1 $ui_toolview_accent_colour $ui_toolview_small_text_size
            uivlist 0 [
                @arg3
            ]
        ]
        uifill 0 $ui_toolview_small_elem_space
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolview_small_elem_space
    ]
]

ui_tool_tex_param_mp_warning = [
    if (isonline) [
        local warn_text

        if $nompedit [
            warn_text = "Parameter not available in multiplayer"
        ] [
            warn_text = "Parameter will cause desynchronization"
        ]

        uispace 0 $ui_toolview_small_elem_space [
            uicolourtext $warn_text $ui_toolview_warn_colour $ui_toolview_tiny_text_size
        ]
    ]
]

ui_tool_tex_dim_params = [
    ui_tool_tex_param_group "Scale" [
        ui_tool_numinput tool_tex_vscale 0.125 8 0 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vscale)
            uit_reset_val = $tool_tex_vscale_default
        ]
    ]

    local step tex_w tex_h
    step = (divf (pow 2 $gridpower) $tool_tex_vscale)
    tex_w = (getvteximgwidth $tool_tex_cur)
    tex_h = (getvteximgheight $tool_tex_cur)

    ui_tool_tex_param_group "Offset" [
        ui_tool_numinput tool_tex_voffsetx 0 $tex_w $step [
            @(ui_tool_tex_param_props $tool_tex_param_idx_voffset)
            uit_val_format = i
            uit_reset_val = $tool_tex_voffsetx_default
        ]
        ui_tool_numinput tool_tex_voffsety 0 $tex_h $step [
            @(ui_tool_tex_param_props $tool_tex_param_idx_voffset)
            uit_val_format = i
            uit_reset_val = $tool_tex_voffsety_default
        ]
    ]

    ui_tool_tex_param_group "Scroll" [
        ui_tool_numinput tool_tex_vscrollx -10 10 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vscroll)
            uit_label = "X"
            uit_reset_val = $tool_tex_vscrollx_default
        ]
        ui_tool_numinput tool_tex_vscrolly -10 10 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vscroll)
            uit_label = "Y"
            uit_reset_val = $tool_tex_voffsety_default
        ]
    ]

    ui_tool_tex_param_group "Rotate" [
        ui_tool_dropdown tool_tex_vrotate [
            "0 degrees"
            "90 degrees"
            "180 degrees"
            "270 degrees"
            "H-flipped"
            "V-flipped"
            "90 degrees, H-flipped"
            "90 degrees, V-flipped"
        ] [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vrotate)
            uit_reset_val = $tool_tex_vrotate_default
        ]
        uialign- 1

        ui_tool_numinput tool_tex_vangle_nudge -8 8 1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vangle)
            uit_label = "Angle nudge"
            uit_val_format = i
            uit_reset_val = $tool_tex_vangle_nudge_default
        ]
    ]
]

ui_tool_tex_blend_params = [
    ui_tool_tex_param_group "Layer" [
        ui_tool_tex_param_mp_warning
        ui_tool_texselect tool_tex_vlayer [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vlayer (isonline))
            uit_reset_val = $tool_tex_vlayer_default
            uit_align = 1
            uit_sky_tex = 0
        ]
    ] (isonline)

    ui_tool_tex_param_group "Detail" [
        ui_tool_tex_param_mp_warning
        ui_tool_texselect tool_tex_vdetail [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vdetail (isonline))
            uit_reset_val = $tool_tex_vdetail_default
            uit_align = 1
            uit_sky_tex = 0
        ]
    ] (isonline)
]

ui_tool_tex_colour_params = [
    ui_tool_tex_param_group "Colour" [
        ui_tool_colour tool_tex_vcolour [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vcolour)
            uit_max_val = 2
            uit_reset_val = $tool_tex_vcolour_default
        ]
    ]

    ui_tool_tex_param_group "Palette" [
        ui_tool_dropdown tool_tex_vpalette_set [
            "Pulse"
            "Team"
            "Weapon"
        ] [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vpalette)
            uit_reset_val = $tool_tex_vpalette_set_default
            uit_label = "Type"
        ]

        ui_tool_dropdown tool_tex_vpalette_id (at $tool_palette_ids $tool_tex_vpalette_set) [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vpalette)
            uit_reset_val = $tool_tex_vpalette_set_default
            uit_label = "Id"
        ]

        uifill 0 $ui_toolview_mid_elem_space

        ui_tool_checkbox tool_tex_vpalette_enforce [
            uit_label = "Enforce"
            uit_label_size = $ui_toolview_tiny_text_size
            @(ui_tool_tex_param_props $tool_tex_param_idx_vpalette)
        ]
        uialign- 1
    ]

    ui_tool_tex_param_group "Alpha" [
        ui_tool_numinput tool_tex_valpha_front 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_valpha)
            uit_label = "Front"
            uit_reset_val = $tool_tex_valpha_front_default
        ]
        ui_tool_numinput tool_tex_valpha_back 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_valpha)
            uit_label = "Back"
            uit_reset_val = $tool_tex_valpha_back_default
        ]
    ]

    ui_tool_tex_param_group "Alpha refract" [
        local refract_col_dis
        refract_col_dis = (|| $tool_tex_props_disabled (=f $tool_tex_vrefract_strength))

        ui_tool_numinput tool_tex_vrefract_strength 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vrefract)
            uit_label = "Strength"
            uit_reset_val = $tool_tex_vrefract_strength_default
        ]

        uifill 0 $ui_toolview_mid_elem_space

        ui_tool_colour tool_tex_vrefract_colour [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vrefract $refract_col_dis)
            uit_reset_val = $tool_tex_vrefract_colour_default
        ]
    ]

    ui_tool_tex_param_group "Alpha shadow" [
        local shadow_alpha_dis
        shadow_alpha_dis = (|| $tool_tex_props_disabled (! $tool_tex_vshadow_en))

        ui_tool_numinput tool_tex_vshadow_alpha 0 1 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vshadow $shadow_alpha_dis)
            uit_label = "Strength"
            uit_reset_val = $tool_tex_vshadow_alpha_default
        ]

        uifill 0 $ui_toolview_mid_elem_space

        ui_tool_checkbox tool_tex_vshadow_en [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vshadow)
            uit_label = "Enable shadow"
        ]
        uialign- 1
    ]
]

ui_tool_tex_shader_params = [
    ui_tool_tex_shader_param_group "Glow" $tool_tex_shader_param_idx_glowcolor [
        ui_tool_colour tool_tex_glowcolor [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_glowcolor)
            uit_max_val = 2
            uit_reset_val = $tool_tex_glowcolor_default
        ]
    ]

    ui_tool_tex_shader_param_group "Pulse glow" $tool_tex_shader_param_idx_pulseglowcolor [
        ui_tool_colour tool_tex_pulseglowcolor [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_pulseglowcolor)
            uit_max_val = 2
            uit_reset_val = $tool_tex_pulseglowcolor_default
        ]
    ]

    ui_tool_tex_shader_param_group "Pulse speed" $tool_tex_shader_param_idx_pulseglowspeed [
        ui_tool_numinput tool_tex_pulseglowspeed_val 0 60 0.01 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_pulseglowspeed)
            uit_reset_val = $tool_tex_pulseglowspeed_val_default
            uit_label = "Hz"
        ]
    ]

    ui_tool_tex_shader_param_group "Pulse offset" $tool_tex_shader_param_idx_pulseglowoffset [
        ui_tool_numinput tool_tex_pulseglowoffset_val 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_pulseglowoffset)
            uit_reset_val = $tool_tex_pulseglowoffset_val_default
        ]
    ]

    ui_tool_tex_shader_param_group "Gloss" $tool_tex_shader_param_idx_gloss [
        ui_tool_numinput tool_tex_gloss_val 0 2 1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_gloss)
            uit_reset_val = $tool_tex_gloss_val_default
            uit_val_format = i
        ]
    ]

    ui_tool_tex_shader_param_group "Specularity" $tool_tex_shader_param_idx_specscale [
        ui_tool_numinput tool_tex_specscale_val 0 10 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_specscale)
            uit_reset_val = $tool_tex_specscale_val_default
        ]
    ]

    ui_tool_tex_shader_param_group "Normal scale" $tool_tex_shader_param_idx_normalscale [
        ui_tool_numinput tool_tex_normalscale_val -10 10 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_normalscale)
            uit_reset_val = $tool_tex_normalscale_val_default
        ]
    ]

    ui_tool_tex_shader_param_group "Reflect min" $tool_tex_shader_param_idx_envmin [
        ui_tool_colour tool_tex_envmin [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_envmin)
            uit_max_val = 2
            uit_reset_val = $tool_tex_envmin_default
        ]
    ]

    ui_tool_tex_shader_param_group "Reflect scale" $tool_tex_shader_param_idx_envscale [
        ui_tool_colour tool_tex_envscale [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_envscale)
            uit_max_val = 2
            uit_reset_val = $tool_tex_envscale_default
        ]
    ]

    ui_tool_tex_shader_param_group "Triplanar bias" $tool_tex_shader_param_idx_triplanarbias [
        ui_tool_numinput tool_tex_triplanarbias_x 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_triplanarbias)
            uit_reset_val = $tool_tex_triplanarbias_x_default
            uit_label = "X"
        ]
        ui_tool_numinput tool_tex_triplanarbias_y 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_triplanarbias)
            uit_reset_val = $tool_tex_triplanarbias_y_default
            uit_label = "Y"
        ]
        ui_tool_numinput tool_tex_triplanarbias_z 0 1 0.1 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_triplanarbias)
            uit_reset_val = $tool_tex_triplanarbias_z_default
            uit_label = "Z"
        ]
    ]

    ui_tool_tex_shader_param_group "Parallax" $tool_tex_shader_param_idx_parallaxscale [
        ui_tool_numinput tool_tex_parallaxscale_low -1 1 0.001 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_parallaxscale)
            uit_reset_val = $tool_tex_parallaxscale_low_default
            uit_label = "Low"
        ]
        ui_tool_numinput tool_tex_parallaxscale_high -1 1 0.001 [
            @(ui_tool_tex_shader_param_props $tool_tex_shader_param_idx_parallaxscale)
            uit_reset_val = $tool_tex_parallaxscale_high_default
            uit_label = "High"
        ]
    ]
]

ui_tool_tex_other_params = [
    ui_tool_tex_param_group "Player coast scale" [
        ui_tool_numinput tool_tex_vcoastscale 0 1000 0.1 [
            @(ui_tool_tex_param_props $tool_tex_param_idx_vcoastscale)
            uit_reset_val = $tool_tex_vcoastscale_default
        ]
    ]
]

ui_tool_tex_params = [
    tool_tex_props_disabled = (< $tool_tex_cur 0)

    ui_tool_collapsegroup tool_tex_params_dim [
        @@ui_tool_tex_dim_params
    ] [
        uit_label = "Dimensions"
    ]

    ui_tool_collapsegroup tool_tex_params_blend [
        @@ui_tool_tex_blend_params
    ] [
        uit_label = "Blending"
    ]

    ui_tool_collapsegroup tool_tex_params_col [
        @@ui_tool_tex_colour_params
    ] [
        uit_label = "Colour"
    ]

    ui_tool_collapsegroup tool_tex_params_shader [
        @@ui_tool_tex_shader_params
    ] [
        uit_label = "Shader parameters"
    ]

    ui_tool_collapsegroup tool_tex_params_other [
        @@ui_tool_tex_other_params
    ] [
        uit_label = "Other"
    ]
]

ui_tool_tex_edit = [
    tool_tex_params_sync

    uihlist 0 [
        ui_tool_button [
            uit_disabled = $tool_tex_autoapply
            uit_icon = "textures/icons/attack"
            uit_icon_align = 0
            uit_tip_simple = "Apply changes"
            uit_width = 0.08
            uit_height = 0.05
            uit_on_click = [
                tool_tex_commit_all_params
            ]
        ]
    ]

    ui_tool_vscrollarea [
        uivlist 0 [
            uiclamp 1 1
            @@@ui_tool_tex_params
        ]
    ] [
        uit_height = 0.45
    ]

    if $tool_tex_autoapply [
        tool_tex_commit_all_params
    ]
]

ui_tool_tex_list = [
    ui_tool_texlist tool_tex_cur [
        uit_on_select = [
            if $tool_tex_autoapply [
                tool_action_tex_apply
            ]
        ]
        uit_sel_area = 0.45
    ]
    tool_tex_variantsrc = (getvindex $tool_tex_cur)
]

ui_tool_textures = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1 1 1
        ui_tool_cur_tex
        ui_tool_tex_options
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolview_small_elem_space

        do $(at [ui_tool_tex_list ui_tool_tex_edit] $tool_tex_edit)
    ]
]

ui_tool_texreplace = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1

        ui_tool_checkbox tool_tex_replace_sel [
            uit_label = "Limit to selection"
        ]
        uialign- -1

        uihlist 0 [
            uiclamp 1 1

            uitext "Old texture" $ui_toolview_small_text_size

            uihlist $ui_toolpanel_elem_space [
                ui_tool_texselect tool_tex_cur [
                    uit_align = 1
                ]
                uialign- 1

                ui_tool_button [
                    uit_icon = "textures/icons/arrow"
                    uit_icon_align = 0
                    uit_width = 0.03
                    uit_tip_simple = "Get from selection"
                    uit_on_click = [
                        tool_focus_tex (getseltex 1)
                    ]
                ]
                uiclamp- 0 0 1 1
            ]
        ]

        ui_tool_button [
            uit_label = "Swap"
            uit_width = 0.1
            uit_on_click = [
                local swap_tmp
                swap_tmp = $tool_tex_cur
                tool_focus_tex $tool_tex_replace
                tool_tex_replace = $swap_tmp
            ]
        ]
        uialign- 1

        uihlist 0 [
            uiclamp 1 1

            uitext "New texture" $ui_toolview_small_text_size

            uihlist $ui_toolpanel_elem_space [
                ui_tool_texselect tool_tex_replace [
                    uit_align = 1
                ]
                uialign- 1

                ui_tool_button [
                    uit_icon = "textures/icons/arrow"
                    uit_icon_align = 0
                    uit_width = 0.03
                    uit_tip_simple = "Get from selection"
                    uit_on_click = [
                        tool_tex_replace = (getseltex 1)
                    ]
                ]
                uiclamp- 0 0 1 1
            ]
        ]

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        uihlist 0 [
            ui_tool_button [
                uit_disabled = (|| [< $tool_tex_cur 0] [< $tool_tex_replace])
                uit_icon = "textures/icons/action"
                uit_icon_align = 0
                uit_width = 0.05
                uit_height = 0.05
                uit_tip_simple = "Replace"
                uit_on_click = [
                    if $tool_tex_replace_sel [
                        replacetexsel $tool_tex_replace $tool_tex_cur
                    ] [
                        replacetex $tool_tex_replace $tool_tex_cur
                    ]
                ]
            ]
            ui_tool_button [
                uit_icon = "textures/icons/warning"
                uit_icon_align = 0
                uit_width = 0.05
                uit_height = 0.05
                uit_tip_simple = "Close"
                uit_on_click = [
                    toolpanel_hide tool_texreplace
                ]
            ]
        ]

        @(ui_tool_undowarn)
    ]
]

ui_tool_texpaint = [
    uivlist $ui_toolpanel_elem_space [
        ui_tool_dropdown blendpaintmode [
            "Off"
            "Overwrite"
            "Merge"
            "Inverted erase"
            "Inverted merge"
            "Erase"
        ] [
            uit_label = "Paint mode"
        ]

        ui_tool_imagelist blendbrush $tool_tex_blend_brushes [
            uit_sel_cols = 6
            uit_sel_size = 0.05
            uit_sel_area = 0.3
            uit_path = "blendbrush"
            uit_allow_custom = 0
            uit_get = [
                getblendbrushname $arg1
            ]
        ]
    ]
]
