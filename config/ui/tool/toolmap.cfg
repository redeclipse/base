ui_toolview_panel_save_as_save_button_width = (uiwidth 0.07)

tool_save_as_filename = ""

ui_tool_save_as = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_textinput "File name:" tool_save_as_filename 16 "[click here to set file name]"
        ui_tool_button [
            uit_label = "Save"
            uit_on_click = [
                savemap $tool_save_as_filename
            ]
            uit_width = $ui_toolview_panel_save_as_save_button_width
            uit_disabled = (=s $tool_save_as_filename "")
        ]
        uialign- 1
    ]
]

ui_tool_map_settings = [
    uivlist 0 [
        uiclamp 1 1
        uiimage $mapname 0xFFFFFF 0 0.24 0.18

        uifill 0 $ui_toolview_small_elem_space
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolview_small_elem_space

        ui_tool_textinput "Map path:" mapname 24
        ui_tool_textinput "Title:" maptitle 24
        ui_tool_textinput "Author:" mapauthor 24

        uifill 0 $ui_toolview_small_elem_space
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolview_small_elem_space

        ui_tool_switch forcemapvariant [
            uit_label = "Shown map variant"
            uit_label_size = $ui_toolview_small_text_size
            uit_options = [ "Normal" "Alternative" ]
            uit_get = [
                = $arg1 2
            ]
            uit_set = [
                ? (= $arg1 2) 1 2
            ]
        ]

        uifill 0 $ui_toolview_small_elem_space
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolview_small_elem_space

        ui_tool_button [
            @@@(ui_tool_action_button ta_map_new)
            uit_height = 0.05
        ]
        uiclamp- 1 1
    ]
]

tool_map_new_size = 64

ui_tool_map_new = [
    uivlist $ui_toolview_base_elem_space [
        uifill 0.2
        ui_tool_numinput tool_map_new_size 64 1024 0 [
            uit_label = "Size (edge in meters)"
            uit_val_format = i
            uit_can_edit = 0
            uit_has_menu = 0
        ]

        uihlist 0 [
            ui_tool_button [
                uit_label = "Ok"
                uit_width = 0.2
                uit_height = 0.05
                uit_on_click = [
                    toolpanel_hide tool_map_new
                    newmap (log2 (* $tool_map_new_size 16))
                ]
            ]
        ]
    ]
]
