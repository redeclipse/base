tool_imageselect_menu_props = [
    [ p_var       []     ]
    [ p_items     []     ]
    [ p_sel_cols  2      ]
    [ p_sel_size  0.16   ]
    [ p_sel_area  0.4    ]
    [ p_sel_space 0.0035 ]
    [ p_on_change []     ]
]

ui_tool_imageselect_picker = [
    @(props $tool_imageselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_imagelist $p_var $p_items [
            p_sel_cols  = @p_sel_cols
            p_sel_size  = @p_sel_size
            p_sel_area  = @p_sel_area
            p_sel_space = @p_sel_space
            p_on_change = [@@p_on_change]
        ]
    ]
]

tool_imageselect_props = [
    [ p_noonline     0                              ]
    [ p_disabled     0                              ]
    [ p_sel_cols     2                              ]
    [ p_sel_size     0.16                           ]
    [ p_sel_area     0.4                            ]
    [ p_sel_space    0.0035                         ]
    [ p_can_reset    0                              ]
    [ p_reset_val    0                              ]
    [ p_text_size    $ui_tool_text_size_xs_unscaled ]
    [ p_tip_simple   ""                             ]
    [ p_size         0.06                           ]
    [ p_picker_width [(uiwidth 0.2)]                ]
    [ p_align        1                              ]
    [ p_pad          $ui_padsmall                   ]
    [ p_on_change    []                             ]
    [ p_id           []                             ]
]

// 1:<var> 2:<images> 3:<props>
ui_tool_imageselect = [
    @(props $tool_imageselect_props arg3)

    @(tool_ui_scale p_text_size)
    @(tool_ui_scale p_size)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _value
    _value = (? $p_disabled "" $$arg1)

    uivlist 0 [
        @@(ui_tool_interactable 0 0 [
            tool_control_highlight

            uispace $p_pad $p_pad [
                uiimage (concatword "<thumbnail:256/256>" $_value) 0xffffff 0 $p_size $p_size
            ]

            if $p_disabled [] [
                uirelease [
                    toolpanel_open tool_imageselect_picker popup [
                        p_position  = (uicursorpos)
                        p_width     = @p_picker_width
                        p_user_data = [
                            p_var       = @@arg1
                            p_items     = [@@@arg2]
                            p_sel_cols  = @@p_sel_cols
                            p_sel_size  = @@p_sel_size
                            p_sel_area  = @@p_sel_area
                            p_sel_space = @@p_sel_space
                            p_on_change = [@@@p_on_change]
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        p_can_reset = @p_can_reset
                        p_reset_val = [@@p_reset_val]
                        p_on_change = [@@p_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1
                    ] [
                        tool_param_paste @arg1 [
                            p_on_change = [@@p_on_change]
                        ]
                    ]
                ]
            ]

            ui_tool_tip [
                p_tip_simple  = [@@p_tip_simple]
                p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
            ]
        ])

        uitext (? $_value $_value "No texture selected" ) $p_text_size

        uialign* $p_align
    ]
]
