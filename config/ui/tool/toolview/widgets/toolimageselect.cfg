tool_imageselect_var = []
tool_imageselect_images = []

tool_imageselect_menu_props = [
    [ uit_sel_cols 2 ]
    [ uit_sel_size 0.16 ]
    [ uit_sel_area 0.4 ]
    [ uit_sel_space 0.0035 ]
    [ uit_on_change [] ]
]

ui_tool_imageselect_picker = [
    @(tool_props $tool_imageselect_menu_props toolpanel_props_menu)

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1
        ui_tool_imagelist $tool_imageselect_var $tool_imageselect_images [
            uit_sel_cols = @uit_sel_cols
            uit_sel_size = @uit_sel_size
            uit_sel_area = @uit_sel_area
            uit_sel_space = @uit_sel_space
            uit_on_select = [
                menu_click_hide = 1
            ]
            uit_on_change = [@@uit_on_change]
        ]
    ]
]

tool_imageselect_props = [
    [ uit_disabled 0 ]
    [ uit_sel_cols 2 ]
    [ uit_sel_size 0.16 ]
    [ uit_sel_area 0.4 ]
    [ uit_sel_space 0.0035 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_text_size $ui_toolview_tiny_text_size ]
    [ uit_tip_simple "" ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_align 1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<images> 3:<props>
ui_tool_imageselect = [
    @(tool_props $tool_imageselect_props arg3)

    local value
    value = (? $uit_disabled "" $$arg1)

    uivlist 0 [
        uicolour 0 0 0 [
            uiimage (concatword "<thumbnail:256/256>" $value) 0xffffff 0 $uit_size $uit_size

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip_simple = [@@uit_tip_simple]
                ]

                uirelease [
                    tool_imageselect_var = $arg1
                    tool_imageselect_images = $arg2

                    toolpanel_show tool_imageselect_picker "" menu [
                        uit_sel_cols = @uit_sel_cols
                        uit_sel_size = @uit_sel_size
                        uit_sel_area = @uit_sel_area
                        uit_sel_space = @uit_sel_space
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_on_change = [@@uit_on_change]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change
                ]
            ]
        ]

        uitext (? $value $value "No texture selected" ) $uit_text_size

        uialign* $uit_align
    ]
]
