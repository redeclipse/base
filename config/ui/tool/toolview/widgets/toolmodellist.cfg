ui_tool_modellist_sel_colour = 0xFF0000

tool_modellist_filter_query = ""
tool_modellist_text_scroll = 0

// 1:<slot>
tool_modellist_filter = [
    local model_name
    model_name = (mapmodelindex $arg1)
    result (|| [! $tool_modellist_filter_query] [!= (strstr $model_name $tool_modellist_filter_query) -1])
]

tool_modellist_item_click = 0

// 1:<model path>
ui_tool_modellist_item_text = [
    uicolour $ui_toolview_text_bg_colour 0 (*f $uit_sel_size 0.2) [
        uiclamp 1 1
    ]
    uialign- 0 1

    uiclip $text_area 0 0 0 [
        uifill 0 (*f $uit_sel_size 0.2) [
            uiclamp 1 1
            uitext $arg1 $ui_toolview_tiny_text_size
        ]
        uiclipoffsetx (*f $tool_modellist_text_scroll (-f (uigetclipvirtw) $text_area))
    ]
    uialign- 0 1
]

// 1:<slot> 2:<highlight>
ui_tool_modellist_item = [
    local model_path text_area
    model_path = (mapmodelindex $arg1)
    model_name = (filename $model_path)
    text_area = (*f $uit_sel_size 0.8)

    tool_modellist_item_click = 0

    uicolour 0 $uit_sel_size $uit_sel_size [
        uimodelpreview $model_path "" 1 1 $uit_sel_size $uit_sel_size [
            uirelease [
                tool_modellist_item_click = 1
            ]
            if $arg2 [
                uioutline $ui_tool_modellist_sel_colour
                uiclamp- 1 1 1 1
            ]
            @@(ui_tool_preview_spin)
        ]

        if (uidrawn) [
            ui_tool_modellist_item_text $model_path
        ]
    ]
]

tool_modellist_props = [
    [ uit_sel_cols 3 ]
    [ uit_sel_size 0.11 ]
    [ uit_sel_area 0.6 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 24 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_on_change [] ]
    [ uit_on_select [] ]
]

// 1:<select var> 2:<props>
ui_tool_modellist = [
    @(tool_props $tool_modellist_props arg2)

    tool_modellist_text_scroll = (minf (divf (mod (getmillis) 5000) 2500) 1)

    uivlist 0 [
        uiclamp 1 1

        local model_num
        model_num = (mapmodelindex)

        ui_tool_textinput tool_modellist_filter_query $uit_filter_query_length [
            uit_label = "Search:"
        ]

        uifill 0 $ui_toolview_base_elem_space

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                uigrid $uit_sel_cols $uit_sel_space $uit_sel_space [
                    loop slot $model_num [
                        if (tool_modellist_filter $slot) [
                            ui_tool_modellist_item $slot (= $slot $$arg1)
                            if $tool_modellist_item_click [
                                tool_param_set $arg1 $slot $uit_on_change
                                uit_on_select
                            ]
                        ]
                    ]
                ]
            ]
            uivscroll $uit_slider_size $uit_sel_area 3
            uialign 1
        ]
    ]
]