ui_tool_modellist_sel_colour = 0xFF0000

tool_modellist_filter_query = ""

// 1:<slot>
tool_modellist_filter = [
    local _model_name
    _model_name = (mapmodelindex $arg1)
    result (|| [! $tool_modellist_filter_query] [!= (strstr $_model_name $tool_modellist_filter_query) -1])
]

// 1:<model path>
ui_tool_modellist_item_text = [
    uicolour $ui_tool_text_bg_colour 0 (*f $p_sel_size 0.2) [
        uiclamp 1 1

        ui_tool_autoscroll_text $arg1 [
            p_width = @_text_area
        ]
    ]
    uialign- 0 1
]

// 1:<slot> 2:<highlight> 3:<var>
ui_tool_modellist_item = [
    local _model_path _text_area
    _model_path = (mapmodelindex $arg1)
    _model_name = (filename $_model_path)
    _text_area = (*f $p_sel_size 0.8)

    uicolour 0 $p_sel_size $p_sel_size [
        uimodelpreview $_model_path "" 1 1 $p_sel_size $p_sel_size [
            uirelease [
                tool_param_set $arg3 $arg1 $p_on_change
                p_on_select
            ]

            if $arg2 [
                uioutline $ui_tool_modellist_sel_colour
                uiclamp- 1 1 1 1
            ]

            @@(ui_tool_preview_spin)
        ]

        if (uidrawn) [
            ui_tool_modellist_item_text $_model_path
        ]
    ]
]

tool_modellist_props = [
    [ p_sel_cols            3                         ]
    [ p_sel_size            0.11                      ]
    [ p_sel_area            0.6                       ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 24                        ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_on_change           []                        ]
    [ p_on_select           []                        ]
]

// 1:<select var> 2:<props>
ui_tool_modellist = [
    @(props $tool_modellist_props arg2)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    uivlist 0 [
        uiclamp 1 1

        local _model_num
        _model_num = (mapmodelindex)

        ui_tool_textinput tool_modellist_filter_query $p_filter_query_length [
            p_label = "Search:"
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $p_sel_area [
                uialign -1 -1
                uigrid $p_sel_cols $p_sel_space $p_sel_space [
                    loop slot $_model_num [
                        if (tool_modellist_filter $slot) [
                            ui_tool_modellist_item $slot (= $slot $$arg1) $arg1
                        ]
                    ]
                ]
            ]
            uivscroll $p_slider_size $p_sel_area 3
            uialign 1
        ]
    ]
]