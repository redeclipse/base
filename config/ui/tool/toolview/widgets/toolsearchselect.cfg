tool_searchselect_menu_props = [
    [ p_var            [] ]
    [ p_width          0  ]
    [ p_on_change      [] ]
    [ p_list           [] ]
    [ p_num_items_func [] ]
    [ p_item_func      [] ]
]

ui_tool_searchselect_picker = [
    @(props $tool_searchselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_searchlist $p_var [
            p_sel_area       = 0.3
            p_width          = (-f @p_width (*f $ui_toolpanel_elem_space 2))
            p_on_change      = [@@p_on_change]
            p_list           = [@@p_list]
            p_num_items_func = [@@p_num_items_func]
            p_item_func      = [@@p_item_func]
        ]
    ]
]

tool_searchselect_props = [
    [ p_disabled        0                               ]
    [ p_can_reset       0                               ]
    [ p_reset_val       0                               ]
    [ p_value           ""                              ]
    [ p_val_size        $ui_tool_text_size_s_unscaled   ]
    [ p_label           ""                              ]
    [ p_label_size      $ui_tool_text_size_s_unscaled   ]
    [ p_width           $ui_tool_numinput_size_unscaled ]
    [ p_height          0                               ]
    [ p_picker_width    [(uiwidth 0.2)]                 ]
    [ p_on_change       []                              ]
    [ p_list            []                              ]
    [ p_num_items_func  [[listlen $p_list]]             ]
    [ p_item_func       [[at $p_list $arg1]]            ]
]

// 1:<var> 2:<props>
ui_tool_searchselect = [
    @(props $tool_searchselect_props arg2)

    @(tool_ui_scale p_val_size)
    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_width)

    local _value _col
    _value = (? $p_disabled "-" (? $p_value $p_value (p_item_func $$arg1)))
    _col = (? $p_disabled $ui_tool_dark_colour $ui_tool_accent_colour)

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $p_label $p_label_size [ uialign -1 ]

        @@(ui_tool_interactable [$p_width] [$p_height] [
            uispace $ui_padsmall $ui_padsmaller [
                uitext $_value $p_val_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uicircle $_col 0.005
            ]

            if $p_disabled [] [
                uirelease [
                    tool_searchselect_var = $arg1

                    toolpanel_open tool_searchselect_picker popup [
                        p_position  = (uicursorpos)
                        p_width     = @p_picker_width
                        p_user_data = [
                            p_var            = @@arg1
                            p_width          = @@p_picker_width
                            p_on_change      = [@@@p_on_change]
                            p_list           = [@@@p_list]
                            p_num_items_func = [@@@p_num_items_func]
                            p_item_func      = [@@@p_item_func]
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        p_can_reset = @p_can_reset
                        p_reset_val = [@@p_reset_val]
                        p_on_change = [@@p_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1
                    ] [
                        tool_param_paste @arg1 [
                            p_on_change = [@@@p_on_change]
                        ]
                    ]
                ]
            ]

        ] [$p_disabled])
    ]
]