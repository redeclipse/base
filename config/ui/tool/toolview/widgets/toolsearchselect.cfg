tool_searchselect_var = []

tool_searchselect_menu_props = [
    [ uit_width 0 ]
    [ uit_on_change [] ]
    [ uit_list [] ]
    [ uit_num_items_func [] ]
    [ uit_item_func [] ]
]

ui_tool_searchselect_picker = [
    @(tool_props $tool_searchselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1
        ui_tool_searchlist $tool_searchselect_var [
            uit_sel_area = 0.3
            uit_width = (-f @uit_width (*f $ui_toolpanel_elem_space 2))
            uit_on_change = [@@uit_on_change]
            uit_list = [@@uit_list]
            uit_num_items_func = [@@uit_num_items_func]
            uit_item_func = [@@uit_item_func]
        ]
    ]
]

tool_searchselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_value "" ]
    [ uit_val_size $ui_toolview_small_text_size ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_width $ui_toolview_num_input_size ]
    [ uit_height 0 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_on_change [] ]
    [ uit_list [] ]
    [ uit_num_items_func [[listlen $uit_list]] ]
    [ uit_item_func [[at $uit_list $arg1]] ]
]

// 1:<var> 2:<props>
ui_tool_searchselect = [
    @(tool_props $tool_searchselect_props arg2)

    local _value _col
    _value = (? $uit_disabled "-" (? $uit_value $uit_value (uit_item_func $$arg1)))
    _col = (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_accent_colour)

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size [ uialign -1 ]
        @@(ui_tool_interactable [$uit_width] [$uit_height] [
            uispace $ui_padsmall $ui_padsmaller [
                uitext $_value $uit_val_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uicircle $_col 0.005
            ]

            if $uit_disabled [] [
                uirelease [
                    tool_searchselect_var = $arg1

                    toolpanel_open tool_searchselect_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_user_data = [
                            uit_width = @@uit_picker_width
                            uit_on_change = [@@@uit_on_change]
                            uit_list = [@@@uit_list]
                            uit_num_items_func = [@@@uit_num_items_func]
                            uit_item_func = [@@@uit_item_func]
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change
                ]
            ]

        ] [$uit_disabled])
    ]
]