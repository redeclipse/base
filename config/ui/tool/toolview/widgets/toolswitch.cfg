tool_switch_props = [
    [ uit_disabled 0 ]
    [ uit_options [["Off" "On"]] ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_tiny_text_size ]
    [ uit_option_size $ui_toolview_tiny_text_size ]
    [ uit_width 0 ]
    [ uit_height 0 ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_on_change [] ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[! $arg1]] ]
]

// 1:<var> 2:<props>
ui_tool_switch = [
    @(tool_props $tool_switch_props arg2)

    local check

    if $uit_disabled [
        check = -1
    ] [
        check = (uit_get $$arg1)
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size [ uialign -1 ]
        @(ui_tool_interactable [$uit_width] [$uit_height] [
            uispace $ui_toolview_base_elem_space $ui_toolview_small_elem_space [
                uihlist 0 [
                    loop state 2 [
                        local colour
                        colour = (? (= $check $state) $ui_toolview_accent_colour $ui_toolview_dark_colour)

                        uispace $ui_toolview_small_elem_space $ui_toolview_small_elem_space [
                            uicolourtext (at $uit_options $state) $colour $uit_option_size
                        ]
                    ]
                ]
            ]

            ui_tool_tip [
                uit_tip = [@@@uit_tip]
                uit_tip_simple = [@@@uit_tip_simple]
            ]

            if $uit_disabled [] [
                uirelease [
                    set $arg1 (uit_set $$arg1)
                    uit_on_change
                ]
            ]
        ] [$uit_disabled])
    ]
]

tool_compactswitch_props = [
    [ uit_disabled 0 ]
    [ uit_label "Enable" ]
    [ uit_label_size $ui_toolview_tiny_text_size ]
    [ uit_width 0 ]
    [ uit_height 0 ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_on_change [] ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[! $arg1]] ]
]

// 1:<var> 2:<props>
ui_tool_compactswitch = [
    @(tool_props $tool_compactswitch_props arg2)

    local check colour
    check = (uit_get $$arg1)
    colour = (? (&& $check [! $uit_disabled]) $ui_toolview_accent_colour $ui_toolview_dark_colour)

    @(ui_tool_interactable [$uit_width] [$uit_height] [
        uispace $ui_padsmall $ui_padsmaller [
            uicolourtext $uit_label $colour $uit_label_size
        ]

        ui_tool_tip [
            uit_tip = [@@uit_tip]
            uit_tip_simple = [@@uit_tip_simple]
        ]

        if $uit_disabled [] [
            uirelease [
                tool_param_set $arg1 (uit_set $$arg1) $uit_on_change
            ]
        ]
    ] [$uit_disabled])
]

tool_checkbox_props = [
    [ uit_disabled 0 ]
    [ uit_label "Enable" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_on_change [] ]
    [ uit_size 0.01 ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[! $arg1]] ]
]

ui_tool_checkbox = [
    @(tool_props $tool_checkbox_props arg2)

    local check colour
    check = (uit_get $$arg1)
    colour = (? (&& $check [! $uit_disabled]) $ui_toolview_accent_colour $ui_toolview_dark_colour)

    uitarget 0 0 [
        uihlist $ui_toolview_small_elem_space [
            @@@(ui_tool_interactable 0 0 [
                uispace $ui_padsmall $ui_padsmall [
                    uicolour $colour $uit_size $uit_size
                ]
            ] [$uit_disabled])

            uicolourtext $uit_label (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_accent_colour) $uit_label_size
        ]

        if $uit_disabled [] [
            uirelease [
                tool_param_set $arg1 (uit_set $$arg1) $uit_on_change
            ]
        ]

        ui_tool_tip [
            uit_tip = [@@uit_tip]
            uit_tip_simple = [@@uit_tip_simple]
        ]
    ]
]
