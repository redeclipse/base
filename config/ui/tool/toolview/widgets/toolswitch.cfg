tool_switch_props = [
    [ p_noonline    0                              ]
    [ p_disabled    0                              ]
    [ p_options     [["Off" "On"]]                 ]
    [ p_label       ""                             ]
    [ p_label_size  $ui_tool_text_size_xs_unscaled ]
    [ p_option_size $ui_tool_text_size_xs_unscaled ]
    [ p_width       0                              ]
    [ p_height      0                              ]
    [ p_tip         ""                             ]
    [ p_tip_simple  ""                             ]
    [ p_tip_action  ""                             ]
    [ p_on_change   []                             ]
    [ p_get         [[result $arg1]]               ]
    [ p_set         [[result $arg1]]               ]
    [ p_id          []                             ]
]

// 1:<var> 2:<props>
ui_tool_switch = [
    @(props $tool_switch_props arg2)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_option_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_height)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _check

    if $p_disabled [
        _check = -1
    ] [
        _check = (p_get $$arg1)
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $p_label $p_label_size [ uialign -1 ]

        @(ui_tool_interactable [$p_width] [$p_height] [
            tool_control_highlight

            uispace $ui_tool_elem_space_l $ui_tool_elem_space_s [
                uihlist 0 [
                    loop state 2 [
                        local _col
                        _col = (? (= (!= $_check 0) $state) $ui_tool_accent_colour $ui_tool_dark_colour)

                        uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                            uicolourtext (at $p_options $state) $_col $p_option_size
                        ]
                    ]
                ]
            ]

            ui_tool_tip [
                p_tip         = [@@p_tip]
                p_tip_simple  = [@@p_tip_simple]
                p_tip_action  = [@@p_tip_action]
                p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
            ]

            if $p_disabled [] [
                uirelease [
                    set $arg1 (p_set (! $_check))
                    p_on_change
                ]
            ]
        ] [$p_disabled])
    ]
]

tool_compactswitch_props = [
    [ p_noonline   0                              ]
    [ p_disabled   0                              ]
    [ p_label      "Enable"                       ]
    [ p_label_size $ui_tool_text_size_xs_unscaled ]
    [ p_width      0                              ]
    [ p_height     0                              ]
    [ p_tip        ""                             ]
    [ p_tip_simple ""                             ]
    [ p_tip_action ""                             ]
    [ p_on_change  []                             ]
    [ p_get        [[result $arg1]]               ]
    [ p_set        [[result $arg1]]               ]
    [ p_id         []                             ]
]

// 1:<var> 2:<props>
ui_tool_compactswitch = [
    @(props $tool_compactswitch_props arg2)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_height)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _check _col
    _check = (p_get $$arg1)
    _col   = (? (&& $_check [! $p_disabled]) $ui_tool_accent_colour $ui_tool_dark_colour)

    @(ui_tool_interactable [$p_width] [$p_height] [
        tool_control_highlight

        uispace $ui_padsmall $ui_padsmaller [
            uicolourtext $p_label $_col $p_label_size
        ]

        ui_tool_tip [
            p_tip         = [@@p_tip]
            p_tip_simple  = [@@p_tip_simple]
            p_tip_action  = [@@p_tip_action]
            p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
        ]

        if $p_disabled [] [
            uirelease [
                tool_param_set $arg1 (p_set (! $_check)) $p_on_change
            ]
        ]
    ] [$p_disabled])
]

tool_checkbox_props = [
    [ p_noonline   0                             ]
    [ p_disabled   0                             ]
    [ p_label      "Enable"                      ]
    [ p_label_size $ui_tool_text_size_s_unscaled ]
    [ p_tip        ""                            ]
    [ p_tip_simple ""                            ]
    [ p_tip_action ""                            ]
    [ p_on_change  []                            ]
    [ p_size       0.01                          ]
    [ p_get        [[result $arg1]]              ]
    [ p_set        [[result $arg1]]              ]
    [ p_id         []                            ]
]

// 1:<var> 2:<props>
ui_tool_checkbox = [
    @(props $tool_checkbox_props arg2)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_size)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _check _col
    _check = (p_get $$arg1)
    _col   = (? (&& $_check [! $p_disabled]) $ui_tool_accent_colour $ui_tool_dark_colour)

    uitarget 0 0 [
        uihlist $ui_tool_elem_space_s [
            @@@(ui_tool_interactable 0 0 [
                tool_control_highlight

                uispace $ui_padsmall $ui_padsmall [
                    uicolour $_col $p_size $p_size
                ]
            ] [$p_disabled])

            uicolourtext $p_label (? $p_disabled $ui_tool_dark_colour $ui_tool_accent_colour) $p_label_size
        ]

        if $p_disabled [] [
            uirelease [
                tool_param_set $arg1 (p_set (! $_check)) $p_on_change
            ]
        ]

        ui_tool_tip [
            p_tip         = [@@p_tip]
            p_tip_simple  = [@@p_tip_simple]
            p_tip_action  = [@@p_tip_action]
            p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
        ]
    ]
]
