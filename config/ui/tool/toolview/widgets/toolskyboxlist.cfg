ui_tool_skyboxlist_sel_colour = 0xFF0000

tool_skyboxlist_filter_query = ""

// 1:<item>
tool_skyboxlist_filter = [
    result (|| [! $tool_skyboxlist_filter_query] [!= (strcasestr $arg1 $tool_skyboxlist_filter_query) -1])
]

// 1:<name> 2:<highlight> 3:<var>
ui_tool_skyboxlist_item = [
    uicolour 0 0 0 [
        uihlist 0 [
            if (!=s $arg1) [
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_ft") $p_sel_size $p_sel_size
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_lf") $p_sel_size $p_sel_size
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_bk") $p_sel_size $p_sel_size
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_rt") $p_sel_size $p_sel_size
            ] [
                uitarget (*f $p_sel_size 4) $p_sel_size
            ]

            uirelease [
                tool_param_set $arg3 $arg1 $p_on_change
                p_on_select
            ]

            ui_tool_tip [
                p_tip_simple = @arg1
            ]
        ]

        if $arg2 [
            uioutline $ui_tool_skyboxlist_sel_colour
            uistyle clampxy -
        ]

        if (=s $arg1 "") [
            uitext "<none>"
        ]
    ]
]

tool_skyboxlist_props = [
    [ p_sel_size            0.13                      ]
    [ p_sel_area            0.6                       ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 32                        ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_on_change           []                        ]
    [ p_on_select           []                        ]
]

// 1:<select var> 2:<props>
ui_tool_skyboxlist = [
    @(props $tool_skyboxlist_props arg2)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    uivlist 0 [
        uistyle clampx
        ui_tool_textinput tool_skyboxlist_filter_query $p_filter_query_length [
            p_label = "Search:"
        ]
        ui_tool_textinput $arg1 $p_filter_query_length [
            p_label = "Custom:"
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uistyle clampx
            uiscroll 0 $p_sel_area [
                uistyle lefttop
                uivlist $p_sel_space [
                    looplist sbox $tool_env_skyboxes [
                        if (tool_skyboxlist_filter $sbox) [
                            ui_tool_skyboxlist_item $sbox (=s $sbox $$arg1) $arg1
                        ]
                    ]
                ]
            ]
            uivscroll $p_slider_size $p_sel_area 1
            uialign 1
        ]
    ]
]
