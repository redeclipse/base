ui_tool_skyboxlist_sel_colour = 0xFF0000

tool_skyboxlist_filter_query = ""
tool_skyboxlist_item_click = 0

// 1:<item>
tool_skyboxlist_filter = [
    result (|| [! $tool_skyboxlist_filter_query] [!= (strcasestr $arg1 $tool_skyboxlist_filter_query) -1])
]

// 1:<name> 2:<highlight>
ui_tool_skyboxlist_item = [
    tool_skyboxlist_item_click = 0

    uicolour 0 0 0 [
        uihlist 0 [
            if (!=s $arg1) [
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_ft") $uit_sel_size $uit_sel_size
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_lf") $uit_sel_size $uit_sel_size
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_bk") $uit_sel_size $uit_sel_size
                uithumbnail (concatword "<thumbnail:256/256>" $arg1 "_rt") $uit_sel_size $uit_sel_size
            ] [
                uitarget (*f $uit_sel_size 4) $uit_sel_size
            ]

            uirelease [
                tool_skyboxlist_item_click = 1
            ]
            ui_tool_tip [
                uit_tip_simple = @arg1
            ]
        ]

        if $arg2 [
            uioutline $ui_tool_skyboxlist_sel_colour
            uiclamp- 1 1 1 1
        ]

        if (=s $arg1 "") [
            uitext "<none>"
        ]
    ]
]

tool_skyboxlist_props = [
    [ uit_sel_size 0.13 ]
    [ uit_sel_area 0.6 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 32 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_on_change [] ]
    [ uit_on_select [] ]
]

// 1:<select var> 2:<props>
ui_tool_skyboxlist = [
    @(tool_props $tool_skyboxlist_props arg2)

    @(tool_ui_scale uit_sel_size)
    @(tool_ui_scale uit_sel_space)
    @(tool_ui_scale uit_slider_size)

    uivlist 0 [
        uiclamp 1 1
        ui_tool_textinput tool_skyboxlist_filter_query $uit_filter_query_length [
            uit_label = "Search:"
        ]
        ui_tool_textinput $arg1 $uit_filter_query_length [
            uit_label = "Custom:"
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                uivlist $uit_sel_space [
                    looplist sbox $tool_env_skyboxes [
                        if (tool_skyboxlist_filter $sbox) [
                            ui_tool_skyboxlist_item $sbox (=s $sbox $$arg1)
                            if $tool_skyboxlist_item_click [
                                tool_param_set $arg1 $sbox $uit_on_change
                                uit_on_select
                            ]
                        ]
                    ]
                ]
            ]
            uivscroll $uit_slider_size $uit_sel_area 1
            uialign 1
        ]
    ]
]
