tool_dropdown_menu_props = [
    [ p_var       [] ]
    [ p_on_change [] ]
    [ p_items     [] ]
    [ p_tips      [] ]
    [ p_filter    [] ]
    [ p_get       [] ]
    [ p_set       [] ]
]

ui_tool_dropdown_list = [
    @(props $tool_dropdown_menu_props (toolpanel_attr ctxmenu user_data))

    uivlist 0 [
        uiclamp 1 1

        local _item _colour
        _colour = 0

        loop i (listlen $p_items) [
            _item = (at $p_items $i)

            if (p_filter $_item $i) [
                _colour = (? (= $i (p_get $$p_var)) $ui_tool_accent_colour $ui_tool_dark_accent_colour)

                ui_tool_button [
                    p_label       = (at $p_items $i)
                    p_label_align = -1
                    p_colour      = $_colour
                    p_on_click    = [
                        local _setter
                        _setter = [@@p_set]

                        toolpanel_close_this
                        tool_param_set @p_var (_setter @i) [@@p_on_change]
                    ]
                    p_tip_simple  = [@@(at $p_tips $i)]
                ]
            ]
        ]

        uiclamp* 1 1
    ]
]

// 1:<var> 2:<dir> 3:<listlen> 4:<on change>
tool_dropdown_scroll = [
    local _new_val
    _new_val = (clamp (+ $$arg1 $arg2) 0 (- $arg3 1))

    tool_param_set $arg1 $_new_val $arg4
]

tool_dropdown_props = [
    [ p_noonline   0                               ]
    [ p_disabled   0                               ]
    [ p_can_reset  1                               ]
    [ p_reset_val  []                              ]
    [ p_val_size   $ui_tool_text_size_s_unscaled   ]
    [ p_label      ""                              ]
    [ p_label_size $ui_tool_text_size_s_unscaled   ]
    [ p_prompt     ""                              ]
    [ p_width      $ui_tool_numinput_size_unscaled ]
    [ p_height     0                               ]
    [ p_tip        ""                              ]
    [ p_tip_simple ""                              ]
    [ p_tips       []                              ]
    [ p_filter     [[!=s $arg1]]                   ]
    [ p_on_change  []                              ]
    [ p_get        [[result $arg1]]                ]
    [ p_set        [[result $arg1]]                ]
    [ p_id         []                              ]
]

// 1:<var> 2:<items> 3:<props>
ui_tool_dropdown = [
    @(props $tool_dropdown_props arg3)

    @(tool_ui_scale p_val_size)
    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_height)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _numeric_val _value _col
    _col         = (? $p_disabled $ui_tool_dark_colour $ui_tool_accent_colour)
    _numeric_val = (p_get $$arg1)

    if $p_disabled [
        _value = "-"
        _col = $ui_tool_dark_colour
    ] [
        _value = (? (< $_numeric_val 0) (concatword "^fA" $p_prompt) (at $arg2 $_numeric_val))
    ]

    uihlist $ui_toolpanel_elem_space [
        if (!=s $p_label "") [
            uiclamp 1 1
            uitext $p_label $p_label_size [ uialign -1 ]
        ]

        @@(ui_tool_interactable [$p_width] [$p_height] [
            tool_control_highlight

            uispace $ui_padsmall $ui_padsmaller [
                uitext $_value $p_val_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uitriangle $_col 0.005 0.005 180
            ]

            ui_tool_tip [
                p_tip         = [@@p_tip]
                p_tip_simple  = [@@p_tip_simple]
                p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
            ]

            if $p_disabled [] [
                uirelease [
                    local _pos_x _pos_y
                    _pos_x = (uigetlastsx)
                    _pos_y = (+f (uigetlastsy) (uigetlasth))

                    tool_dropdown_items = $arg2

                    toolpanel_open tool_dropdown_list ctxmenu [
                        p_position  = [@@_pos_x @@_pos_y]
                        p_width     = @p_width
                        p_user_data = [
                            p_var       = @arg1
                            p_on_change = [@@p_on_change]
                            p_get       = [@@p_get]
                            p_set       = [@@p_set]
                            p_items     = [@@arg2]
                            p_tips      = [@@p_tips]
                            p_filter    = [@@p_filter]
                        ]
                    ]
                ]

                uialtrelease [
                    if (=s $p_reset_val) [
                        p_reset_val = (at $arg2 (getvardef $arg1))
                    ]

                    tool_param_menu $arg1 [
                        p_can_reset = @p_can_reset
                        p_reset_val = [@@p_reset_val]
                        p_on_change = [@@p_on_change]
                        p_get       = [
                            local _getter
                            _getter = [@@@p_get]

                            at [@@@arg2] (_getter $arg1)
                        ]
                        p_set       = [
                            local _setter
                            _setter = [@@@p_set]

                            _setter (listfind=s [@@@arg2] $arg1)
                        ]
                    ]
                ]

                uihover [
                    if (|| $curshiftmod $toolfreevaluescroll) uilockscroll
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1 [
                            p_get = [
                                local _getter
                                _getter = [@@@@p_get]

                                at [@@@@arg2] (_getter $arg1)
                            ]
                        ]
                    ] [
                        tool_param_paste @arg1 [
                            p_set       = [
                                local _setter
                                _setter = [@@@@p_get]

                                _setter (listfind=s [@@@@arg2] $arg1)
                            ]
                            p_on_change = [@@p_on_change]
                        ]
                    ]
                ]

                uiscrollup [
                    if (|| $curshiftmod $toolfreevaluescroll) [
                        tool_dropdown_scroll $arg1 -1 (listlen $arg2) $p_on_change
                    ]
                ]

                uiscrolldown [
                    if (|| $curshiftmod $toolfreevaluescroll) [
                        tool_dropdown_scroll $arg1 1 (listlen $arg2) $p_on_change
                    ]
                ]
            ]
        ] [$p_disabled])
    ]
]
