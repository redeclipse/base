tool_button_props = [
    [ p_noonline      0                             ]
    [ p_disabled      0                             ]
    [ p_label         ""                            ]
    [ p_label_size    $ui_tool_text_size_s_unscaled ]
    [ p_label_align   0                             ]
    [ p_pad           $ui_padsmaller                ]
    [ p_icon          ""                            ]
    [ p_icon_size     $ui_tool_icon_size_m_unscaled ]
    [ p_icon_align    -1                            ]
    [ p_children      []                            ]
    [ p_colour        $ui_tool_accent_colour        ]
    [ p_width         0                             ]
    [ p_height        0                             ]
    [ p_tip           ""                            ]
    [ p_tip_simple    ""                            ]
    [ p_tip_action    ""                            ]
    [ p_highlight     0                             ]
    [ p_on_click      []                            ]
    [ p_on_rightclick []                            ]
    [ p_id            []                            ]
]

// 1:<props>
ui_tool_button = [
    @(props $tool_button_props arg1)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_pad)
    @(tool_ui_scale p_icon_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_height)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _col
    _col  = (? $p_disabled $ui_tool_dark_colour $p_colour)

    @(ui_tool_interactable [$p_width] [$p_height] [
        tool_control_highlight

        uispace $p_pad $p_pad [
            uistyle clampxy

            doargs $p_children

            uicolourtext $p_label $_col $p_label_size
            uialign- $p_label_align

            if $p_icon [
                uiimage $p_icon $_col 0 $p_icon_size $p_icon_size
                uialign- $p_icon_align
            ]
        ]

        ui_tool_tip [
            p_tip         = [@@p_tip]
            p_tip_simple  = [@@p_tip_simple]
            p_tip_action  = [@@p_tip_action]
            p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
        ]

        if $p_disabled [] [
            uirelease [
                p_on_click
            ]

            uialtrelease [
                p_on_rightclick
            ]

            uihover [
                if (!=s $p_on_rightclick) [
                    tool_rightclickable
                ]
            ]
        ]
    ] [$p_disabled])
]

tool_get_action_button_override_props = [
    [ p_override_label    0 ]
    [ p_override_icon     0 ]
    [ p_override_tip      0 ]
    [ p_override_on_click 0 ]
]

// 1:<action> 2:<props> 3:<override props>
ui_tool_get_action_button = [
    @(props $tool_get_action_button_override_props arg3)

    local _prop

    if (= $p_override_label) [
        _prop = (at $$arg1 $TA_PROP_SHORT_DESC)
        appendword arg2 [p_label = [@@_prop]]
        appendword arg2 "^n"
    ]

    if (= $p_override_icon) [
        _prop = (at $$arg1 $TA_PROP_ICON)
        appendword arg2 [p_icon = [@@_prop]]
        appendword arg2 "^n"
    ]

    if (= $p_override_tip) [
        appendword arg2 [p_tip_action = @@arg1]
        appendword arg2 "^n"
    ]

    if (& (at $$arg1 $TA_PROP_FLAGS) $TA_FLAG_NOONLINE) [
        appendword arg2 [p_noonline = 1]
        appendword arg2 "^n"
    ]

    if (= $p_override_on_click) [
        appendword arg2 [p_on_click = [tool_do_action @@@arg1]]
    ]

    result [
        ui_tool_button [
            @@arg2
        ]
    ]
]

// Properties defining a small square button with an icon
ui_tool_square_button_s = [
    p_icon_size  = 0.015
    p_icon_align = 0
    p_pad        = 0
    p_width      = $ui_tool_button_height_s_unscaled
    p_height     = $ui_tool_button_height_s_unscaled
]

// Properties defining a medium square button with an icon
ui_tool_square_button_m = [
    p_icon_size  = 0.02
    p_icon_align = 0
    p_pad        = 0
    p_width      = $ui_tool_button_height_m_unscaled
    p_height     = $ui_tool_button_height_m_unscaled
]
