tool_collapsegroup_props = [
    [ p_disabled   0                             ]
    [ p_label      ""                            ]
    [ p_label_size $ui_tool_text_size_s_unscaled ]
    [ p_force_open 0                             ]
    [ p_id         []                            ]
]

// 1:<id> 2:<children> 3:<props>
ui_tool_collapsegroup = [
    @(props $tool_collapsegroup_props arg3)

    @(tool_ui_scale p_label_size)

    local _show
    _show = (|| $p_force_open [? (getalias $arg1) 1 0])

    uitablerow [] [
        uistyle clampx
        @(ui_tool_interactable 0 0 [
            tool_control_highlight
            uistyle clampx

            uispace $ui_padsmall $ui_padsmaller [
                uitext $p_label $p_label_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uitriangle $ui_tool_accent_colour 0.005 0.005 (? $_show 180 90)
            ]

            uirelease [
                $arg1 = (! $_show)
            ]
        ])
    ]

    if $_show [
        doargs $arg2
    ]
]