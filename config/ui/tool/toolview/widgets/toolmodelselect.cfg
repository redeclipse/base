tool_modelselect_var = []

tool_modelselect_menu_props = [
    [ uit_on_change [] ]
]

ui_tool_modelselect_picker = [
    @(tool_props $tool_modelselect_menu_props toolpanel_props_menu)

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1
        ui_tool_modellist $tool_modelselect_var [
            uit_sel_area = 0.3
            uit_on_select = [
                menu_click_hide = 1
            ]
            uit_on_change = [@@uit_on_change]
        ]
    ]
]

tool_modelselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_text_size $ui_toolview_tiny_text_size ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_align -1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<props>
ui_tool_modelselect = [
    @(tool_props $tool_modelselect_props arg2)

    local value model_name model_info
    value = (? $uit_disabled -1 $$arg1)
    model_name = (mapmodelindex $value)

    if (< $value 0) [
        model_info = (? $uit_disabled "-" "No model selected")
    ] [
        model_info = $model_name
    ]

    uivlist 0 [
        uicolour 0 $uit_size $uit_size [
            uimodelpreview $model_name "" 1 1 $uit_size $uit_size [
                if (< $value 0) [
                    uioutline (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_dark_accent_colour)
                    uiclamp- 1 1 1 1
                ]

                if $uit_disabled [] [
                    ui_tool_tip [
                        uit_tip_simple = "Click to browse models"
                    ]

                    uirelease [
                        tool_modelselect_var = $arg1

                        toolpanel_show tool_modelselect_picker "" menu [
                            uit_position = (uicursorpos)
                            uit_width = @uit_picker_width
                            uit_on_change = [@@uit_on_change]
                        ]
                    ]

                    uialtrelease [
                        tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change
                    ]
                ]

                @@@@(ui_tool_preview_spin)
            ]
        ]

        uitext $model_info $uit_text_size

        uialign* $uit_align
    ]
]
