tool_modelselect_menu_props = [
    [ p_var       [] ]
    [ p_on_change [] ]
]

ui_tool_modelselect_picker = [
    @(props $tool_modelselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uistyle clampx
        ui_tool_modellist $p_var [
            p_sel_area  = 0.3
            p_on_change = [@@p_on_change]
        ]
    ]
]

tool_modelselect_props = [
    [ p_disabled     0                              ]
    [ p_can_reset    0                              ]
    [ p_reset_val    0                              ]
    [ p_text_size    $ui_tool_text_size_xs_unscaled ]
    [ p_size         0.06                           ]
    [ p_picker_width [(uiwidth 0.2)]                ]
    [ p_align        -1                             ]
    [ p_on_change    []                             ]
]

// 1:<var> 2:<props>
# ui_tool_modelselect = [
    @(props $tool_modelselect_props arg2)

    @(tool_ui_scale p_text_size)
    @(tool_ui_scale p_size)

    local _value _model_name _model_info
    _value = (? $p_disabled -1 $$arg1)
    _model_name = (mapmodelindex $_value)

    if (< $_value 0) [
        _model_info = (? $p_disabled "-" "No model selected")
    ] [
        _model_info = $_model_name
    ]

    uivlist 0 [
        uicolour 0 $p_size $p_size [
            uimodelpreview $_model_name "" 1 1 $p_size $p_size [
                if (< $_value 0) [
                    uioutline (? $p_disabled $ui_tool_dark_colour $ui_tool_dark_accent_colour)
                    uiprev [uistyle clampxy]
                ]

                if $p_disabled [] [
                    ui_tool_tip [
                        p_tip_simple = "Click to browse models"
                    ]

                    uirelease [
                        toolpanel_open tool_modelselect_picker popup [
                            p_position  = (uicursorpos)
                            p_width     = @p_picker_width
                            p_user_data = [
                                p_var       = @@arg1
                                p_on_change = [@@@p_on_change]
                            ]
                        ]
                    ]

                    uialtrelease [
                        tool_param_menu $arg1 [
                            p_can_reset = @p_can_reset
                            p_reset_val = [@@p_reset_val]
                            p_on_change = [@@p_on_change]
                        ]
                    ]

                    uihover [
                        uiinteractive 1
                        
                        tool_rightclickable

                        tool_copy_handler [
                            tool_param_copy @arg1
                        ] [
                            tool_param_paste @arg1 [
                                p_on_change = [@@@p_on_change]
                            ]
                        ]
                    ]
                ]

                #(ui_tool_preview_spin)
            ]

            uicolouradd 0x555555
        ]

        uitext $_model_info $p_text_size

        uipropchild [uialign $p_align]
    ]
]
