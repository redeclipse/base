tool_numinput_drag_id       = []
tool_numinput_focus_id      = []
tool_numinput_focus         = 0
tool_numinput_drag_old_val  = 0
tool_numinput_drag_val      = 0
tool_numinput_drag_var      = []
tool_numinput_edit          = 0
tool_numinput_last_val      = 0
tool_numinput_last_distance = 0

tool_numinput_drag_mul = 75
tool_numinput_drag_millis = 0

tool_numinput_get_drag = [
    local _distance _distance_d

    _distance = (toint (round (*f (uimousetrackx) $tool_numinput_drag_mul)))
    _distance_d = (- $_distance $tool_numinput_last_distance)
    tool_numinput_last_distance = $_distance

    result $_distance_d
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_numinput_drag_f = [
    local _drag _new_val _key_mods

    uilockcursor
    uicursortype 2

    _drag = (tool_numinput_get_drag)
    _key_mods = (getkeymodifiers)

    if (>f $arg4 0) [
        caseif (& $_key_mods $KMOD_SHIFT) [
            arg4 = (*f $arg4 10)
        ] (& $_key_mods @KMOD_ALT) [
            arg4 = (*f $arg4 0.1)
        ]

        _new_val = (+f $tool_numinput_drag_val (*f $arg4 $_drag))
    ] [
        _new_val = (*f $tool_numinput_drag_val (divf 1 (pow 2 (*f -1 $_drag))))
    ]

    _new_val = (round $_new_val 1e-05)

    if (= $arg2 $arg3) [
        case (getvartype $tool_numinput_drag_var) $ididxvar [
            arg2 = (p_get (getvarmin $tool_numinput_drag_var))
            arg3 = (p_get (getvarmax $tool_numinput_drag_var))
        ] $ididxfvar [
            arg2 = (p_get (getfvarmin $tool_numinput_drag_var))
            arg3 = (p_get (getfvarmax $tool_numinput_drag_var))
        ]
    ]

    $_clamp_func $_new_val $arg2 $arg3
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_numinput_drag_i = [
    local _drag _new_val _key_mods

    uilockcursor
    uicursortype 2

    _drag = (tool_numinput_get_drag)
    _key_mods = (getkeymodifiers)

    if (> $arg4 0) [
        caseif (& $_key_mods $KMOD_SHIFT) [
            arg4 = (*f $arg4 10)
        ] (& $_key_mods @KMOD_ALT) [
            arg4 = (max (*f $arg4 0.1) 1)
        ]

        _new_val = (+ $tool_numinput_drag_val (* $arg4 $_drag))
    ] [
        _new_val = (toint (*f $tool_numinput_drag_val (divf 1 (pow 2 (*f -1 $_drag)))))
    ]

    if (= $arg2 $arg3) [
        case (getvartype $tool_numinput_drag_var) $ididxvar [
            arg2 = (p_get (getvarmin $tool_numinput_drag_var))
            arg3 = (p_get (getvarmax $tool_numinput_drag_var))
        ] $ididxfvar [
            arg2 = (p_get (getfvarmin $tool_numinput_drag_var))
            arg3 = (p_get (getfvarmax $tool_numinput_drag_var))
        ]
    ]

    toint ($_clamp_func $_new_val $arg2 $arg3)
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<dir>
tool_numinput_scroll_f = [
    local _new_val

    if (>f $arg4 0) [
        _new_val = (+f $$arg1 (*f $arg4 $arg5))
    ] [
        _new_val = (*f $$arg1 (pow 2 $arg5))
    ]

    $_clamp_func $_new_val $arg2 $arg3
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<dir>
tool_numinput_scroll_i = [
    local _new_val

    if (> $arg4 0) [
        _new_val = (+ $$arg1 (* $arg4 $arg5))
    ] [
        _new_val = (* $$arg1 (pow 2 $arg5))
    ]

    toint ($_clamp_func $_new_val $arg2 $arg3)
]

// 1:<text>
tool_numinput_parse_field = [
    local _num_a _num_b _op_pos _op

    if (=s $p_val_format f) [
        _num_a = (*f $arg1 1)
    ] [
        _num_a = (* $arg1 1)
    ]

    _op_pos = (strpbrk $arg1 "+-*/" 1)

    if (>= $_op_pos 0) [
        _op = (substr $arg1 $_op_pos 1)

        _num_b = (substr $arg1 (+ $_op_pos 1))

        if (is_number $_num_b) [
            if (=s $_op "/") [
                _op = div
            ]

            if (=s $p_val_format f) [
                appendword _op f
            ]

            result ($_op $_num_a $_num_b)
        ] [
            result $_num_a
        ]
    ] [
        result $_num_a
    ]
]

tool_numinput_props = [
    [ p_disabled     0                               ]
    [ p_has_menu     1                               ]
    [ p_can_reset    1                               ]
    [ p_reset_val    []                              ]
    [ p_val_format   f                               ]
    [ p_circular     0                               ]
    [ p_val_size     $ui_tool_text_size_s_unscaled   ]
    [ p_val_text     [[result $arg1]]                ]
    [ p_immediate    1                               ]
    [ p_can_edit     1                               ]
    [ p_label        ""                              ]
    [ p_label_size   $ui_tool_text_size_s_unscaled   ]
    [ p_input_length 11                              ]
    [ p_width        $ui_tool_numinput_size_unscaled ]
    [ p_height       0                               ]
    [ p_on_change    []                              ]
    [ p_delta        0                               ]
    [ p_interval     0                               ]
    [ p_id           0                               ]
    [ p_round        0.0001                          ]
    [ p_tip          ""                              ]
    [ p_tip_simple   ""                              ]
    [ p_tip_action   ""                              ]
    [ p_get          [[result $arg1]]                ]
    [ p_set          [[result $arg1]]                ]
    [ p_menu_props   []                              ]
]

ui_tool_numinput_field = [
    uifield tool_numinput_edit $p_input_length [] $p_val_size 0 [
        uieditorsetfocusable 0

        // Focus once
        if $tool_numinput_focus [
            uieditorsetfocus
            tool_numinput_focus = 0
        ]

        // Hide this field when done interacting
        if (uifocus?) [
            uiallowinput 1
        ] [
            tool_numinput_focus_id = []
            _new_value = (tool_numinput_parse_field $tool_numinput_edit)
        ]
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<props>
ui_tool_numinput = [
    @(tool_props $tool_numinput_props arg5)

    @(tool_ui_scale p_val_size)
    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_height)

    local _drag _value _col _new_value _drag_func _clamp_func _millis _id
    _id         = (concatword $arg1 $p_id)
    _drag       = (=s $tool_numinput_drag_id $_id)
    _drag_func  = (? (=s $p_val_format f) tool_numinput_drag_f tool_numinput_drag_i)
    _clamp_func = (concatword (? $p_circular circ) clamp (? (=s $p_val_format f) f))

    if $p_delta [
        arg2 = -99999
        arg3 = 99999
        set $arg1 (p_set 0)
    ]

    if $_drag [
        _value = $tool_numinput_drag_val
    ] [
        _value = (? $p_disabled "-" (p_get $$arg1))
    ]

    if (&& (!=s $_value "-") (=s $p_val_format f)) [
        _value = (round $_value $p_round)
    ]

    _col = (? $p_disabled $ui_tool_dark_colour $ui_tool_accent_colour)
    _new_value = ""

    _millis = (getmillis 1)
    if (< (- $_millis $tool_numinput_drag_millis) $p_interval) [
        p_immediate = 0
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $p_label $p_label_size

        @(ui_tool_interactable [$p_width] [$p_height] [
            if (=s $tool_numinput_focus_id $_id) [
                uispace $ui_padsmall $ui_padsmaller [
                    @ui_tool_numinput_field
                ]
            ] [
                uispace $ui_padsmall $ui_padsmaller [
                    uitext (p_val_text $_value) $p_val_size
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign -1
                    uitriangle $_col 0.005 0.005 90
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign 1
                    uitriangle $_col 0.005 0.005 -90
                ]
            ]

            uihover [
                if (! $_drag) [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1 [
                            p_get = [@@@p_get]
                        ]
                    ] [
                        tool_param_paste @arg1 [
                            p_set       = [@@@p_set]
                            p_on_change = [@@@p_on_change]
                        ]
                    ]
                ]
            ] [
                if $_drag [
                    _drag = 0
                ]
            ]

            ui_tool_tip [
                p_tip        = [@@p_tip]
                p_tip_simple = [@@p_tip_simple]
                p_tip_action = [@@p_tip_action]
            ]

            uidoublepress $_id [
                if (&& [!= $p_can_edit 0] [! $p_disabled]) [
                    // Show input field
                    tool_numinput_focus_id = $_id
                    tool_numinput_focus    = 1
                    tool_numinput_edit     = (p_get $$arg1)
                    tool_numinput_last_val = 0

                    if $_drag [
                        tool_numinput_drag_id = []
                    ]
                ]
            ]

            uipress [
                if (&& [!=s $tool_numinput_focus_id $_id] [! $p_disabled]) [
                    // Init _drag
                    tool_numinput_drag_val      = (p_get $$arg1)
                    tool_numinput_drag_old_val  = (p_get $$arg1)
                    tool_numinput_drag_id       = $_id
                    tool_numinput_drag_var      = $arg1
                    tool_numinput_last_val      = 0
                    tool_numinput_last_distance = 0
                    tool_numinput_drag_millis   = $_millis
                ]
            ]

            uirelease [
                if (&& [!= $_drag 0] [! $p_disabled]) [
                    if $p_immediate [] [
                        _new_value = $tool_numinput_drag_val
                    ]

                    tool_numinput_drag_id = []
                ]
            ]

            uialtrelease [
                if (! $p_disabled) [
                    caseif $_drag [
                        // Cancel _drag
                        tool_numinput_drag_id = []
                        _drag                 = 0
                        _new_value            = $tool_numinput_drag_old_val
                    ] $p_has_menu [
                        local _resetval
                        _resetval = [@p_reset_val]

                        if (=s $_resetval) [
                            _resetval = (? (=s $p_val_format f) (getfvardef $arg1) (getvardef $arg1))
                        ]

                        tool_param_menu $arg1 [
                            p_can_reset = @p_can_reset
                            p_reset_val = [@@_resetval]
                            p_on_change = [@@p_on_change]
                            p_get       = [@@p_get]
                            p_set       = [@@p_set]
                            @p_menu_props
                        ]
                    ]
                ]
            ]
        ])
    ]

    if (&& [!= $_drag 0] [! $p_disabled]) [
        tool_numinput_drag_val = ($_drag_func $tool_numinput_drag_old_val $arg2 $arg3 $arg4)

        if $p_immediate [
            _new_value                = $tool_numinput_drag_val
            tool_numinput_drag_millis = $_millis
        ]
    ]

    if (!=s $_new_value "") [
        if (= $arg2 $arg3) [
            case (getvartype $arg1) $ididxvar [
                arg2 = (p_get (getvarmin $arg1))
                arg3 = (p_get (getvarmax $arg1))
            ] $ididxfvar [
                arg2 = (p_get (getfvarmin $arg1))
                arg3 = (p_get (getfvarmax $arg1))
            ]
        ]

        _new_value = ($_clamp_func $_new_value $arg2 $arg3)
        if $p_delta [
            local _delta
            if (=s $p_val_format f) [
                _delta = (-f $_new_value $tool_numinput_last_val)
            ] [
                _delta = (- $_new_value $tool_numinput_last_val)
            ]
            tool_param_delta $arg1 (p_set $_delta $arg1) $p_on_change
            tool_numinput_last_val = $_new_value
        ] [
            tool_param_set $arg1 (p_set $_new_value $arg1) $p_on_change
        ]
    ]
]
