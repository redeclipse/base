tool_num_input_drag_id = []
tool_num_input_focus_id = []
tool_num_input_focus = 0
tool_num_input_drag_old_val = 0
tool_num_input_drag_val = 0
tool_num_input_drag_var = []
tool_num_input_edit = 0
tool_num_input_last_val = 0
tool_num_input_last_distance = 0

tool_num_input_drag_mul = 75
tool_num_input_drag_millis = 0

tool_num_input_get_drag = [
    local distance distance_d

    distance = (toint (round (*f (uimousetrackx) $tool_num_input_drag_mul)))
    distance_d = (- $distance $tool_num_input_last_distance)
    tool_num_input_last_distance = $distance

    result $distance_d
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_num_input_drag_f = [
    local drag new_val key_mods

    uilockcursor
    uicursortype 2

    drag = (tool_num_input_get_drag)
    key_mods = (getkeymodifiers)

    if (>f $arg4 0) [
        caseif (& $key_mods $KMOD_SHIFT) [
            arg4 = (*f $arg4 10)
        ] (& $key_mods @KMOD_ALT) [
            arg4 = (*f $arg4 0.1)
        ]

        new_val = (+f $tool_num_input_drag_val (*f $arg4 $drag))
    ] [
        new_val = (*f $tool_num_input_drag_val (divf 1 (pow 2 (*f -1 $drag))))
    ]

    new_val = (round $new_val 1e-05)

    if (=f $arg2 $arg3) [
        arg2 = (getfvarmin $tool_num_input_drag_var)
        arg3 = (getfvarmax $tool_num_input_drag_var)
    ]

    if $uit_circular [
        circclampf $new_val $arg2 $arg3
    ] [
        clampf $new_val $arg2 $arg3
    ]
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_num_input_drag_i = [
    local drag new_val key_mods

    uilockcursor
    uicursortype 2

    drag = (tool_num_input_get_drag)
    key_mods = (getkeymodifiers)

    if (> $arg4 0) [
        caseif (& $key_mods $KMOD_SHIFT) [
            arg4 = (*f $arg4 10)
        ] (& $key_mods @KMOD_ALT) [
            arg4 = (max (*f $arg4 0.1) 1)
        ]

        new_val = (+ $tool_num_input_drag_val (* $arg4 $drag))
    ] [
        new_val = (toint (*f $tool_num_input_drag_val (divf 1 (pow 2 (*f -1 $drag)))))
    ]

    if (= $arg2 $arg3) [
        arg2 = (getvarmin $tool_num_input_drag_var)
        arg3 = (getvarmax $tool_num_input_drag_var)
    ]

    if $uit_circular [
        toint (circclamp $new_val $arg2 $arg3)
    ] [
        toint (clamp $new_val $arg2 $arg3)
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<dir>
tool_num_input_scroll_f = [
    local new_val

    if (>f $arg4 0) [
        new_val = (+f $$arg1 (*f $arg4 $arg5))
    ] [
        new_val = (*f $$arg1 (pow 2 $arg5))
    ]

    if $uit_circular [
        circclampf $new_val $arg2 $arg3
    ] [
        clampf $new_val $arg2 $arg3
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<dir>
tool_num_input_scroll_i = [
    local new_val

    if (> $arg4 0) [
        new_val = (+ $$arg1 (* $arg4 $arg5))
    ] [
        new_val = (* $$arg1 (pow 2 $arg5))
    ]

    if $uit_circular [
        circclamp $new_val $arg2 $arg3
    ] [
        clamp $new_val $arg2 $arg3
    ]
]

tool_numinput_props = [
    [ uit_disabled 0 ]
    [ uit_has_menu 1 ]
    [ uit_can_reset 1 ]
    [ uit_reset_val [] ]
    [ uit_val_format f ]
    [ uit_circular 0 ]
    [ uit_val_size $ui_toolview_small_text_size ]
    [ uit_immediate 1 ]
    [ uit_can_edit 1 ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_input_length 8 ]
    [ uit_width $ui_toolview_num_input_size ]
    [ uit_height 0 ]
    [ uit_on_change [] ]
    [ uit_delta 0 ]
    [ uit_interval 0 ]
    [ uit_id 0 ]
    [ uit_round 0.0001 ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[result $arg1]] ]
]

ui_tool_numinput_field = [
    uifield tool_num_input_edit $uit_input_length [] $uit_val_size 0 [
        uieditorsetfocusable 0

        // Focus once
        if $tool_num_input_focus [
            uieditorsetfocus
            tool_num_input_focus = 0
        ]

        // Hide this field when done interacting
        if (uifocus?) [
            uiallowinput 1
        ] [
            tool_num_input_focus_id = []
            new_value = $tool_num_input_edit
        ]
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<props>
ui_tool_numinput = [
    @(tool_props $tool_numinput_props arg5)

    local drag value colour new_value drag_func clamp_func millis id
    id = (concatword $arg1 $uit_id)
    drag = (=s $tool_num_input_drag_id $id)
    drag_func = (? (=s $uit_val_format f) tool_num_input_drag_f tool_num_input_drag_i)
    clamp_func = (? (=s $uit_val_format f) clampf clamp)

    if $uit_delta [
        arg2 = -99999
        arg3 = 99999
        set $arg1 (uit_set 0)
    ]

    if $drag [
        value = $tool_num_input_drag_val
    ] [
        value = (? $uit_disabled "-" (uit_get $$arg1))
    ]

    if (&& (!=s $value "-") (=s $uit_val_format f)) [
        value = (round $value $uit_round)
    ]

    colour = (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_accent_colour)
    new_value = ""

    millis = (getmillis 1)
    if (< (- $millis $tool_num_input_drag_millis) $uit_interval) [
        uit_immediate = 0
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size

        @(ui_tool_interactable [$uit_width] [$uit_height] [
            if (=s $tool_num_input_focus_id $id) [
                uispace $ui_padsmall $ui_padsmaller [
                    @ui_tool_numinput_field
                ]
            ] [
                uispace $ui_padsmall $ui_padsmaller [
                    uitext $value $uit_val_size
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign -1
                    uitriangle $colour 0.005 0.005 90
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign 1
                    uitriangle $colour 0.005 0.005 -90
                ]
            ]

            uihover [] [
                if $drag [
                    drag = 0
                ]
            ]

            uidoublepress $id [
                if (&& [!= $uit_can_edit 0] [! $uit_disabled]) [
                    // Show input field
                    tool_num_input_focus_id = $id
                    tool_num_input_focus = 1
                    tool_num_input_edit = (uit_get $$arg1)
                    tool_num_input_last_val = 0

                    if $drag [
                        tool_num_input_drag_id = []
                    ]
                ]
            ]

            uipress [
                if (&& [!=s $tool_num_input_focus_id $id] [! $uit_disabled]) [
                    // Init drag
                    tool_num_input_drag_val = (uit_get $$arg1)
                    tool_num_input_drag_old_val = (uit_get $$arg1)
                    tool_num_input_drag_id = $id
                    tool_num_input_drag_var = $arg1
                    tool_num_input_last_val = 0
                    tool_num_input_last_distance = 0
                    tool_num_input_drag_millis = $millis
                ]
            ]

            uirelease [
                if (&& [!= $drag 0] [! $uit_disabled]) [
                    if $uit_immediate [] [
                        new_value = $tool_num_input_drag_val
                    ]

                    tool_num_input_drag_id = []
                ]
            ]

            uialtrelease [
                if (! $uit_disabled) [
                    caseif $drag [
                        // Cancel drag
                        tool_num_input_drag_id = []
                        drag = 0
                        new_value = $tool_num_input_drag_old_val
                    ] $uit_has_menu [
                        if (=s $uit_reset_val) [
                            uit_reset_val = (? (=s $uit_val_format f) (getfvardef $arg1) (getvardef $arg1))
                        ]

                        tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change $uit_get $uit_set
                    ]
                ]
            ]
        ])
    ]

    if (&& [!= $drag 0] [! $uit_disabled]) [
        tool_num_input_drag_val = ($drag_func $tool_num_input_drag_old_val $arg2 $arg3 $arg4)

        if $uit_immediate [
            new_value = $tool_num_input_drag_val
            tool_num_input_drag_millis = $millis
        ]
    ]

    if (!=s $new_value "") [
        if (=s $uit_val_format f) [
            if (=f $arg2 $arg3) [
                arg2 = (getfvarmin $arg1)
                arg3 = (getfvarmax $arg1)
            ]
        ] [
            if (= $arg2 $arg3) [
                arg2 = (getvarmin $arg1)
                arg3 = (getvarmax $arg1)
            ]
        ]

        new_value = ($clamp_func $new_value $arg2 $arg3)
        if $uit_delta [
            local delta
            if (=s $uit_val_format f) [
                delta = (-f $new_value $tool_num_input_last_val)
            ] [
                delta = (- $new_value $tool_num_input_last_val)
            ]
            tool_param_delta $arg1 (uit_set $delta $arg1) $uit_on_change
            tool_num_input_last_val = $new_value
        ] [
            tool_param_set $arg1 (uit_set $new_value $arg1) $uit_on_change
        ]
    ]
]
