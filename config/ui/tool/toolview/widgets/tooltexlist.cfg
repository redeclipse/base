ui_tool_texlist_sel_colour = 0xFF0000

tool_texlist_groups               = 1
tool_texlist_variants             = 1
tool_texlist_filter_query         = ""
tool_texlist_filter_shader_select = 0
tool_texlist_filter_shader        = []

tool_texlist_shader_filters = [
    [ "B"  "bump"      "Bump map"   ]
    [ "S"  "spec"      "Specular"   ]
    [ "R"  "env"       "Reflective" ]
    [ "G"  "glow"      "Glow"       ]
    [ "P"  "pulse"     "Pulse"      ]
    [ "A"  "alpha"     "Alpha mask" ]
    [ "Px" "parallax"  "Parallax"   ]
    [ "T"  "triplanar" "Triplanar"  ]
    [ "D"  "detail"    "Detail"     ]
]

tool_texlist_shader_filter_bump      = 0
tool_texlist_shader_filter_spec      = 0
tool_texlist_shader_filter_env       = 0
tool_texlist_shader_filter_glow      = 0
tool_texlist_shader_filter_pulse     = 0
tool_texlist_shader_filter_alpha     = 0
tool_texlist_shader_filter_parallax  = 0
tool_texlist_shader_filter_triplanar = 0
tool_texlist_shader_filter_detail    = 0

tool_textlist_shader_make_filter = [
    local _filter_name

    tool_texlist_filter_shader = []

    looplist sfilter $tool_texlist_shader_filters [
        _filter_name = (at $sfilter 1)
        if (getalias [tool_texlist_shader_filter_@_filter_name]) [
            append tool_texlist_filter_shader $_filter_name
        ]
    ]
]

// 1:<slot>
tool_texlist_filter_shader_check = [
    local _shader_name _match
    _shader_name = (getvshadername $arg1)
    _match = 1

    looplist sfilter $tool_texlist_filter_shader [
        if (< (strstr $_shader_name $sfilter) 0) [
            _match = 0
        ]
    ]

    result $_match
]

// 1:<slot>
tool_texlist_filter = [
    local _tex_name _name_match _isvariant_match
    _tex_name = (gettexname $arg1)
    _name_match = (|| [! $tool_texlist_filter_query] [!= (strstr $_tex_name $tool_texlist_filter_query) -1])
    _isvariant_match = (|| [= $tool_texlist_variants 1] [! (istexvariant $arg1)])

    result (&& $_name_match $_isvariant_match (tool_texlist_filter_shader_check $arg1))
]

// 1:<slot>
tool_texlist_decal_filter = [
    local _tex_name
    _tex_name = (getdecalname $arg1)

    || [! $tool_texlist_filter_query] [!= (strstr $_tex_name $tool_texlist_filter_query) -1]
]

# tool_texlist_item_menu_normal [
    toolpanel_open_menu [
        p_width = 0.25
        p_nav_enable = 0
        p_item_names = [
            "Edit slot"
            "Clone slot"
            "Remove slot"
            "Cull from here"
            [#1 _cull_group_text]
        ]
        p_tips = @(? (texhasvariants $arg1) [["Unable to remove slots with variants"]] [])
        p_on_select = [
            do (at [
                [
                    tool_tex_editslot #1 arg1 #1 p_decal
                ] [
                    cloneslot #1 arg1
                ] [
                    tool_confirm_prompt [#1(format "Do you want to remove slot #%1?" $arg1)] [
                        tool_tex_remove #1 arg1 #1 p_decal
                    ] [
                        p_noundo_warn = 1
                    ]
                ] [
                    tool_confirm_prompt [#1(format "Do you want to cull slots starting from #%1?" $arg1)] [
                        tool_tex_cull_from #1 arg1 #1 p_decal
                    ] [
                        p_noundo_warn = 1
                    ]
                ] [
                    tool_confirm_prompt [#1(format "Do you want to cull slot group '%1'?" (gettexgroup $arg1 $p_decal))] [
                        tool_tex_cull_group #1 arg1 #1 p_decal
                    ] [
                        p_noundo_warn = 1
                    ]
                ]
            ] $arg1)
        ]
        p_disabled = [
            case $arg1 1 [
                && [! #1 p_decal] [texhasvariants #1 arg1]
            ] 3 [
                =s (gettexgroup #1 arg1 #1 p_decal)
            ]
        ]
        p_item_noonline = [1 1 1 1 1]
    ]
]

# tool_texlist_item_menu_decal [
    toolpanel_open_menu [
        p_width = 0.25
        p_nav_enable = 0
        p_item_names = [
            "Edit slot"
            "Remove slot"
            "Cull from here"
            [#1 _cull_group_text]
        ]
        p_tips = @(? (texhasvariants $arg1) [["Unable to remove slots with variants"]] [])
        p_on_select = [
            do (at [
                [
                    tool_tex_editslot #1 arg1 #1 p_decal
                ] [
                    tool_confirm_prompt [#1(format "Do you want to remove slot #%1?" $arg1)] [
                        tool_tex_remove #1 arg1 #1 p_decal
                    ] [
                        p_noundo_warn = 1
                    ]
                ] [
                    tool_confirm_prompt [#1(format "Do you want to cull slots starting from #%1?" $arg1)] [
                        tool_tex_cull_from #1 arg1 #1 p_decal
                    ] [
                        p_noundo_warn = 1
                    ]
                ] [
                    tool_confirm_prompt [#1(format "Do you want to cull slot group '%1'?" (gettexgroup $arg1 $p_decal))] [
                        tool_tex_cull_group #1 arg1 #1 p_decal
                    ] [
                        p_noundo_warn = 1
                    ]
                ]
            ] $arg1)
        ]
        p_disabled = [
            case $arg1 1 [
                && [! #1 p_decal] [texhasvariants #1 arg1]
            ] 3 [
                =s (gettexgroup #1 arg1 #1 p_decal)
            ]
        ]
        p_item_noonline = [1 1 1 1]
    ]
]

// 1:<slot>
# tool_texlist_item_menu [
    local _cull_group_text _slot
    _cull_group_text = "Cull group"

    if (gettexgroup $arg1 $p_decal) [
        _cull_group_text = (format "Cull group (%1)" (gettexgroup $arg1 $p_decal))
    ]

    _slot = (? $p_decal $arg1 (getslottex (getvindex $arg1)))

    if $p_decal [
        tool_texlist_item_menu_decal $arg1
    ] [
        tool_texlist_item_menu_normal $arg1
    ]
]

// 1:<slot>
tool_texlist_item_on_rightclick = [
    if (|| $p_decal [> $arg1 1]) [
        tool_texlist_item_menu $arg1
    ]
]

// 1:<display type> 2:<slot> 3:<highlight> 4:<var>
ui_tool_texlist_item = [
    uifill $p_sel_size $p_sel_size [
        if $uidrawn [
            local _namefunc _tip
            _namefunc = (? $p_decal getdecalname gettexname)
            _tip = (format "#%1: %2" $arg2 ($_namefunc $arg2))

            $arg1 $arg2 $p_sel_size $p_sel_size [
                uirelease [
                    tool_param_set @arg4 @arg2 [@@p_on_change]
                    p_on_select
                ]

                uidoublepress $arg2 [
                    p_on_doubleclick $arg2
                ]

                uialtrelease [
                    tool_texlist_item_on_rightclick $arg2
                ]

                uihover [
                    tool_rightclickable
                ]

                ui_tool_tip [
                    p_tip_simple = $_tip
                ]

                if $arg3 [
                    uioutline $ui_tool_texlist_sel_colour
                    uiprev [uistyle clampxy]
                ]
            ]
        ]
    ]
]

// 1:<var>
ui_tool_texlist_groups = [
    local _curgroup _lastgroup _slot _break
    _curgroup  = ""
    _lastgroup = ""
    _slot      = 0
    _break     = 0

    uivlist $p_sel_space [
        while [< $_slot $num_slots] [
            uigrid $p_sel_cols $p_sel_space $p_sel_space [
                uistyle lefttop
                _break = 0

                while [&& [= $_break 0] [< $_slot $num_slots]] [
                    if ($_filterfunc $_slot) [
                        _curgroup = (gettexgroup $_slot $p_decal)

                        if (|| [>= $_slot $num_slots] [!=s $_curgroup $_lastgroup]) [
                            _lastgroup = $_curgroup
                            _break     = 1
                            _slot      = (- $_slot 1)
                        ] [
                            ui_tool_texlist_item $_tex_display $_slot (= $_slot $$arg1) $arg1

                            _curgroup = ""
                        ]
                    ]

                    _slot = (+ $_slot 1)
                ]
            ]

            uitext $_curgroup $ui_tool_text_size_xs
            uiprev [uialign -1 -1]
        ]
    ]
]

// 1:<var>
ui_tool_texlist_simple = [
    uigrid $p_sel_cols $p_sel_space $p_sel_space [
        uistyle lefttop

        loop slot $num_slots [
            if ($_filterfunc $slot) [
                ui_tool_texlist_item $_tex_display $slot (= $slot $$arg1) $arg1
            ]
        ]
    ]
]

tool_texlist_props = [
    [ p_sel_cols            7                         ]
    [ p_sel_size            0.045                     ]
    [ p_sel_area            0.6                       ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 20                        ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_on_change           []                        ]
    [ p_on_select           []                        ]
    [ p_on_doubleclick      []                        ]
    [ p_decal               0                         ]
]

// 1:<select var> 2:<props>
ui_tool_texlist = [
    @(props $tool_texlist_props arg2)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    local _filterfunc _listfunc _tex_display
    _filterfunc = (? $p_decal tool_texlist_decal_filter tool_texlist_filter)

    uivlist $ui_tool_elem_space_s [
        uistyle clampx

        uihlist $ui_tool_elem_space_l [
            uistyle clampx

            uihlist 0 [
                uialign -1

                if $p_decal [
                    _tex_display = uidecalslotview
                    num_slots    = $numdecalslots
                ] [
                    ui_tool_compactswitch tool_texlist_variants [
                        p_label = "Show variants"
                    ]
                    uiprev [uialign -1 -1]

                    _tex_display = uivslotview
                    num_slots    = $numvslots
                ]

                ui_tool_compactswitch tool_texlist_groups [
                    p_label = "Show groups"
                ]
            ]

            ui_tool_button [
                p_noonline   = 1
                p_label      = "New"
                p_label_size = $ui_tool_text_size_xs
                p_icon       = "textures/icons/edit/new"
                p_icon_size  = 0.015
                p_tip_simple = "Add a new texture slot"
                p_width      = (uiwidth 0.05)
                p_on_click = [
                    tool_confirm_prompt "Create new slot?" [
                        if @@p_decal [
                            tool_do_action ta_newdecal
                        ] [
                            tool_do_action ta_newslot
                        ]
                    ]
                ]
            ]
            uiprev [uialign 1]
        ]

        _listfunc = (? $tool_texlist_groups ui_tool_texlist_groups ui_tool_texlist_simple)

        uihlist 0 [
            uistyle clampx

            ui_tool_textinput tool_texlist_filter_query $p_filter_query_length [
                p_label = "Search:"
            ]

            if $p_decal [] [
                local _shader_filter_label
                _shader_filter_label = (? $tool_texlist_filter_shader "Shader filter (active)" "Shader filter")

                ui_tool_button [
                    p_icon       = "textures/icons/settings"
                    p_tip_simple = $_shader_filter_label
                    p_colour     = @(? $tool_texlist_filter_shader $ui_tool_warn_colour $ui_tool_accent_colour)
                    p_on_click   = [
                        tool_texlist_filter_shader_select = (! $tool_texlist_filter_shader_select)
                    ]
                ]
            ]
        ]

        if (&& (! $p_decal) $tool_texlist_filter_shader_select) [
            uihlist 0 [
                uistyle clampx
                uitext "Shader filter:" $ui_tool_text_size_xs
                uiprev [uialign -1]

                uihlist 0 [
                    uialign 1
                    p_sel_area = (-f $p_sel_area 0.025)

                    looplist sfilter $tool_texlist_shader_filters [
                        ui_tool_compactswitch [tool_texlist_shader_filter_@(at $sfilter 1)] [
                            p_label      = @(at $sfilter 0)
                            p_height     = 0.025
                            p_width      = 0.025
                            p_tip_simple = [@(at $sfilter 2)]
                            p_on_change  = [
                                tool_textlist_shader_make_filter
                            ]
                        ]
                    ]
                ]
            ]
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uistyle clampx
            uiscroll 0 $p_sel_area [
                uistyle lefttop
                $_listfunc $arg1
            ]
            uivscroll $p_slider_size $p_sel_area 1
            uialign 1
        ]
    ]
]
