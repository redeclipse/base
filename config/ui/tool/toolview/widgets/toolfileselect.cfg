tool_fileselect_menu_props = [
    [ p_var                 []                        ]
    [ p_get                 []                        ]
    [ p_set                 []                        ]
    [ p_on_change           []                        ]
    [ p_sel_size            0.08                      ]
    [ p_sel_area            0.45                      ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 32                        ]
    [ p_columns             5                         ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_file_type           $TOOL_FILE_ANY            ]
    [ p_strip_ext           1                         ]
    [ p_width               0                         ]
    [ p_can_deselect        1                         ]
]

ui_tool_fileselect_picker = [
    @(props $tool_fileselect_menu_props (toolpanel_attr center user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_filelist $p_var [
            p_sel_size            = @p_sel_size
            p_sel_area            = @p_sel_area
            p_sel_space           = @p_sel_space
            p_filter_query_length = @p_filter_query_length
            p_columns             = @p_columns
            p_slider_size         = @p_slider_size
            p_file_type           = @p_file_type
            p_strip_ext           = @p_strip_ext
            p_width               = @p_width
            p_can_deselect        = @p_can_deselect
            p_get                 = [@@p_get]
            p_set                 = [@@p_set]
            p_on_change           = [
                @@p_on_change
                toolpanel_pop_close_this
            ]
        ]
    ]
]

tool_fileselect_props = [
    [ p_noonline            0                                     ]
    [ p_disabled            0                                     ]
    [ p_can_reset           1                                     ]
    [ p_can_deselect        1                                     ]
    [ p_can_imgedit         1                                     ]
    [ p_reset_val           []                                    ]
    [ p_text_size           $ui_tool_text_size_xs_unscaled        ]
    [ p_dir                 ""                                    ]
    [ p_file_type           $TOOL_FILE_ANY                        ]
    [ p_strip_ext           1                                     ]
    [ p_label               ""                                    ]
    [ p_label_size          $ui_tool_text_size_s_unscaled         ]
    [ p_size                0.06                                  ]
    [ p_text_width          0                                     ]
    [ p_picker_width        [(uiwidth 0.25)]                      ]
    [ p_align               1                                     ]
    [ p_sel_size            0.08                                  ]
    [ p_sel_area            0.45                                  ]
    [ p_sel_space           0.0035                                ]
    [ p_filter_query_length 32                                    ]
    [ p_columns             5                                     ]
    [ p_slider_size         $ui_toolpanel_slider_size             ]
    [ p_show_value          1                                     ]
    [ p_browse_button_props [[p_label = "Browse"; p_width = 0.1]] ]
    [ p_default_text        ["No file selected"]                  ]
    [ p_disabled_text       ["Disabled"]                          ]
    [ p_on_change           []                                    ]
    [ p_get                 [[result $arg1]]                      ]
    [ p_set                 [[result $arg1]]                      ]
    [ p_id                  []                                    ]
]

// 1:<var> 2:<props>
# ui_tool_fileselect [
    @(props $tool_fileselect_props arg2)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_text_size)
    @(tool_ui_scale p_size)
    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _text _value _browse

    if $p_disabled [
        _text = (concatword "^fA" $p_disabled_text)
        _value = ""
    ] [
        _value = (? $p_disabled "" (p_get $$arg1))
        _text  = (? $_value $_value $p_default_text)
    ]

    _browse = [
        if (!=s #1 _value "") [
            local _path

            if (= #1 p_file_type $TOOL_FILE_IMAGE) [
                _path = (filenoext (tool_path_striptexcmds #1 _value))
            ] [
                _path = #1 _value
            ]

            tool_filelist_curdir = (filedir $_path)
        ] [
            tool_filelist_curdir = #1 p_dir
        ]

        toolpanel_open tool_fileselect_picker center [
            p_title     = "File browser"
            p_width     = #1 p_picker_width
            p_user_data = [
                p_var                 = #1 arg1
                p_on_change           = [#1 p_on_change]
                p_get                 = [#1 p_get]
                p_set                 = [#1 p_set]
                p_sel_size            = #1 p_sel_size
                p_sel_area            = #1 p_sel_area
                p_sel_space           = #1 p_sel_space
                p_filter_query_length = #1 p_filter_query_length
                p_columns             = #1 p_columns
                p_slider_size         = #1 p_slider_size
                p_file_type           = #1 p_file_type
                p_width               = #1 p_picker_width
                p_strip_ext           = #1 p_strip_ext
                p_can_deselect        = #1 p_can_deselect
            ]
        ]
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1

        uitext $p_label $p_label_size

        uivlist 0 [
            uiclamp 1 1

            if $p_show_value [
                #(ui_tool_interactable 0 0 [
                    tool_control_highlight

                    uispace $ui_tool_elem_space_m $ui_tool_elem_space_s [
                        uivlist 0 [
                            if (= $p_file_type $TOOL_FILE_IMAGE) [
                                if (!=s $_value "") [
                                    uithumbnailunclamped (concatword "<thumbnail:256,-1>" $_value) $p_size $p_size
                                ] [
                                    uifill $p_size $p_size
                                ]
                            ]
                            if $p_text_width [
                                uifill $p_text_width 0 [
                                    ui_tool_autoscroll_text $_text [
                                        p_width     = $p_text_width
                                        p_text_size = $p_text_size
                                    ]
                                    uialign- 1
                                ]
                            ] [
                                uitext $_text $p_text_size
                            ]

                            uialign* $p_align 1
                        ]
                    ]

                    ui_tool_tip [
                        p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
                    ]

                    if $p_disabled [] [
                        uirelease [
                            _browse
                        ]

                        uialtrelease [
                            if (=s $p_reset_val) [
                                p_reset_val = (getsvardef $arg1)
                            ]

                            tool_param_menu $arg1 [
                                p_can_reset = @p_can_reset
                                p_reset_val = [@@p_reset_val]
                                p_on_change = [@@p_on_change]
                                @(? (&& $p_can_imgedit [= $p_file_type $TOOL_FILE_IMAGE] [!=s $_value ""]) [
                                    p_item_names = [
                                        "Edit image"
                                    ]
                                    p_on_select = [
                                        tool_imgedit_open @@arg1 [@@@p_get] [@@@p_set] [@@@p_on_change]
                                    ]
                                ])
                            ]
                        ]

                        uihover [
                            tool_rightclickable

                            tool_copy_handler [
                                tool_param_copy @arg1 [
                                    p_get = [@@p_get]
                                ]
                            ] [
                                tool_param_paste @arg1 [
                                    p_on_change = [@@p_on_change]
                                    p_set       = [@@p_set]
                                ]
                            ]
                        ]
                    ]

                ] [$p_disabled])
            ] [
                ui_tool_button [
                    p_noonline = @p_noonline
                    p_disabled = @p_disabled
                    p_on_click = [@@_browse]
                    p_id       = @p_id
                    @p_browse_button_props
                ]
            ]

            uialign* $p_align
        ]
    ]
]
