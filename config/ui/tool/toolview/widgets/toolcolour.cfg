tool_colour_mode = 0

// 1:<var> 2:<disabled> 3:<on change>
ui_tool_colour_hsv = [
    result [
        local hsv_var
        hsv_var = (concatword $arg1 _hsv)
        $hsv_var = (rgbtohsv $$arg1)

        local hvar svar vvar
        hvar = (concatword $arg1 _h)
        svar = (concatword $arg1 _s)
        vvar = (concatword $arg1 _v)

        $hvar = (at $$hsv_var 0)
        $svar = (at $$hsv_var 1)
        $vvar = (at $$hsv_var 2)

        ui_tool_numinput $hvar 0 360 1 [
            uit_disabled = @arg2
            uit_label = "H"
            uit_label_size = @uit_label_size
            uit_val_format = i
            uit_has_menu = 0
            uit_circular = 1
            uit_on_change = [
                local hue sat val
                hue = $@hvar
                sat = $@svar
                val = $@vvar
                tool_param_set @arg1 (hsvtorgb [@hue @sat @val]) @@@arg3
            ]
            uit_interval = @uit_interval
        ]
        ui_tool_numinput $svar 0 1 0.1 [
            uit_disabled = @arg2
            uit_label = "S"
            uit_label_size = @uit_label_size
            uit_has_menu = 0
            uit_on_change = [
                local hue sat val
                hue = $@hvar
                sat = $@svar
                val = $@vvar
                tool_param_set @arg1 (hsvtorgb [@hue @sat @val]) @@@arg3
            ]
            uit_interval = @uit_interval
        ]
        ui_tool_numinput $vvar 0 $uit_max_val 0.1 [
            uit_disabled = @arg2
            uit_label = "V"
            uit_label_size = @uit_label_size
            uit_has_menu = 0
            uit_on_change = [
                local hue sat val
                hue = $@hvar
                sat = $@svar
                val = $@vvar
                tool_param_set @arg1 (hsvtorgb [@hue @sat @val]) @@@arg3
            ]
            uit_interval = @uit_interval
        ]
    ]
]

// 1:<var> 2:<disabled> 3:<on change>
ui_tool_colour_rgb = [
    result [
        local rvar gvar bvar
        rvar = (concatword $arg1 _r)
        gvar = (concatword $arg1 _g)
        bvar = (concatword $arg1 _b)

        $rvar = (at $$arg1 0)
        $gvar = (at $$arg1 1)
        $bvar = (at $$arg1 2)

        ui_tool_numinput $rvar 0 $uit_max_val 0.1 [
            uit_disabled = @arg2
            uit_label = "R"
            uit_label_size = @uit_label_size
            uit_has_menu = 0
            uit_on_change = [
                local red green blue
                red = $@rvar
                green = $@gvar
                blue = $@bvar
                tool_param_set @arg1 [@red @green @blue] @@@arg3
            ]
            uit_interval = @uit_interval
        ]
        ui_tool_numinput $gvar 0 $uit_max_val 0.1 [
            uit_disabled = @arg2
            uit_label = "G"
            uit_label_size = @uit_label_size
            uit_has_menu = 0
            uit_on_change = [
                local red green blue
                red = $@rvar
                green = $@gvar
                blue = $@bvar
                tool_param_set @arg1 [@red @green @blue] @@@arg3
            ]
            uit_interval = @uit_interval
        ]
        ui_tool_numinput $bvar 0 $uit_max_val 0.1 [
            uit_disabled = @arg2
            uit_label = "B"
            uit_label_size = @uit_label_size
            uit_has_menu = 0
            uit_on_change = [
                local red green blue
                red = $@rvar
                green = $@gvar
                blue = $@bvar
                tool_param_set @arg1 [@red @green @blue] @@@arg3
            ]
            uit_interval = @uit_interval
        ]
    ]
]

tool_colour_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_label "Colour" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_preview_width 0.02 ]
    [ uit_max_val 1 ]
    [ uit_on_change [] ]
    [ uit_interval 0 ]
]

// 1:<var> 2:<props>
ui_tool_colour = [
    @(tool_props $tool_colour_props arg2)

    local colour

    if $uit_disabled [
        colour = 0
    ] [
        local red green blue
        red = (*f (at $$arg1 0) 255)
        green = (*f (at $$arg1 1) 255)
        blue = (*f (at $$arg1 2) 255)
        colour = (rgbtoint (minf $red 255) (minf $green 255) (minf $blue 255))
    ]

    uihlist $ui_toolview_small_elem_space [
        uivlist 0 [
            uihlist 0 [
                uiclamp 1 1
                uitext $uit_label $uit_label_size
                ui_tool_switch tool_colour_mode [
                    uit_options = ["HSV" "RGB"]
                    uit_tip_simple = "Switch colour mode"
                ]
            ]
            uialign- 1
            if $tool_colour_mode [
                @@@@(ui_tool_colour_rgb [$arg1] [$uit_disabled] @uit_on_change)
            ] [
                @@@@(ui_tool_colour_hsv [$arg1] [$uit_disabled] @uit_on_change)
            ]
        ]

        uicolour $colour $uit_preview_width 0.08 [
            if $uit_disabled [] [
                uialtrelease [
                    tool_param_menu @@@@arg1 $uit_can_reset $uit_reset_val $uit_on_change
                ]
            ]
        ]
    ]
]
