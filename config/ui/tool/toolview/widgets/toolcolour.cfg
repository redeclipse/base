// widget flags
TOOL_COLOUR_BLUEFIX = 1
TOOL_COLOUR_ZEROFIX = (<< 1 1)

TOOL_COLOUR_MAX_RECENT_COLOURS = 16

tool_colour_mode           = 0
tool_colour_pipette        = 0
tool_colour_pipette_colour = 0

// 1:<value> 2:<format> 3:<mode>
tool_colour_conv_rgb_vec = [
    local _vec
    _vec = $arg1

    if (=s $arg2 i) [
        @@(tool_colour_fromint [_vec] [arg1])
    ]

    if $arg3 [] [
        _vec = (rgbtohsv $_vec)
    ]

    result $_vec
]

// 1:<value> 2:<format> 3:<mode> 4:<max val> 5:<flags>
tool_colour_conv_storage = [
    local _vec
    _vec = $arg1

    if $arg3 [
        _vec = [@@(minf $arg4 (at $_vec 0)) @@(minf $arg4 (at $_vec 1)) @@(minf $arg4 (at $_vec 2)) ]
    ] [
        _vec = [@@(at $_vec 0) @@(at $_vec 1) @@(minf $arg4 (at $_vec 2)) ]
        _vec = (hsvtorgb $_vec)
    ]

    if (=s $arg2 i) [
        local _bluefix _zerofix

        _bluefix = (& $arg5 $TOOL_COLOUR_BLUEFIX)
        _zerofix = (& $arg5 $TOOL_COLOUR_ZEROFIX)

        tool_colour_toint $_vec $_bluefix $_zerofix
    ] [
        result $_vec
    ]
]

// 1:<var> 2:<component idx> 3:<format>
tool_colour_get_component = [
    local _vec
    _vec = (tool_colour_conv_rgb_vec $$arg1 $arg3 $tool_colour_mode)

    at $_vec $arg2
]

// 1:<var> 2:<component idx> 3:<value> 4:<format> 5:<flags> 6:<max value>
tool_colour_set_component = [
    local _vec
    _vec = (tool_colour_conv_rgb_vec $$arg1 $arg4 $tool_colour_mode)
    _vec = (listsplice $_vec $arg3 $arg2 1)

    tool_colour_conv_storage $_vec $arg4 $tool_colour_mode $arg6 $arg5
]

// 1:<var> 2:<format>
tool_colour_get_int = [
    if (=s $arg2 i) [
        result $$arg1
    ] [
        tool_colour_toint $$arg1
    ]
]

tool_colour_picker_menu_props = [
    [ p_var        [] ]
    [ p_val_format v  ]
    [ p_flags      0  ]
    [ p_on_change  [] ]
    [ p_max_val    1  ]
    [ p_interval   0  ]
    // Value before user's edits
    [ p_reset_val  [] ]
]

ui_tool_colour_picker_on_close = [
    @(tool_props $tool_colour_picker_menu_props (toolpanel_attr popup user_data))

    local _recent _num_recent _new_colour

    _recent     = (getalias tool_colour_recent)
    _num_recent = (listlen $_recent)
    _new_colour = (tool_colour_conv_rgb_vec $$p_var $p_val_format 1)

    // If new colour not on the list
    if (listhas (getalias tool_colour_recent) $_new_colour) [] [
        // Cut overflow
        if (>= $_num_recent $TOOL_COLOUR_MAX_RECENT_COLOURS) [
            _recent = (listsplice $_recent [] (- $TOOL_COLOUR_MAX_RECENT_COLOURS 1) 1)
        ]

        // Add new colour to the list
        worldmeta tool_colour_recent [[@@_new_colour] @_recent]
    ]

    tool_colour_pipette = 0
]

ui_tool_colour_picker = [
    @(tool_props $tool_colour_picker_menu_props (toolpanel_attr popup user_data))

    local _label1 _label2 _label3 _ctrl1max _ctrl1step _num_recent _recent _recent_int

    if $tool_colour_mode [
        _label1    = "Red"
        _label2    = "Green"
        _label3    = "Blue"
        _ctrl1max  = $p_max_val
        _ctrl1step = 0.1
    ] [
        _label1    = "Hue"
        _label2    = "Saturation"
        _label3    = "Value"
        _ctrl1max  = 360
        _ctrl1step = 1
    ]

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist 0 [
            uiclamp 1 1

            uicolour (tool_colour_get_int $p_var $p_val_format) 0 0.02
            uiclamp- 1 1

            ui_tool_switch tool_colour_mode [
                p_options    = ["HSV" "RGB"]
                p_tip_simple = "Switch colour mode"
            ]

            ui_tool_numinput $p_var 0 $_ctrl1max $_ctrl1step [
                p_label      = $_label1
                p_label_size = $ui_tool_text_size_s_unscaled
                p_val_format = (? $tool_colour_mode f i)
                p_circular   = (? $tool_colour_mode 0 1)
                p_has_menu   = 0
                p_interval   = @p_interval
                p_on_change  = [@@p_on_change]
                p_get        = [tool_colour_get_component @@p_var 0 @@p_val_format]
                p_set        = [
                    tool_colour_set_component @@p_var 0 $arg1 @@p_val_format @@p_flags @@p_max_val
                ]
                p_id         = 0
            ]

            ui_tool_numinput $p_var 0 1 0.1 [
                p_label      = $_label2
                p_label_size = $ui_tool_text_size_s_unscaled
                p_has_menu   = 0
                p_interval   = @p_interval
                p_on_change  = [@@p_on_change]
                p_get        = [tool_colour_get_component @@p_var 1 @@p_val_format]
                p_set        = [
                    tool_colour_set_component @@p_var 1 $arg1 @@p_val_format @@p_flags @@p_max_val
                ]
                p_val_text   = [ round $arg1 0.01 ]
                p_id         = 1
            ]

            ui_tool_numinput $p_var 0 $p_max_val 0.1 [
                p_label      = $_label3
                p_label_size = $ui_tool_text_size_s_unscaled
                p_has_menu   = 0
                p_interval   = @p_interval
                p_on_change  = [@@p_on_change]
                p_get        = [tool_colour_get_component @@p_var 2 @@p_val_format]
                p_set        = [
                    tool_colour_set_component @@p_var 2 $arg1 @@p_val_format @@p_flags @@p_max_val
                ]
                p_val_text   = [ round $arg1 0.01 ]
                p_id         = 2
            ]

            uifill 0 $ui_tool_elem_space_s
            uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
            uifill 0 $ui_tool_elem_space_s

            uigrid 8 0 0 [
                uiclamp 1 1

                _num_recent = (listlen (getalias tool_colour_recent))

                loop i $_num_recent [
                    _recent = (at (getalias tool_colour_recent) $i)
                    _recent_int = (tool_colour_toint $_recent)

                    uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                        uiclamp 1 1

                        uicolour $_recent_int 0 $ui_tool_button_height_s [
                            uirelease [
                                local _recent_val
                                _recent_val = (tool_colour_conv_storage $_recent $p_val_format 1 $p_max_val $p_flags)
                                tool_param_set $p_var $_recent_val $p_on_change
                            ]
                        ]
                        uiclamp- 1 1
                    ]
                ]
            ]

            uifill 0 $ui_tool_elem_space_s
            uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
            uifill 0 $ui_tool_elem_space_s

            uihlist 0 [
                ui_tool_button [
                    p_label    = "Revert"
                    p_width    = $ui_tool_button_width_s_unscaled
                    p_height   = $ui_tool_button_height_m_unscaled
                    p_on_click = [
                        local _reset_val
                        _reset_val = [@@(tool_colour_conv_storage $p_reset_val $p_val_format 1 $p_max_val $p_flags)]
                        tool_param_set @p_var $_reset_val @p_on_change
                    ]
                ]

                ui_tool_button [
                    p_label    = "Pipette"
                    p_width    = $ui_tool_button_width_s_unscaled
                    p_height   = $ui_tool_button_height_m_unscaled
                    p_on_click = [
                        tool_colour_pipette = 1
                    ]
                ]
            ]
        ]
    ]
]

tool_colour_pipette_undercursor = [
    readpixel [
        tool_colour_pipette_colour = (getpixel 0)
    ] (uicursorpxx) (uicursorpxy)
    uicolour $tool_colour_pipette_colour 0.05 0.05
]

// 1:<if right click>
tool_colour_outside_click_handler = [
    if $tool_colour_pipette [
        if $arg1 [] [
            @(tool_props $tool_colour_picker_menu_props (toolpanel_attr popup user_data))

            local _col
            _col = (tool_colour_conv_rgb_vec $tool_colour_pipette_colour i 1)
            _col = (tool_colour_conv_storage $_col $p_val_format 1 $p_max_val $p_flags)

            tool_param_set $p_var $_col $p_on_change
        ]

        tool_colour_pipette = 0
    ] [
        toolpanel_close_this
    ]
]

tool_colour_props = [
    [ p_disabled     0                             ]
    [ p_can_reset    1                             ]
    [ p_reset_val    []                            ]
    [ p_label        "Colour"                      ]
    [ p_label_size   $ui_tool_text_size_s_unscaled ]
    [ p_preview_size 0.02                          ]
    [ p_max_val      1                             ]
    [ p_on_change    []                            ]
    [ p_interval     0                             ]
    [ p_val_format   v                             ]
    [ p_flags        0                             ]
    [ p_picker_width [(uiwidth 0.15)]              ]
    [ p_tip          ""                            ]
    [ p_tip_simple   ["Click to change"]           ]
    [ p_tip_action   ""                            ]
    [ p_id           []                            ]
]

// 1:<var> 2:<props>
ui_tool_colour = [
    @(tool_props $tool_colour_props arg2)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_preview_size)

    local _col
    _col = 0

    if $tool_colour_pipette [
        tool_set_cursor_icon "textures/icons/edit/name"
        toolview_undercursor = tool_colour_pipette_undercursor
    ]

    if $p_disabled [] [
        if (=s $p_val_format i) [
            _col = $$arg1
        ] [
            _col = (tool_colour_toint $$arg1)
        ]
    ]

    uihlist $ui_tool_elem_space_l [
        uiclamp 1 1

        uitext $p_label $p_label_size

        @@(ui_tool_interactable 0 0 [
            tool_control_highlight

            uispace $ui_tool_elem_space_m $ui_tool_elem_space_m [
                uicolour $_col (*f $p_preview_size 2) $p_preview_size [
                    if $p_disabled [] [
                        ui_tool_tip [
                            p_tip        = [@@p_tip]
                            p_tip_simple = [@@p_tip_simple]
                            p_tip_action = [@@p_tip_action]
                        ]

                        uirelease [
                            tool_colour_pipette = 0

                            toolpanel_open tool_colour_picker popup [
                                p_position                    = (uicursorpos)
                                p_width                       = @p_picker_width
                                p_popup_outside_click_handler = tool_colour_outside_click_handler
                                p_user_data                   = [
                                    p_var        = @arg1
                                    p_val_format = @p_val_format
                                    p_flags      = @p_flags
                                    p_max_val    = @p_max_val
                                    p_on_change  = [@@p_on_change]
                                    p_max_val    = @p_max_val
                                    p_interval   = @p_interval
                                    p_reset_val  = [@@(tool_colour_conv_rgb_vec $$arg1 $p_val_format 1)]
                                ]
                            ]
                        ]

                        uialtrelease [
                            if (=s $p_reset_val) [
                                p_reset_val = (getvardef $arg1)
                            ]

                            tool_param_menu $arg1 [
                                p_can_reset = @p_can_reset
                                p_reset_val = [@@p_reset_val]
                                p_on_change = [@@p_on_change]
                                p_get       = [
                                    tool_colour_conv_rgb_vec $arg1 @@p_val_format 1
                                ]
                                p_set       = [
                                    tool_colour_conv_storage $arg1 @@p_val_format 1 @@p_max_val @@p_flags
                                ]
                            ]
                        ]

                        uihover [
                            tool_rightclickable

                            tool_copy_handler [
                                tool_param_copy @arg1 [
                                    p_get = [
                                        tool_colour_conv_rgb_vec $arg1 @@p_val_format 1
                                    ]
                                ]
                            ] [
                                tool_param_paste @arg1 [
                                    p_on_change = [@@p_on_change]
                                    p_set       = [
                                        tool_colour_conv_storage $arg1 @@p_val_format 1 @@p_max_val @@p_flags
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ])
    ]
]
