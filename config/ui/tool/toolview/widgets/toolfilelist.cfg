tool_filelist_filter_query = ""

tool_filelist_curdir       = ""
tool_filelist_dirs         = []
tool_filelist_files        = []
tool_filelist_numdirs      = 0
tool_filelist_numfiles     = 0
tool_filelist_sel_type     = 0
tool_filelist_sel_index    = -1
tool_filelist_sel_path     = ""
tool_filelist_active_path  = ""
tool_filelist_preview      = 1

tool_filelist_updir = [
    local _path
    _path = (strreplace $tool_filelist_curdir "/" " ")
    _path = (listsplice $_path "" (- (listlen $_path) 1) 1)
    tool_filelist_curdir = (strreplace $_path " " "/")

    tool_filelist_fetchdir
]

// 1:<var>
tool_filelist_get_active = [
    if (= $p_file_type $TOOL_FILE_IMAGE) [
        tool_filelist_active_path = (tool_path_striptexcmds (filenoext (p_get $$arg1)))
    ] [
        tool_filelist_active_path = (p_get $$arg1)
    ]
]

// 1:<var>
tool_filelist_select = [
    local _selname
    _selname = ""

    if (= $tool_filelist_sel_type 0) [
        _selname = (at $tool_filelist_files $tool_filelist_sel_index)
    ] [
        _selname = (at $tool_filelist_dirs $tool_filelist_sel_index)
    ]

    if (!=s $tool_filelist_curdir) [
        _selname = (concatword $tool_filelist_curdir "/" $_selname)
    ]

    if $p_strip_ext [
        _selname = (filenoext $_selname)
    ]

    tool_param_set $arg1 (p_set $_selname) $p_on_change
    tool_filelist_get_active $arg1
]

// 1:<index> 2:<name> 3:<selected> 4:<dir> 5:<var>
ui_tool_filelist_item = [
    local _icon_size
    _icon_size = (*f $p_sel_size 0.8)

    uitarget $p_sel_size $p_sel_size [
        if (uidrawn) [
            local _fullfname

            if $p_strip_ext [
                _fullfname = (filenoext $arg2)
            ] [
                _fullfname = $arg2
            ]

            uivlist 0 [
                local _text_area _fname
                _text_area = (*f $p_sel_size 0.8)
                _fname = (filename $arg2)

                caseif [!= $arg4 0] [
                    uiimage "textures/icons/action" 0xffffff 0 $_icon_size $_icon_size
                ] [tool_file_isimage $arg2] [
                    uithumbnailclamped (concatword "<thumbnail:64,-1>" $tool_filelist_curdir "/" $arg2) $_icon_size $_icon_size
                ] [&& (=s $_fname "textures") [= $p_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/texture" 0xffffff 0 $_icon_size $_icon_size
                ] [&& (=s $_fname "decals") [= $p_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/decal" 0xffffff 0 $_icon_size $_icon_size
                ] [&& (=s $_fname "models") [= $p_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/cube" 0xffffff 0 $_icon_size $_icon_size
                ] [&& (=s $_fname "sounds") [= $p_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/sound" 0xffffff 0 $_icon_size $_icon_size
                ] () [
                    uiimage "textures/icons/edit/new" 0xffffff 0 $_icon_size $_icon_size
                ]

                ui_tool_autoscroll_text $_fullfname [
                    p_width = @_text_area
                ]
            ]

            uipress [
                local _selname
                _selname = (? (= $arg4 0) (at $tool_filelist_files $arg1) (at $tool_filelist_dirs $arg1))
                tool_filelist_sel_type = $arg4
                tool_filelist_sel_index = $arg1
                if (!=s $tool_filelist_curdir) [
                    tool_filelist_sel_path = (concatword $tool_filelist_curdir "/" $_selname)
                ] [
                    tool_filelist_sel_path = $_selname
                ]

                if (&& $tool_filelist_preview [! $arg4] [= $p_file_type $TOOL_FILE_SOUND]) [
                    previewsound $tool_filelist_sel_path 0.5
                ]
            ]

            uidoublepress (concatword tool_filelist_item_ (at ["f" "d"] $arg4) _ $arg1) [
                if (!= $arg4 0) [
                    if (!=s $tool_filelist_curdir) [
                        appendword tool_filelist_curdir (concatword "/" $_fullfname)
                    ] [
                        tool_filelist_curdir = $_fullfname
                    ]
                    tool_filelist_fetchdir
                ] [
                    tool_filelist_select $arg5
                ]
            ]

            if (&& (= $tool_filelist_sel_type $arg4) [= $tool_filelist_sel_index $arg1]) [
                uicolour $ui_tool_skyboxlist_sel_colour 0 0 [
                    uichangeblends 0.2
                ]
                uiclamp- 1 1 1 1
            ]

            if (=s $tool_filelist_active_path (concatword $tool_filelist_curdir "/" $_fullfname)) [
                uioutline $ui_tool_skyboxlist_sel_colour
                uiclamp- 1 1 1 1
            ]
        ]
    ]
]

// 1:<list>
tool_filelist_rem_hidden = [
    local _done _index
    _index = -1
    _done = 0

    while [(= $_done 0)] [
        _index = (listfind i $$arg1 [
            tool_file_ishidden $i
        ])

        if (>= $_index 0) [
            $arg1 = (listsplice $$arg1 "" $_index 1)
        ] [
            _done = 1
        ]
    ]
]

tool_filelist_filter_type = [
    local _done _index
    _index = -1
    _done = 0

    if (!= $p_file_type @TOOL_FILE_ANY) [
        while [(= $_done 0)] [
            _index = (listfind i $tool_filelist_files [
                ! (tool_file_is_type $i $p_file_type)
            ])

            if (>= $_index 0) [
                tool_filelist_files = (listsplice $tool_filelist_files "" $_index 1)
            ] [
                _done = 1
            ]
        ]
    ]
]

tool_filelist_asset_types = [
    textures
    sounds
    models
    decals
]

// 1:<dir>
tool_filelist_assetscan = [
    local _dirs _files _assetname
    _files = []
    _dirs = (listfiles (concatword "data/" $arg1) "" 2)

    looplist dir $_dirs [
        if (!=s (substr $dir 0 1) ".") [
            tool_filelist_assetscan $dir
            _files = (listfiles (concatword "data/" $arg1 "/" $dir) "" 1)

            looplist type $tool_filelist_asset_types [
                if (>= (listfind=s $_files (concatword $type ".cfg")) 0) [
                    if (=s $arg1) [
                        _assetname = (concatword $dir "/" $type)
                    ] [
                        _assetname = (concatword $arg1 "/" $dir "/" $type)
                    ]
                    append tool_filelist_files $_assetname
                    tool_filelist_numfiles = (+ $tool_filelist_numfiles 1)
                ]
            ]
        ]
    ]
]

tool_filelist_fetchdir = [
    // Fetch dirs and files only when not scanning for asset packs
    if (!= $p_file_type $TOOL_FILE_ASSETPACK) [
        tool_filelist_dirs  = (listfiles (concatword "data/" $tool_filelist_curdir) "" 2)
        tool_filelist_files = (listfiles (concatword "data/" $tool_filelist_curdir) "" 1)

        tool_filelist_rem_hidden tool_filelist_dirs
        tool_filelist_rem_hidden tool_filelist_files

        tool_filelist_filter_type

        tool_filelist_numdirs  = (listlen $tool_filelist_dirs)
        tool_filelist_numfiles = (listlen $tool_filelist_files)
    ] [
        tool_filelist_files    = []
        tool_filelist_numdirs  = 0
        tool_filelist_numfiles = 0
        tool_filelist_assetscan $tool_filelist_curdir
    ]

    tool_filelist_sel_type  = 0
    tool_filelist_sel_index = -1
]

// 1:<item>
tool_filelist_filter = [
    result (|| [! $tool_filelist_filter_query] [!= (strcasestr $arg1 $tool_filelist_filter_query) -1])
]

tool_filelist_props = [
    [ p_sel_size            0.08                      ]
    [ p_sel_area            0.45                      ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 32                        ]
    [ p_columns             5                         ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_file_type           $TOOL_FILE_ANY            ]
    [ p_on_change           []                        ]
    [ p_get                 [[result $arg1]]          ]
    [ p_set                 [[result $arg1]]          ]
    [ p_width               0                         ]
    [ p_can_deselect        1                         ]
]

// 1:<var> 2:<props>
ui_tool_filelist = [
    @(props $tool_filelist_props arg2)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)
    @(tool_ui_scale p_width)

    if $toolpanel_this_isinit [
        tool_filelist_get_active $arg1
        tool_filelist_fetchdir
    ]

    uivlist 0 [
        uiclamp 1 1

        ui_tool_textinput tool_filelist_filter_query $p_filter_query_length [
            p_label = "Search:"
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist $ui_tool_elem_space_s [
            ui_tool_button [
                p_icon       = "textures/icons/shock"
                p_tip_simple = "Refresh"
                p_icon_size  = 0.015
                p_on_click   = [
                    tool_filelist_fetchdir
                ]
            ]

            if (!= $p_file_type $TOOL_FILE_ASSETPACK) [
                ui_tool_button [
                    p_disabled   = (=s $tool_filelist_curdir "data")
                    p_icon       = "textures/icons/arrow"
                    p_tip_simple = "Go up"
                    p_icon_size  = 0.015
                    p_on_click   = [
                        tool_filelist_updir
                    ]
                ]
                uitext "^fADirectory: " $ui_tool_text_size_s
                ui_tool_autoscroll_text $tool_filelist_curdir [
                    p_width     = @(*f $p_width 0.7)
                    p_text_size = $ui_tool_text_size_s
                ]
            ] [
                uitext "Assets packs:" $ui_tool_text_size_s
            ]

            uialign -1
        ]

        uifill 0 $ui_tool_elem_space_l
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_l

        local _item_name
        _item_name = ""

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $p_sel_area [
                uialign -1 -1
                uigrid $p_columns $p_sel_space $p_sel_space [
                    loop i $tool_filelist_numdirs [
                        _item_name = (at $tool_filelist_dirs $i)
                        if (tool_filelist_filter $_item_name) [
                            ui_tool_filelist_item $i $_item_name 0 1 $arg1
                        ]
                    ]
                    loop i $tool_filelist_numfiles [
                        _item_name = (at $tool_filelist_files $i)
                        if (tool_filelist_filter $_item_name) [
                            ui_tool_filelist_item $i $_item_name 0 0 $arg1
                        ]
                    ]
                ]
            ]
            uivscroll $p_slider_size $p_sel_area 1
            uialign 1
        ]

        uifill 0 $ui_tool_elem_space_l
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_l

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1
            uigrid 2 0 $ui_tool_elem_space_s [
                uitext "^fASelected: " $ui_tool_text_size_s
                ui_tool_autoscroll_text $tool_filelist_sel_path [
                    p_width     = @(*f $p_width 0.8)
                    p_text_size = $ui_tool_text_size_s
                ]

                uitext "^fACurrent: " $ui_tool_text_size_s
                ui_tool_autoscroll_text (p_get $$arg1) [
                    p_width     = @(*f $p_width 0.8)
                    p_text_size = $ui_tool_text_size_s
                ]

                uialign -1
            ]

            if (= $p_file_type $TOOL_FILE_SOUND) [
                ui_tool_checkbox tool_filelist_preview [
                    p_label = "Enable preview"
                ]
                uialign- -1
            ]

            uihlist $ui_tool_elem_space_l [
                if $p_can_deselect [
                    ui_tool_button [
                        p_icon       = "textures/hud/exit"
                        p_icon_size  = 0.015
                        p_on_click   = [
                            tool_param_set @arg1 "" [@@p_on_change]
                        ]
                        p_tip_simple = "Clear selection"
                    ]
                ]

                ui_tool_button [
                    p_label    = "Ok"
                    p_width    = 0.18
                    p_on_click = [
                        tool_filelist_select @arg1
                    ]
                ]
                ui_tool_button [
                    p_label    = "Cancel"
                    p_width    = 0.18
                    p_on_click = [
                        toolpanel_pop_close_this
                    ]
                ]
            ]
        ]
    ]
]
