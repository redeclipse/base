ui_tool_imagelist_sel_colour = 0xFF0000

tool_imagelist_filter_query = ""

// 1:<item>
tool_imagelist_filter = [
    result (|| [! $tool_imagelist_filter_query] [!= (strcasestr $arg1 $tool_imagelist_filter_query) -1])
]

// 1:<name> 2:<highlight> 3:<var>
ui_tool_imagelist_item = [
    uicolour 0 0 0 [
        uihlist 0 [
            uiimage (concatword "<thumbnail:256/256"> $p_path "/" $arg1) 0xffffff 0 $p_sel_size $p_sel_size

            uirelease [
                tool_param_set $arg3 (p_set $arg1) $p_on_change
                p_on_select
            ]

            ui_tool_tip [
                p_tip_simple = @arg1
            ]
        ]

        if $arg2 [
            uioutline $ui_tool_imagelist_sel_colour
            uiclamp- 1 1 1 1
        ]

        if (=s $arg1 "") [
            uitext "<none>"
        ]
    ]
]

tool_imagelist_props = [
    [ p_sel_cols            2                         ]
    [ p_sel_size            0.16                      ]
    [ p_sel_area            0.6                       ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 24                        ]
    [ p_path                ""                        ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_on_change           []                        ]
    [ p_on_select           []                        ]
    [ p_get                 [[result $arg1]]          ]
    [ p_set                 [[result $arg1]]          ]
    [ p_allow_custom        1                         ]
]

// 1:<select var> 2:<images> 3:<props>
ui_tool_imagelist = [
    @(tool_props $tool_imagelist_props arg3)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    uivlist 0 [
        uiclamp 1 1
        ui_tool_textinput tool_imagelist_filter_query $p_filter_query_length [
            p_label = "Search:"
        ]
        if $p_allow_custom [
            ui_tool_textinput $arg1 $p_filter_query_length [
                p_label = "Custom:"
            ]
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $p_sel_area [
                uialign -1 -1
                uigrid $p_sel_cols $p_sel_space $p_sel_space [
                    looplist image $arg2 [
                        if (tool_imagelist_filter $image) [
                            ui_tool_imagelist_item $image (=s $image (p_get $$arg1)) $arg1
                        ]
                    ]
                ]
            ]
            uivscroll $p_slider_size $p_sel_area 1
            uialign 1
        ]
    ]
]
