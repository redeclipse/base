ui_tool_imagelist_sel_colour = 0xFF0000

tool_imagelist_filter_query = ""
tool_imagelist_item_click = 0

// 1:<item>
tool_imagelist_filter = [
    result (|| [! $tool_imagelist_filter_query] [!= (strcasestr $arg1 $tool_imagelist_filter_query) -1])
]

// 1:<name> 2:<highlight>
ui_tool_imagelist_item = [
    tool_imagelist_item_click = 0

    uicolour 0 0 0 [
        uihlist 0 [
            uiimage (concatword "<thumbnail:256/256"> $uit_path "/" $arg1) 0xffffff 0 $uit_sel_size $uit_sel_size

            uirelease [
                tool_imagelist_item_click = 1
            ]
            ui_tool_tip [
                uit_tip_simple = $arg1
            ]
        ]

        if $arg2 [
            uioutline $ui_tool_imagelist_sel_colour
            uiclamp- 1 1 1 1
        ]

        if (=s $arg1 "") [
            uitext "<none>"
        ]
    ]
]

tool_imagelist_props = [
    [ uit_sel_cols 2 ]
    [ uit_sel_size 0.16 ]
    [ uit_sel_area 0.6 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 24 ]
    [ uit_path "" ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_on_change [] ]
    [ uit_on_select [] ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[result $arg1]] ]
    [ uit_allow_custom 1 ]
]

// 1:<select var> 2:<images> 3:<props>
ui_tool_imagelist = [
    @(tool_props $tool_imagelist_props arg3)

    uivlist 0 [
        uiclamp 1 1
        ui_tool_textinput tool_imagelist_filter_query $uit_filter_query_length [
            uit_label = "Search:"
        ]
        if $uit_allow_custom [
            ui_tool_textinput $arg1 $uit_filter_query_length [
                uit_label = "Custom:"
            ]
        ]

        uifill 0 $ui_toolview_base_elem_space

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                uigrid $uit_sel_cols $uit_sel_space $uit_sel_space [
                    looplist image $arg2 [
                        if (tool_imagelist_filter $image) [
                            ui_tool_imagelist_item $image (=s $image (uit_get $$arg1))
                            if $tool_imagelist_item_click [
                                tool_param_set $arg1 (uit_set $image) $uit_on_change
                                uit_on_select
                            ]
                        ]
                    ]
                ]
            ]
            uivscroll $uit_slider_size $uit_sel_area 1
            uialign 1
        ]
    ]
]
