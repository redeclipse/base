// 1:<vec> 2:<component> 3:<delta>
tool_vec_get_component = [
    ? $arg3 0 (at $arg1 $arg2)
]

// 1:<vec var> 2:<component> 3:<value> 4:<delta>
tool_vec_set_component = [
    listsplice (? $arg4 [0 0 0] $$arg1) $arg3 $arg2 1
]

tool_vec_picker_props = [
    [ p_var       [] ]
    [ p_on_change [] ]
    [ p_delta     0  ]
    [ p_allow_neg 0  ]
    [ p_immediate 1  ]
]

ui_tool_vec_picker = [
    @(props $tool_vec_picker_props (toolpanel_attr popup user_data))

    local _step _min
    _step = (pow 2 $gridpower)
    _min = 0

    if $p_allow_neg [
        _min = -99999999
    ]

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uistyle clampx

        uivlist 0 [
            ui_tool_numinput $p_var $_min 99999999 $_step [
                p_label     = "X"
                p_on_change = [@@p_on_change]
                p_get       = [tool_vec_get_component $arg1 0 @@p_delta]
                p_set       = [tool_vec_set_component @@p_var 0 $arg1 @@p_delta]
                p_width     = $ui_tool_button_width_l_unscaled
                p_delta     = @p_delta
                p_immediate = @p_immediate
                p_id        = 0
            ]
            ui_tool_numinput $p_var $_min 99999999 $_step [
                p_label     = "Y"
                p_on_change = [@@p_on_change]
                p_get       = [tool_vec_get_component $arg1 1 @@p_delta]
                p_set       = [tool_vec_set_component @@p_var 1 $arg1 @@p_delta]
                p_width     = $ui_tool_button_width_l_unscaled
                p_delta     = @p_delta
                p_immediate = @p_immediate
                p_id        = 1
            ]
            ui_tool_numinput $p_var $_min 99999999 $_step [
                p_label     = "Z"
                p_on_change = [@@p_on_change]
                p_get       = [tool_vec_get_component $arg1 2 @@p_delta]
                p_set       = [tool_vec_set_component @@p_var 2 $arg1 @@p_delta]
                p_width     = $ui_tool_button_width_l_unscaled
                p_delta     = @p_delta
                p_immediate = @p_immediate
                p_id        = 2
            ]

            uistyle clampx *
        ]
    ]
]

tool_vec_props = [
    [ p_disabled   0                                ]
    [ p_label      ""                               ]
    [ p_label_size $ui_tool_text_size_s_unscaled    ]
    [ p_val_size   $ui_tool_text_size_xs_unscaled   ]
    [ p_width      $ui_tool_button_width_s_unscaled ]
    [ p_on_change  []                               ]
    [ p_delta      0                                ]
    [ p_allow_neg  0                                ]
    [ p_get        [[result $arg1]]                 ]
    [ p_set        [[result $arg1]]                 ]
    [ p_immediate  1                                ]
    [ p_id         []                               ]
]

// 1:<var> 2:<props>
ui_tool_vec = [
    @(props $tool_vec_props arg2)

    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_val_size)
    @(tool_ui_scale p_width)

    local _value
    _value = [- - -]

    if $p_disabled [] [
        if $p_delta [
            _value = [0 0 0]
            p_get  = [result 0]
        ] [
            _value = (p_get $$arg1)
        ]
    ]

    uihlist $ui_tool_elem_space_l [
        uistyle clampx

        uitext $p_label $p_label_size

        @@(ui_tool_interactable [$p_width] 0 [
            tool_control_highlight

            uispace $ui_tool_elem_pad_l $ui_tool_elem_pad_s [
                uistyle clampx
                uigrid 2 $ui_tool_elem_space_s $ui_tool_elem_space_s [
                    uistyle clampx
                    uitext "^fAX:" $p_val_size; uitext (at $_value 0) $p_val_size; uialign- 1
                    uitext "^fAY:" $p_val_size; uitext (at $_value 1) $p_val_size; uialign- 1
                    uitext "^fAZ:" $p_val_size; uitext (at $_value 2) $p_val_size; uialign- 1
                ]
            ]

            if $p_disabled [] [
                uirelease [
                    toolpanel_open tool_vec_picker popup [
                        p_position  = (uicursorpos)
                        p_width     = (uiwidth 0.15)
                        p_user_data = [
                            p_on_change = [@@@p_on_change]
                            p_var       = @@arg1
                            p_delta     = @@p_delta
                            p_allow_neg = @@p_allow_neg
                            p_immediate = @@p_immediate
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        p_can_reset = 0
                        p_on_change = [@@p_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1
                    ] [
                        tool_param_paste @arg1 [
                            p_on_change = [@@@p_on_change]
                        ]
                    ]
                ]
            ]
        ])
    ]
]
