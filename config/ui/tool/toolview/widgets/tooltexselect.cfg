tool_texselect_menu_props = [
    [ p_var       [] ]
    [ p_on_change [] ]
    [ p_decal     0  ]
]

ui_tool_texselect_picker = [
    @(props $tool_texselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uistyle clampx
        ui_tool_texlist $tool_texselect_var [
            p_sel_area  = 0.3
            p_on_change = [@@p_on_change]
            p_decal     = @p_decal
        ]
    ]
]

tool_texselect_props = [
    [ p_noonline     0                              ]
    [ p_disabled     0                              ]
    [ p_can_reset    0                              ]
    [ p_reset_val    0                              ]
    [ p_sky_tex      1                              ]
    [ p_text_size    $ui_tool_text_size_xs_unscaled ]
    [ p_size         0.06                           ]
    [ p_picker_width [(uiwidth 0.2)]                ]
    [ p_align        -1                             ]
    [ p_on_change    []                             ]
    [ p_decal        0                              ]
    [ p_tip          ""                             ]
    [ p_tip_simple   ["Click to browse textures"]   ]
    [ p_tip_action   ""                             ]
]

// 1:<var> 2:<props>
# ui_tool_texselect = [
    @(props $tool_texselect_props arg2)

    @(tool_ui_scale p_text_size)
    @(tool_ui_scale p_size)

    p_disabled = (|| $p_disabled [&& $p_noonline (tool_onlinelock)])

    local _value _viewfunc
    _value    = (? $p_disabled -1 $$arg1)
    _viewfunc = (? $p_decal uidecalslotview uivslotview)

    if (= $_value 0) [
        _value = (? $p_sky_tex $$arg1 -1)
    ]

    uivlist 0 [
        #(ui_tool_interactable 0 0 [
            uispace $ui_tool_elem_space_m $ui_tool_elem_space_m [
                $_viewfunc $_value $p_size $p_size [
                    ui_tool_tip [
                        p_tip         = [@@p_tip]
                        p_tip_simple  = [@@p_tip_simple]
                        p_tip_action  = [@@p_tip_action]
                        p_tip_subtext = [@@(? $p_noonline (tool_nompedit_text) "")]
                    ]

                    if $p_disabled [] [
                        uirelease [
                            tool_texselect_var = $arg1

                            toolpanel_open tool_texselect_picker popup [
                                p_position  = (uicursorpos)
                                p_width     = @p_picker_width
                                p_user_data = [
                                    p_var       = @@arg1
                                    p_on_change = [@@@p_on_change]
                                    p_decal     = @@p_decal
                                ]
                            ]
                        ]

                        uialtrelease [
                            tool_param_menu $arg1 [
                                p_can_reset  = @p_can_reset
                                p_reset_val  = [@@p_reset_val]
                                p_on_change  = [@@p_on_change]
                                p_item_names = [
                                    "Edit slot"
                                ]
                                p_on_select  = [
                                    if @p_decal [
                                        tool_tex_editslot $@@arg1 1
                                    ] [
                                        tool_tex_editslot (getslottex (getvindex $@@arg1))
                                    ]
                                ]
                            ]
                        ]

                        uihover [
                            tool_rightclickable

                            tool_copy_handler [
                                tool_param_copy @arg1
                            ] [
                                tool_param_paste @arg1 [
                                    p_on_change = [@@@p_on_change]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ] [$p_disabled])

        ui_tool_texinfo $_value [
            p_disabled  = @p_disabled
            p_text_size = @p_text_size
            p_decal     = @p_decal
        ]

        uipropchild [uialign $p_align]
    ]
]
