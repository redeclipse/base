tool_texselect_var = []

tool_texselect_menu_props = [
    [ uit_on_change [] ]
    [ uit_decal 0 ]
]

ui_tool_texselect_picker = [
    @(tool_props $tool_texselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1
        ui_tool_texlist $tool_texselect_var [
            uit_sel_area = 0.3
            uit_on_change = [@@uit_on_change]
            uit_decal = @uit_decal
        ]
    ]
]

tool_texselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_sky_tex 1 ]
    [ uit_text_size $ui_toolview_tiny_text_size ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_align -1 ]
    [ uit_on_change [] ]
    [ uit_decal 0 ]
]

// 1:<var> 2:<props>
ui_tool_texselect = [
    @(tool_props $tool_texselect_props arg2)

    local value viewfunc
    value = (? $uit_disabled -1 $$arg1)
    viewfunc = (? $uit_decal uidecalslotview uivslotview)

    if (= $value 0) [
        value = (? $uit_sky_tex $$arg1 -1)
    ]

    uivlist 0 [
        $viewfunc $value $uit_size $uit_size [
            if (< $value 0) [
                uioutline (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_dark_accent_colour)
                uiclamp- 1 1 1 1
            ]

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip_simple = "Click to browse textures"
                ]

                uirelease [
                    tool_texselect_var = $arg1

                    toolpanel_open tool_texselect_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_user_data = [
                            uit_on_change = [@@@uit_on_change]
                            uit_decal = @@uit_decal
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change
                ]
            ]
        ]

        ui_tool_texinfo $value [
            uit_disabled = @uit_disabled
            uit_text_size = @uit_text_size
            uit_decal = @uit_decal
        ]

        uialign* $uit_align
    ]
]
