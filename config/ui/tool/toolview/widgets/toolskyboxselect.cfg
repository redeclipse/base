tool_skyboxselect_menu_props = [
    [ p_var       [] ]
    [ p_on_change [] ]
]

ui_tool_skyboxselect_picker = [
    @(tool_props $tool_skyboxselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_skyboxlist $p_var [
            p_sel_area = 0.5
            p_on_change = [@@p_on_change]
        ]
    ]
]

tool_skyboxselect_props = [
    [ p_disabled     0                              ]
    [ p_can_reset    0                              ]
    [ p_reset_val    0                              ]
    [ p_text_size    $ui_tool_text_size_xs_unscaled ]
    [ p_size         0.06                           ]
    [ p_picker_width [(uiwidth 0.3)]                ]
    [ p_align        1                              ]
    [ p_on_change    []                             ]
]

// 1:<var> 2:<props>
ui_tool_skyboxselect = [
    @(tool_props $tool_skyboxselect_props arg2)

    @(tool_ui_scale p_text_size)
    @(tool_ui_scale p_size)
    @(tool_ui_scale p_picker_width)

    local _value
    _value = (? $p_disabled "" $$arg1)

    uivlist 0 [
        uicolour 0 0 0 [
            uiimage (concatword "<thumbnail:256/256>" $_value "_ft") 0xffffff 0 $p_size $p_size

            if $p_disabled [] [
                ui_tool_tip [
                    p_tip_simple = "Click to browse cubemaps"
                ]

                uirelease [
                    toolpanel_open tool_skyboxselect_picker popup [
                        p_position  = (uicursorpos)
                        p_width     = @p_picker_width
                        p_user_data = [
                            p_var       = @@arg1
                            p_on_change = [@@@p_on_change]
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        p_can_reset = @@p_can_reset
                        p_reset_val = [@@@p_reset_val]
                        p_on_change = [@@@p_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1
                    ] [
                        tool_param_paste @arg1 [
                            p_on_change = [@@@p_on_change]
                        ]
                    ]
                ]
            ]
        ]

        uitext (? $_value $_value "No cubemap selected") $p_text_size

        uialign* $p_align
    ]
]
