tool_skyboxselect_var = []

tool_skyboxselect_menu_props = [
    [ uit_on_change [] ]
]

ui_tool_skyboxselect_picker = [
    @(tool_props $tool_skyboxselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1
        ui_tool_skyboxlist $tool_skyboxselect_var [
            uit_sel_area = 0.5
            uit_on_change = [@@uit_on_change]
        ]
    ]
]

tool_skyboxselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_text_size $ui_toolview_tiny_text_size ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.3)] ]
    [ uit_align 1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<props>
ui_tool_skyboxselect = [
    @(tool_props $tool_skyboxselect_props arg2)

    local value
    value = (? $uit_disabled "" $$arg1)

    uivlist 0 [
        uicolour 0 0 0 [
            uiimage (concatword "<thumbnail:256/256>" $value "_ft") 0xffffff 0 $uit_size $uit_size

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip_simple = "Click to browse cubemaps"
                ]

                uirelease [
                    tool_skyboxselect_var = $arg1

                    toolpanel_open tool_skyboxselect_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_user_data = [
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change
                ]
            ]
        ]

        uitext (? $value $value "No cubemap selected") $uit_text_size

        uialign* $uit_align
    ]
]
