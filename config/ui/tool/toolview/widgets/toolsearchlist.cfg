tool_searchlist_filter_query = ""

// 1:<item>
tool_searchlist_filter = [
    result (|| [! $tool_searchlist_filter_query] [!= (strcasestr $arg1 $tool_searchlist_filter_query) -1])
]

// 1:<item> 2:<highlight> 3:<item index> 4:<var>
ui_tool_searchlist_item = [
    @(ui_tool_interactable 0 0 [
        uistyle clampx
        uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
            uistyle clampx
            uicolourtext $arg1 (? $arg2 $ui_tool_sel_accent_colour 0xffffff) $p_sel_size
            uialign- -1
        ]

        uirelease [
            tool_param_set $arg4 $arg3 $p_on_change
            p_on_select
        ]
    ])
]

tool_searchlist_props = [
    [ p_sel_size            $ui_tool_text_size_s_unscaled ]
    [ p_sel_area            0.6                           ]
    [ p_filter_query_length 24                            ]
    [ p_width               0                             ]
    [ p_slider_size         $ui_toolpanel_slider_size     ]
    [ p_on_change           []                            ]
    [ p_on_select           []                            ]
    [ p_num_items_func      []                            ]
    [ p_item_func           []                            ]
]

// 1:<select var> 2:<props>
ui_tool_searchlist = [
    @(props $tool_searchlist_props arg2)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_slider_size)

    uivlist 0 [
        local _item_num _item
        _item_num = (p_num_items_func)

        ui_tool_textinput tool_searchlist_filter_query $p_filter_query_length [
            p_label = "Search:"
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiscroll 0 $p_sel_area [
                uistyle lefttop
                uivlist 0 [
                    uifill (-f $p_width $p_slider_size)
                    loop i $_item_num [
                        _item = (p_item_func $i)

                        if (tool_searchlist_filter $_item) [
                            ui_tool_searchlist_item $_item (= $i $$arg1) $i $arg1
                        ]
                    ]
                ]
            ]
            uivscroll $p_slider_size $p_sel_area 1
            uialign 1
        ]
    ]
]
