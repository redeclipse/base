tool_cursor_icon = ""

tool_set_cursor_icon = [
    if $tool_uihover [] [
        tool_cursor_icon = $arg1
    ]
]

tool_last_id = -1

tool_get_id = [
    tool_last_id = (+ $tool_last_id 1)
    result $tool_last_id
]

TOOL_COPY = 0
TOOL_PASTE = 1

tool_uihover = 0
tool_copy_has_handler = 0
tool_copy_handler_op = -1
tool_can_rightclick = 0

tool_rightclickable = [
    tool_can_rightclick = 1
]

tool_copy_do_op = [
    if $tool_copy_has_handler [
        tool_copy_handler_op = $arg1
    ] [
        tool_copy_handler_op = -1
    ]

    result (! $tool_uihover)
]

// 1:<copy code> 2:<paste code>
tool_copy_handler = [
    tool_copy_has_handler = 1

    case $tool_copy_handler_op $TOOL_COPY [
        arg1
    ] $TOOL_PASTE [
        arg2
    ]

    tool_copy_handler_op = -1
]

ui_tool_allowinput = [
    result [
        if $hasinput [
            uihover [
                uiallowinput 1
                tool_uihover = 1
                tool_cursor_icon = ""
            ]
        ]
    ]
]

exec "config/ui/tool/toolview/toolbar.cfg"
exec "config/ui/tool/toolview/toolpanel.cfg"
exec "config/ui/tool/toolview/toolwidgets.cfg"
exec "config/ui/tool/toolview/toolinfo.cfg"

toolview_autoscroll = 0

// 1:<time>
tool_autoscroll = [
    local _period _tension
    _period = 5
    _tension = 2

    clampf (-f (*f (absf (-f (*f (modf (divf $arg1 $_period) 1) 2) 1)) (+f $_tension 1)) (*f $_tension 0.5)) 0 1
]

rl = [
    clearconsole
    clearlog
    exec config/tool.cfg
    exec config/ui/tool.cfg
]

toolview_pos_undercursor = [
    uiposition (+f (at (uicursorpos) 0) 0.015) (+f (at (uicursorpos) 1) 0.015)
]

toolview_undercursor = []

ui_hud_toolview = [
    tool_can_rightclick = 0
    tool_copy_has_handler = 0
    tool_uihover = 0
    tool_cursor_icon = ""
    toolview_undercursor = []
    toolview_autoscroll = (tool_autoscroll (divf (getmillis) 1000))

    tool_proc

    if $debugruncodestack [
        uitext $runcodestack
    ]

    uifont $textfonttool [
        uifill $uiaspect 1
        @@ui_toolbar
        @@ui_toolpanels
        @@ui_toolinfo

        if $tool_can_rightclick [
            tool_cursor_icon = (concatword $textkeyprefix "mouse2")
        ]

        uihlist $ui_tool_elem_space_m [
            if (!=s $tool_cursor_icon "") [
                uiaspectimage $tool_cursor_icon 0xffffff 0 0.02 0.02
                uialign- -1 -1
            ]

            if (!=s $toolview_undercursor []) [
                uiskin 0 0 $ui_tool_base_colour 0 0 [
                    uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                        $toolview_undercursor
                    ]
                ]
            ]

            @toolview_pos_undercursor
        ]
    ]
]

toolview_init = [
    toolbar_init
]
