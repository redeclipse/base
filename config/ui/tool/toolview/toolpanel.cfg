ui_toolpanel_close_button_width = (uiwidth 0.01)
ui_toolpanel_pad_x = 0.01
ui_toolpanel_pad_y = 0.03
ui_toolpanel_width = (uiwidth 0.2)
ui_toolpanel_content_width = (-f $ui_toolpanel_width (*f 2 $ui_toolpanel_pad_x))
ui_toolpanel_menu_width = (uiwidth 0.08)
ui_toolpanel_offset = (+f $ui_toolbar_height 0.05)
ui_toolpanel_elem_space = 0.005
ui_toolpanel_close_button_base_colour = 0xFFFF6666
ui_toolpanel_close_button_highlight_colour = 0xFFFF9999
ui_toolpanel_close_button_select_colour = 0xFFFFCCCC
ui_toolpanel_slider_size = 0.01
ui_toolpanel_slider_thin_size = 0.01

ui_toolpanel_bar_text_size = $ui_toolview_base_text_size
ui_toolpanel_text_size = $ui_toolview_small_text_size

ui_toolpanel_icon_size = 0.02
ui_toolpanel_icon_margin = 0.01

toolpanel_show_left = 0
toolpanel_show_mid = 0
toolpanel_show_right = 0
toolpanel_show_menu = 0

toolpanel_init_left = 0
toolpanel_init_mid = 0
toolpanel_init_right = 0
toolpanel_init_menu = 0

toolpanel_title_left = []
toolpanel_title_mid = []
toolpanel_title_right = []
toolpanel_title_menu = []

toolpanel_body_left = []
toolpanel_body_mid = []
toolpanel_body_right = []
toolpanel_body_menu = []

toolpanel_name_left = []
toolpanel_name_mid = []
toolpanel_name_right = []
toolpanel_name_menu = []

toolpanel_props_left = []
toolpanel_props_mid = []
toolpanel_props_right = []
toolpanel_props_menu = []

toolpanel_init = 0

toolpanel_nav_handler = []

toolpanel_menu_origin_x = 0
toolpanel_menu_origin_y = 0
toolpanel_menu_width = 0

tool_nav = [
    if $toolpanel_nav_handler [
        $toolpanel_nav_handler $arg1 $arg2
    ]
]

tool_cancel = [
]

tool_confirm = [
    if $toolpanel_nav_handler [
        $toolpanel_nav_handler 0 0 1
    ]
]

// 1:<left/mid/right>
toolpanel_getpanel_name = [
    result $[toolpanel_name_@arg1]
]

// 1:<name>
toolpanel_getpanel = [
    if (strcmp $arg1 $toolpanel_name_left) [ result left ] [
        if (strcmp $arg1 $toolpanel_name_mid) [ result mid ] [
            if (strcmp $arg1 $toolpanel_name_right) [ result right ]
        ]
    ]
]

// 1:<name> 2:<left/mid/right>
toolpanel_setpanel = [
    if $arg2 [
        [toolpanel_name_@arg2] = $arg1
    ]
]

// 1:<name> 2:<title> 3:<left/mid/right/menu> 4:<props>
toolpanel_show = [
    local uit_nav_enable uit_position uit_width

    uit_nav_enable = 0

    uit_position = []

    uit_width = $ui_toolpanel_menu_width

    doargs $arg4

    [toolpanel_show_@arg3] = 1
    [toolpanel_init_@arg3] = 1
    [toolpanel_title_@arg3] = $arg2
    [toolpanel_props_@arg3] = $arg4
    [toolpanel_body_@arg3] = $[ui_@arg1]
    toolpanel_setpanel $arg1 $arg3

    if (=s $arg3 menu) [
        toolpanel_menu_origin_x = (at $uit_position 0)
        toolpanel_menu_origin_y = (at $uit_position 1)
        toolpanel_menu_width = $uit_width

        local origin_error_x menu_right

        contect_right = (+f $uit_width $toolpanel_menu_origin_x)
        origin_error_x = (-f $contect_right (uiaspect))

        if (>f $origin_error_x 0) [
            toolpanel_menu_origin_x = (-f $toolpanel_menu_origin_x $origin_error_x)
        ]
    ]

    if (&& $uit_nav_enable (=s $arg3 mid)) [
        toolpanel_nav_handler = (concatword $arg1 _nav)
    ]

    if (=s $arg3 menu) [
        showui "ui_toolpanel_menu"
    ]
]

// 1:<left/mid/right/menu>
toolpanel_hide_panel = [
    [toolpanel_show_@arg1] = 0
    [toolpanel_name_@arg1] = []

    if (=s $arg1 mid) [
        toolpanel_nav_handler = []
    ]

    if (=s $arg1 menu) [
        hideui "ui_toolpanel_menu"
    ]
]

// 1:<name>
toolpanel_hide = [
    local panel
    panel = (toolpanel_getpanel $arg1)
    if $panel [
        toolpanel_hide_panel $panel
    ]
]

// 1:<name>
toolpanel_isopen = [
    local panel
    panel = (toolpanel_getpanel $arg1)

    ? $panel 1 0
]

// 1:<name> 2:<title> 3:<left/mid/right/menu> 4:<props>
toolpanel_toggle = [
    local panel
    panel = (toolpanel_getpanel $arg1)

    if $panel [
        toolpanel_hide_panel $panel
    ] [
        toolpanel_show $arg1 $arg2 $arg3 $arg4
    ]
]

// 1:<panel>
ui_toolpanel_bar = [
    result [
        local title
        title = $toolpanel_title_@arg1

        uiskin 0 0 $ui_toolview_dark_colour 0 0 [
            uiclamp 1 1
            uitext $title $ui_toolpanel_bar_text_size [ uialign 0 0 ]
            uiskin $ui_toolpanel_close_button_width 0 $ui_toolpanel_close_button_base_colour $ui_toolpanel_close_button_highlight_colour $ui_toolpanel_close_button_select_colour [
                uialign 1 0
                uitext "X" $ui_toolpanel_bar_text_size
                uirelease [ toolpanel_hide_panel @@@@arg1 ]
            ] 1
        ]
    ]
]

// 1:<panel>
ui_toolpanel = [
    local panel_bar
    panel_bar = (ui_toolpanel_bar $arg1)

    result [
        doargs $toolpanel_props_@arg1

        uiskin 0 0 $ui_toolview_base_colour 0 0 [
            @@(ui_tool_allowinput)

            uiclamp 1 1
            uialign -1 -1
            @@panel_bar
            uispace $ui_toolpanel_pad_x $ui_toolpanel_pad_y [
                uiclamp 1 1
                toolpanel_init = $toolpanel_init_@@@arg1
                toolpanel_curpanel = @@@arg1
                toolpanel_body_@@@arg1
                toolpanel_init_@@@arg1 = 0
            ]
            uialign* -1 -1
        ]
    ]
]

// 1:<panel> 2:<align>
toolpanel_panel_template = [
    local contents
    contents = (ui_toolpanel $arg1)

    [ui_toolpanel_@arg1] = [
        uipad 0 0 $ui_toolpanel_offset 0 [
            uialign @@arg2 -1
            uiclamp 0 0 1 1
            uifill $ui_toolpanel_width
            @@contents
        ]
    ]
]

toolpanel_panel_template left -1
toolpanel_panel_template mid 0
toolpanel_panel_template right 1

ui_toolpanel_menu = [
    local max_h
    max_h = (-f 1 $toolpanel_menu_origin_y)

    @(ui_tool_allowinput)

    ui_tool_vscrollarea [
        uihover [
            menu_click_hide = 0
            uiallowinput  1
        ]

        uiskin 0 0 $ui_toolview_dark_colour 0 0 toolpanel_body_menu
        uiclamp- 1 1
    ] [
        uit_width = $toolpanel_menu_width
        uit_height = $max_h
    ]

    uiposition- $toolpanel_menu_origin_x $toolpanel_menu_origin_y
]

newui "ui_toolpanel_menu" [
    ui_menu_current = "ui_toolpanel_menu"
    ui_menu_close_ui_toolpanel_menu = 0

    local menu_click_hide
    menu_click_hide = 1

    uisetpos 0 0

    uitarget (uiaspect) 1 [
        ui_toolpanel_menu

        if $menu_click_hide [
            uirelease [
                ui_menu_close_ui_toolpanel_menu = 1
                toolpanel_hide_panel menu
            ]

            uialtrelease [
                ui_menu_close_ui_toolpanel_menu = 1
                toolpanel_hide_panel menu
            ]
        ]

        uiescpress [
            ui_menu_close_ui_toolpanel_menu = 1
            toolpanel_hide_panel menu
        ]
    ]

    ui_menu_current = ""
]

ui_toolpanels = [
    if $toolpanel_show_left    [ @@ui_toolpanel_left  ]
    if $toolpanel_show_mid     [ @@ui_toolpanel_mid   ]
    if $toolpanel_show_right   [ @@ui_toolpanel_right ]
]
