ui_toolbar_height = 0.05
ui_toolbar_map_button_width = (uiwidth 0.2)
ui_toolbar_map_button_space = $ui_toolview_small_elem_space
ui_toolbar_map_button_text_scale = $ui_toolview_base_text_size
ui_toolbar_button_space = $ui_toolview_base_elem_space
ui_toolbar_button_size = 0.035
ui_toolbar_icon_size = (*f $ui_toolview_base_icon_size 0.9)
ui_toolbar_space = (*f $ui_toolbar_map_button_space 2)

// Dynamically calculated
ui_toolbar_tools_width = 0

toolbar_actions_left = []
toolbar_actions_right = []

ui_toolbar_left = [
    uifill $ui_toolbar_tools_width 0 [
        uiclamp 1 1 1 1
        uihlist $ui_toolbar_button_space [
            uialign -1 0
            uiclamp 0 0 1 1
            uifill $ui_toolbar_button_space 0

            looplist action $toolbar_actions_left [
                ui_tool_button [
                    uit_icon = @(tool_action_prop [$action] icon)
                    uit_icon_size = @ui_toolbar_icon_size
                    uit_icon_align = 0
                    uit_width = @ui_toolbar_button_size
                    uit_height = @ui_toolbar_button_size
                    uit_tip_action = $action
                    uit_on_click = [
                        tool_do_action $action
                    ]
                ]
            ]

            uialign* 0 -1
        ]
    ]
]

ui_toolbar_right = [
    uifill $ui_toolbar_tools_width 0 [
        uiclamp 1 1 1 1
        uihlist $ui_toolbar_button_space [
            uialign 1 0
            uiclamp 0 0 1 1

            looplist action $toolbar_actions_right [
                ui_tool_button [
                    uit_icon = @(tool_action_prop [$action] icon)
                    uit_icon_size = @ui_toolbar_icon_size
                    uit_icon_align = 0
                    uit_width = @ui_toolbar_button_size
                    uit_height = @ui_toolbar_button_size
                    uit_tip_action = $action
                    uit_on_click = [
                        tool_do_action $action
                    ]
                ]
            ]

            uialign* 0 -1
            uifill $ui_toolbar_button_space 0
        ]
    ]
]

ui_toolbar_map = [
    uifill $ui_toolbar_map_button_width $ui_toolbar_height [
        @(ui_tool_interactable 0 0 [
            uiclamp 1 1 1 1
            uihlist $ui_toolbar_map_button_space [
                uiimage "textures/icons/editing" $ui_toolview_accent_colour 0 $ui_toolbar_icon_size $ui_toolbar_icon_size
                uitext $mapname $ui_toolbar_map_button_text_scale
                uifill $ui_toolview_small_elem_space
                uicolourtext (concatword "r" (maprevision)) $ui_toolview_dark_accent_colour $ui_toolview_tiny_text_size
            ]

            uirelease [
                toolpanel_show tool_map_settings "Map settings" mid
            ]
        ])
    ]
]

ui_toolbar_controls_settings_contents = []

ui_toolbar_controls_wireframe = [
    tool_edit_outline_sync

    uivlist $ui_toolview_base_elem_space [
        uitext "Wireframe settings"

        uifill 0 $ui_toolview_small_elem_space
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolview_small_elem_space

        uihlist $ui_toolview_base_elem_space [
            uivlist $ui_toolview_small_elem_space [
                ui_tool_dropdown tool_edit_outline [
                    "None"
                    "Wireframe"
                    "Outline"
                ] [
                    uit_on_change = [
                        tool_edit_outline_set
                    ]
                ]

                ui_tool_checkbox dtoutline [
                    uit_label = "Outline depth test"
                ]
            ]

            ui_tool_colour tool_edit_outline_colour [
                uit_label_size = $ui_toolview_tiny_text_size
                uit_on_change = [
                    tool_edit_outline_set
                ]
            ]
        ]

        ui_tool_button [
            uit_label = "Close"
            uit_width = 0.2
            uit_on_click = [
                ui_toolbar_controls_settings_contents = []
            ]
        ]
    ]
]

ui_toolbar_controls_settings = [
    if $ui_toolbar_controls_settings_contents [
        uiskin 0 0 $ui_toolview_base_colour 0 0 [
            uispace $ui_toolview_base_elem_space $ui_toolview_base_elem_space [
                ui_toolbar_controls_settings_contents
            ]
        ]

        @@(ui_tool_allowinput)
    ]
]

ui_toolbar_controls = [
    local space_bottom
    space_bottom = (+f $ui_toolview_base_elem_space 0.02)

    uispace $ui_toolview_base_elem_space $space_bottom [
        uihlist $ui_toolview_small_elem_space [
            ui_tool_button [
                uit_label = "WF"
                uit_label_size = $ui_toolview_tiny_text_size
                uit_tip_simple = "Outline/wireframe"
                uit_colour = @(? (|| $outline $wireframe) $ui_toolview_accent_colour $ui_toolview_dark_colour)
                uit_width = 0.02
                uit_height = 0.02
                uit_on_click = [
                    ui_toolbar_controls_settings_contents = ui_toolbar_controls_wireframe
                ]
            ]

            ui_tool_compactswitch fullbright [
                uit_label = "FB"
                uit_tip_simple = "Fullbright"
                uit_width = 0.02
                uit_height = 0.02
            ]

            ui_tool_compactswitch blankgeom [
                uit_label = "BG"
                uit_tip_simple = "Blank geometry"
                uit_width = 0.02
                uit_height = 0.02
            ]
        ]
    ]
    uialign- -1 1
]

ui_toolbar_sel = [
    uiskin 0 0 $ui_toolview_base_colour 0 0 [
        uispace $ui_padsmall $ui_padsmall [
            uivlist $ui_toolpanel_elem_space [
                uitext "Selection" $ui_toolview_small_text_size

                if (< ((getenginestat 41)) 16) [
                    uitext (format "^fAGrid: ^fw%1 (%2cm)" (getenginestat 41) (divf (getenginestat 41) 0.16)) $ui_toolview_tiny_text_size
                ] [
                    uitext (format "^fAGrid: ^fw%1 (%2m)" (getenginestat 41) (divf (getenginestat 41) 16)) $ui_toolview_tiny_text_size
                ]

                uitext (format "^fASide: ^fw%1" (getenginestat 40)) $ui_toolview_tiny_text_size
                uitext (format "^fASize: ^fw%1,%2,%3" (getenginestat 30) (getenginestat 31) (getenginestat 32)) $ui_toolview_tiny_text_size
                uitext (format "^fAPos: ^fw%1,%2,%3" (getenginestat 27) (getenginestat 28) (getenginestat 29)) $ui_toolview_tiny_text_size

                uialign* -1
            ]
        ]
    ]
]

ui_toolbar_bottom = [
    local space_bottom
    space_bottom = (+f $ui_toolview_base_elem_space 0.05)

    uispace $ui_toolview_base_elem_space $space_bottom [
        uihlist $ui_toolview_base_elem_space [
            @@@ui_toolbar_controls_settings
            @@@ui_toolbar_sel
            uialign* -1 1
        ]
    ]
    uialign- -1 1
]

toolbar_calc_layout = [
    ui_toolbar_tools_width = (-f (uiaspect) $ui_toolbar_map_button_width)
    ui_toolbar_tools_width = (*f $ui_toolbar_tools_width 0.5)
    ui_toolbar_tools_width = (-f $ui_toolbar_tools_width $ui_toolbar_button_space)
]

ui_toolbar = [
    toolbar_calc_layout

    uitarget 0 $ui_toolbar_height [
        uiclamp 1 1
        uialign 0 -1
        uihlist $ui_toolbar_space [
            @@@ui_toolbar_left
            @@@ui_toolbar_map
            @@@ui_toolbar_right
        ]

        @@(ui_tool_allowinput)
    ]

    @ui_toolbar_controls
    @ui_toolbar_bottom
]

toolbar_init = [
    // Left toolbar
    append toolbar_actions_left ta_menu
    append toolbar_actions_left ta_edit_settings
    append toolbar_actions_left ta_save
    append toolbar_actions_left ta_save_as
    append toolbar_actions_left ta_undo
    append toolbar_actions_left ta_redo
    append toolbar_actions_left ta_copy
    append toolbar_actions_left ta_paste
    append toolbar_actions_left ta_search

    // Right toolbar
    append toolbar_actions_right ta_textures
    append toolbar_actions_right ta_ents
    append toolbar_actions_right ta_env
]
