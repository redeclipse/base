ui_toolinfo_action_offset = 0.025
ui_toolinfo_action_height = 0.1

ui_toolinfo_action_image_size = 0.06

tool_info_action_fadeout = 1000

tool_info_text = ""
tool_info_right_content = []
tool_info_time = 0

defvarp toolinfoactionsound 0 1 1
defvarp toolinfoactionlength 1000 5000 60000

defvarp toolinfoextendedstatus 0 0 1

// 1:<text> 2:<content>
tool_info_show = [
    tool_info_text = $arg1
    tool_info_right_content = $arg2
    tool_info_time = (getmillis)

    if $toolinfoactionsound [
        soundbyname S_ACTION
    ]
]

// 1:<text> 2:<action>
tool_info_show_action = [
    local icon
    icon = @(tool_action_prop [$arg2] icon)

    tool_info_show $arg1 [
        uiimage @icon $ui_toolview_accent_colour 0 $ui_toolinfo_action_image_size $ui_toolinfo_action_image_size
    ]
]

ui_toolinfo_action = [
    uipad 0 $ui_toolinfo_action_offset 0 $ui_toolinfo_action_offset [
        uiskin 0 $ui_toolinfo_action_height $ui_toolview_base_colour 0 0 [
            uispace $ui_toolview_large_elem_pad $ui_toolview_base_elem_pad [
                uiclamp 1 1 1 1
                uihlist $ui_toolview_large_elem_space [
                    uiclamp 1 1
                    uitext $tool_info_text
                    uialign- -1
                    doargs $tool_info_right_content
                    uialign 1
                ]
            ]

            local blend fadeout_time
            fadeout_time = (+f $tool_info_time (-f $toolinfoactionlength $tool_info_action_fadeout))
            blend = 1

            blend = (divf (-f (getmillis) $fadeout_time) $tool_info_action_fadeout)

            uichangeblends (clampf (-f 1 $blend) 0 1)
        ]
    ]
    uialign- 1 1
]

ui_toolinfo_status = [
    uiskin (uiaspect) 0 $ui_toolview_base_colour 0 0 [
        uispace $ui_padsmall $ui_padsmall [
            uiclamp 1 1
            uihlist $ui_toolview_base_elem_space [
                uitext (format "^fApos:^fw%1,%2,%3 ^fAyaw:^fw%4 ^fApitch:^fw%5" (getenginestat 21) (getenginestat 22) (getenginestat 23) (getenginestat 24) (getenginestat 25)) $ui_toolview_tiny_text_size
                uitext (format "^fAents:^fw%1 ^fAwaypoints:^fw%2" (getenginestat 14) (getenginestat 16)) $ui_toolview_tiny_text_size
                uitext (format "^fAworld tris:^fw%1k(%2%%) ^fAworld verts:^fw%3k(%4%%) ^fAevt:^fw%5k ^fAeva:^fw%6k" (getenginestat 0) (getenginestat 1) (getenginestat 2) (getenginestat 3) (getenginestat 4) (getenginestat 5)) $ui_toolview_tiny_text_size
                if $toolinfoextendedstatus [
                    uitext (format "^fAond:^fw%1 ^fAva:^fw%2 ^fAgl:^fw%3(%4) ^fAoq:^fw%5" (getenginestat 6) (getenginestat 7) (getenginestat 8) (getenginestat 9) (getenginestat 10)) $ui_toolview_tiny_text_size
                ]
            ]
            uialign- -1
            uihlist $ui_toolview_base_elem_space [
                uitext (concat (getenginestat 11) "fps") $ui_toolview_tiny_text_size
                uitext (concatword "Max: " (div 1000 (- (getenginestat 11) (getenginestat 13)) ) "ms" ) $ui_toolview_tiny_text_size
            ]
            uialign- 1
        ]
    ]
    uialign- -1 1
]

ui_toolinfo = [
    if (< (- (getmillis) $tool_info_time) $toolinfoactionlength) [
        @@ui_toolinfo_action
    ]

    @ui_toolinfo_status
]
