toolinfo_calc_dimensions = [
    ui_toolinfo_action_offset = (*f 0.025 $uitoolscale)
    ui_toolinfo_action_height = (*f 0.05 $uitoolscale)
    ui_toolinfo_action_image_size = (*f 0.06 $uitoolscale)
    ui_toolinfo_status_height = (*f 0.021 $uitoolscale)
]
tool_calc_ui_dimensions_handler toolinfo_calc_dimensions
toolinfo_calc_dimensions

tool_info_text = ""
tool_info_subtext = ""
tool_info_right_content = []
tool_info_time = 0

defvarp toolinfoactionsound 0 1 1
defvarp toolinfoactionlength 1000 5000 60000

defvarp toolinfoextendedstatus 0 0 1

tool_info_show_props = [
    [ p_subtext "" ]
    [ p_children [] ]
    [ p_sound S_ACTION ]
]

// 1:<text> 2:<props>
tool_info_show = [
    @(props $tool_info_show_props arg2)

    tool_info_text = $arg1
    tool_info_subtext = $p_subtext
    tool_info_right_content = $p_children
    tool_info_time = (getmillis)

    if $toolinfoactionsound [
        soundbyname $p_sound
    ]
]

// 1:<text> 2:<action> 3:<props>
tool_info_show_action = [
    @(props $tool_info_show_props arg3)

    local _icon
    _icon = (at $$arg2 $TA_PROP_ICON)

    tool_info_show $arg1 [
        p_subtext = [@@p_subtext]
        p_sound = @p_sound
        p_children = [
            uiimage @_icon $ui_tool_accent_colour 0 $ui_toolinfo_action_image_size $ui_toolinfo_action_image_size
            @@p_children
        ]
    ]
]

ui_toolinfo_action = [
    local _colour
    _colour = (ui_tool_flash $ui_tool_dark_colour $tool_info_time)

    uipad 0 $ui_toolinfo_action_offset 0 $ui_toolinfo_action_offset [
        uiskin 0 $ui_toolinfo_action_height $_colour 0 0 [
            uispace $ui_tool_elem_pad_l $ui_tool_elem_pad_m [
                uiclamp 1 1 1 1
                uihlist $ui_tool_elem_space_xl [
                    uiclamp 1 1
                    uivlist 0 [
                        uiclamp 1 1
                        uitext $tool_info_text $ui_tool_text_size_m
                        if (!=s $tool_info_subtext) [
                            uicolourtext $tool_info_subtext $ui_tool_dark_accent_colour $ui_tool_text_size_s
                        ]
                        uialign* -1
                    ]
                    uialign- -1
                    doargs $tool_info_right_content
                    uialign 1
                ]
            ]

            ui_tool_fadeout $tool_info_time $toolinfoactionlength
        ]
    ]
    uialign- 1 1
]

ui_toolinfo_status = [
    local _sel_ents _sel_ents_text
    _sel_ents = (getenginestat 15)
    _sel_ents_text = ""

    if (> $_sel_ents 0) [
        _sel_ents_text = (concatword "(" $_sel_ents ")")
    ]

    uiskin $uiaspect $ui_toolinfo_status_height $ui_tool_base_colour 0 0 [
        uispace $ui_padsmall $ui_padsmall [
            uiclamp 1 1
            uihlist $ui_tool_elem_space_l [
                uitext (format "^fApos:^fw%1,%2,%3 ^fAyaw:^fw%4 ^fApitch:^fw%5" (getenginestat 21) (getenginestat 22) (getenginestat 23) (getenginestat 24) (getenginestat 25)) $ui_tool_text_size_xs
                uitext (format "^fAents:^fw%1%2 ^fAwaypoints:^fw%3" (getenginestat 14) $_sel_ents_text (getenginestat 16)) $ui_tool_text_size_xs
                uitext (format "^fAmap tris:^fw%1k(%2%%) ^fAmap verts:^fw%3k(%4%%) ^fAevt:^fw%5k ^fAeva:^fw%6k" (getenginestat 0) (getenginestat 1) (getenginestat 2) (getenginestat 3) (getenginestat 4) (getenginestat 5)) $ui_tool_text_size_xs
                uitext (format "^fAparticles:^fw%1" $numenvparts) $ui_tool_text_size_xs
                if $toolinfoextendedstatus [
                    uitext (format "^fAond:^fw%1 ^fAva:^fw%2 ^fAgl:^fw%3(%4) ^fAoq:^fw%5" (getenginestat 6) (getenginestat 7) (getenginestat 8) (getenginestat 9) (getenginestat 10)) $ui_tool_text_size_xs
                ]
            ]
            uialign- -1
            uihlist $ui_tool_elem_space_l [
                uitext (concat (getenginestat 11) "fps") $ui_tool_text_size_xs
                uitext (concatword "Max: " (div 1000 (- (getenginestat 11) (getenginestat 13)) ) "ms" ) $ui_tool_text_size_xs
            ]
            uialign- 1
        ]
    ]
    uialign- -1 1
]

ui_toolinfo = [
    if (< (- (getmillis) $tool_info_time) $toolinfoactionlength) [
        @@ui_toolinfo_action
    ]

    @ui_toolinfo_status
]
