// 1:<type> 2:<attr name list> 3:<subtype>
ui_tool_ent_attr_props = [
    local attr_list nodelta disable
    attr_list = []
    nodelta = 0

    looplist attr_name $arg2 [
        append attr_list (tool_ent_attr_idx_map $arg1 $attr_name $arg3)
        if (& (tool_ent_aflags $arg1 $attr_name) $T_ENT_NODELTA) [
            nodelta = 1
        ]
    ]

    disable = ""
    if $nodelta [
        disable = "uit_disabled = (tool_ent_delta_edit)"
    ]

    result [
        uit_label_size = $ui_toolview_tiny_text_size
        uit_on_change = [
            tool_ent_attr_change @@arg1 [@@@attr_list]
        ]
        uit_can_reset = 1
        uit_reset_val = 0
        @disable
        uit_has_menu = (! (tool_ent_delta_edit))
        uit_delta = (tool_ent_delta_edit)
    ]
]

// 1:<text> 2:<fields>
ui_tool_ent_param_group = [
    uihlist 0 [
        uiclamp 1 1
        uitext $arg1 $ui_toolview_small_text_size
        uivlist 0 [
            @arg2
        ]
    ]
    uifill 0 $ui_toolview_small_elem_space
    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uifill 0 $ui_toolview_small_elem_space
]

// 1:<type> 2:<subtype> 3:<suffix>
ui_tool_ent_palette_group = [
    result [
        ui_tool_ent_param_group [Palette @@arg3] [
            ui_tool_dropdown @@(tool_ent_attr $arg1 [palette@arg3] $arg2) [
                "Pulse"
                "Team"
                "Weapon"
            ] [
                @@@(ui_tool_ent_attr_props $arg1 [palette@arg3] $arg2)
                uit_label = "Type"
            ]

            ui_tool_dropdown tool_ent_attr_palindex@@[arg3]_val (at $tool_palette_ids $@@(tool_ent_attr $arg1 [palette@arg3] $arg2)) [
                @@@(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
                uit_label = "Id"
            ]

            uifill 0 $ui_toolview_mid_elem_space

            ui_tool_checkbox tool_ent_attr_palindex@@[arg3]_enforce [
                uit_label = "Enforce"
                uit_label_size = $ui_toolview_tiny_text_size
                @@@(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
            ]
            uialign- 1
        ]
    ]
]

// 1:<type> 2:<fx>
ui_tool_ent_variant_group = [
    local fxlevel_group fxlevel_var

    if $arg2 [
        fxlevel_group = [
            uihlist 0 [
                uitext "FX Level:" $ui_toolview_tiny_text_size
                uifill $ui_toolview_base_elem_space
                loop i 3 [
                    fxlevel_var = [tool_ent_attr_fxlevel_@(+ $i 1)]
                    ui_tool_compactswitch $fxlevel_var [
                        @@@@(ui_tool_ent_attr_props $arg1 fxlevel $arg2)
                        uit_label = @(+ $i 1)
                        uit_width = 0.02
                        uit_height = 0.02
                    ]
                ]
            ]
            uialign- 1
        ]
    ]

    result [
        ui_tool_ent_param_group "Variant" [
            ui_tool_dropdown @@(tool_ent_attr $arg1 variant $arg2) [
                "All"
                "Default"
                "Alternate"
            ] [
                @@@(ui_tool_ent_attr_props $arg1 variant $arg2)
                uit_label = "Map variant"
            ]

            @@fxlevel_group
        ]
    ]
]

// 1:<type>
ui_tool_ent_gamemode_group = [
    result [
        ui_tool_ent_param_group "Game mode" [
            local attr_mode

            uigrid 3 0 0 [
                loop i (listlen $tool_modes) [
                    attr_mode = (at $tool_modes $i)
                    ui_tool_compactswitch [tool_ent_attr_modes_@attr_mode] [
                        @@@@@(ui_tool_ent_attr_props $arg1 modes $arg2)
                        uit_label = @attr_mode
                        uit_tip_simple = [@(at $modename (+ $i 2))]
                    ]
                    uiclamp- 1 1
                ]
            ]
        ]

        ui_tool_ent_param_group "Game mutators" [
            local attr_mut

            uigrid 3 0 0 [
                loop i (listlen $tool_muts) [
                    attr_mut = (at $tool_muts $i)
                    ui_tool_compactswitch [tool_ent_attr_muts_@attr_mut] [
                        @@@@@(ui_tool_ent_attr_props $arg1 muts $arg2)
                        uit_label = @attr_mut
                        uit_tip_simple = [@(at $mutsname $i)]
                    ]
                    uiclamp- 1 1
                ]
            ]
        ]
    ]
]

// 1:<type> 2:<flags> 3:<labels> 4:<attr> 5:<title>
ui_tool_ent_flags_group = [
    local code label var attr_name props
    code = ""
    attr_name = (? $arg4 $arg4 flags)

    append code (concatword "ui_tool_ent_param_group ^"" (? $arg5 $arg5 "Flags") "^" [^n")
    append code "uigrid 2 $ui_toolview_base_elem_space $ui_toolview_small_elem_space [^n"
    append code "uiclamp 1 1^n"

    props = (ui_tool_ent_attr_props $arg1 $attr_name $arg2)

    loop i (listlen $arg2) [
        label = (at $arg3 $i)
        var = [tool_ent_attr_@[attr_name]_@(at $arg2 $i)]

        append code (concatword "ui_tool_checkbox " $var " [^n")
        append code (concatword "uit_label = [" $label "]")
        append code $props
        append code "uit_label_size = $ui_toolview_tiny_text_size"
        append code "^n]^n"
    ]

    append code "uialign* -1 -1^n"
    append code "]^n]^n"

    result $code
]

exec "config/ui/tool/ents/toollight.cfg"
exec "config/ui/tool/ents/toolmapmodel.cfg"
exec "config/ui/tool/ents/toolplayerstart.cfg"
exec "config/ui/tool/ents/toolenvmap.cfg"
exec "config/ui/tool/ents/toolparticles.cfg"
exec "config/ui/tool/ents/toolsound.cfg"
exec "config/ui/tool/ents/toollightfx.cfg"
exec "config/ui/tool/ents/tooldecal.cfg"
exec "config/ui/tool/ents/toolwind.cfg"
exec "config/ui/tool/ents/toolweapon.cfg"
exec "config/ui/tool/ents/toolteleport.cfg"
exec "config/ui/tool/ents/toolactor.cfg"
exec "config/ui/tool/ents/tooltrigger.cfg"
exec "config/ui/tool/ents/toolpusher.cfg"
exec "config/ui/tool/ents/toolaffinity.cfg"
exec "config/ui/tool/ents/toolcheckpoint.cfg"
exec "config/ui/tool/ents/toolroute.cfg"
exec "config/ui/tool/ents/toolrail.cfg"
exec "config/ui/tool/ents/toolcamera.cfg"

ui_tool_ent_none = []
ui_tool_ent_outline = []

tool_ent_edit_attr = 0

ui_tool_ent_edit_attr = [
    local type_idx num_attrs
    type_idx = (indexof $tool_ent_types $tool_ent_type)
    num_attrs = (entityattrs $type_idx 1)

    uivlist $ui_toolview_small_elem_space [
        uiclamp 1 1
        loop attridx $num_attrs [
            ui_tool_numinput [tool_ent_attr_@attridx] $intmin $intmax 1 [
                uit_label_size = $ui_toolview_tiny_text_size
                uit_label = (getentattr $type_idx @attridx $tool_ent_attr_0)
                uit_val_format = i
                uit_can_reset = 1
                uit_has_menu = (! (tool_ent_delta_edit))
                uit_delta = (tool_ent_delta_edit)
                uit_on_change = [
                    local set_func
                    set_func = (tool_ent_set_func)
                    $set_func @attridx $[tool_ent_attr_@@attridx]
                ]
            ]
        ]
    ]
]

ui_tool_ent_cur = [
    local edit_type
    edit_type = (? $tool_ent_edit_attr ui_tool_ent_edit_attr [ui_tool_ent_@tool_ent_type])

    if (&& $tool_ent_compatible tool_ent_has_sel) [
        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1
                edit_type
            ]
        ] [
            uit_height = 0.6
        ]
    ]
]

tool_ent_list_height = 0.08
tool_last_sel_ents = 0

ui_tool_ent_list = [
    local sel_ents sel_idx text_col
    sel_ents = (getenginestat 15)
    sel_idx = 0

    uitext (concat $sel_ents (? (= $sel_ents 1) "entity" "entities") "selected") $ui_toolview_small_text_size
    uialign- -1 -2

    uicolour $ui_toolview_dark_colour 0 0 [
        uiclamp 1 1

        ui_tool_vscrollarea [
            uigrid 3 0 0 [
                uiclamp 1 1

                entloopread [
                    text_col = (? (= $sel_idx (- $sel_ents 1)) $ui_toolview_accent_colour $ui_toolview_dark_accent_colour)

                    ui_tool_button [
                        uit_label = (concatword (enttype) " ("(entindex) ")")
                        uit_label_align = -1
                        uit_label_size = $ui_toolview_tiny_text_size
                        uit_colour = $text_col
                        uit_on_click = [
                            entautoview @sel_idx 1
                        ]
                        uit_tip_simple = "Click to view"
                    ]

                    sel_idx = (+ $sel_idx 1)
                ]
                uiclamp* 1 1
            ]

            if (!= $tool_last_sel_ents $sel_ents) [
                uiclipoffsety 1
            ]
        ] [
            uit_height = $tool_ent_list_height
            uit_scroll_speed = 0.2
        ]
    ]

    tool_last_sel_ents = $sel_ents
]

// 1:<header text> 2:<on pick>
ui_tool_ent_pick = [
    result [
        local on_pick
        on_pick = [@@arg2]

        uivlist 0 [
            uiclamp 1 1

            uihover [
                menu_click_hide = 1
            ]

            uitext [@@@arg1] $ui_toolview_small_text_size

            uifill 0 $ui_toolview_small_elem_space

            uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

            uigrid 3 0 0 [
                uiclamp 1 1
                looplist ent_type $tool_ent_types [
                    if (!=s $ent_type "none") [
                        ui_tool_button [
                            uit_label = [@ent_type]
                            uit_label_align = -1
                            uit_on_click = [
                                on_pick @ent_type
                            ]
                        ]
                    ]
                ]

                uiclamp* 1 1
            ]
        ]
    ]
]

ui_tool_ent_add = [
    @(ui_tool_ent_pick "New entity" [
        newent $arg1
    ])
]

ui_tool_ent_add_on_camera = [
    @(ui_tool_ent_pick "New entity on camera" [
        local entdrop_old
        entdrop_old = $entdrop

        entdrop 0
        newent $arg1
        entdrop $entdrop_old
    ])
]

ui_tool_ent_link = [
    uivlist 0 [
        uiclamp 1 1

        uitext "Link entities" $ui_toolview_small_text_size

        uifill 0 $ui_toolview_small_elem_space

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_button [
            uit_label = "Chain-link"
            uit_icon = @(tool_action_prop ta_ent_chain_link icon)
            uit_tip_action = ta_ent_chain_link
            uit_on_click = [
                tool_do_action ta_ent_chain_link
            ]
        ]
        uiclamp- 1 1

        ui_tool_button [
            uit_label = "Star-link"
            uit_icon = @(tool_action_prop ta_ent_star_link icon)
            uit_tip_action = ta_ent_star_link
            uit_on_click = [
                tool_do_action ta_ent_star_link
            ]
        ]
        uiclamp- 1 1
    ]
]

ui_tool_ent_controls = [
    uifill 0 0 [
        uiclamp 1 1
        uivlist 0 [
            uiclamp 1 1
            uihlist 0 [
                uiclamp 1 1
                ui_tool_button [
                    uit_label = [@(? (tool_ent_placer_canplace) "Place" "New")]
                    uit_icon = @(tool_action_prop ta_ent_add icon)
                    uit_tip_action = ta_ent_add
                    uit_on_click = [
                        tool_do_action ta_ent_add
                    ]
                    uit_width = 0.17
                    uit_height = 0.03
                ]
                ui_tool_button [
                    uit_label = "Delete"
                    uit_icon = @(tool_action_prop ta_ent_delete icon)
                    uit_tip_action = ta_ent_delete
                    uit_on_click = [
                        tool_do_action ta_ent_delete
                    ]
                    uit_width = 0.17
                    uit_height = 0.03
                    uit_disabled = (! (getenginestat 15))
                ]
            ]
            uialign- -1 -2
            uihlist 0 [
                ui_tool_button [
                    uit_icon = @(tool_action_prop ta_ent_sel_links icon)
                    uit_tip_action = ta_ent_sel_links
                    uit_on_click = [
                        tool_do_action ta_ent_sel_links
                    ]
                    uit_width = 0.03
                    uit_height = 0.03
                    uit_disabled = (! (tool_ent_has_sel))
                ]

                ui_tool_button [
                    uit_icon = @(tool_action_prop ta_ent_unify icon)
                    uit_tip_action = ta_ent_unify
                    uit_on_click = [
                        tool_do_action ta_ent_unify
                    ]
                    uit_width = 0.03
                    uit_height = 0.03
                ]

                ui_tool_button [
                    uit_icon = @(tool_action_prop ta_ent_search icon)
                    uit_tip_action = ta_ent_search
                    uit_on_click = [
                        tool_do_action ta_ent_search
                    ]
                    uit_width = 0.03
                    uit_height = 0.03
                ]

                ui_tool_button [
                    uit_icon = @(tool_action_prop ta_ent_placer icon)
                    uit_tip_action = ta_ent_placer
                    uit_on_click = [
                        tool_do_action ta_ent_placer
                    ]
                    uit_width = 0.03
                    uit_height = 0.03
                ]

                ui_tool_button [
                    uit_icon = "textures/icons/shock"
                    uit_tip_simple = "Link entities"
                    uit_on_click = [
                        toolpanel_show tool_ent_link "" menu [
                            uit_position = (uicursorpos)
                        ]
                    ]
                    uit_width = 0.03
                    uit_height = 0.03
                ]
            ]
            uialign- -1 -2
        ]
        uialign- -1 -2

        uihlist 0 [
            ui_tool_compactswitch tool_ent_edit_attr [
                uit_label = "Atr"
                uit_tip_simple = "Toggle simple attribute view"
                uit_width = 0.03
                uit_height = 0.03
            ]
            ui_tool_compactswitch tool_ent_delta_edit_on [
                uit_label = "Inc"
                uit_tip_simple = "Toggle incremental multi-editing"
                uit_width = 0.03
                uit_height = 0.03
            ]
        ]
        uialign- 1 1
    ]
]

ui_tool_ent_unify = [
    uitext "Unify entities"

    local type_idx num_attrs
    type_idx = (indexof $tool_ent_types $tool_ent_type)
    num_attrs = (entityattrs $type_idx 1)

    if (tool_ent_multiple) [
        uihlist $ui_toolview_base_elem_space [
            ui_tool_button [
                uit_label = "Select all"
                uit_on_click = [
                    tool_ent_unify_mask = 0xFFFFFF
                ]
            ]
            ui_tool_button [
                uit_label = "Deselect all"
                uit_on_click = [
                    tool_ent_unify_mask = 0
                ]
            ]
        ]

        uigrid 3 $ui_toolview_base_elem_space $ui_toolview_small_elem_space [
            loop attridx $num_attrs [
                ui_tool_checkbox tool_ent_unify_mask [
                    uit_label = (getentattr $type_idx @attridx $tool_ent_attr_0)
                    uit_get = [
                        & $tool_ent_unify_mask (<< 1 @@attridx)
                    ]
                    uit_set = [
                        ^ $arg1 (<< 1 @@attridx)
                    ]
                ]
            ]
        ]

        uihlist 0 [
            ui_tool_switch tool_ent_unify_type [
                uit_label = "Ent type differences"
                uit_options = ["Skip" "Change"]
                uit_tip_simple = "Whether entities of different types^nshould be skipped or have their types changed"
            ]
        ]
    ] [
        uicolourtext "Must have at least two entities selected" $ui_toolview_warn_colour $ui_toolview_small_text_size
    ]

    uihlist $ui_toolview_base_elem_space [
        ui_tool_button [
            uit_label = "Unify"
            uit_label_size = $ui_toolview_base_text_size
            uit_on_click = [
                tool_ent_unify_perform
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
            uit_disabled = (! (tool_ent_multiple))
        ]
        ui_tool_button [
            uit_label = "Cancel"
            uit_label_size = $ui_toolview_base_text_size
            uit_on_click = [
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
        ]
    ]
]

ui_tool_ent_search = [
    uitext "Search"

    ui_tool_checkbox tool_ent_search_insel [
        uit_label = "Limit to selection"
        uit_tip_simple = "Search within the selected area"
    ]

    uigrid 2 0 0 [
        ui_tool_button [
            uit_label = "Selected"
            uit_icon = "textures/icons/arrow"
            uit_tip_simple = "Set filters from current entity selection"
            uit_width = 0.12
            uit_height = 0.03
            uit_on_click = [
                tool_ent_search_get_filters
            ]
        ]
        ui_tool_button [
            uit_label = "Clear"
            uit_icon = "textures/icons/action"
            uit_tip_simple = "Clear filters"
            uit_width = 0.12
            uit_height = 0.03
            uit_on_click = [
                tool_ent_search_reset_filters
            ]
        ]
        ui_tool_button [
            uit_label = "Search"
            uit_icon = "textures/icons/spectator"
            uit_tip_simple = "Perform search"
            uit_width = 0.12
            uit_height = 0.03
            uit_on_click = [
                tool_ent_search_find
            ]
        ]
        ui_tool_button [
            uit_label = "Cancel"
            uit_icon = "textures/icons/warning"
            uit_tip_simple = "Cancel"
            uit_width = 0.12
            uit_height = 0.03
            uit_on_click = [
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
        ]
    ]

    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

    uihlist 0 [
        uiclamp 1 1

        ui_tool_dropdown tool_ent_search_type $tool_ent_types [
            uit_label = "Type"
        ]
        ui_tool_button [
            uit_icon = "textures/icons/arrow"
            uit_tip_simple = "Get type from currently selected entity"
            uit_icon_size = 0.012
            uit_on_click = [
                tool_ent_search_get_type
            ]
        ]
    ]

    local type_idx num_attrs
    type_idx = $tool_ent_search_type
    num_attrs = (entityattrs $type_idx 1)

    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uitext "Attribute filters"

    ui_tool_vscrollarea [
        uivlist 0 [
            uiclamp 1 1
            uigrid 3 $ui_toolview_base_elem_space $ui_toolview_small_elem_space [
                uiclamp 1 1
                loop attridx $num_attrs [
                    ui_tool_dropdown [tool_ent_search_attr_op_@attridx] $tool_ent_search_operators [
                        uit_width = 0.06
                        uit_menu_width = 0.06
                    ]
                    ui_tool_numinput [tool_ent_search_attr_@attridx] $intmin $intmax 1 [
                        uit_label = (getentattr $type_idx @attridx $tool_ent_search_attr_0)
                        uit_val_format = i
                        uit_can_reset = 1
                    ]
                    ui_tool_button [
                        uit_icon = "textures/icons/arrow"
                        uit_tip_simple = "Get value from currently selected entity"
                        uit_icon_size = 0.012
                        uit_on_click = [
                            tool_ent_search_get_attr $attridx
                        ]
                    ]
                ]
            ]
        ]
    ] [
        uit_height = 0.3
    ]
]

ui_tool_ent_placer_template_add = [
    @(ui_tool_ent_pick "New template" [
        tool_ent_placer_template_add $arg1
    ])
]

ui_tool_ent_placer = [
    local template text_col

    uitext "Entity placer"

    uifill 0 $ui_toolview_base_elem_space

    uitext "Templates" $ui_toolview_small_text_size

    uicolour $ui_toolview_dark_colour 0 0 [
        uiclamp 1 1

        ui_tool_vscrollarea [
            uigrid 2 0 0 [
                uiclamp 1 1
                loop temp_idx (listlen $tool_ent_placer_templates) [
                    template = (at $tool_ent_placer_templates $temp_idx)
                    text_col = (? (tool_ent_placer_template_isfocus $temp_idx) $ui_toolview_accent_colour $ui_toolview_dark_accent_colour)

                    ui_tool_button [
                        uit_label = "X"
                        uit_label_size = $ui_toolview_tiny_text_size
                        uit_tip_simple = "Remove template"
                        uit_on_click = [
                            tool_ent_placer_template_remove @temp_idx
                        ]
                    ]

                    ui_tool_button [
                        uit_label = [@(concatword (at $template 0) " (" $temp_idx ")")]
                        uit_label_align = -1
                        uit_label_size = $ui_toolview_tiny_text_size
                        uit_width = 0.32
                        uit_colour = $text_col
                        uit_tip_simple = "Select template"
                        uit_on_click = [
                            tool_ent_placer_template_setfocus @temp_idx
                        ]
                    ]
                ]

                uialign* -1 -1
            ]
        ] [
            uit_height = $tool_ent_list_height
            uit_scroll_speed = 0.2
            uit_scrollbar_autohide = 0
        ]
    ]

    uihlist $ui_toolview_small_elem_space [
        ui_tool_button [
            uit_label = "New template"
            uit_on_click = [
                toolpanel_show tool_ent_placer_template_add "" menu [
                    uit_position = (uicursorpos)
                    uit_width = 0.3
                ]
            ]
        ]
        ui_tool_button [
            uit_label = "From entity"
            uit_on_click = [
                tool_ent_placer_template_froment
            ]
            uit_disabled = (! (getenginestat 15))
        ]
        ui_tool_button [
            uit_label = "Duplicate"
            uit_on_click = [
                tool_ent_placer_template_duplicate
            ]
            uit_disabled = (! (tool_ent_placer_template_hasfocus))
        ]
    ]
    uialign- -1

    uihlist 0 [
        ui_tool_switch tool_ent_placer_random [
            uit_options = [ "Selected" "Random" ]
            uit_label = "Place mode"
        ]
    ]
    uialign- -1

    local edit_type
    edit_type = (? $tool_ent_edit_attr ui_tool_ent_edit_attr [ui_tool_ent_@tool_ent_type])

    if (&& (!=s $tool_ent_type "") (tool_ent_placer_template_hasfocus)) [
        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1
                edit_type
            ]
        ] [
            uit_height = 0.3
        ]
    ]
]

ui_tool_ent = [
    tool_ent_edit = 0

    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1 1 1

        uihlist 0 [
            uiclamp 1 1
            ui_tool_checkbox entselsnap [
                uit_label = "Snap entities to grid"
            ]

            ui_tool_dropdown entdrop [
                "Drop on camera"
                "Drop on floor"
                "Drop on selection"
                "Drop on sel. floor"
            ] [
                uit_tip_simple = "Entity drop position"
                uit_width = 0.15
            ]
            uialign- 1
        ]
        uialign- -1

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_ent_list
        ui_tool_ent_controls

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        local ent_uis
        ent_uis = [
            ui_tool_ent_cur
            ui_tool_ent_unify
            ui_tool_ent_search
            ui_tool_ent_placer
        ]

        if (= $tool_ent_ui_mode @T_ENT_UI_EDIT) [
            tool_ent_edit = 1
        ]

        tool_ent_sync
        do (at $ent_uis $tool_ent_ui_mode)
    ]
]
