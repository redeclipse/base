// 1:<type> 2:<attr name list> 3:<subtype>
tool_ent_force_attr = [
    local _attr_list
    _attr_list = []

    looplist attr_name $arg2 [
        append _attr_list (tool_ent_attr_idx_map $arg1 $attr_name $arg3)
    ]

    tool_ent_attr_change $arg1 $_attr_list
]

// 1:<type> 2:<attr name list> 3:<subtype> 4:<template mods>
# ui_tool_ent_attr_props = [
    local _attr_list _nodelta _disable _temp_mod_menu
    _attr_list = []
    _nodelta = 0
    _temp_mod_menu = (&& $arg4 [= (listlen $arg2) 1])

    looplist attr_name $arg2 [
        append _attr_list (tool_ent_attr_idx_map $arg1 $attr_name $arg3)
        if (& (tool_ent_aflags $arg1 $attr_name) $T_ENT_NODELTA) [
            _nodelta = 1
        ]
    ]

    _disable = []
    if $_nodelta [
        _disable = "p_disabled = (tool_ent_delta_edit)"
    ]

    _temp_mod_menu = []
    if (&& $arg4 [= (listlen $arg2) 1]) [
        _temp_mod_menu = [
            p_menu_props = (? (= $tool_ent_ui_mode $T_ENT_UI_TEMPLATES) [
                p_item_names = ["Template modifiers"]
                p_on_select = [
                    // Assuming single element in _attr_list
                    toolpanel_open tool_ent_templates_modifiers popup [
                        tool_ent_templates_cur_property  = (+ $TOOL_ENTTEMP_PROP_ATTR_START_IDX #1 _attr_list)
                        tool_ent_templates_cur_modifiers = $TOOL_ENTTEMP_MOD_VARIANCE
                        #1(? (=s $arg2 yaw) [
                            tool_ent_templates_cur_modifiers = (| $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_CAMYAW)
                        ])
                        #1(? (=s $arg2 pitch) [
                            tool_ent_templates_cur_modifiers = (| $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_CAMPITCH)
                        ])
                        p_position = (uicursorpos)
                        p_width = 0.3
                    ]
                ]
            ] [])
        ]
    ]

    result [
        p_label_size = $ui_tool_text_size_xs
        p_on_change = [
            tool_ent_attr_change #1 arg1 [#1 _attr_list]
        ]
        p_can_reset = 1
        p_reset_val = 0
        @_disable
        p_has_menu = (! (tool_ent_delta_edit))
        p_delta = (tool_ent_delta_edit)
        @_temp_mod_menu
        p_immediate = (!= $tool_ent_ui_mode $T_ENT_UI_TEMPLATES)
    ]
]

// 1:<text> 2:<fields>
# ui_tool_ent_param_group = [
    uihlist 0 [
        uistyle clampx
        uitext $arg1 $ui_tool_text_size_s
        uivlist 0 [
            @arg2
        ]
    ]

    #ui_tool_divider
]

// 1:<type> 2:<subtype> 3:<suffix>
# ui_tool_ent_palette_group = [
    result [
        ui_tool_ent_param_group [Palette #1 arg3] [
            ui_tool_dropdown @@(tool_ent_attr $arg1 [palette@arg3] $arg2) [
                "Pulse"
                "Team"
                "Weapon"
            ] [
                #1(ui_tool_ent_attr_props $arg1 [palette@arg3] $arg2)
                p_label = "Type"
            ]

            ui_tool_dropdown tool_ent_attr_palindex#1[arg3]_val (at $tool_palette_ids $#1(tool_ent_attr $arg1 [palette@arg3] $arg2)) [
                #1(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
                p_label = "Id"
            ]

            uifill 0 $ui_tool_elem_space_m

            ui_tool_checkbox tool_ent_attr_palindex#1[arg3]_enforce [
                p_label = "Enforce"
                p_label_size = $ui_tool_text_size_xs
                #1(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
            ]
            uiprev [uialign 1]
        ]
    ]
]

// 1:<type> 2:<fx>
# ui_tool_ent_variant_group = [
    local _fxlevel_group _fxlevel_var

    if $arg2 [
        _fxlevel_group = [
            uihlist 0 [
                uitext "FX Level:" $ui_tool_text_size_xs
                uifill $ui_tool_elem_space_l
                loop i 3 [
                    _fxlevel_var = [tool_ent_attr_fxlevel_@(+ $i 1)]
                    ui_tool_compactswitch $_fxlevel_var [
                        #2(ui_tool_ent_attr_props $arg1 fxlevel $arg3)
                        p_label = @(+ $i 1)
                        p_width = 0.02
                        p_height = 0.02
                    ]
                ]
            ]
            uiprev [uialign 1]
        ]
    ]

    result [
        ui_tool_ent_param_group "Variant" [
            ui_tool_dropdown @@(tool_ent_attr $arg1 variant $arg3) (sublist $MPV_NAMES 0 $MPV_MAX) [
                #1(ui_tool_ent_attr_props $arg1 variant $arg3)
                p_label = "Map variant"
            ]

            #1 _fxlevel_group
        ]
    ]
]

// 1:<type> 2:<subtype>
# ui_tool_ent_gamemode_group = [
    result [
        ui_tool_ent_param_group "Game mode" [
            uigrid 3 0 0 [
                loop i (listlen $tool_modes) [
                    ui_tool_compactswitch #1(tool_ent_attr $arg1 modes $arg2) [
                        #1(ui_tool_ent_attr_props $arg1 modes $arg2)
                        p_label      = @(at $tool_modes $i)
                        p_tip_simple = [@(at $G_NAMES (+ $i 2))]
                        p_get        = [& (abs $arg1) @(<< 1 $i)]
                        p_set        = [
                            local _neg
                            _neg = (? (< $#1(tool_ent_attr $arg1 modes $arg2) 0) -1 1)
                            * (^ (abs $#1(tool_ent_attr $arg1 modes $arg2)) @(<< 1 $i)) $_neg
                        ]
                    ]
                    uiprev [uistyle clampx]
                ]
            ]
            uiprev [uialign 1]

            uifill 0 $ui_tool_elem_space_m

            ui_tool_switch #1(tool_ent_attr $arg1 modes $arg2) [
                #1(ui_tool_ent_attr_props $arg1 modes $arg2)
                p_options  = [ "Normal" "Inverted" ]
                p_get      = [< $arg1 0]
                p_set      = [* $#1(tool_ent_attr $arg1 modes $arg2) -1]
                p_disabled = (! $#1(tool_ent_attr $arg1 modes $arg2))
            ]
        ]

        ui_tool_ent_param_group "Game mutators" [
            uigrid 3 0 0 [
                loop i (listlen $tool_muts) [
                    ui_tool_compactswitch #1(tool_ent_attr $arg1 muts $arg2) [
                        #1(ui_tool_ent_attr_props $arg1 muts $arg2)
                        p_label      = @(at $tool_muts $i)
                        p_tip_simple = [@(at $G_M_NAMES $i)]
                        p_get        = [& (abs $arg1) @(<< 1 $i)]
                        p_set        = [
                            local _neg
                            _neg = (? (< $#1(tool_ent_attr $arg1 muts $arg2) 0) -1 1)
                            * (^ (abs $#1(tool_ent_attr $arg1 muts $arg2)) @(<< 1 $i)) $_neg
                        ]
                    ]
                    uiprev [uistyle clampx]
                ]
            ]
            uiprev [uialign 1]

            uifill 0 $ui_tool_elem_space_m

            ui_tool_switch #1(tool_ent_attr $arg1 muts $arg2) [
                #1(ui_tool_ent_attr_props $arg1 muts $arg2)
                p_options  = [ "Normal" "Inverted" ]
                p_get      = [< $arg1 0]
                p_set      = [* $#1(tool_ent_attr $arg1 muts $arg2) -1]
                p_disabled = (! $#1(tool_ent_attr $arg1 muts $arg2))
            ]
        ]
    ]
]

// 1:<type> 2:<flags> 3:<labels> 4:<attr> 5:<title>
# ui_tool_ent_flags_group = [
    local _attr_name
    _attr_name = (? $arg4 $arg4 flags)

    result [
        ui_tool_ent_param_group #1(? $arg5 $arg5 "Flags") [
            uigrid 2 $ui_tool_elem_space_l $ui_tool_elem_space_s [
                uistyle clampx

                #1(loopconcat i (listlen $arg2) [
                    result [
                        ui_tool_checkbox @(tool_ent_attr $arg1 $_attr_name $arg2) [
                            @@(ui_tool_ent_attr_props $arg1 $_attr_name $arg2)
                            p_label      = [@@@(at $arg3 $i)]
                            p_get        = [& $@@@(tool_ent_attr $arg1 $_attr_name $arg2) (<< 1 @@@i)]
                            p_set        = [^ $@@@(tool_ent_attr $arg1 $_attr_name $arg2) (<< 1 @@@i)]
                            p_label_size = $ui_tool_text_size_xs_unscaled
                        ]
                    ]
                ])
            ]
        ]
    ]
]

exec "config/ui/tool/ents/toollight.cfg"
exec "config/ui/tool/ents/toolmapmodel.cfg"
exec "config/ui/tool/ents/toolplayerstart.cfg"
exec "config/ui/tool/ents/toolenvmap.cfg"
exec "config/ui/tool/ents/toolparticles.cfg"
exec "config/ui/tool/ents/toolsound.cfg"
exec "config/ui/tool/ents/toollightfx.cfg"
exec "config/ui/tool/ents/tooldecal.cfg"
exec "config/ui/tool/ents/toolwind.cfg"
exec "config/ui/tool/ents/toolweapon.cfg"
exec "config/ui/tool/ents/toolteleport.cfg"
exec "config/ui/tool/ents/toolactor.cfg"
exec "config/ui/tool/ents/tooltrigger.cfg"
exec "config/ui/tool/ents/toolpusher.cfg"
exec "config/ui/tool/ents/toolaffinity.cfg"
exec "config/ui/tool/ents/toolcheckpoint.cfg"
exec "config/ui/tool/ents/toolroute.cfg"
exec "config/ui/tool/ents/toolrail.cfg"
exec "config/ui/tool/ents/toolcamera.cfg"
exec "config/ui/tool/ents/toolsoundenv.cfg"
exec "config/ui/tool/ents/toolmapui.cfg"
exec "config/ui/tool/ents/toolphysics.cfg"

ui_tool_ent_none = []
ui_tool_ent_outline = []

tool_ent_edit_attr = 0

ui_tool_ent_edit_attr = [
    local _type_idx _num_attrs
    _type_idx = (indexof $tool_ent_types $tool_ent_type)
    _num_attrs = (entityattrs $_type_idx 1)

    uivlist $ui_tool_elem_space_s [
        uistyle clampx
        loop attridx $_num_attrs [
            ui_tool_numinput [tool_ent_attr_@attridx] $intmin $intmax 1 [
                p_label_size = $ui_tool_text_size_xs
                p_label = (getentattr $_type_idx @attridx $tool_ent_attr_0)
                p_val_format = i
                p_can_reset = 1
                p_has_menu = (! (tool_ent_delta_edit))
                p_delta = (tool_ent_delta_edit)
                p_on_change = [
                    local _set_func
                    _set_func = (tool_ent_set_func)
                    $_set_func @attridx $[tool_ent_attr_@@attridx]
                ]
            ]
        ]
    ]
]

# ui_tool_ent_cur = [
    local _edit_type _first_idx
    _edit_type = (? $tool_ent_edit_attr ui_tool_ent_edit_attr [ui_tool_ent_@tool_ent_type])
    _first_idx = (entgroupidx 0)

    if (&& $tool_ent_compatible tool_ent_has_sel) [
        ui_tool_vscrollarea [
            uivlist 0 [
                uistyle clampx

                uifill 0 $ui_tool_elem_space_l

                local _x _y _z _yaw _pitch
                _x     = (getentity $_first_idx 5 1)
                _y     = (getentity $_first_idx 5 2)
                _z     = (getentity $_first_idx 5 3)
                _yaw   = (getentity $_first_idx 5 4)
                _pitch = (getentity $_first_idx 5 5)

                uiviewport (concatword "ui_tool_ent_cur_" $_first_idx) $_x $_y $_z $_yaw $_pitch (uiwidth 0.15) (uiwidth 0.05) [
                    uispace 0 0.005 [
                        uialign -1 -1
                        uiclamp 1 1

                        uicolour 0x88010101 0 0 [
                            uiclamp 1 1

                            uitext (format "%1 #%2" $tool_ent_type $_first_idx) $ui_tool_text_size_m
                        ]
                    ]

                    uiviewportwidth $viewportsize
                    uiviewportheight (*f $viewportsize 0.33)
                    uiviewportfov 90

                    if $uidrawn [
                        uiviewportuprate 1000
                    ]
                ]

                uifill 0 $ui_tool_elem_space_l
                uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]
                uifill 0 $ui_tool_elem_space_l

                ui_tool_ent_param_group "Position" [
                    ui_tool_vec tool_ent_pos [
                        p_on_change = [
                            if (tool_ent_delta_edit) [
                                do [ entmove $tool_ent_pos ]
                            ] [
                                do [ entpos @tool_ent_pos ]
                            ]
                        ]
                        p_delta = (tool_ent_delta_edit)
                    ]
                ]

                uifill 0 $ui_tool_elem_space_l
                $_edit_type
            ]
        ] [
            p_height = $toolpanel_dyn_content_h
            p_id     = #(tool_get_id)
        ]
    ]
]

tool_ent_list_height = 0.08
tool_last_sel_ents = 0

// 1:<entgroup index>
# tool_ent_list_contextmenu = [
    toolpanel_open_menu [
        p_width = 0.2
        p_item_names = [
            "Remove from selection"
        ]
        p_on_select = [
            do (at [
                [enttoggleidx #1 arg1]
            ] $arg1)
        ]
    ]
]

# ui_tool_ent_list = [
    local _sel_ents _sel_idx _text_col
    _sel_ents = (getenginestat 15)
    _sel_idx = 0

    uitext (concat $_sel_ents (? (= $_sel_ents 1) "entity" "entities") "selected") $ui_tool_text_size_s
    uiprev [uialign -1 -2]

    uicolour $ui_tool_dark_colour 0 0 [
        uistyle clampx

        ui_tool_vscrollarea [
            uigrid 3 0 0 [
                uistyle clampx

                entloopread [
                    _text_col = (? (= $_sel_idx (- $_sel_ents 1)) $ui_tool_accent_colour $ui_tool_dark_accent_colour)

                    ui_tool_button [
                        p_label = (concatword (enttype) " ("$entindex ")")
                        p_label_align = -1
                        p_label_size = $ui_tool_text_size_xs
                        p_colour = $_text_col
                        p_on_click = [
                            entautoview @_sel_idx 1
                        ]
                        p_on_rightclick = [
                            tool_ent_list_contextmenu @entindex
                        ]
                        p_tip_simple = "Click to view"
                    ]

                    _sel_idx = (+ $_sel_idx 1)
                ]
                uipropchild [uistyle clampx]
            ]

            if (!= $tool_last_sel_ents $_sel_ents) [
                uiclipoffsety 1
            ]
        ] [
            p_height       = $tool_ent_list_height
            p_scroll_speed = 0.2
            p_id           = #(tool_get_id)
        ]
        uiprev [uistyle clampx]
    ]

    tool_last_sel_ents = $_sel_ents
]

# ui_tool_ent_controls = [
    uifill 0 0 [
        uistyle clampx
        uivlist 0 [
            uistyle clampx
            uihlist 0 [
                uistyle clampx
                #(ui_tool_get_action_button ta_ent_add [
                    p_label = "New"
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    p_override_label = 1
                ])
                #(ui_tool_get_action_button ta_ent_delete [
                    p_label = "Delete"
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                    p_disabled = (! (tool_ent_has_sel))
                ] [
                    p_override_label = 1
                ])
                uipropchild [uistyle clampx]
            ]
            uiprev [uialign -1 -2]
            uihlist 0 [
                #(ui_tool_get_action_button ta_ent_sel_links [
                    p_icon_align = 0
                    p_width = $ui_tool_button_height_m_unscaled
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                    p_disabled = (! (tool_ent_has_sel))
                ] [
                    p_override_label = 1
                ])

                #(ui_tool_get_action_button ta_ent_unify [
                    p_icon_align = 0
                    p_width = $ui_tool_button_height_m_unscaled
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                    p_disabled = (< (getenginestat 15) 2)
                ] [
                    p_override_label = 1
                ])

                #(ui_tool_get_action_button ta_ent_search [
                    p_icon_align = 0
                    p_width = $ui_tool_button_height_m_unscaled
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    p_override_label = 1
                ])

                #(ui_tool_get_action_button ta_ent_placer [
                    p_icon_align = 0
                    p_width = $ui_tool_button_height_m_unscaled
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    p_override_label = 1
                ])

                ui_tool_button [
                    p_icon_align = 0
                    p_icon = "<grey>textures/icons/edit/chain"
                    p_tip_simple = "Link entities"
                    p_on_click = [
                        toolpanel_open_menu [
                            p_text = "Link entities"
                            p_item_actions = [
                                ta_ent_chain_link
                                ta_ent_star_link
                                ta_ent_unlink
                            ]
                            p_disabled = [
                                && [< $arg1 2] [< (getenginestat 15) 2]
                            ]
                        ]
                    ]
                    p_width = $ui_tool_button_height_m_unscaled
                    p_height = $ui_tool_button_height_m_unscaled
                    p_icon_size = $ui_tool_icon_size_m_unscaled
                    p_disabled = (= (getenginestat 15) 0)
                ]
            ]
            uiprev [uialign -1 -2]
        ]
        uiprev [uialign -1 -2]

        uihlist 0 [
            ui_tool_compactswitch tool_ent_edit_attr [
                p_label = "Atr"
                p_label_size = $ui_tool_text_size_xs_unscaled
                p_tip_simple = "Toggle simple attribute view"
                p_width = $ui_tool_button_height_m_unscaled
                p_height = $ui_tool_button_height_m_unscaled
            ]
            ui_tool_compactswitch tool_ent_delta_edit_on [
                p_label = "Inc"
                p_label_size = $ui_tool_text_size_xs_unscaled
                p_tip_simple = "Toggle incremental multi-editing"
                p_width = $ui_tool_button_height_m_unscaled
                p_height = $ui_tool_button_height_m_unscaled
            ]
        ]
        uiprev [uialign 1 1]
    ]
]

ui_tool_ent_unify = [
    uitext "Unify entities" $ui_tool_text_size_m

    local _type_idx _num_attrs
    _type_idx = (indexof $tool_ent_types $tool_ent_type)
    _num_attrs = (entityattrs $_type_idx 1)

    if (tool_ent_multiple) [
        uihlist $ui_tool_elem_space_l [
            ui_tool_button [
                p_label = "Select all"
                p_on_click = [
                    tool_ent_unify_mask = 0xFFFFFF
                ]
            ]
            ui_tool_button [
                p_label = "Deselect all"
                p_on_click = [
                    tool_ent_unify_mask = 0
                ]
            ]
        ]

        uigrid 3 $ui_tool_elem_space_l $ui_tool_elem_space_s [
            loop attridx $_num_attrs [
                ui_tool_checkbox tool_ent_unify_mask [
                    p_label = (getentattr $_type_idx @attridx $tool_ent_attr_0)
                    p_get = [
                        & $tool_ent_unify_mask (<< 1 @@attridx)
                    ]
                    p_set = [
                        ^ $tool_ent_unify_mask (<< 1 @@attridx)
                    ]
                ]
            ]
        ]

        uihlist 0 [
            ui_tool_switch tool_ent_unify_type [
                p_label = "Ent type differences"
                p_options = ["Skip" "Change"]
                p_tip_simple = "Whether entities of different types^nshould be skipped or have their types changed"
            ]
        ]
    ] [
        uicolourtext "Must have at least two entities selected" $ui_tool_warn_colour $ui_tool_text_size_s
    ]

    uihlist $ui_tool_elem_space_l [
        ui_tool_button [
            p_label = "Unify"
            p_label_size = $ui_tool_text_size_m
            p_on_click = [
                tool_ent_unify_perform
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
            p_disabled = (! (tool_ent_multiple))
        ]
        ui_tool_button [
            p_label = "Cancel"
            p_label_size = $ui_tool_text_size_m
            p_on_click = [
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
        ]
    ]
]

# ui_tool_ent_search = [
    uivlist $ui_toolpanel_elem_space [
        uitext "Search" $ui_tool_text_size_m

        ui_tool_checkbox tool_ent_search_insel [
            p_label = "Limit to selection"
            p_tip_simple = "Search within the selected area"
        ]

        uigrid 2 0 0 [
            ui_tool_button [
                p_label = "Selected"
                p_icon = "<grey>textures/icons/arrow"
                p_tip_simple = "Set filters from current entity selection"
                p_width = 0.12
                p_height = 0.03
                p_on_click = [
                    tool_ent_search_get_filters
                ]
            ]
            ui_tool_button [
                p_label = "Clear"
                p_icon = "<grey>textures/icons/action"
                p_tip_simple = "Clear filters"
                p_width = 0.12
                p_height = 0.03
                p_on_click = [
                    tool_ent_search_reset_filters
                ]
            ]
            ui_tool_button [
                p_label = "Search"
                p_icon = "<grey>textures/icons/spectator"
                p_tip_simple = "Perform search"
                p_width = 0.12
                p_height = 0.03
                p_on_click = [
                    tool_ent_search_find
                ]
            ]
            ui_tool_button [
                p_label = "Cancel"
                p_icon = "<grey>textures/icons/warning"
                p_tip_simple = "Cancel"
                p_width = 0.12
                p_height = 0.03
                p_on_click = [
                    tool_ent_ui_mode = $T_ENT_UI_EDIT
                ]
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

        uihlist 0 [
            uistyle clampx

            ui_tool_dropdown tool_ent_search_type $tool_ent_searchtypes [
                p_label = "Type"
            ]
            ui_tool_button [
                p_icon = "<grey>textures/icons/arrow"
                p_tip_simple = "Get type from currently selected entity"
                p_icon_size = 0.012
                p_on_click = [
                    tool_ent_search_get_type
                ]
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]
        uitext "Attribute filters" $ui_tool_text_size_m

        toolpanel_calc_dyn_content $uilasth
    ]

    local _type_idx _num_attrs
    _type_idx = $tool_ent_search_type
    _num_attrs = (entityattrs $_type_idx 1)

    ui_tool_vscrollarea [
        uivlist 0 [
            uistyle clampx
            uigrid 3 $ui_tool_elem_space_l $ui_tool_elem_space_s [
                uistyle clampx
                loop attridx $_num_attrs [
                    ui_tool_dropdown [tool_ent_search_attr_op_@attridx] $tool_ent_search_operators [
                        p_width = 0.06
                    ]
                    ui_tool_numinput [tool_ent_search_attr_@attridx] $intmin $intmax 1 [
                        p_label = (getentattr $_type_idx @attridx $tool_ent_search_attr_0)
                        p_val_format = i
                        p_can_reset = 1
                    ]
                    ui_tool_button [
                        p_icon = "<grey>textures/icons/arrow"
                        p_tip_simple = "Get value from currently selected entity"
                        p_icon_size = 0.012
                        p_on_click = [
                            tool_ent_search_get_attr $attridx
                        ]
                    ]
                ]
            ]
        ]
    ] [
        p_height = $toolpanel_dyn_content_h
        p_id     = #(tool_get_id)
    ]
]

ui_tool_ent_undercursor = [
    if (> $numenthover 1) [
        uitext (concat $numenthover "entities here") $ui_tool_text_size_xs
    ] [
        // Assuming single iteration, as per the value in `numenthover`
        enthoverloopread [
            uivlist $ui_tool_elem_space_s [
                uitext (concat "^fAEntity:^fw" (enttype)) $ui_tool_text_size_xs
                uiprev [uialign -1]
                uigrid 8 $ui_tool_elem_space_s $ui_tool_elem_space_s [
                    local _type_idx
                    _type_idx = (indexof $tool_ent_types (enttype))
                    loop i (getentattr $_type_idx -1) [
                        uitext (concatword "^fA" (getentattr $_type_idx $i (entattr 0)) ":") $ui_tool_text_size_xs
                        uitext (entattr $i) $ui_tool_text_size_xs
                    ]
                ]
            ]
        ]
    ]
]

tool_ent_undercursor_proc = [
    if $numenthover [
        toolview_undercursor = ui_tool_ent_undercursor

        if (> $numenthover 1) [
            tool_rightclickable
        ]
    ]
]
tool_proc_add tool_ent_undercursor_proc

ui_tool_ent_on_open = [
    if $enteditingonpanel [
        entediting 1
    ]
]

ui_tool_ent_on_close = [
    if $enteditingonpanel [
        entediting 0
    ]

    tool_enttemp_place_enabled = 0
]

ui_tool_ent_ctx_save = [
    // Store selection

    if $enteditingonpanel [
        local _ctx
        _ctx = []

        entloopread [
            append _ctx $entindex
        ]

        result $_ctx
    ]
]

// 1:<context>
ui_tool_ent_ctx_restore = [
    // Restore selection

    if $enteditingonpanel [
        entselect [
            listhas $arg1 $entindex
        ]
    ]
]

exec "config/ui/tool/toolenttemp.cfg"

# ui_tool_ent = [
    uivlist 0 [
        uivlist $ui_toolpanel_elem_space [
            uistyle clampxy

            uihlist 0 [
                uistyle clampx
                ui_tool_checkbox entselsnap [
                    p_label = "Snap entities"
                    p_id     = #(tool_get_id)
                ]
                #(tool_ent_register_control "Enable entity snap" "selection move drag")

                ui_tool_dropdown entselsnapmode [
                    "Snap to grid"
                    "Snap to ents"
                ] [
                    p_width      = $ui_tool_button_width_m_unscaled
                    p_tip_simple = "Entity snapping mode"
                    p_id         = #(tool_get_id)
                ]
                #(tool_ent_register_control "Entity snap mode" "selection move drag")
            ]

            uihlist $ui_tool_elem_space_l [
                ui_tool_checkbox entediting [
                    p_label      = "Select entities"
                    p_tip_simple = "Allow selecting of entities"
                    p_id         = #(tool_get_id)
                ]
                #(tool_ent_register_control "Enable entity selection" "move drag editing")

                ui_tool_checkbox entselmapmodelbox [
                    p_label      = "BB select"
                    p_tip_simple = "Allow selection of entity bounding boxes"
                    p_id         = #(tool_get_id)
                ]
                #(tool_ent_register_control "Enable entity bounding-box selection" "move drag editing")

                ui_tool_dropdown entdrop [
                    "Drop on camera"
                    "Drop on floor"
                    "Drop on selection"
                    "Drop on sel. floor"
                ] [
                    p_tip_simple = "Entity drop position"
                    p_width      = $ui_tool_button_width_m_unscaled
                    p_id         = #(tool_get_id)
                ]
                #(tool_ent_register_control "Entity drop position" "")
                uiprev [uialign 1]
            ]
            uiprev [uialign -1]

            uiline $ui_tool_dark_accent_colour 0 0 [ uistyle clampx ]

            ui_tool_ent_list
            ui_tool_ent_controls

            uicolour $ui_tool_dark_accent_colour 0 0.001 [ uistyle clampx ]

            toolpanel_calc_dyn_content $uilasth
        ]

        local _ent_uis
        _ent_uis = [
            ui_tool_ent_cur
            ui_tool_ent_unify
            ui_tool_ent_search
            ui_tool_ent_templates
        ]

        tool_ent_edit              = (= $tool_ent_ui_mode $T_ENT_UI_EDIT)
        tool_enttemp_place_enabled = (= $tool_ent_ui_mode $T_ENT_UI_TEMPLATES)

        tool_ent_sync
        do (at $_ent_uis $tool_ent_ui_mode)
    ]
]
