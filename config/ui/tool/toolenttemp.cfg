tool_ent_templates_cur_property  = -1
tool_ent_templates_cur_modifiers = 0

tool_ent_templates_update_modfiers = [
    local _prop_idx _param
    _prop_idx = $tool_ent_templates_cur_property

    if (& $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_VARIANCE) [
        _param = $tool_ent_templates_modifier_param
        tool_enttemp_modifier_update $_prop_idx $TOOL_ENTTEMP_MOD_VARIANCE $_param
    ]
    if (& $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_CAMYAW) [
        _param = (& $tool_ent_templates_modifier_mode $TOOL_ENTTEMP_MOD_CAMYAW)
        tool_enttemp_modifier_update $_prop_idx $TOOL_ENTTEMP_MOD_CAMYAW $_param
    ]
    if (& $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_CAMPITCH) [
        _param = (& $tool_ent_templates_modifier_mode $TOOL_ENTTEMP_MOD_CAMPITCH)
        tool_enttemp_modifier_update $_prop_idx $TOOL_ENTTEMP_MOD_CAMPITCH $_param
    ]
]

ui_tool_ent_templates_modifiers = [
    if $toolpanel_this_isinit [
        tool_ent_templates_modifier_param = (tool_enttemp_modifier_get_param $tool_ent_templates_cur_property)
        tool_ent_templates_modifier_mode  = (tool_enttemp_modifier_get_mode $tool_ent_templates_cur_property)
    ]

    uispace $ui_tool_elem_space_l $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1

            if (& $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_VARIANCE) [
                ui_tool_numinput tool_ent_templates_modifier_param 0 99999 1 [
                    p_label = "Variance"
                    p_val_format = i
                ]
            ]

            if (& $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_CAMYAW) [
                ui_tool_checkbox tool_ent_templates_modifier_mode [
                    p_label = "Add yaw from camera"
                    p_get = [& $arg1 $TOOL_ENTTEMP_MOD_CAMYAW]
                    p_set = [^ $arg1 $TOOL_ENTTEMP_MOD_CAMYAW]
                ]
            ]

            if (& $tool_ent_templates_cur_modifiers $TOOL_ENTTEMP_MOD_CAMPITCH) [
                ui_tool_checkbox tool_ent_templates_modifier_mode [
                    p_label = "Add pitch from camera"
                    p_get = [& $arg1 $TOOL_ENTTEMP_MOD_CAMPITCH]
                    p_set = [^ $arg1 $TOOL_ENTTEMP_MOD_CAMPITCH]
                ]
            ]

            ui_tool_button [
                p_label = "Confirm"
                p_on_click = [
                    tool_ent_templates_update_modfiers
                    toolpanel_close_this
                ]
                p_width = 0.1
            ]
        ]
    ]
]

ui_tool_ent_templates_rename_col = [
    if $toolpanel_this_isinit [
        tool_ent_templates_col_name = (tool_enttemp_collection_get_name $tool_enttemp_sel_col_idx)
    ]

    uispace $ui_tool_elem_space_l $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1

            ui_tool_textinput tool_ent_templates_col_name 16 [
                p_label = "Collection name"
            ]

            ui_tool_button [
                p_label    = "Rename"
                p_on_click = [
                    tool_enttemp_rename_collection $tool_enttemp_sel_col_idx $tool_ent_templates_col_name
                    toolpanel_close_this
                ]
                p_width    = 0.2
            ]
        ]
    ]
]

ui_tool_ent_templates_new_col = [
    if $toolpanel_this_isinit [
        tool_ent_templates_col_name = ""
    ]

    uispace $ui_tool_elem_space_l $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1

            ui_tool_textinput tool_ent_templates_col_name 16 [
                p_label = "Collection name"
            ]

            ui_tool_button [
                p_label    = "Add"
                p_on_click = [
                    tool_enttemp_add_collection $tool_ent_templates_col_name
                    toolpanel_close_this
                ]
                p_width    = 0.2
            ]
        ]
    ]
]

# ui_tool_ent_templates [
    local _col_names
    _col_names = (getalias tool_enttemp_col_names)

    if (tool_enttemp_can_place) [
        tool_set_cursor_icon "textures/icons/attack"
    ]

    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1

        uitext "Entity templates" $ui_tool_text_size_m

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiclamp 1 1

            uitext "Templates:" $ui_tool_text_size_s
            uihlist 0 [
                ui_tool_button [
                    #ui_tool_square_button_s
                    p_icon       = "textures/hud/exit"
                    p_tip_simple = "Remove list"
                    p_on_click   = [
                        tool_enttemp_rem_collection $tool_enttemp_sel_col_idx
                    ]
                    p_disabled   = (< $tool_enttemp_sel_col_idx 0)
                ]

                ui_tool_button [
                    #ui_tool_square_button_s
                    p_icon       = "textures/icons/edit/copy"
                    p_tip_simple = "Duplicate list"
                    p_on_click   = [
                        local _name
                        _name = (tool_enttemp_collection_get_name)
                        tool_enttemp_add_collection $_name $tool_enttemp_sel_col_idx
                    ]
                    p_disabled   = (< $tool_enttemp_sel_col_idx 0)
                ]

                ui_tool_button [
                    #ui_tool_square_button_s
                    p_icon       = "textures/icons/edit/new"
                    p_tip_simple = "New list"
                    p_on_click   = [
                        toolpanel_open tool_ent_templates_new_col popup [
                            p_position = [@@(uicursorpos)]
                            p_width    = 0.3
                        ]
                    ]
                ]

                ui_tool_button [
                    #ui_tool_square_button_s
                    p_icon       = "textures/icons/edit/name"
                    p_tip_simple = "Rename list"
                    p_on_click   = [
                        toolpanel_open tool_ent_templates_rename_col popup [
                            p_position = [@@(uicursorpos)]
                            p_width    = 0.3
                        ]
                    ]
                    p_disabled   = (< $tool_enttemp_sel_col_idx 0)
                ]

                ui_tool_dropdown tool_enttemp_sel_col_idx $_col_names [
                    p_prompt    = "<Pick a list>"
                    p_width     = 0.15
                    p_on_change = [
                        tool_enttemp_set_collection $tool_enttemp_sel_col_idx
                    ]
                ]
            ]
        ]

        uicolour $ui_tool_dark_colour 0 0 [
            uiclamp 1 1

            ui_tool_vscrollarea [
                uigrid 2 0 0 [
                    uiclamp 1 1

                    local _temp_ent _temp_name _temp_label

                    loop i (listlen (getalias $tool_enttemp_sel_template_col)) [
                        _temp_ent  = (tool_enttemp_template_get_ent $i)
                        _temp_name = (tool_enttemp_template_get_name $i)
                        _text_col  = (? (= $tool_enttemp_sel_template_idx $i) $ui_tool_accent_colour $ui_tool_dark_accent_colour)

                        if (!=s $_temp_name) [
                            _temp_label = (concatword (at $_temp_ent 0) " (" $_temp_name ")")
                        ] [
                            _temp_label = (concatword (at $_temp_ent 0) " (" $i ")")
                        ]

                        ui_tool_button [
                            p_icon       = "textures/hud/exit"
                            p_icon_size  = 0.015
                            p_pad        = 0
                            p_tip_simple = "Remove template"
                            p_on_click   = [
                                tool_enttemp_template_remove @i
                            ]
                        ]

                        ui_tool_button [
                            p_label         = [@@_temp_label]
                            p_label_align   = -1
                            p_label_size    = $ui_tool_text_size_xs
                            p_width         = 0.32
                            p_colour        = $_text_col
                            p_tip_simple    = "Select template"
                            p_on_click      = [
                                tool_enttemp_template_select @i
                            ]
                            p_on_rightclick = [
                                toolpanel_open_menu [
                                    p_width      = 0.2
                                    p_item_names = [
                                        "Select matching instances"
                                    ]
                                    p_on_select  = [
                                        do (at [
                                            [tool_enttemp_search @@@@@i]
                                        ] $arg1)
                                    ]
                                ]
                            ]
                        ]
                    ]

                    uialign* -1 -1
                ]
            ] [
                p_height             = $tool_ent_list_height
                p_scroll_speed       = 0.2
                p_scrollbar_autohide = 0
            ]
            uiclamp- 1 1
        ]

        uihlist $ui_tool_elem_space_s [
            uiclamp 1 1

            uihlist 0 [
                ui_tool_button [
                    #ui_tool_square_button_m
                    p_icon       = "textures/icons/edit/entadd"
                    p_tip_simple = "New template"
                    p_on_click   = [
                        tool_ent_pick [
                            tool_enttemp_template_add $arg1
                        ]
                    ]
                    p_disabled   = (< $tool_enttemp_sel_col_idx 0)
                ]
                ui_tool_button [
                    #ui_tool_square_button_m
                    p_icon       = "textures/icons/edit/entplace"
                    p_tip_simple = "New template from selected entity"
                    p_on_click   = [
                        tool_enttemp_template_froment
                    ]
                    p_disabled   = (|| [! (getenginestat 15)] [< $tool_enttemp_sel_col_idx 0])
                ]
                ui_tool_button [
                    #ui_tool_square_button_m
                    p_icon       = "textures/icons/edit/copy"
                    p_tip_simple = "Duplicate current template"
                    p_on_click   = [
                        tool_enttemp_template_duplicate
                    ]
                    p_disabled   = (< $tool_enttemp_sel_template_idx 0)
                ]
            ]
            uialign- -1

            uihlist 0 [
                ui_tool_dropdown tool_enttemp_place_mode [
                    "Selected"
                    "Sequential"
                    "Random"
                    "All"
                ] [
                    p_label = "Place mode"
                    p_width = $ui_tool_button_width_s_unscaled
                ]
            ]
        ]

        ui_tool_dropdown tool_enttemp_link_mode [
            "Don't link"
            "New to previous"
            "Previous to new"
            "Both ways"
        ] [
            p_label = "Linking"
            p_width = $ui_tool_button_width_l_unscaled
        ]

        uicolour $ui_tool_dark_accent_colour 0 0.001 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_l

        tool_ent_calc_dyn_content_h
    ]

    local _edit_type
    _edit_type = (? $tool_ent_edit_attr ui_tool_ent_edit_attr [ui_tool_ent_@tool_ent_type])

    if (&& (!=s $tool_ent_type "") (>= $tool_enttemp_sel_template_idx 0)) [
        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1

                tool_ent_templates_tempname   = (tool_enttemp_template_get_name)
                tool_ent_templates_tempoffset = (tool_enttemp_template_get_offset)

                ui_tool_textinput tool_ent_templates_tempname 20 [
                    p_label     = "Name"
                    p_immediate = 0
                    p_on_change = [
                        tool_enttemp_template_update_name $tool_ent_templates_tempname
                    ]
                ]

                uifill 0 $ui_tool_elem_space_s
                uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
                uifill 0 $ui_tool_elem_space_s

                ui_tool_vec tool_ent_templates_tempoffset [
                    p_label = "Position offset"
                    p_immediate = 0
                    p_allow_neg = 1
                    p_on_change = [
                        tool_enttemp_template_update_offset $tool_ent_templates_tempoffset
                    ]
                ]

                uifill 0 $ui_tool_elem_space_s
                uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
                uifill 0 $ui_tool_elem_space_s

                _edit_type
            ]
        ] [
            p_height = $tool_ent_dyn_content_h
        ]
    ]
]
