ui_hud_radar_flags = [compass players affinities]

// 1:name 2:w 3:h 4:body 5:radar
ui_hud_radar = [
    local _dist _offset _border _blipsize _blipsizesmall _blipsizelarge _names _maxdist _flags _shape _bearing

    _dist = $[@[arg1]dist]
    _offset = $[@[arg1]offset]
    _border = $[@[arg1]border]
    _blipsize = $[@[arg1]blipsize]
    _blipsizesmall = $[@[arg1]blipsizesmall]
    _blipsizelarge = $[@[arg1]blipsizelarge]
    _names = $[@[arg1]names]
    _maxdist = $[@[arg1]maxdist]

    _bearing = (=s $arg1 "bearing")
    if $_bearing [
        _shape = 2
    ] [
        _shape = $[@[arg1]shape]
    ]

    local _flags
    _flags = 0
    looplistn i j $ui_hud_radar_flags [
        if $[@@arg1@i] [
            _flags = (| $_flags (<< 1 $j))
        ]
    ]

    doargs $arg4

    uiradar $_dist $_offset $_border $arg2 $arg3 [
        uistyle centermiddle
        uiradarshape $_shape
        uiradarmaxdist $_maxdist

        if (& $_flags 1) [
            local _rot _dir _name
            if $_bearing [
                loop i 16 [
                    _rot = (modf (+f $mapnorth (*f $i 22.5)) 360)
                    _dir = (cameraoffyaw $_rot)
                    _name = (at "N NNE NE ENE E ESE ES SSE S SSW SW WSW W WNW NW NNW" $i)
                    uiradarblip "" 0x00000000 $_dir $_dir (*f $_dist 2.0) $_blipsize $_blipsize [
                        uiradarblippriority 9
                        uicolourblend 0
                        uivlist 0 [
                            uiline 0xFFFFFF 0 0.005
                            uitext $_name (*f $ui_textcompass (? (=f (modf $_rot 90.0) 0.0) 1.0 (? (=f (modf $_rot 45.0) 0.0) 0.875 0.75))) [ uicolourset 0xFFFFFF ]
                            uiline 0xFFFFFF 0 0.005
                        ]
                    ]
                ]
            ] [
                loop i 4 [
                    _rot = (modf (+f $mapnorth (*f $i 90)) 360)
                    _dir = (cameraoffyaw $_rot)
                    _name = (at "N E S W" $i)
                    uiradarblip $bliptex 0x40000000 $_dir $_dir (*f $_dist 2.0) $_blipsize $_blipsize [
                        uiradarblippriority 9
                        uivlist 0 [ uitext $_name $ui_textradar ]
                    ]
                ]
            ]
        ]

        if (& $_flags 2) [
            local _dir _yaw _vel _dist _state _dominator _pcolour _blend _team _tcolour _blend2 _rcolour

            loopclientsif 0 0 i [getclientradarallow $i] [
                _dir = (getclientradardir $i)
                _yaw = (getclientradaryaw $i)
                _vel = (getclientvelocity $i)
                _dist = (getclientradardist $i)
                _state = (getclientstate $i)
                _dominator = (&& (= $_state 0) (getclientisdominator $ui_hud_focus $i))
                _pcolour = (getclientcolour $i $playerundertone $playerundertonelevel $playerundertonemix)

                if $_dominator [
                    _tcolour = (getclientpulsecolour $i $PULSE_DISCO)

                    uiradarblip $playerbliptex (| 0x80000000 $_pcolour) $_dir $_yaw $_dist $_blipsize $_blipsize [
                        uiradarblippriority 3
                        uiimage $dominatedtex $_colour 0 $_blipsize $_blipsize

                        if $_names [
                            uivlist 0 [
                                uitext (getclientname $i 0 0) $ui_textradar [uicolourset $_tcolour]
                                uispace 0 (*f $_blipsize 0.75)
                            ]
                            if $_bearing [ uistyle centerbottom ]
                        ]
                    ]
                ] [
                    _blend = (? (= $_state 0) 255 178)
                    _team = (getclientteam $i)
                    _tcolour = $[team@(at $T_NAMES $_team)colour]
                    _blend2 = (clamp (*f $_blend 0.85) 1 255)
                    _rcolour = (getclientresidualfx $i 3000)
                    if (>= $_rcolour 0) [ _pcolour = $_rcolour ]

                    uiradarblip $[@(at $ui_hud_statetex2 $_state)] (| (<< $_blend2 24) $_tcolour) $_dir (? (= $_state 0) $_yaw $_dir) $_dist $_blipsize $_blipsize [
                        if (= $_state 0) [
                            uiimage $bliptex (| (<< $_blend 24) $_pcolour) 0 $_blipsizesmall $_blipsizesmall
                        ]

                        if $_names [
                            uivlist 0 [
                                uitext (getclientname $i 0 0) $ui_textradar [
                                    uicolourset (| (<< $_blend 24) $_tcolour)
                                ]
                                uispace 0 (*f $_blipsize 0.75)
                            ]
                            if $_bearing [ uistyle centerbottom ]
                        ]
                    ]
                ]
            ]
        ]

        if (& $_flags 4) [
            case $gamemode $G_CAPTURE [
                local _dir _dist _team _tcolour _owner _drop _tex
                loopcaptureif 0 0 i [getcaptureradarallow $i] [
                    _dir = (getcaptureradardir $i)
                    _dist = (getcaptureradardist $i)
                    _team = (getcaptureteam $i)
                    _tcolour = $[team@(at $T_NAMES $_team)colour]
                    _owner = (getcaptureowner $i)
                    _drop = (getcapturedroptime $i)
                    _tex = ""

                    doif (&& $ui_hud_hasaffinity (= $_team $ui_hud_team)) [
                        if (|| (< $_owner 0) (= $_owner $ui_hud_focus)) [
                            _dir = (getcaptureradardir $i 1)
                            _dist = (getcaptureradardist $i 1)
                        ]
                        _tcolour = (getclientpulsecolour $ui_hud_focus $PULSE_DISCO)
                        _tex = $arrowtex
                    ] (|| $_drop (&& (>= $_owner 0) (= $_team $ui_hud_team) (!= $_owner $ui_hud_focus))) [
                        _tex = (? $_bearing $arrowtex $flagtex)
                        _tcolour = (getclientpulsecolour $ui_hud_focus $PULSE_DISCO)
                    ] (!= $_owner $ui_hud_focus) [
                        _tex = (? $_bearing $arrowtex $flagtex)
                    ]

                    if (!=s $_tex "") [
                        uiradarblip $_tex $_tcolour $_dir $_dir $_dist $_blipsizelarge $_blipsizelarge [
                            uiradarblippriority 7

                            if $_names [
                                uivlist 0 [
                                    uispace 0 (*f $_blipsizelarge 0.75)
                                    uitext $[team@(at $T_NAMES $_team)name] $ui_textradar [
                                        uicolourset (| $ui_transor $_tcolour)
                                    ]
                                ]
                                if $_bearing [ uistyle centertop ]
                            ]
                        ]
                    ]
                ]
            ] $G_DEFEND [
                local _dir _dist _team _tcolour _name
                loopdefendif 0 0 i [getdefendradarallow $i] [
                    _dir = (getdefendradardir $i)
                    _dist = (getdefendradardist $i)
                    _team = (getdefendowner $i)
                    _tcolour = $[team@(at $T_NAMES $_team)colour]
                    _name = (getprintable $i)

                    uiradarblip (? $_bearing $arrowtex $pointtex) $_tcolour $_dir $_dir $_dist $_blipsizelarge $_blipsizelarge [
                        uiradarblippriority 7

                        if $_names [
                            uivlist 0 [
                                uispace 0 (*f $_blipsizelarge 0.75)
                                uitext $_name $ui_textradar [
                                    uitextwrap 0.04
                                    uicolourset (| $ui_transor $_tcolour)
                                ]
                            ]
                            if $_bearing [ uistyle centertop ]
                        ]
                    ]
                ]
            ] $G_BOMBER [
                local _dir _dist _team _owner _owner _tex _name
                loopbomberif 0 0 i [getbomberradarallow $i] [
                    _dir = (getbomberradardir $i)
                    _team = (getbomberteam $i)
                    _owner = (getbomberowner $i)
                    _tex = ""
                    _name = ""
                    doif (!= $_team $T_NEUTRAL) [
                        if (&& $ui_hud_hasaffinity (!= $_team $ui_hud_team)) [
                            _tex = $arrowtex
                            _name = "Destroy"
                        ]
                    ] (&& (= $_team $T_NEUTRAL) (!= $_owner $ui_hud_focus)) [
                        _tex = (? $_bearing $arrowtex $bombtex)
                        _name = "Bomb"
                    ]
                    if (!=s $_tex "") [
                        _dist = (getbomberradardist $i)
                        uiradarblip $_tex (getclientpulsecolour $ui_hud_focus $PULSE_DISCO) $_dir $_dir $_dist $_blipsizelarge $_blipsizelarge [
                            uiradarblippriority 7

                            if (&& $_names (!=s $_name "")) [
                                uivlist 0 [
                                    uispace 0 (*f $_blipsizelarge 0.75)
                                    uitext $_name $ui_textradar [
                                        uicolourset (| $ui_transor (getclientpulsecolour $ui_hud_focus $PULSE_DISCO))
                                    ]
                                ]
                                if $_bearing [ uistyle centertop ]
                            ]
                        ]
                    ]
                ]
            ]
        ]

        doargs $arg5
    ]
]

defvarp showradar 0 0 1
deffvarp radardist 0 128 $fvaridxmax
deffvarp radarmaxdist 0 256 $fvaridxmax
deffvarp radaroffset 0 0.5 1
deffvarp radarborder 0 0 1
deffvarp radarblipsize 0 0.015 1
deffvarp radarblipsizesmall 0 0.0085 1
deffvarp radarblipsizelarge 0 0.02 1
defvarp radarcompass 0 0 1
defvarp radarplayers 0 1 1
defvarp radaraffinities 0 1 1
defvarp radarnames 0 0 1
deffvarp radarsize 0 0.25 1
deffvarp radarblend 0 0.5 1
defvarp radaraspect 0 0 1
defvarp radarshape 0 0 1

ui_hud_midradar = [
    uigroup [
        ui_hud_radar "radar" (? $radaraspect (*f $radarsize $uiaspect) $radarsize) $radarsize
        uipropagate [ uicolourblend $radarblend ]
    ]
]

defvarp showminimap 0 0 1
deffvarp minimapscale $fvaridxnonzero 0.22 $fvaridxmax
deffvarp minimapblend $fvaridxnonzero 0.8 $fvaridxmax
deffvarp minimapdist 0 512 $fvaridxmax
deffvarp minimapmaxdist 0 2048 $fvaridxmax
deffvarp minimapoffset 0 0 1
deffvarp minimapborder 0 0.1 1
deffvarp minimapblipsize 0 0.01 1
deffvarp minimapblipsizesmall 0 0.005 1
deffvarp minimapblipsizelarge 0 0.015 1
defvarp minimapcompass 0 1 1
defvarp minimapplayers 0 1 1
defvarp minimapaffinities 0 1 1
defvarp minimapnames 0 0 1
defvarp minimapshape 0 0 1

ui_hud_minimap = [
    local _colour _tex _scale
    _tex = (? $minimapshape $radarsquaretex $radarcircletex)
    _scale = (*f $minimapscale $game_hud_scale)

    uigroup [
        uistyle righttop

        uioffset 0 0.0025 [
            uiimage $_tex 0 0 $_scale $_scale
        ]

        uiminimap $_tex $ui_hud_colour $minimapdist 0.1 $_scale $_scale [
            uiminimapshape $minimapshape
            uigroup [
                ui_hud_radar "minimap" $_scale $_scale
                uipropagate [ uicolourblend $minimapblend ]
            ]
        ]
   ]
]

defvarp showbearing 0 1 1
deffvarp bearingsize $fvaridxnonzero 0.8 $fvaridxmax
deffvarp bearingblend $fvaridxnonzero 1.0 $fvaridxmax
deffvarp bearingdist 0 1024 $fvaridxmax
deffvarp bearingmaxdist 0 2048 $fvaridxmax
deffvarp bearingoffset -1 0 1
deffvarp bearingborder 0 0.025 1
deffvarp bearingblipsize 0 0.018 1
deffvarp bearingblipsizesmall 0 0.008 1
deffvarp bearingblipsizelarge 0 0.026 1
defvarp bearingcompass 0 1 1
defvarp bearingplayers 0 1 1
defvarp bearingaffinities 0 1 1
defvarp bearingnames 0 1 1

ui_hud_bearing = [
    local _colour _width _height
    _width = (*f $bearingsize $game_hud_scale)
    _height = (*f $bearingblipsizelarge (? $bearingnames 3.0 1.5) $game_hud_scale)

    uigroup [
        uistyle centerbottom

        uifill $_width $_height [
            ui_gameui_advshadowhoriz [
                p_colour  = 0x80000000
                p_bgblend = 0.5
            ]
        ]
        uigroup [
            uistyle clampy
            uivlist 0 [
                uistyle clampy
                if $bearingnames [
                    uispace (*f $_height $bearingborder) (*f $_height $bearingborder) [
                        uistyle centertop
                        uiimage $pointsharptex 0x40FFFFFF 0 $bearingblipsize $bearingblipsize
                    ]
                ]
                uispace (*f $_height $bearingborder) (*f $_height $bearingborder) [
                    uistyle centerbottom
                    uiimage $arrowsharptex 0x40FFFFFF 0 $bearingblipsize $bearingblipsize
                ]
            ]
            ui_hud_radar "bearing" $_width $_height
            uipropagate [ uicolourblend $bearingblend ]
        ]
    ]
]
