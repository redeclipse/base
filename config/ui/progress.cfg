defvarp progressinfo 0 1 1
defvarp progresstips 0 1 1

ui_progress = [
    uivlist 0 [
        uistyle clampx
        uistyle lefttop

        if $showloadinglogos [
            uicolour 0x000000 $uiaspect 0 [
                uistyle clampx
                uistyle centertop
                uicolourblend 0.75
                uihlist 0 [
                    uistyle clampx
                    uistyle centertop
                    uispace $ui_padsmall $ui_padsmall [
                        uistyle leftmiddle
                        uiimage $logocroptex 0xFFFFFF 1 0.2 0 [
                            uistyle leftmiddle
                            uiimageaspect 1
                        ]
                    ]
                    uispace $ui_padlarge $ui_padsmall [
                        uistyle rightmiddle
                        uivlist 0 [
                            uistyle centermiddle
                            uifont $textfontbold [
                                uistyle centermiddle
                                uitext (gameui_pretty_version) $ui_textsmall [ uistyle centermiddle ]
                            ]
                        ]
                    ]
                ]
            ]
        ]

        if $arg6 [ doargs $arg6 ]
    ]

    uivlist 0 [
        uistyle clampx
        uistyle leftbottom

        if $arg7 [ doargs $arg7 ]

        uicolour 0x000000 $uiaspect 0 [
            uistyle clampx
            uistyle centerbottom
            uicolourblend 0.75

            uivlist 0 [
                uistyle clampx
                uistyle centerbottom

                uispace $ui_padsmall $ui_padsmall [
                    uistyle clampx
                    uistyle centerbottom
                    uihlist 0 [
                        uistyle clampx
                        uistyle centerbottom
                        uispace $ui_padsmall 0 [
                            uistyle leftmiddle
                            uihlist $ui_padnormal [
                                uistyle leftmiddle
                                uiimage $uiloadtex 0xffffff 0 0.02 0.02 [ uistyle leftmiddle ]
                                if (&& $progressinfo (!=s $arg4 "")) [
                                    uitext $arg4 $ui_text [ uistyle leftmiddle; uitextalign -1 ]
                                ]
                            ]
                        ]
                        uispace $ui_padsmall 0 [
                            uistyle rightmiddle
                            if (&& $progresstips (!=s $arg5 "")) [
                                uitext $arg5 $ui_text [ uistyle rightmiddle; uitextalign -1 ]
                            ]
                        ]
                    ]
                ]

                if (>f $arg3 0.0) [
                    uiclip (*f $uiaspect $arg3) 0.0075 0.0 0.0 [
                        uistyle leftbottom
                        uicolour $colourred $uiaspect 0.0075 [
                            uicolouradd (| 0x40000000 (modcolour $colourdarkred 0.25))
                            uistyle leftbottom
                        ]
                    ]
                ] [
                    uifill (*f $uiaspect $arg3) 0.0075 [ uistyle leftbottom ]
                ]
            ]
        ]
    ]

    if (>f $arg2 0) [
        uigroup [
            uiborderedimageclamped $skinalphatex 0x40000000 0 $ui_texborder $ui_screenborder 0.75 0.25 [
                uiborderedimageclamped $skinshadowtex 0x80000000 0 $ui_texborder $ui_screenborder 0.75 0.25 [
                    uiclip 0.4 0.25 0.0 0.075 [
                        uiimage (modetex) 0x80000000 0 0.4 0.4
                    ]
                    uispace $ui_padlarge $ui_padlarge [
                        uistyle clampxy
                        uivlist 0 [
                            uistyle clampxy
                            uivlist 0 [
                                uistyle clampx
                                uistyle lefttop
                                if (&& (! $emptymap) (|| [= $arg1 $PROGRESS_MAPLOAD] [= $arg1 $PROGRESS_GAMESTATE] [= $arg1 $PROGRESS_GAMEWAIT])) [
                                    uifont $textfontbold [
                                        uistyle clampx
                                        uistyle lefttop
                                        uitext (getmaptitle $mapname) $ui_textxlarge [ uistyle leftmiddle; uitextalign -1; uicolourset $colourorange ]
                                    ]
                                    if (!=s (getmapauthor $mapname) "") [
                                        uispace $ui_padnormal 0 [
                                            uistyle leftmiddle
                                            uistyle clampx
                                            uitext (concat "by" (getmapauthor $mapname)) $ui_text [ uistyle leftmiddle; uitextalign -1; uicolourset $colourgrey ]
                                        ]
                                    ]
                                ] [
                                    uifont $textfontbold [
                                        uistyle clampx
                                        uistyle lefttop
                                        uitext (? $arg1 (at $PROGRESS_NAMES $arg1) "Loading") $ui_textxlarge [ uistyle leftmiddle; uitextalign -1; uicolourset $colourgreen ]
                                    ]
                                    uispace $ui_padnormal 0 [
                                        uistyle leftmiddle
                                        uistyle clampx
                                        uitext "Please wait" $ui_text [ uistyle leftmiddle; uitextalign -1; uicolourset $colourgrey ]
                                    ]
                                ]
                            ]
                            uivlist 0 [
                                uistyle clampx
                                uistyle rightbottom
                                uifont $textfontbold [
                                    uistyle clampx
                                    uistyle rightbottom
                                    uivlist 0 [
                                        uistyle clampx
                                        uistyle rightbottom
                                        if $mutators [
                                            uitext (mutsname $gamemode $mutators 0 64) $ui_text [ uistyle rightmiddle; uitextalign 1; uicolourset $colouryellow ]
                                        ]
                                        uitext (modename $gamemode $mutators) $ui_textxlarge [ uistyle rightmiddle; uitextalign 1; uicolourset $colouryellow ]
                                    ]
                                ]
                                uitext (modedesc $gamemode $mutators 5) $ui_text [ uistyle rightmiddle; uitextalign 1; uicolourset $colourgrey ]
                            ]
                        ]
                    ]
                ]
            ]
            uipropagate [ uicolourblend $arg2 ]
        ]
    ]
]

newui "default" $SURFACE_PROGRESS [
    uistyle clampxy
    ui_progress $progresstype 1.0 $progressamt (getprogresstitle) (tipshow)
]
