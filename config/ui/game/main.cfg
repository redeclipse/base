gameui_begin_ids

ui_gameui_main_dims = [0.5 0.6]

# ui_gameui_main = [
    uivlist 0 [
        uiimage $logotex 0xffffff 1 0.46 0.23

        if $isconnected [
            ui_gameui_prettybutton [
                p_label    = "Change team"
                p_tip      = "Change your team"
                p_on_click = [
                    gameui_open ui_gameui_team
                ]
                p_id       = #(gameui_get_id prettybutton)
            ]
        ]

        ui_gameui_prettybutton [
            p_label    = "Play Online"
            p_tip      = "Browse online matches with other players"
            p_on_click = [
                gameui_open ui_gameui_online
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label    = (? $isonline "Vote Map/Mode" "Offline Match")
            p_tip      = (? $isonline "Begin a vote to change the map and/or mode" "Start an offline match with bots")
            p_on_click = [
                if $isonline [
                    gameui_open ui_gameui_vote
                ] [
                    gameui_open ui_gameui_maps
                ]
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label    = "Player Setup"
            p_tip      = "Customize the game profile and player character"
            p_on_click = [
                gameui_open ui_gameui_player
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        if (ismodelocked 2) [] [
            ui_gameui_prettybutton [
                p_label    = "Map Editor"
                p_tip      = "Create and edit maps in-game"
                p_on_click = [
                    gameui_open ui_gameui_editor
                ]
                p_id       = #(gameui_get_id prettybutton)
            ]
        ]

        ui_gameui_prettybutton [
            p_label    = "Settings"
            p_tip      = "Change game, graphics and sound settings"
            p_on_click = [
                gameui_open ui_gameui_settings
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label = "Support"
            p_tip   = "Game support"
            p_on_click = [
                gameui_open ui_gameui_support
            ]
            p_id    = #(gameui_get_id prettybutton)
        ]

        if $isconnected [
            ui_gameui_prettybutton [
                p_label    = "Leave game"
                p_tip      = "Leave the match and go back to the main menu"
                p_on_click = [
                    gameui_confirm "Are you sure you wish to leave this match?" [
                        disconnect
                    ]
                ]
                p_id       = #(gameui_get_id prettybutton)
            ]
        ]

        ui_gameui_prettybutton [
            p_label    = "Quit to desktop"
            p_tip      = "Leave the game and quit to desktop"
            p_on_click = [
                gameui_confirm "Quit the game to desktop?" [
                    quit
                ]
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        uifill 0 0.05

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ] [
            p_width = $gameui_panel_new_size
        ]
    ]

    if $isconnected [
        #(ui_gameui_backbutton)
    ]
]

gameui_main_handler = [
    togglescores 0
    gameui_open ui_gameui_main
]
gameui_panel_default = gameui_main_handler
