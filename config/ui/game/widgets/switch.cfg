gameui_switch_props = [
    [ p_width     0.3              ]
    [ p_options   [["Off" "On"]]   ]
    [ p_custom    [[result ""]]    ]
    [ p_tip       ""               ]
    [ p_get       [[result $arg1]] ]
    [ p_set       [[result $arg1]] ]
    [ p_on_change []               ]
    [ p_disabled  0                ]
    [ p_id        []               ]
]

// 1:<var> 2:<props>
ui_gameui_switch = [
    @(props $gameui_switch_props arg2)

    local _num_options _val _prev_val _mid_size _has_dots _text _prev_text
    local _anim_state_var _anim_state _anim_state_scroll _dir

    _num_options    = (listlen $p_options)
    _mid_size       = (-f $p_width 0.06)
    _anim_state_var = @(gameui_get_anim_var p_id)
    _anim_state     = (getalias $_anim_state_var)

    if $p_disabled [
        _val      = -1
        _has_dots = 0
    ] [
        _val      = (p_get (get $arg1))
        _has_dots = (<= $_num_options 22)
    ]

    if (=s $_anim_state []) [
        _anim_state = $_val
    ]

    // Check if we have a change in value
    if (&& [
        !=s $gameui_interact_helper []
        ] [
            = (at $gameui_interact_helper 0) $p_id
        ] [
            > (listlen $gameui_interact_helper) 1
        ]) [
        _dir               = (at $gameui_interact_helper 1)
        _prev_val          = $_val
        _val               = (+ (p_get $$arg1) $_dir)
        _anim_state_scroll = 0

        set $arg1 (p_set $_val)

        p_on_change

        // Clear the change
        gameui_interact_helper = []
    ] [
        _prev_val          = (at $_anim_state 0)
        _dir               = (at $_anim_state 1)
        _anim_state_scroll = (at $_anim_state 2)
    ]

    _text      = (? (&& (>= $_val      0) (< $_val      $_num_options)) (at $p_options $_val)      (p_custom))
    _prev_text = (? (&& (>= $_prev_val 0) (< $_prev_val $_num_options)) (at $p_options $_prev_val) (p_custom))

    uitarget $p_width 0 [
        uihover [
            if (gameui_can_interact) [
                gameui_hover 1
            ]
        ]

        uihlist 0 [
            uistyle clampx

            ui_gameui_button [
                p_width          = 0.03
                p_height         = 0.03
                p_on_click       = [
                    gameui_interact_helper = [@@@p_id -1]
                ]
                p_disabled       = @(|| $p_disabled [<= $_val 0])
                p_icon_size      = 0.01
                p_children       = [
                    uitriangle $_colour $_icon_size $_icon_size 90
                ]
                p_tip            = [@@p_tip]
                p_id             = @(+ $p_id 1)
                p_sound_activate = S_UI_ACTION_DISABLE
            ]

            uiborderedimageclamped $skintex (? $p_disabled 0x44010101 0x22010101) 0 $ui_texborder $ui_screenborder $_mid_size 0.03 [
                uivlist 0 [
                    local _clip_offset _prev_fade _cur_fade

                    if (<f $_anim_state_scroll 1) [
                        // Animated text scroll transition

                        caseif (< $_dir 0) [
                            _clip_offset = (lerpf $_mid_size 0 (smoothstep $_anim_state_scroll))
                        ] (> $_dir 0) [
                            _clip_offset = (lerpf 0 $_mid_size (smoothstep $_anim_state_scroll))
                        ]

                        _prev_fade = (lerpf 1 0 (gameui_anim_segment $_anim_state_scroll 0 0.25))
                        _cur_fade  = (lerpf 0 1 (gameui_anim_segment $_anim_state_scroll 0.75 1))
                    ] [
                        _clip_offset = 0
                        _cur_fade    = 1
                        _dir         = 0
                    ]

                    uiclip $_mid_size 0 $_clip_offset 0 [
                        uihlist 0 [
                            if (> $_dir 0) [
                                uifill $_mid_size 0 [
                                    uicolourtext $_prev_text (? $p_disabled 0x888888 0xffffff) 0.95 [
                                        uipropagate [uicolourblend $_prev_fade]
                                    ]
                                ]
                            ]
                            uifill $_mid_size 0 [
                                uicolourtext $_text (? $p_disabled 0x888888 0xffffff) 0.95 [
                                    uipropagate [uicolourblend $_cur_fade]
                                ]
                            ]
                            if (< $_dir 0) [
                                uifill $_mid_size 0 [
                                    uicolourtext $_prev_text (? $p_disabled 0x888888 0xffffff) 0.95 [
                                        uipropagate [uicolourblend $_prev_fade]
                                    ]
                                ]
                            ]
                        ]
                    ]

                    if $_has_dots [
                        local _ref_x _prev_x _target_x _dot_anim
                        _target_x = 0

                        uigroup [
                            // Required for calculating the relative position of the dots
                            _ref_x = $uilastsx

                            uihlist 0.005 [
                                loop i $_num_options [
                                    uiimage $bliptex 0x88888888 0 0.004 0.004 [
                                        if (= $i $_prev_val) [
                                            _prev_x = (-f $uilastsx $_ref_x)
                                        ]
                                        if (= $i $_val) [
                                            _target_x = (-f $uilastsx $_ref_x)
                                        ]
                                    ]
                                ]
                            ]

                            if (>= $_val 0) [
                                if (<f $_anim_state_scroll 1) [
                                    // Animated dot scroll transition

                                    _dot_anim = (smoothstep (gameui_anim_segment $_anim_state_scroll 0 0.5))
                                    _dot_anim = (lerpf $_prev_x $_target_x $_dot_anim)
                                ] [
                                    _dot_anim = $_target_x
                                ]

                                uioffset $_dot_anim 0 [
                                    uialign -1

                                    uiimage $bliptex 0xcccccc 0 0.006 0.006
                                ]
                            ] [
                                uifill 0 0.006
                            ]
                        ]

                        uifill 0 0.002
                    ] [
                        uifill 0 0.008
                    ]
                ]

                ui_gameui_shadow
            ]

            ui_gameui_button [
                p_width          = 0.03
                p_height         = 0.03
                p_on_click       = [
                    gameui_interact_helper = [@@@p_id 1]
                ]
                p_disabled       = @(|| $p_disabled [>= $_val (- $_num_options 1)])
                p_icon_size      = 0.01
                p_children       = [
                    uitriangle $_colour $_icon_size $_icon_size -90
                ]
                p_tip            = [@@p_tip]
                p_id             = @(+ $p_id 2)
                p_sound_activate = S_UI_ACTION_ENABLE
            ]
        ]
    ]

    _anim_state_scroll = (animstep $_anim_state_scroll 200 1)
    $_anim_state_var  = [@_prev_val @_dir @_anim_state_scroll]
]
gameui_widget_register switch 3

gameui_compactswitch_props = [
    [ p_label_size 1                ]
    [ p_size       0.025            ]
    [ p_tip        ""               ]
    [ p_get        [[result $arg1]] ]
    [ p_set        [[result $arg1]] ]
    [ p_on_change  []               ]
    [ p_id         []               ]
]

// 1:<var> 2:<props>
ui_gameui_compactswitch = [
    @(props $gameui_compactswitch_props arg2)

    // Check if we have a change in value
    if (= $gameui_interact_helper $p_id) [
        local _new_val
        _new_val = (! (p_get $$arg1))
        set $arg1 (p_set $_new_val)

        p_on_change

        // Clear the change
        gameui_interact_helper = []
    ]

    local _val
    _val = (p_get $$arg1)

    uitarget 0 0 [
        ui_gameui_button [
            p_label_size = @p_label_size
            p_width      = @p_size
            p_height     = @p_size
            p_on_click   = [
                gameui_interact_helper = @@p_id
            ]
            p_children   = [
                local _size _val _colour
                _val         = @@_val
                _size        = @@p_size

                if $_val [
                    _size = (*f $_size (lerpf 0.5 0.3 $_anim_state_activate))
                ] [
                    _size = (*f $_size (lerpf 0.3 0.5 $_anim_state_activate))
                ]

                _colour = (? $_val 0xcccccc 0x88888888)
                uiborderedimageclamped $skintex $_colour 0 $ui_texborder $ui_screenborder $_size $_size
            ]
            p_tip            = [@@p_tip]
            p_sound_activate = (? @_val S_UI_ACTION_DISABLE S_UI_ACTION_ENABLE)
            p_id             = @p_id
        ]
    ]
]
gameui_widget_register compactswitch 1
