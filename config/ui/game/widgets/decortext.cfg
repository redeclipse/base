gameui_decortext_props = [
    [ p_label_size      2                 ]
    [ p_colour          0xffffff          ]
    [ p_light_intensity 0.15              ]
    [ p_pad_h           0.05              ]
    [ p_pad_v           0.01              ]
    [ p_width           0.9               ]
]

// 1:<text> 2:<props>
# ui_gameui_decortext = [
    @(props $gameui_decortext_props arg2)

    local _anim_state _light_blend _light_blend_anim _light_len
    _anim_state       = (*f (+f (sin (*f $totalmillis 0.1)) 1) 0.5)
    _light_blend_anim = (lerpf 0.25 1 $_anim_state)
    _light_blend      = (*f $_light_blend_anim (lerpf 1 $p_light_intensity $_light_blend_anim))
    _light_len        = (*f $p_width (lerpf 0.66 1 $_anim_state))

    uifill $p_width 0 [
        uiclip $p_width 0.06 (*f $p_width 1.5) 0 [
            uifill (*f $p_width 3) 0.05 [
                local _anim_x _anim_y
                _anim_x = (*f (sin (*f $totalmillis 0.04)) $p_width 0.75)
                _anim_y = (*f (cos (*f $totalmillis 0.04)) 0.01)

                uipad (+f $_anim_x $p_width) 0 (+f $_anim_y 0.01) 0 [
                    uiimage $hinttex #(hsvtohex [8 0.2 1]) 0 0.5 0.05 [
                        uipropagate [uicolourblend $_light_blend]
                    ]
                ]
            ]
        ]

        uiimage "<comp>stain" #(hsvtohex [8 0.8 1]) 0 $_light_len 0.02 [
            uipropagate [uicolourblend $_light_blend]
        ]

        uiimage "<comp>stain" #(hsvtohex [8 0.1 1]) 0 $_light_len 0.01 [
            uipropagate [uicolourblend $_light_blend]
        ]

        uiimage $hintenttex #(hsvtohex [8 0.2 1]) 0 $_light_len 0.04 [
            uipropagate [uicolourblend (*f $_light_blend 0.75)]
        ]

        uispace $p_pad_h $p_pad_v [
            uiimage $hintboldtex #(hsvtohex [8 0.5 0.5]) 0 0 0 [
                uipropagate [uicolourblend (*f $_light_blend 0.33)]
            ]
            uiprev [uistyle clampxy]

            uicolourtext $arg1 $p_colour $p_label_size

            uialign $p_label_align
        ]
    ]
]
