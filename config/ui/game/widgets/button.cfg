loadsound S_UI_ACTION_LONG "sounds/interface/ui_action_long" 0.8 1 1 -1 -1 1
loadsound S_UI_HOVER_FANCY "sounds/interface/ui_fancy_hover" 0.8 1 1 -1 -1 1

# gameui_prettybutton_props [
    [ p_label            ""                       ]
    [ p_label_size       2                        ]
    [ p_tip              ""                       ]
    [ p_label_align      1                        ]
    [ p_colour           #(hsvtohex [8 0.5 0.5])  ]
    [ p_highlight_colour #(hsvtohex [8 0.2 0.8])  ]
    [ p_sel_colour       0xffffffff               ]
    [ p_light_intensity  0.33                     ]
    [ p_pad_h            0.0486                   ]
    [ p_pad_h_shift      0.0252                   ]
    [ p_pad_v            0.0075                   ]
    [ p_id               []                       ]
    [ p_anim_on_time     100                      ]
    [ p_anim_off_time    200                      ]
    [ p_width            0.45                     ]
    [ p_on_click         []                       ]
    [ p_on_hover         []                       ]
    [ p_disabled         0                        ]
    [ p_highlight        0                        ]
    [ p_sound_activate   S_UI_ACTION_LONG         ]
    [ p_sound_hover      S_UI_HOVER_FANCY         ]
]

// 1:<props>
# ui_gameui_prettybutton [
    @(props $gameui_prettybutton_props arg1)

    local _colour _anim_state_var _anim_state _anim_state_hover _anim_state_activate _flash _light_blend _light_len
    _colour              = (? $p_disabled 0x55010101 (? $p_highlight $p_highlight_colour $p_colour))
    _anim_state_var      = @(gameui_get_anim_var p_id)
    _anim_state          = (getalias $_anim_state_var)
    _anim_state_hover    = (at $_anim_state 0)
    _anim_state_activate = (at $_anim_state 1)
    _flash               = 0

    _anim_state_activate = (animstep $_anim_state_activate $p_anim_off_time -1)
    p_light_intensity = (lerpf $p_light_intensity 1 (gameui_anim_flash (-f 1 $_anim_state_activate) 1 1))

    uitarget $p_width 0 [
        if $p_disabled [] [
            uihover [
                if (gameui_can_interact) [
                    gameui_hover

                    _anim_state_hover = (animstep $_anim_state_hover $p_anim_on_time 1)
                    _light_blend      = (*f $_anim_state_hover (lerpf 1 $p_light_intensity $_anim_state_hover))
                    _flash            = 1
                    _colour           = $p_sel_colour

                    p_on_hover
                ]
            ] [
                _anim_state_hover = (animstep $_anim_state_hover $p_anim_off_time -1)
                _light_blend      = (*f $_anim_state_hover $p_light_intensity)
            ]

            uirelease [
                _anim_state_activate = 1
                soundbyname $p_sound_activate
                sleep 0 [
                    local _func
                    _func = [@@p_on_click]
                    _func @uigetlastsx @uigetlastsy @uigetlastw @uigetlasth
                ]
            ]
        ]

        _light_len = (*f $p_width $_anim_state_hover)

        uiimage "particles/plasma" #(hsvtohex [8 0.8 1]) 0 $_light_len 0.01 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/plasma" #(hsvtohex [8 0.1 1]) 0 $_light_len 0.005 [
            uichangeblends $_light_blend
        ]

        uiimage "!hintent" #(hsvtohex [8 0.2 1]) 0 $_light_len 0.04 [
            uichangeblends (*f $_light_blend 0.75)
        ]

        if $_flash [
            uiimage "!hint" 0xffffff 0 $_light_len (*f $_anim_state_hover 0.04) [
                uichangeblends (gameui_anim_flash $_anim_state_hover 1 1)
            ]
        ]


        if (&& $p_highlight [! $p_disabled]) [
            uiimage "!hintbold" #(hsvtohex [8 0.5 1]) 0 (*f (gameui_anim_pulse 0.2 0.5 1) $p_width) (gameui_anim_pulse 0.1 0.01 0.03) [
                uichangeblends (gameui_anim_pulse 0.2 0.2 0.3)
            ]

            uiimage "!hintbold" #(hsvtohex [8 0.1 1]) 0 (*f (gameui_anim_pulse 0.2 0.5 1) $p_width) (gameui_anim_pulse 0.1 0.005 0.01) [
                uichangeblends (gameui_anim_pulse 0.2 0.2 0.3)
            ]
        ]

        uispace (+f $p_pad_h (*f $_anim_state_hover $p_pad_h_shift)) $p_pad_v [
            uiimage "!hintbold" #(hsvtohex [8 0.5 1]) 0 0 0 [
                uichangeblends (*f $_anim_state_hover $_anim_state_hover $_light_blend)
            ]

            uiclamp- 1 1 1 1

            uicolourtext $p_label $_colour $p_label_size

            uialign $p_label_align
        ]
    ]

    $_anim_state_var = [@_anim_state_hover @_anim_state_activate]
]
gameui_widget_register prettybutton 1

gameui_button_props = [
    [ p_label          ""              ]
    [ p_label_size     1.5             ]
    [ p_tip            ""              ]
    [ p_icon           ""              ]
    [ p_icon_size      0.1             ]
    [ p_children       []              ]
    [ p_colour         0xcccccc        ]
    [ p_background     0               ]
    [ p_width          0.18            ]
    [ p_height         0.05            ]
    [ p_on_click       []              ]
    [ p_on_press       []              ]
    [ p_on_hover       []              ]
    [ p_border_scale   0.5             ]
    [ p_anim_on_time   100             ]
    [ p_anim_off_time  200             ]
    [ p_id             []              ]
    [ p_disabled       0               ]
    [ p_highlight      0               ]
    [ p_sound_activate S_UI_ACTION     ]
    [ p_sound_hover    S_UI_HOVER      ]
]

# ui_gameui_button [
    @(props $gameui_button_props arg1)

    local _anim_state_var _anim_state _anim_state_hover _anim_state_activate _border1 _border2 _hovering _icon_size
    _colour              = (? $p_disabled 0x55010101 $p_colour)
    _anim_state_var      = @(gameui_get_anim_var p_id)
    _anim_state          = (getalias $_anim_state_var)
    _anim_state_hover    = (at $_anim_state 0)
    _anim_state_activate = (at $_anim_state 1)
    _hovering            = 0

    _anim_state_activate = (animstep $_anim_state_activate 175 -1)

    uitarget $p_width $p_height [
        if $p_disabled [] [
            uihover [
                if (gameui_can_interact) [
                    gameui_hover

                    _anim_state_hover = (animstep $_anim_state_hover $p_anim_on_time 1)
                    _hovering         = 1

                    p_on_hover
                ]
            ] [
                _anim_state_hover = (animstep $_anim_state_hover $p_anim_off_time -1)
            ]

            uipress [
                if (!=s $p_on_press "") [
                    _anim_state_activate = 1
                    soundbyname $p_sound_activate
                    p_on_press
                ]
            ]

            uirelease [
                if (!=s $p_on_click "") [
                    _anim_state_activate = 1
                    soundbyname $p_sound_activate
                    sleep 0 [
                        local _func
                        _func = [@@p_on_click]
                        _func @uigetlastsx @uigetlastsy @uigetlastw @uigetlasth
                    ]
                ]
            ]
        ]

        _border1 = (+f 0.566667 (*f 0.3 $p_border_scale) (*f -0.066667 $p_border_scale $p_border_scale))
        _border2 = (*f $p_border_scale 3)

        _border1 = (*f $ui_texborder    (lerpf 1 $_border1 $_anim_state_hover))
        _border2 = (*f $ui_screenborder (lerpf 1 $_border2 $_anim_state_hover))

        _icon_size = (lerpf (*f $p_icon_size 0.9) $p_icon_size $_anim_state_hover)

        uiborderedimageclamped $skintex 0x222222 0 $ui_texborder $ui_screenborder 0 0 [
            uiclamp 1 1 1 1

            uichangeblends (? $p_background 1 (lerpf 0.8 1 $_anim_state_hover))
        ]

        if (>f $_anim_state_activate 0) [
            uiborderedimageclamped $blurskintex 0xffffff 0 $_border1 $_border2 0 (*f $p_height (-f 1 $_anim_state_activate)) [
                uiclamp 1 1

                uichangeblends (*f $_anim_state_activate 0.5)
            ]
        ]

        uiborderedimageclamped $skinbordertex (? (|| $p_highlight $_hovering) $_colour 0x666666) 0 $_border1 $_border2 0 0 [
            uiclamp 1 1 1 1

            uiborderedimageclamped "<invert>textures/hud/uiskinshadow" (? (|| $p_highlight $_hovering) $_colour 0x666666) 0 $_border1 $_border2 0 0 [
                uiclamp 1 1 1 1
                uichangeblends 0.5
            ]

            p_children

            caseif (!=s $p_icon "") [
                uiimage $p_icon $_colour 0 $_icon_size $_icon_size [
                    uichangeblends (lerpf 0.8 1 $_anim_state_hover)
                ]
            ] (!=s $p_label "") [
                uicolourtext $p_label $_colour (lerpf (*f $p_label_size 0.9) $p_label_size $_anim_state_hover) [
                    uichangeblends (lerpf 0.8 1 $_anim_state_hover)
                ]
            ]

            uichangeblends (-f 1 (*f (gameui_anim_flash $_anim_state_hover 1 1) 0.4))
        ]

        ui_gameui_shadow
    ]

    $_anim_state_var = [@_anim_state_hover @_anim_state_activate]
]
gameui_widget_register button 1
