gameui_pages_props = [
    [ p_width       0            ]
    [ p_height      0            ]
    [ p_can_prev    [[result 1]] ]
    [ p_can_next    [[result 1]] ]
    [ p_on_finish   []           ]
    [ p_finish_text "Complete"   ]
    [ p_id          []           ]
]

// 1:<var> 2:<pages> 3:<props>
# ui_gameui_pages [
    @(props $gameui_pages_props arg3)

    local _num_pages _page _prev_page _mid_size _content _prev_content
    local _anim_state_var _anim_state _anim_state_scroll _dir

    _num_pages      = (listlen $arg2)
    _anim_state_var = @(gameui_get_anim_var p_id)
    _anim_state     = (getalias $_anim_state_var)
    _page           = $$arg1

    if (=s $_anim_state []) [
        _anim_state = 0
    ]

    // Check if we have a change in value
    if (&& [
        !=s $gameui_interact_helper []
        ] [
            = (at $gameui_interact_helper 0) $p_id
        ] [
            > (listlen $gameui_interact_helper) 1
        ]) [
        _dir               = (at $gameui_interact_helper 1)
        _prev_page         = $_page
        _page              = (+ $_page $_dir)
        _anim_state_scroll = 0

        set $arg1 $_page

        // Clear the change
        gameui_interact_helper = []
    ] [
        _prev_page         = (at $_anim_state 1)
        _dir               = (at $_anim_state 2)
        _anim_state_scroll = (at $_anim_state 3)
    ]

    _content      = (at $arg2 $_page)
    _prev_content = (at $arg2 $_prev_page)

    uivlist 0.025 [
        uistyle clampx

        local _clip_offset _prev_fade _cur_fade
        if (<f $_anim_state_scroll 1) [
            // Animated content scroll transition

            caseif (< $_dir 0) [
                _clip_offset = (lerpf $p_width 0 (smoothstep $_anim_state_scroll))
            ] (> $_dir 0) [
                _clip_offset = (lerpf 0 $p_width (smoothstep $_anim_state_scroll))
            ]

            _prev_fade = (lerpf 1 0 (gameui_anim_segment $_anim_state_scroll 0 0.25))
            _cur_fade  = (lerpf 0 1 (gameui_anim_segment $_anim_state_scroll 0.75 1))
        ] [
            _clip_offset = 0
            _cur_fade    = 1
            _dir         = 0
        ]

        ui_gameui_horizgroup [
            uiclip $p_width $p_height $_clip_offset 0 [
                uifill $p_width $p_height

                uihlist 0 [
                    if (> $_dir 0) [
                        uifill $p_width 0 [
                            $_prev_content
                            uipropagate [uicolourblend $_prev_fade]
                        ]
                    ]
                    uifill $p_width 0 [
                        $_content
                        uipropagate [uicolourblend $_cur_fade]
                    ]
                    if (< $_dir 0) [
                        uifill $p_width 0 [
                            $_prev_content
                            uipropagate [uicolourblend $_prev_fade]
                        ]
                    ]
                ]
            ]
        ] [
            p_width  = @p_width
            p_height = @p_height
        ]

        ui_gameui_horizgroup [
            uistyle clampx

            uihlist 0 [
                uistyle clampx

                if (> $_page 0) [
                    ui_gameui_button [
                        p_label       = "<"
                        p_label_size  = 2
                        p_width       = 0.05
                        p_height      = 0.05
                        p_on_click    = [
                            gameui_interact_helper = [@@@p_id -1]
                            soundbyname S_UI_SWOOSH
                        ]
                        p_disabled    = @(! (p_can_prev $_page))
                        p_id          = @(+ $p_id 1)
                    ]
                    uiprev [uialign -1]
                ]

                if (< $_page (- $_num_pages 1)) [
                    ui_gameui_button [
                        p_label       = ">"
                        p_label_size  = 2
                        p_width       = 0.05
                        p_height      = 0.05
                        p_on_click    = [
                            gameui_interact_helper = [@@@p_id 1]
                            soundbyname S_UI_SWOOSH
                        ]
                        p_disabled    = @(! (p_can_next $_page))
                        p_id          = @(+ $p_id 2)
                    ]
                    uiprev [uialign 1]
                ] [
                    if (!=s $p_on_finish []) [
                        ui_gameui_button [
                            p_label       = @p_finish_text
                            p_width       = 0.18
                            p_height      = 0.05
                            p_on_click    = [@@@p_on_finish]
                            p_id          = @(+ $p_id 2)
                            p_colour      = #(hsvtohex [8 0.3 1])
                            p_highlight   = 1
                        ]
                        uiprev [uialign 1]
                    ]
                ]
            ]
        ]
    ]

    _anim_state_scroll = (animstep $_anim_state_scroll 500 1)
    $_anim_state_var   = [@_page @_prev_page @_dir @_anim_state_scroll]
]
gameui_widget_register pages 3
