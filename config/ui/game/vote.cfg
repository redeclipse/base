gameui_begin_ids

gameui_vote_entry_counter = 0

ui_gameui_vote_entry_mapshot = [
    uipad 0.02 0 0 0 [
        uialign -1
        uicolour 0 0.2 0.1 [
            uitext "?" 4
            uicroppedimage (concatword "maps/" $_mapname) 0xffffff 0 0.2 0.1 0 0.25 1 0.75 [
                uifill 0.2 0.1 [
                    uioffset 0 0.06 [
                        uistyle clampxy

                        uicolour 0x66010101 0 0.025 [
                            ui_gameui_shadowhoriz
                        ]
                        uiprev [uistyle clampx]
                    ]
                    uispace 0.01 0.01 [
                        uistyle rightbottom

                        ui_gameui_autoscrollh [
                            uitext $_maptitle
                        ] [
                            p_width = 0.18
                        ]
                    ]
                ]
            ]
        ]
    ]
]

# ui_gameui_vote_entry_info = [
    uispace 0.01 0.01 [
        uistyle clampxy

        uistyle lefttop

        uifill 0 0 [
            uistyle clampxy

            uivlist 0 [
                uistyle lefttop

                ui_gameui_autoscrollh [
                    uitext $_gamemode 1.3
                ] [
                    p_width = 0.54
                ]

                uipropchild [uialign -1]
            ]

            uihlist 0 [
                uistyle rightbottom

                uicolourtext $_votes 0xcccccc 1.2
                uioffset 0 0.003 [
                    uicolourtext " votes" 0xaaaaaa
                ]
            ]
        ]
    ]
]

# ui_gameui_vote_entry = [
    local _bg

    _bg = (? (& $gameui_vote_entry_counter 1) 0x555555 0x888888)

    uiborderedimageclamped $skintex $_bg 0 $ui_texborder $ui_screenborder 0.9 0.1 [
        if $uidrawn [
            local _mapname _maptitle _gamemode _votes
            _mapname  = (getvote $arg1 3)
            _maptitle = (getmaptitle $_mapname)
            _gamemode = (gamename (getvote $arg1 1) (getvote $arg1 2) 0 64)
            _votes    = (getvote $arg1 0 -1)

            uihover [
                gameui_tip      = "Double-click to vote"
                gameui_hovering = 1

                uipropagate [uicolourblend 0.66]
            ] [
                uipropagate [uicolourblend 0.33]
            ]

            uidoublepress $arg1 [
                gameui_close_all
                start $_mapname (getvote $arg1 1) (getvote $arg1 2)
            ]

            ui_gameui_vote_entry_mapshot

            uifill (-f 0.9 0.2 0.02) 0 [
                uistyle clampy
                uialign 1

                ui_gameui_vote_entry_info
            ]
        ]

        ui_gameui_shadow
    ]

    gameui_vote_entry_counter = (+ $gameui_vote_entry_counter 1)
]

ui_gameui_vote_dims = 1.2

# ui_gameui_vote = [
    gameui_vote_entry_counter = 0

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Vote" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uihlist 0.02 [
            uialign -1 -2

            uispace 0.02 0 [
                uistyle lefttop

                uivlist 0.005 [
                    if (= $gamestate $G_S_VOTING) [
                        uigroup [
                            uitext "" 1.5
                            uicolourtext (timestr $gametimeremain $showtimestyle) (pulsecolour $PULSE_FLASH) (gameui_anim_pulse 0.2 1.2 1.5)
                        ]
                        uifill 0 0.01
                    ]

                    ui_gameui_button [
                        p_label       = "New vote"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_tip         = "Start a new vote"
                        p_on_click    = [
                            gameui_open ui_gameui_maps
                        ]
                        p_highlight   = 1
                        p_id          = #(gameui_get_id button)
                    ]

                    ui_gameui_button [
                        p_label       = "Cancel vote"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_tip         = "Cancel your vote"
                        p_on_click    = [
                            clearvote
                        ]
                        p_id          = #(gameui_get_id button)
                    ]
                ]
            ]

            uivlist 0 [
                ui_gameui_vscrollarea [
                    uiborderedimageclamped $skintex 0x44010101 0 $ui_texborder $ui_screenborder 0.9 0.5 [
                        if (= (getvote -1)) [
                            uitext "No votes yet"
                        ] [
                            uivlist 0 [
                                uistyle lefttop

                                loopvotes i [
                                    ui_gameui_vote_entry $i
                                ]
                            ]
                        ]
                    ]
                ] [
                    p_width  = 0.9
                    p_height = 0.5
                ]

                ui_gameui_chat 1 0.9
            ]
        ]

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ] [
            p_width = $gameui_panel_new_size
        ]
    ]

    #(ui_gameui_backbutton)
]

gameui_vote_show = [
    local _can_show_vote
    _can_show_vote = 1

    // Don't show the vote screen if the chat is open
    if $gameui_chat_typing [
        _can_show_vote = 0
    ]

    // Don't show the vote screen if menu is open
    if (gameui_has_panel) [
        _can_show_vote = 0
    ]

    if (&& [= $gamestate $G_S_VOTING] [! (gameui_panel_isopen ui_gameui_vote)]) [
        if $_can_show_vote [
            gameui_open ui_gameui_vote
        ] [
            sleep 100 [gameui_vote_show]
        ]
    ]
]
onevent $CMD_EVENT_GAME_VOTE gameui_vote_show
