///////////////
// CONSTANTS //
///////////////



// Player menu content types

// Main player menu
GAMEUI_PLAYER_CONTENT_BASIC          = 0
// Vanity player menu
GAMEUI_PLAYER_CONTENT_VANITY         = 1
// Loadout player menu
GAMEUI_PLAYER_CONTENT_LOADOUT        = 2

// Vanity line thickness
GAMEUI_PLAYER_VANITY_LINE_THICKNESS  = 0.0015

// Vanoty line curve size
GAMEUI_PLAYER_VANITY_LINE_CURVE_SIZE = 0.013

// Vanity preview yaw values
GAMEUI_PLAYER_VANITY_PREVIEW_YAWS = [
    140 // 0
    210 // 1
    150 // 2
    140 // 3
    190 // 4
    140 // 5
    150 // 6
    320 // 7
    140 // 8
    90  // 9
    200 // 10
    100 // 11
    300 // 12
]



///////////
// STATE //
///////////



gameui_player_first_show       = 1
gameui_player_content_shortcut = 0

gameui_player_content_type = -1
gameui_player_content_old  = []
gameui_player_content_new  = []

gameui_player_content_old_sizes = [0 0]
gameui_player_content_new_sizes = [0 0]

gameui_player_content_anim  = 1

gameui_player_name         = ""
gameui_player_model        = 0
gameui_player_colour       = 0
gameui_player_colour_h     = 0
gameui_player_colour_s     = 1000
gameui_player_colour_v     = 1000
gameui_player_scolour      = 0
gameui_player_scolour_h    = 0
gameui_player_scolour_s    = 1000
gameui_player_scolour_v    = 1000
gameui_player_pattern      = 0
gameui_player_vanities     = []
gameui_player_vanities_ids = []

gameui_player_vanity_type_focus_interp   = [0 0 0 140]
gameui_player_vanity_type_focus_anim     = 1
gameui_player_vanity_type_focus          = -1
gameui_player_vanity_type_highlight_anim = 0
gameui_player_vanity_type_highlight      = -1
gameui_player_vanity_type_highlight_from = 0
gameui_player_vanity_type_highlight_to   = 0

gameui_player_vanity_type_positions      = []

gameui_player_loadout_cur_slot     = 0
gameui_player_loadout_preview      = $W_PISTOL
gameui_player_loadout_slots        = []
gameui_player_loadout_filter       = 0

// 1:<content type> 2:<no anim> 3:<shortcut>
gameui_player_set_content = [
    local _prev_content
    _prev_content = $gameui_player_content_type

    gameui_player_content_old      = $gameui_player_content_new
    gameui_player_content_type     = $arg1
    gameui_player_content_shortcut = $arg3

    case $arg1 $GAMEUI_PLAYER_CONTENT_BASIC [
        gameui_player_content_new     = [[] ui_gameui_player_basic]
        gameui_player_loadout_preview = $W_PISTOL
    ] $GAMEUI_PLAYER_CONTENT_VANITY [
        gameui_player_content_new     = [ui_gameui_player_vanity_l ui_gameui_player_vanity_r]
        gameui_player_loadout_preview = $W_MELEE
    ] $GAMEUI_PLAYER_CONTENT_LOADOUT [
        gameui_player_content_new     = [[] ui_gameui_player_loadout]
        gameui_player_loadout_preview = $W_PISTOL
    ]

    if (!= $_prev_content $gameui_player_content_type) [
        gameui_player_content_old_sizes = $gameui_player_content_new_sizes

        case $gameui_player_content_type $GAMEUI_PLAYER_CONTENT_BASIC [
            gameui_player_content_new_sizes = [0    0.5 ]
        ] $GAMEUI_PLAYER_CONTENT_VANITY [
            gameui_player_content_new_sizes = [0.32 0.32]
        ] $GAMEUI_PLAYER_CONTENT_LOADOUT [
            gameui_player_content_new_sizes = [0    0.5 ]
        ]

        gameui_player_content_anim = (? $arg2 1 0)
    ]
]

// Finds the vanity index from the vanity name
// 1:<name>
gameui_player_find_vanity = [
    local _result
    _result = -1

    loopwhile i (getvanity -1) [< $_result 0] [
        if (=s (getvanity $i 1) $arg1) [
            _result = $i
        ]
    ]

    result $_result
]

// Reset the preview to the current settings
gameui_player_reset = [
    gameui_player_name     = $playername
    gameui_player_model    = $playermodel
    gameui_player_colour   = $playercolour
    gameui_player_scolour  = $playerscolour
    gameui_player_pattern  = $playerpattern
    gameui_player_vanities = $playervanity

    // Calculate helper color values
    local _hsv
    _hsv = (inttohsv $gameui_player_colour)

    gameui_player_colour_h = (at $_hsv 0)
    gameui_player_colour_s = (toint (*f (at $_hsv 1) 1000))
    gameui_player_colour_v = (toint (*f (at $_hsv 2) 1000))

    _hsv = (inttohsv $gameui_player_scolour)

    gameui_player_scolour_h = (at $_hsv 0)
    gameui_player_scolour_s = (toint (*f (at $_hsv 1) 1000))
    gameui_player_scolour_v = (toint (*f (at $_hsv 2) 1000))

    // Get helper vanity IDs
    gameui_player_vanities_ids = []
    loop i (listlen $gameui_player_vanities) [
        append gameui_player_vanities_ids (gameui_player_find_vanity (at $gameui_player_vanities $i))
    ]

    // Get loadout
    gameui_player_loadout_slots  = $playerloadweap

    // Get loadout random filter
    gameui_player_loadout_filter = 0
    loop i (listlen $playerrandweap) [
        if (at $playerrandweap $i) [
            gameui_player_loadout_filter = (| $gameui_player_loadout_filter (<< 1 $i))
        ]
    ]
]

// Update the player appearance to match the preview
gameui_player_set = [
    playername     $gameui_player_name
    playermodel    $gameui_player_model
    playercolour   $gameui_player_colour
    playerscolour  $gameui_player_scolour
    playerpattern  $gameui_player_pattern
    playervanity   $gameui_player_vanities

    // Don't attempt to set loadout if it's invalid
    if (gameui_player_loadout_validate) [
        playerloadweap $gameui_player_loadout_slots

        // Set loadout random filter
        local _tmp
        _tmp = []
        loop i 32 [
            append _tmp (& $gameui_player_loadout_filter (<< 1 $i))
        ]
        playerrandweap $_tmp
    ]
]

gameui_player_calc_colour = [
    local _sat _val
    _sat = (divf $gameui_player_colour_s 1000)
    _val = (divf $gameui_player_colour_v 1000)

    gameui_player_colour = (hsvtohex [@gameui_player_colour_h @_sat @_val])

    // Value needs to be above 0
    if $gameui_player_colour [] [
        gameui_player_colour = 0x010101
    ]
]

gameui_player_calc_scolour = [
    local _sat _val

    _sat = (divf $gameui_player_scolour_s 1000)
    _val = (divf $gameui_player_scolour_v 1000)

    gameui_player_scolour = (hsvtohex [@gameui_player_scolour_h @_sat @_val])

    // Value needs to be above 0
    if $gameui_player_scolour [] [
        gameui_player_scolour = 0x010101
    ]
]

// Update vanity names
gameui_player_update_vanity = [
    gameui_player_vanities = []
    loop i (listlen $gameui_player_vanities_ids) [
        append gameui_player_vanities (getvanity (at $gameui_player_vanities_ids $i) 1)
    ]
]

// 1:<type> 2:<update>
gameui_player_rem_vanity = [
    loop i (listlen $gameui_player_vanities_ids) [
        if (= (getvanity (at $gameui_player_vanities_ids $i) 0) $arg1) [
            gameui_player_vanities_ids = (listsplice $gameui_player_vanities_ids "" $i 1)
            i = (- $i 1)
        ]
    ]

    if $arg2 [
        gameui_player_update_vanity
    ]
]

// 1:<vanity id>
gameui_player_set_vanity = [
    local _type
    _type = (getvanity $arg1 0)

    // Remove vanities of the same type
    gameui_player_rem_vanity $_type

    // Add vanity
    append gameui_player_vanities_ids $arg1

    // Update vanity names
    gameui_player_update_vanity
]

// 1:<vanity type>
gameui_player_get_vanity_id = [
    local _result
    _result = -1

    loopwhile i (listlen $gameui_player_vanities_ids) [= $_result -1] [
        _result = (at $gameui_player_vanities_ids $i)
        if (= (getvanity $_result 0) $arg1) [] [
            _result = -1
        ]
    ]

    result $_result
]

gameui_begin_ids

# ui_gameui_player_character [
    uiborderedimageclamped $skintex 0x55010101 0 $ui_texborder $ui_screenborder 0 0 [
        uihlist 0 [
            ui_gameui_button [
                p_label       = "Male"
                p_label_size  = 1.5
                p_width       = 0.25
                p_on_click    = [
                    gameui_player_model = 0
                ]
                @(if (!= $gameui_player_model 0) [
                    result [ p_colour = 0x555555 ]
                ])
                p_id          = #(gameui_get_id button)
            ]

            ui_gameui_button [
                p_label       = "Female"
                p_label_size  = 1.5
                p_width       = 0.25
                p_on_click    = [
                    gameui_player_model = 1
                ]
                @(if (!= $gameui_player_model 1) [
                    result [ p_colour = 0x555555 ]
                ])
                p_id          = #(gameui_get_id button)
            ]
        ]
    ]
]

# ui_gameui_player_colour [
    uiborderedimageclamped $skintex 0x55010101 0 $ui_texborder $ui_screenborder 0.5 0 [
        uispace 0.0018 0 [
            uistyle clampx
            uihlist 0 [
                uistyle clampx
                uivlist 0 [
                    uistyle clampx
                    uialign -2 -1

                    uitext "Primary" 0.75

                    local _sat _val
                    _sat = (divf $gameui_player_colour_s 1000)
                    _val = (divf $gameui_player_colour_v 1000)

                    uihcolourslider gameui_player_colour_h 0 359 0xff0000 0xffff00 0 0.02 [
                        gameui_player_calc_colour
                    ] [
                        0x00ff00 0x00ffff 0x0000ff 0xff00ff 0xff0000
                    ]
                    uiprev [uistyle clampx]

                    uihcolourslider gameui_player_colour_s 0 1000 (hsvtohex [@gameui_player_colour_h 0 @_val]) (hsvtohex [@gameui_player_colour_h 1 @_val]) 0 0.02 [
                        gameui_player_calc_colour
                    ]
                    uiprev [uistyle clampx]

                    uihcolourslider gameui_player_colour_v 0 1000 (hsvtohex [@gameui_player_colour_h @_sat 0]) (hsvtohex [@gameui_player_colour_h @_sat 1]) 0 0.02 [
                        gameui_player_calc_colour
                    ]
                    uiprev [uistyle clampx]
                ]
                uivlist 0 [
                    uistyle clampx
                    uialign -2 -1

                    uitext "Secondary" 0.75

                    local _sat _val
                    _sat = (divf $gameui_player_scolour_s 1000)
                    _val = (divf $gameui_player_scolour_v 1000)

                    uihcolourslider gameui_player_scolour_h 0 359 0xff0000 0xffff00 0 0.02 [
                        gameui_player_calc_scolour
                    ] [
                        0x00ff00 0x00ffff 0x0000ff 0xff00ff 0xff0000
                    ]
                    uiprev [uistyle clampx]

                    uihcolourslider gameui_player_scolour_s 0 1000 (hsvtohex [@gameui_player_scolour_h 0 @_val]) (hsvtohex [@gameui_player_scolour_h 1 @_val]) 0 0.02 [
                        gameui_player_calc_scolour
                    ]
                    uiprev [uistyle clampx]

                    uihcolourslider gameui_player_scolour_v 0 1000 (hsvtohex [@gameui_player_scolour_h @_sat 0]) (hsvtohex [@gameui_player_scolour_h @_sat 1]) 0 0.02 [
                        gameui_player_calc_scolour
                    ]
                    uiprev [uistyle clampx]
                ]
            ]
        ]
    ]
]

# ui_gameui_player_pattern [
    uiborderedimageclamped $skintex 0x55010101 0 $ui_texborder $ui_screenborder 0.5 0 [
        uispace 0.0025 0.0025 [
            ui_gameui_hscrollarea [
                uihlist 0.001 [
                    loop i $numpatterns [
                        uicolour $gameui_player_scolour 0 0 [
                            uiimage (getpattern $i 0) $gameui_player_colour 0 0.08 0.08 [
                                uirelease [
                                    gameui_player_pattern = $i
                                ]

                                if (= $gameui_player_pattern $i) [
                                    local _pulse
                                    _pulse = (*f (+f (sin (*f $totalmillis 0.5)) 1) 0.5)

                                    uiborderedimageclamped $skinbordertex (tool_colour_lerp_int 0xff0000 0xffffff $_pulse) 0 $ui_texborder $ui_screenborder
                                    uiprev [uistyle clampxy]
                                ]
                            ]
                        ]
                    ]
                ]
            ] [
                p_width       = #(-f 0.5 ((*f 0.0025 2)))
                p_height      = 0.082
                p_slider_size = 0.005
            ]
        ]
    ]
]

# ui_gameui_player_basic [
    uitext "Name" 2
    ui_gameui_textinput gameui_player_name 20 [
        p_prompt    = "[Enter name here]"
        p_width     = 0.5
        p_immediate = 1
    ]

    uifill 0 0.01

    uitext "Character" 2
    ui_gameui_player_character

    uifill 0 0.01

    uitext "Colours" 2
    ui_gameui_player_colour

    uifill 0 0.01

    uitext "Pattern" 2
    ui_gameui_player_pattern

    uifill 0 0.01

    uihlist 0 [
        ui_gameui_button [
            p_label       = "Vanities"
            p_label_size  = 1.3
            p_width       = 0.2
            p_height      = 0.04
            p_on_click    = [
                gameui_player_set_content $GAMEUI_PLAYER_CONTENT_VANITY
            ]
            p_id          = #(gameui_get_id button)
        ]

        ui_gameui_button [
            p_label       = "Loadout"
            p_label_size  = 1.3
            p_width       = 0.2
            p_height      = 0.04
            p_on_click    = [
                gameui_player_set_content $GAMEUI_PLAYER_CONTENT_LOADOUT
            ]
            p_id          = #(gameui_get_id button)
        ]
    ]
]

// 1:<weapon>
gameui_player_loadout_set = [
    local _other_slot
    _other_slot = (? $gameui_player_loadout_cur_slot 0 1)

    // Swap weapons if the other slot is the same, unless random
    if (&& [> $arg1 0] [= $arg1 (at $gameui_player_loadout_slots $_other_slot)]) [
        gameui_player_loadout_slots = [@(at $gameui_player_loadout_slots 1) @(at $gameui_player_loadout_slots 0)]
    ]

    gameui_player_loadout_slots = (listsplice $gameui_player_loadout_slots $arg1 $gameui_player_loadout_cur_slot 1)
]

gameui_player_loadout_validate = [
    local _result
    _result = 1

    if (|| [= (at $gameui_player_loadout_slots 0) 0] [= (at $gameui_player_loadout_slots 1) 0]) [
        // Random weapons need filter to be set
        if (= $gameui_player_loadout_filter 0) [
            _result = 0
        ]
    ]

    result $_result
]

# ui_gameui_player_loadout_overlay [
    local _cur_weap _weap_name _hover_weap_name _colour
    _cur_weap  = (at $gameui_player_loadout_slots $gameui_player_loadout_cur_slot)
    _weap_name = (at $W_NAMES $_cur_weap)
    _colour    = $(concatword $_weap_name "colour")

    if (> $_cur_weap 0) [
        gameui_player_loadout_preview = $_cur_weap
    ]

    uivlist 0 [
        uialign 2 -1

        uicolour $_colour 0.0025 0.02
        uihlist 0 [
            ui_gameui_player_loadout_sel 0 [
                gameui_player_loadout_set 0
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 2 [
                gameui_player_loadout_set 2
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 3 [
                gameui_player_loadout_set 3
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 4 [
                gameui_player_loadout_set 4
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 5 [
                gameui_player_loadout_set 5
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 6 [
                gameui_player_loadout_set 6
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 7 [
                gameui_player_loadout_set 7
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 8 [
                gameui_player_loadout_set 8
            ] #(gameui_get_id button) 1
            ui_gameui_player_loadout_sel 9 [
                gameui_player_loadout_set 9
            ] #(gameui_get_id button) 1
        ]

        if (!=s $_hover_weap_name "") [
            uiborderedimageclamped $skintex 0x222222 0 $ui_texborder $ui_screenborder 0 0 [
                uistyle clampx

                uispace 0.005 0.005 [
                    uitext $_hover_weap_name
                ]

                ui_gameui_shadow
            ]
        ]
    ]
]

// 1:<weapon> 2:<on click> 3:<widget id> 4:<change mode>
ui_gameui_player_loadout_sel = [
    local _weap_name _colour _icon _size _icon_size

    if (< $arg1 1) [
        _colour = 0xcccccc
        _icon   = "<grey>textures/icons/question"
    ] [
        _weap_name      = (at $W_NAMES $arg1)
        _icon           = (concatword "<grey>textures/weapons/" $_weap_name)
        _colour         = $(concatword $_weap_name "colour")
    ]

    _size      = (*f 0.12 (? $arg4 0.5 1))
    _icon_size = (*f 0.1  (? $arg4 0.5 1))

    ui_gameui_button [
        p_width        = $_size
        p_height       = $_size
        p_colour       = $_colour
        p_icon         = $_icon
        p_icon_size    = $_icon_size
        p_border_scale = @(? $arg4 0.5 1)
        p_highlight    = @(! $arg4)
        p_background   = $arg4
        p_on_click     = $arg2
        p_on_hover     = [
            if @arg1 [
                _hover_weap_name = $(concatword (at $W_NAMES @@arg1) "longname")
            ] [
                _hover_weap_name = "Random"
            ]
        ]
        p_children     = [
            uiimage $glowtex $_colour 0 $_icon_size $_icon_size [
                uipropagate [uicolourblend (lerpf 0 0.33 $_anim_state_hover)]
            ]
        ]
        p_id           = $arg3
    ]
]

// 1:<weapon slot> 2:<x> 3:<y>
gameui_player_loadout_open_overlay = [
    // Place layout
    arg2 = (-f $arg2 0.18)
    arg3 = (+f $arg3 0.12)

    gameui_player_loadout_cur_slot = $arg1

    gameui_overlay_open ui_gameui_player_loadout_overlay [@arg2 @arg3]
]

# ui_gameui_player_loadout_selection [
    uihlist 0.05 [
        uivlist 0.01 [
            uitext "Primary" 1.6
            ui_gameui_player_loadout_sel (at $gameui_player_loadout_slots 0) [
                gameui_player_loadout_open_overlay 0 $arg1 $arg2
            ] #(gameui_get_id button)
        ]

        uivlist 0.01 [
            uitext "Secondary" 1.6
            ui_gameui_player_loadout_sel (at $gameui_player_loadout_slots 1) [
                gameui_player_loadout_open_overlay 1 $arg1 $arg2
            ] #(gameui_get_id button)
        ]
    ]

    uifill 0 0.03

    uitext "Random filter" 1.6
    uihlist 0 [
        local _weap_name _colour _image_size
        _weap_name  = ""
        _colour     = 0xffffff
        _image_size = 0

        loop i $W_LOADOUT [
            _weap_name  = (at $W_NAMES (+ $i 2))
            _colour     = $(concatword $_weap_name "colour")
            _colour     = (? (& $gameui_player_loadout_filter (<< 1 $i)) $_colour 0x88555555)
            _image_size = (? (& $gameui_player_loadout_filter (<< 1 $i)) 0.04 0.03)

            uitarget 0.04 0.04 [
                uiimage (concatword "<grey>textures/weapons/" $_weap_name) $_colour 0 $_image_size $_image_size

                uirelease [
                    gameui_player_loadout_filter = (^ $gameui_player_loadout_filter (<< 1 $i))
                ]
            ]
        ]
    ]
]

# ui_gameui_player_loadout [
    uistyle clampx

    uifill 0 0.05

    ui_gameui_player_loadout_selection

    // Do not show the back navigation button if we are in the shortcut menu
    if (! $gameui_player_content_shortcut) [
        uispace 0.01 0.02 [
            uistyle clampx

            uiline 0xaaaaaa
            uiprev [uistyle clampx]
        ]

        ui_gameui_button [
            p_label       = "Close"
            p_label_size  = 1.3
            p_width       = 0.2
            p_height      = 0.04
            p_on_click    = [
                gameui_player_set_content $GAMEUI_PLAYER_CONTENT_BASIC
            ]
            p_id          = #(gameui_get_id button)
        ]
    ]
]

gameui_player_vanity_preview_tweaker_vanity_id = 0

ui_gameui_player_vanity_preview_tweaker_dims = 0.6

ui_gameui_player_vanity_preview_tweaker = [
    uivlist 0.01 [
        uitext "x"
        uihslider tweaker_prev_x -20 20 0.1 [] [
            uicolour 0x888888 0.54 0.02
            uisliderbutton [ uiline 0xff0000 0 0.02 ]
            uitext (round $tweaker_prev_x 0.01) 0.5
        ]

        uitext "y"
        uihslider tweaker_prev_y -20 20 0.1 [] [
            uicolour 0x888888 0.54 0.02
            uisliderbutton [ uiline 0xff0000 0 0.02 ]
            uitext (round $tweaker_prev_y 0.01) 0.5
        ]

        uitext "z"
        uihslider tweaker_prev_z -20 20 0.1 [] [
            uicolour 0x888888 0.54 0.02
            uisliderbutton [ uiline 0xff0000 0 0.02 ]
            uitext (round $tweaker_prev_z 0.01) 0.5
        ]

        uitext "yaw"
        uihslider tweaker_prev_yaw 0 360 1 [] [
            uicolour 0x888888 0.54 0.02
            uisliderbutton [ uiline 0xff0000 0 0.02 ]
            uitext (round $tweaker_prev_yaw 0.01) 0.5
        ]

        uitext "pitch"
        uihslider tweaker_prev_pitch 0 360 1 [] [
            uicolour 0x888888 0.54 0.02
            uisliderbutton [ uiline 0xff0000 0 0.02 ]
            uitext (round $tweaker_prev_pitch 0.01) 0.5
        ]

        uitext "roll"
        uihslider tweaker_prev_roll 0 360 1 [] [
            uicolour 0x888888 0.54 0.02
            uisliderbutton [ uiline 0xff0000 0 0.02 ]
            uitext (round $tweaker_prev_roll 0.01) 0.5
        ]

        uipropchild [uialign -1 -1]

        uicolour 0 0 0 [
            uimodelpreview (vanityfname $gameui_player_vanity_preview_tweaker_vanity_id) "" 1 1 0.18 0.18 [
                uimodelpreviewyaw   $tweaker_prev_yaw
                uimodelpreviewpitch $tweaker_prev_pitch
                uimodelpreviewroll  $tweaker_prev_roll
                uipreviewtranslate  $tweaker_prev_x $tweaker_prev_y $tweaker_prev_z
            ]
        ]
    ]
]

// Opens the utility for finding offset/orientation values for previews
// 1:<vanity path>
gameui_player_vanity_tweaker_open = [
    gameui_player_vanity_preview_tweaker_vanity_id = -1

    loopwhile i (getvanity -1) [< $gameui_player_vanity_preview_tweaker_vanity_id 0] [
        if (=s (getvanity $i 1) $arg1) [
            gameui_player_vanity_preview_tweaker_vanity_id = $i
        ]
    ]

    if (>= $gameui_player_vanity_preview_tweaker_vanity_id 0) [
        tweaker_prev_x     = 0
        tweaker_prev_y     = 0
        tweaker_prev_z     = 0
        tweaker_prev_yaw   = 0
        tweaker_prev_pitch = 0
        tweaker_prev_roll  = 0

        showui main
        gameui_open ui_gameui_player_vanity_preview_tweaker
    ] [
        echo "Error: Vanity not found"
    ]
]

ui_gameui_player_vanity_select_overlay = [
    local _prev_config _prev_x _prev_y _prev_z _prev_yaw _prev_pitch _prev_roll _cur_sel

    _cur_sel = (gameui_player_get_vanity_id $gameui_player_vanity_type_focus)

    uiborderedimageclamped $skintex 0xaa010101 0 $ui_texborder $ui_screenborder 0 0 [
        uispace 0.018 0.018 [
            uivlist 0.01 [
                uitext (at $vanitynames $gameui_player_vanity_type_focus) 2
                uiprev [uialign -1 -1]

                ui_gameui_hscrollarea [
                    uihlist 0.009 [
                        uiborderedimageclamped $skintex 0x222222 0 $ui_texborder $ui_screenborder 0 0 [
                            uistyle clampy

                            uiimage $exittex 0xff0000 0 0.036 0.036

                            uirelease [
                                gameui_player_rem_vanity $gameui_player_vanity_type_focus 1
                            ]

                            if (< $_cur_sel 0) [
                                uiborderedimageclamped $skinbordertex 0xff0000 0 $ui_texborder $ui_screenborder
                                uiprev [uistyle clampxy]
                            ]

                            ui_gameui_shadow
                        ]

                        loop i (getvanity -1) [
                            if (= (getvanity $i 0) $gameui_player_vanity_type_focus) [
                                _prev_config = (at $vanityprevs $i)
                                _prev_x      = (at $_prev_config 0)
                                _prev_y      = (at $_prev_config 1)
                                _prev_z      = (at $_prev_config 2)
                                _prev_yaw    = (at $_prev_config 3)
                                _prev_pitch  = (at $_prev_config 4)
                                _prev_roll   = (at $_prev_config 5)

                                uiborderedimageclamped $skintex 0x000000 0 $ui_texborder $ui_screenborder 0 0 [
                                    uimodelpreview (vanityfname $i) "" 1 1 0.18 0.18 [
                                        uimodelpreviewyaw   $_prev_yaw
                                        uimodelpreviewpitch $_prev_pitch
                                        uimodelpreviewroll  $_prev_roll
                                        uipreviewtranslate  $_prev_x $_prev_y $_prev_z
                                    ]

                                    uifill 0 0.08 [
                                        uistyle clampx
                                        uialign -2 1

                                        uicolour 0x88010101 0 0 [
                                            uistyle clampx

                                            uispace 0.005 0.005 [
                                                uitext (getvanity $i 2)
                                            ]
                                        ]
                                    ]

                                    uirelease [
                                        gameui_player_set_vanity $i
                                    ]

                                    if (= $_cur_sel $i) [
                                        uiborderedimageclamped $skinbordertex 0xff0000 0 $ui_texborder $ui_screenborder
                                        uiprev [uistyle clampxy]
                                    ]

                                    uicolouradd 0x555555

                                    ui_gameui_shadow
                                ]
                            ]
                        ]
                    ]
                ] [
                    p_width       = (-f 0.72 (*f 0.018 2))
                    p_slider_size = 0.005
                ]
            ]
        ]

        ui_gameui_shadow
    ]
]

// 1:<vanity type>
gameui_player_vanity_hover = [
    local _x _y
    _x = (+f $uilastsx (*f $uilastw 0.5))
    _y = (+f $uilastsy (*f $uilasth 0.5))
    gameui_player_vanity_type_highlight_to = [@_x @_y]
    _cur_vanity_hover                      = $arg1
]

// 1:<vanity type>
gameui_player_vanity_focus = [
    gameui_player_vanity_type_focus      = $arg1
    gameui_player_vanity_type_focus_anim = 0

    local _center_x
    _center_x = (-f (uiwidth 0.5) (*f 0.72 0.5))

    gameui_overlay_open ui_gameui_player_vanity_select_overlay [ @_center_x 0.65 ]
]

# ui_gameui_player_vanity_l [
    ui_gameui_prettybutton [
        p_label          = "Head top"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 3
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 3
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Forehead"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 4
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 4
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Face"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 6
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 6
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Right shoulder"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 11
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 11
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Right foot"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 9
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 9
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Bottom"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 12
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 12
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]
]

# ui_gameui_player_vanity_r [
    ui_gameui_prettybutton [
        p_label          = "Head"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 0
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 0
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Eyes"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 2
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 2
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Ears"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 5
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 5
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Chest"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 1
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 1
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Back"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 7
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 7
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Left foot"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 8
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 8
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    ui_gameui_prettybutton [
        p_label          = "Left shoulder"
        p_label_size     = 1
        p_label_align    = 0
        p_width          = 0.2
        p_pad_h_shift    = 0
        p_on_click       = [
            gameui_player_vanity_focus 10
        ]
        p_on_hover       = [
            gameui_player_vanity_hover 10
        ]
        p_sound_hover    = S_UI_HOVER
        p_sound_activate = S_UI_ACTION
        p_id             = #(gameui_get_id prettybutton)
    ]

    uispace 0.01 0.02 [
        uistyle clampx

        uiline 0xaaaaaa
        uiprev [uistyle clampx]
    ]

    // Do not show the back navigation button if we are in the shortcut menu
    if (! $gameui_player_content_shortcut) [
        ui_gameui_button [
            p_label       = "Close"
            p_label_size  = 1.3
            p_width       = 0.2
            p_height      = 0.04
            p_on_click    = [
                gameui_player_vanity_type_focus      = -1
                gameui_player_vanity_type_focus_anim = 0
                gameui_player_set_content $GAMEUI_PLAYER_CONTENT_BASIC
            ]
            p_id          = #(gameui_get_id button)
        ]
    ]
]

# ui_gameui_player_vanity_line_curve1 [
    // If going downwards
    if (>f $_vanity_y $_button_y) [
        // If going left
        if (>f $_button_x $_vanity_x) [
            _draw_x     = (-f $_draw_x $_curve_size)
            _draw_y     = (-f $_draw_y #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0 0 0.5 0.5 ]
        ] [
            _draw_x     = (+f $_draw_x $_horiz_len)
            _draw_y     = (-f $_draw_y #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0.5 0 0.5 0.5 ]
        ]
    ] [
        // If going upwards

        _draw_y = (-f $_draw_y $_curve_size)

        // If going left
        if (>f $_button_x $_vanity_x) [
            _draw_x     = (-f $_draw_x $_curve_size)
            _draw_y     = (+f $_draw_y #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0 0.5 0.5 0.5 ]
        ] [
            _draw_x     = (+f $_draw_x $_horiz_len)
            _draw_y     = (+f $_draw_y #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0.5 0.5 0.5 0.5 ]
        ]
    ]

    // Draw first curve
    uicroppedimage $circlebartex 0xffaa88 0 $_curve_size $_curve_size (at $_curve_crop 0) (at $_curve_crop 1) (at $_curve_crop 2) (at $_curve_crop 3) [
        uiposition $_draw_x $_draw_y
    ]

    // If going downwards
    if (>f $_vanity_y $_button_y) [
        // If going left
        if (>f $_button_x $_vanity_x) [
            _draw_x = (+f $_draw_x #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
        ] [
            _draw_x = (+f $_draw_x $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
        ]

        _draw_y = (+f $_draw_y $_curve_size)

        // Recalculate the vertical length
        _vert_len = (-f (absf (-f $_draw_y $_vanity_y)) $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)

        // Animate vertical length
        _vert_len = (*f $_vert_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.33 0.66))
    ] [
        // If going upwards
        // If going left
        if (>f $_button_x $_vanity_x) [
            _draw_x = (+f $_draw_x #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
        ] [
            _draw_x = (+f $_draw_x $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
        ]

        // Recalculate the vertical length
        _vert_len = (-f (absf (-f $_draw_y $_vanity_y)) $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)

        // Animate vertical length
        _vert_len = (*f $_vert_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.33 0.66))

        _draw_y = (-f $_draw_y $_vert_len)
    ]
]

# ui_gameui_player_vanity_line_curve2 [
    // If going downwards
    if (>f $_vanity_y $_button_y) [
        // If going left
        if (>f $_button_x $_vanity_x) [
            _draw_x = (-f $_draw_x $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0.5 0.5 0.5 0.5 ]
        ] [
            _draw_x = (-f $_draw_x #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0 0.5 0.5 0.5 ]
        ]

        _draw_y = (+f $_draw_y $_vert_len)
    ] [
        // If going upwards
        // If going left
        if (>f $_button_x $_vanity_x) [
            _draw_x = (-f $_draw_x $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0.5 0 0.5 0.5 ]
        ] [
            _draw_x = (-f $_draw_x #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
            _curve_crop = [ 0 0 0.5 0.5 ]
        ]

        _draw_y = (-f $_draw_y $_curve_size)
    ]

    // Draw second curve
    uicroppedimage $circlebartex 0xffaa88 0 $_curve_size $_curve_size (at $_curve_crop 0) (at $_curve_crop 1) (at $_curve_crop 2) (at $_curve_crop 3) [
        uiposition $_draw_x $_draw_y
    ]

    // If going downwards
    if (>f $_vanity_y $_button_y) [
        // If going left
        if (>f $_button_x $_vanity_x) [
            // Recalculate the horizontal length
            _horiz_len2 = (absf (-f $_draw_x $_vanity_x))

            // Animate horizontal length
            _horiz_len2 = (*f $_horiz_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.66 1))

            _draw_x = (-f $_draw_x $_horiz_len2)
        ] [
            _draw_x = (+f $_draw_x $_curve_size)

            // Recalculate the horizontal length
            _horiz_len2 = (absf (-f $_draw_x $_vanity_x))

            // Animate horizontal length
            _horiz_len2 = (*f $_horiz_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.66 1))
        ]

        _draw_y = (+f $_draw_y $_curve_size -#GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
    ] [
        // If going upwards
        // If going left
        if (>f $_button_x $_vanity_x) [
            // Recalculate the horizontal length
            _horiz_len2 = (absf (-f $_draw_x $_vanity_x))

            // Animate horizontal length
            _horiz_len2 = (*f $_horiz_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.66 1))

            _draw_x = (-f $_draw_x $_horiz_len2)
        ] [
            _draw_x = (+f $_draw_x $_curve_size)

            // Recalculate the horizontal length
            _horiz_len2 = (absf (-f $_draw_x $_vanity_x))

            // Animate horizontal length
            _horiz_len2 = (*f $_horiz_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.66 1))
        ]

        _draw_y = (+f $_draw_y #GAMEUI_PLAYER_VANITY_LINE_THICKNESS)
    ]
]

# ui_gameui_player_vanity_line [
    local _button_x _button_y _vanity_x _vanity_y _horiz_len1 _horiz_len2 _vert_len _draw_x _draw_y _curve_size _curve_crop
    local _horiz_col1 _horiz_col2

    // Coordinates
    _button_x = (at $gameui_player_vanity_type_highlight_to 0)
    _button_y = (at $gameui_player_vanity_type_highlight_to 1)
    _vanity_x = (at $gameui_player_vanity_type_highlight_from 0)
    _vanity_y = (at $gameui_player_vanity_type_highlight_from 1)

    // Disable curve if vertical distance is too small
    if (<f (absf (-f $_button_y $_vanity_y)) (*f @GAMEUI_PLAYER_VANITY_LINE_CURVE_SIZE 1.5)) [
        _curve_size = 0
    ] [
        _curve_size = @GAMEUI_PLAYER_VANITY_LINE_CURVE_SIZE
    ]

    // Compensate for the position of the panel
    _button_x = (-f $_button_x (uiwidth $gameui_panel_new_pos))
    _vanity_x = (-f $_vanity_x (uiwidth $gameui_panel_new_pos))

    // Lengths
    _horiz_len = (*f (absf (-f $_button_x $_vanity_x)) 0.5) // Two lines, so half the length
    _vert_len  = (absf (-f $_button_y $_vanity_y))

    // Compensate for the curve
    _horiz_len = (-f $_horiz_len $_curve_size)
    _vert_len  = (-f $_vert_len (*f $_curve_size 2))

    // Draw coordinates
    _draw_x = $_button_x
    _draw_y = $_button_y

    // Animate horizontal length
    _horiz_len1 = (*f $_horiz_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0 0.33))

    if (! (at $gameui_player_vanity_type_highlight_from 2)) [
        uifill 0.04 0.04 [
            uiposition (-f $_vanity_x 0.02) (-f $_vanity_y 0.02)

            uiimage $hinttex 0x88ffffff 0 0.04 0.04
            uiimage $skinbordertex 0xffaa88 0 0.02 0.02

            uipropagate [uicolourblend (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.33 1)]
        ]
    ]

    // Compensate if the button is on the right side of the vanity
    if (>f $_button_x $_vanity_x) [
        _draw_x = (-f $_draw_x $_horiz_len1)
        _horiz_col1 = 0xffaa88
        _horiz_col2 = 0x01ff0000
    ] [
        _horiz_col1 = 0x01ff0000
        _horiz_col2 = 0xffaa88
    ]

    // Draw the first horizontal line
    uihgradient $_horiz_col1 $_horiz_col2 $_horiz_len1 #GAMEUI_PLAYER_VANITY_LINE_THICKNESS [
        uiposition $_draw_x (-f $_draw_y #(*f $GAMEUI_PLAYER_VANITY_LINE_THICKNESS 0.5))
    ]

    if (>f $_curve_size 0) [
        if (>f $gameui_player_vanity_type_highlight_anim 0.33) [
            ui_gameui_player_vanity_line_curve1
        ] [
            // Vertical length would be calculated above
            _vert_len = 0
        ]
    ] [
        // Animate vertical length
        _vert_len = (*f $_vert_len $gameui_player_vanity_type_highlight_anim)

        if (<f $_vanity_y $_button_y) [
            _draw_y = (-f $_draw_y $_vert_len)
        ]

        if (>f $_vanity_x $_button_x) [
            _draw_x = (+f $_draw_x $_horiz_len1)
        ]
    ]

    // Draw the vertical line
    uicolour 0xffaa88 #GAMEUI_PLAYER_VANITY_LINE_THICKNESS $_vert_len [
        uiposition (-f $_draw_x #(*f $GAMEUI_PLAYER_VANITY_LINE_THICKNESS 0.5)) $_draw_y
    ]

    if (>f $_curve_size 0) [
        if (>f $gameui_player_vanity_type_highlight_anim 0.66) [
            ui_gameui_player_vanity_line_curve2
        ]
    ] [
        // Animate horizontal length
        _horiz_len2 = (*f $_horiz_len (gameui_anim_segment $gameui_player_vanity_type_highlight_anim 0.66 1))

        if (<f $_vanity_x $_button_x) [
            _draw_x = (-f $_draw_x $_horiz_len2)
        ]

        if (>f $_vanity_y $_button_y) [
            _draw_y = (+f $_draw_y $_vert_len)
        ]
    ]

    // Draw the second horizontal line
    uihgradient $_horiz_col2 $_horiz_col1 $_horiz_len2 #GAMEUI_PLAYER_VANITY_LINE_THICKNESS [
        uiposition $_draw_x (-f $_draw_y #(*f $GAMEUI_PLAYER_VANITY_LINE_THICKNESS 0.5))
    ]
]

// 1:<width> 2:<height>
ui_gameui_player_preview = [
    uiplayerpreview 1 1 $arg1 $arg2 [
        uiplayerpreviewmodel      $gameui_player_model
        uiplayerpreviewcolour     $gameui_player_colour
        uiplayerpreviewcolour     $gameui_player_scolour 1
        uiplayerpreviewpattern    $gameui_player_pattern
        uiplayerpreviewvanity     $gameui_player_vanities
        uiplayerpreviewweapselect $gameui_player_loadout_preview
    ] [
        uidoublepress 0 [
            uipreviewresetoffset
        ]

        local _pos _outside _pos3d _sx _sy _pos3d_x _pos3d_y _pos3d_z _anim _yaw
        _anim = (lerpf 0 0.006 $gameui_player_vanity_type_focus_anim)
        _sx   = $uilastsx
        _sy   = $uilastsy

        if (= $gameui_player_content_type $GAMEUI_PLAYER_CONTENT_VANITY) [
            uipreviewinteract 0
            uipreviewresetoffset
        ]

        if (> $gameui_player_vanity_type_focus -1) [
            _pos3d = (uiplayerpreviewvanitypos $gameui_player_vanity_type_focus)

            // Set the target values
            _pos3d_x = (-f (at $_pos3d 0))
            _pos3d_y = (+f (-f (at $_pos3d 1)) 40)
            _pos3d_z = (-f (-f (at $_pos3d 2)) 1)
            _yaw     = (at $GAMEUI_PLAYER_VANITY_PREVIEW_YAWS $gameui_player_vanity_type_focus)
        ] [
            uipreviewinteract 1

            _pos3d_x = 0
            _pos3d_y = 0
            _pos3d_z = 0
            _yaw     = 140
        ]

        // Converge on the right position while easing out, will make sure the camera is positioned optimally
        _yaw     = (lerp360  (at $gameui_player_vanity_type_focus_interp 3) $_yaw     (*f $_anim $curtime))
        _pos3d_x = (lerpf    (at $gameui_player_vanity_type_focus_interp 0) $_pos3d_x (*f $_anim $curtime))
        _pos3d_y = (lerpf    (at $gameui_player_vanity_type_focus_interp 1) $_pos3d_y (*f $_anim $curtime))
        _pos3d_z = (lerpf    (at $gameui_player_vanity_type_focus_interp 2) $_pos3d_z (*f $_anim $curtime))

        uipreviewyaw $_yaw
        uipreviewtranslate $_pos3d_x $_pos3d_y $_pos3d_z

        gameui_player_vanity_type_focus_interp = [@_pos3d_x @_pos3d_y @_pos3d_z @_yaw]

        if (> $gameui_player_vanity_type_highlight -1) [
            _pos = (uiplayerpreviewvanityscreenpos $gameui_player_vanity_type_highlight)

            _outside = (|| [
                <f (at $_pos 0) 0
            ] [
                >f (at $_pos 0) $uilastw
            ] [
                <f (at $_pos 1) 0
            ] [
                >f (at $_pos 1) $uilasth
            ])

            gameui_player_vanity_type_highlight_from = [
                @(+f $_sx (clampf (at $_pos 0) 0 $uilastw))
                @(+f $_sy (clampf (at $_pos 1) 0 $uilasth))
                @_outside
            ]
        ]
    ]
]

ui_gameui_player_on_open = [
    if $gameui_player_first_show [
        gameui_player_reset
        gameui_player_first_show = 0
    ]

    gameui_player_set_content $GAMEUI_PLAYER_CONTENT_BASIC 1

    gameui_player_vanity_type_focus_interp   = [0 0 0 140]
    gameui_player_vanity_type_focus_anim     = 1
    gameui_player_vanity_type_focus          = -1
]

ui_gameui_player_dims = 1.2

# ui_gameui_player [
    local _cur_vanity_hover
    _cur_vanity_hover = -1

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            local _text

            case $gameui_player_content_type $GAMEUI_PLAYER_CONTENT_BASIC [
                _text = "Player setup"
            ] $GAMEUI_PLAYER_CONTENT_VANITY [
                _text = "Player vanities"
            ] $GAMEUI_PLAYER_CONTENT_LOADOUT [
                _text = "Player loadout"
            ]

            uistyle clampx

            ui_gameui_decortext $_text
            ui_gameui_shadowhoriz
        ]

        uicolour 0x33010101 0 0.65 [
            uistyle clampx

            local _left_size _right_size _size_anim _left_content _right_content _content_fade
            _size_anim  = (gameui_anim_segment $gameui_player_content_anim 0.2 0.8)
            _size_anim  = (smoothstep $_size_anim)

            _left_size = (lerpf (
                    at $gameui_player_content_old_sizes 0
                ) (
                    at $gameui_player_content_new_sizes 0
                ) $_size_anim)

            _right_size = (lerpf (
                    at $gameui_player_content_old_sizes 1
                ) (
                    at $gameui_player_content_new_sizes 1
                ) $_size_anim)

            if (<=f $gameui_player_content_anim 0.8) [
                _left_content  = (at $gameui_player_content_old 0)
                _right_content = (at $gameui_player_content_old 1)
            ] [
                _left_content  = (at $gameui_player_content_new 0)
                _right_content = (at $gameui_player_content_new 1)
            ]

            _content_fade = (gameui_anim_dual_edge $gameui_player_content_anim 0.2 0.8)
            _content_fade = (-f 1 (smoothstep $_content_fade))

            uigrid 3 0 0.018 [
                uistyle clampy

                uicolour 0x33010101 $_left_size 0 [
                    uistyle clampy

                    uiclip (maxf $_left_size 0.00001) 0 0 0 [
                        uialign -2 -1
                        uifill $_left_size

                        uivlist 0 [
                            if $_left_content [
                                $_left_content
                            ]
                        ]

                        uipropagate [uicolourblend $_content_fade]
                    ]
                ]

                ui_gameui_player_preview (-f $gameui_panel_new_size $_left_size $_right_size 0.072)
                uiprev [uistyle clampy]

                uicolour 0x33010101 $_right_size 0 [
                    uistyle clampy

                    uiclip (maxf $_right_size 0.00001) 0 0 0 [
                        uialign -2 -1
                        uifill $_right_size

                        uivlist 0 [
                            $_right_content

                            uispace 0.01 0.02 [
                                uistyle clampx

                                uiline 0xaaaaaa
                                uiprev [uistyle clampx]
                            ]

                            uihlist 0 [
                                ui_gameui_button [
                                    p_label       = "Reset"
                                    p_label_size  = 1.5
                                    p_width       = 0.144
                                    p_height      = 0.05
                                    p_on_click    = [
                                        gameui_player_reset
                                    ]
                                    p_border_scale = 0.5
                                    p_id          = #(gameui_get_id button)
                                ]

                                ui_gameui_button [
                                    p_label       = "Save"
                                    p_label_size  = 1.5
                                    p_width       = 0.144
                                    p_height      = 0.05
                                    p_colour      = #(hsvtohex [8 0.3 1])
                                    p_highlight   = 1
                                    p_on_click    = [
                                        gameui_player_set
                                    ]
                                    p_id          = #(gameui_get_id button)
                                ]
                            ]

                            uipropagate [uicolourblend $_content_fade]
                        ]
                    ]
                ]
            ]

            ui_gameui_shadowhoriz
        ]
    ]

    #(ui_gameui_backbutton)

    if (!= $_cur_vanity_hover $gameui_player_vanity_type_highlight) [
        gameui_player_vanity_type_highlight_anim = 0
        gameui_player_vanity_type_highlight = $_cur_vanity_hover
    ]

    gameui_player_vanity_type_focus_anim = (animstep $gameui_player_vanity_type_focus_anim 600 1)

    if (> $gameui_player_vanity_type_highlight -1) [
        gameui_player_vanity_type_highlight_anim = (animstep $gameui_player_vanity_type_highlight_anim 200 1)

        ui_gameui_player_vanity_line
    ]

    gameui_player_content_anim = (animstep $gameui_player_content_anim $gameui_panel_anim_time 1)
]



////////////////////
// WELCOME WIZARD //
////////////////////



ui_gameui_welcome_on_open = [
    // Same initializer as in ui_gameui_player
    ui_gameui_player_on_open

    // Start on the first page
    gameui_welcome_wizard = 0

    // Set default weapon loadout values
    gameui_player_loadout_slots  = [0 0]

    gameui_player_loadout_filter = 0
    loop i $W_LOADOUT [
        gameui_player_loadout_filter = (| $gameui_player_loadout_filter (<< 1 $i))
    ]
]

ui_gameui_welcome_can_close = [result 0]

ui_gameui_welcome_page1 = [
    uivlist 0.025 [
        uitext "Welcome to Red Eclipse" 1.5
        uicolourtext "Before you can play, you need to set up your player settings." 0xcccccc

        ui_gameui_player_preview 0.6 0.3
    ]
]

ui_gameui_welcome_page2 = [
    uivlist 0.005 [
        uitext "First, you need an ingame name." 1.5
        uicolourtext "This will be your handle for online play." 0xcccccc

        uifill 0 0.02

        ui_gameui_textinput gameui_player_name 20 [
            p_prompt    = "[Enter name here]"
            p_width     = 0.5
            p_immediate = 1
        ]
    ]
]

ui_gameui_welcome_page3 = [
    uivlist 0.005 [
        uitext "Now, let's customize your appearance." 1.5
        uicolourtext "Pick a body type." 0xcccccc

        uifill 0 0.02

        ui_gameui_player_character
        ui_gameui_player_preview 0.6 0.3
    ]
]

ui_gameui_welcome_page4 = [
    uivlist 0.005 [
        uicolourtext "Pick your main colours." 0xcccccc

        uifill 0 0.02

        ui_gameui_player_colour
        ui_gameui_player_preview 0.6 0.3
    ]
]

ui_gameui_welcome_page5 = [
    uivlist 0.005 [
        uicolourtext "Pick a skin pattern." 0xcccccc

        uifill 0 0.02

        ui_gameui_player_pattern
        ui_gameui_player_preview 0.6 0.3
    ]
]

ui_gameui_welcome_page6 = [
    uivlist 0.005 [
        uitext "Lastly, select your loadout." 1.5
        uicolourtext "You can change your spawn loadout at any time^nwith ^f{=gameui_player_show_loadout} in game." 0xcccccc

        uifill 0 0.02

        ui_gameui_player_loadout_selection
    ]
]

ui_gameui_welcome_page7 = [
    uivlist 0.005 [
        uitext "You're all set!" 1.5

        uifill 0 0.02

        uicolourtext "Your player settings are ready for online play." 0xcccccc
        uicolourtext "You may revisit and further tweak player settings by going into" 0xcccccc
        uicolourtext "Player Setup" 0xcccccc
    ]
]

ui_gameui_welcome_dims = 0.9

# ui_gameui_welcome [
    uivlist 0.025 [
        uistyle lefttop
        uistyle clampxy

        uifill 0 0.01

        uicolour 0x55010101 0 0 [
            uistyle clampx

            uiimage $logotex 0xffffff 1 0.46 0.23
            ui_gameui_shadowhoriz
        ]

        ui_gameui_pages gameui_welcome_wizard [
            ui_gameui_welcome_page1
            ui_gameui_welcome_page2
            ui_gameui_welcome_page3
            ui_gameui_welcome_page4
            ui_gameui_welcome_page5
            ui_gameui_welcome_page6
            ui_gameui_welcome_page7
        ] [
            p_width    = 0.6
            p_height   = 0.5
            p_id       = #(gameui_get_id pages)
            p_can_next = [
                case $arg1 1 [
                    result (!=s $gameui_player_name "")
                ] 5 [
                    gameui_player_loadout_validate
                ] () [
                    result 1
                ]
            ]
            p_on_finish = [
                gameui_player_set
                gameui_close
            ]
        ]

        uifill 0 0.025
    ]
]

// Schedule name check in the next frame after config is fully loaded
sleep 1 [
    if (needname) [
        // Ensure the default UI is open beforehand, otherwise the wizard won't complete out into
        // the main menu
        if (gameui_has_panel) [] [
            gameui_panel_default
        ]

        gameui_open ui_gameui_welcome
    ]
]



///////////////
// SHORTCUTS //
///////////////



gameui_player_show_loadout = [
    gameui_open ui_gameui_player
    gameui_player_set_content $GAMEUI_PLAYER_CONTENT_LOADOUT 1 1
]

gameui_player_show_loadout_event = [
    if (! (gameui_has_panel)) [
        gameui_player_show_loadout
    ]
]
onevent $CMD_EVENT_GAME_LOADOUT gameui_player_show_loadout_event
