gameui_score_last_update = 0

gameui_score_groups_sorted = []
gameui_score_best_group    = -1
gameui_score_format        = gameui_scoreboard_team_score_format_standard
gameui_score_pointsleft    = -1
gameui_score_position      = 0

// 1:<group id>
gameui_score_group_get_sorted_index = [
    // Two available positions, 0 or 1, best group is always 0
    != (getscoreteam 0) $arg1
]

// 1:<group id>
gameui_score_group_is_best = [
    && [!= (getscoretotal 0) (getscoretotal 1)] [= (gameui_score_group_get_sorted_index $arg1) 0]
]

gameui_score_team_score_format_standard = [
    result $arg1
]

gameui_score_team_score_format_speedrun_timed = [
    ? (> $arg1 0) (timestr $arg1 $scorespeedrunstyle) "-"
]

// 1:<no pos update>
gameui_score_update = [
    if (!= $totalmillis $gameui_score_last_update) [
        gameui_score_last_update = $totalmillis

        caseif (&& [= $gamemode $G_SPEEDRUN] [hasmut $G_M_GSP1]) [
            // Lapped speedrun
            gameui_score_format = gameui_score_team_score_format_standard
        ] (= $gamemode $G_SPEEDRUN) [
            // Timed speedrun
            gameui_score_format = gameui_score_team_score_format_speedrun_timed
        ] 1 [
            gameui_score_format = gameui_score_team_score_format_standard
        ]

        local _bestscore _maxscore _player

        case $gamemode $G_DEATHMATCH [
            _maxscore = $pointlimit
        ] $G_CAPTURE [
            _maxscore = $capturelimit
        ] $G_BOMBER [
            _maxscore = $bomberlimit
        ] $G_DEFEND [
            _maxscore = $defendlimit
        ]

        if $_maxscore [
            if (hasmut $G_M_FFA) [
                _bestscore = (getclientpoints (getscoreplayer 0 0))
            ] [
                _bestscore = (getscoretotal 0)
            ]

            gameui_score_pointsleft = (- $_maxscore $_bestscore)

        ] [
            gameui_score_pointsleft = -1
        ]

        if (&& [hasmut $G_M_FFA] [! $arg1]) [
            gameui_score_position = 0
            loopscoreboard _player 0 [
                if (!= (getscoreplayer 0 $gameui_score_position) $ui_hud_focus) [
                    if (!= $_player $ui_hud_focus) [
                        inc gameui_score_position 1
                    ]
                ]
            ]
        ]

        refreshscoreboard
    ]
]

gameui_score_on_mapload = [
    gameui_score_last_update = 0
    gameui_score_position    = 0
    gameui_score_update 1
]
onevent $CMD_EVENT_MAPLOAD gameui_score_on_mapload

# ui_gameui_scoreboard_matchinfo = [
    local _mapname _time_colours _large
    _mapname     = (? (!=s $maptitle "") $maptitle (filename $mapname))
    _time_colour = (at ["^fr" "^fr" "^fr" "^fr" "^fr" "^fg" "^fzoy" "^fg"] $gamestate)
    _large       = (&& $numscoregroups [! (hasmut $G_M_FFA)])

    ui_gameui_group [
        uistyle clampx

        uigroup [
            uistyle clampx

            uioffset 0.05 0 [
                uialign -1

                uiclip 0 0.15 0.075 0.075 [
                    uiimage (modetex) 0x44010101 0 0.3 0.3
                ]
            ]


            uipad 0.025 0 0 0.025 [
                uialign -1

                uivlist 0 [
                    uistyle clampx

                    uihlist 0.01 [
                        uifont "play/clear/bold" [
                            uitext $_mapname 2
                        ]

                        if (!=s $mapauthor "") [
                            uipad 0 0.025 0 0.005 [
                                ui_gameui_autoscrollh [
                                    uicolourtext (concat "by" $mapauthor) 0xaaaaaa
                                ] [
                                    p_width = #1(? $_large 0.8 0.4)
                                ]
                            ]
                        ]

                        uipropchild [uialign -2 1]
                    ]
                    uifont "play/clear" [
                        uivlist 0 [
                            uicolourtext (modename $gamemode $mutators) 0xaaaaaa 1.5

                            if $mutators [
                                uicolourtext (mutsname $gamemode $mutators 0 64) 0x555555 0.8
                            ]
                        ]

                        uipropagate [uialign -1]
                    ]

                    uipropchild [uialign -1]
                ]
            ]

            if (!= $gamemode $G_EDITING) [
                uipad 0 0.025 0.025 0.025 [
                    uialign 1 1

                    uivlist 0.001 [
                        uialign 1 1

                        if (!= $gamestate 5) [
                            uitext (at $G_S_NAMES $gamestate) [
                                uialign 1 1
                            ]
                        ] [
                            uigrid 2 0.01 0.001 [
                                if $gametimeremain [
                                    uioffset 0 0.0045 [
                                        uialign 1 -2
                                        uicolourtext "Time left:" 0x888888 0.8
                                    ]
                                    uitext (timestr $gametimeremain $showtimestyle) 1.1
                                ]
                                if (>= $gameui_score_pointsleft 0) [
                                    uioffset 0 0.0045 [
                                        uialign 1 -2
                                        uicolourtext "Points left:" 0x888888 0.8
                                    ]
                                    uitext $gameui_score_pointsleft 1.1
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ] [
        p_space_x = 0
        p_space_y = 0
    ]
]

ui_gameui_scoreboard_header_standard = [
    // Frags
    uiimage "<grey>textures/modes/classic" 0xffffff 0 0.04 0.04 [
        uiimageshadow 0.002 0x88010101
    ]

    // Deaths
    uiimage "<grey>textures/icons/dead" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]

    // Points
    uiimage "<grey>textures/icons/action" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]
]

ui_gameui_scoreboard_player_standard = [
    // Frags
    uitext (getclientfrags $_player)

    // Deaths
    uitext (getclientdeaths $_player)

    // Points
    uitext (getclientpoints $_player)
]

ui_gameui_scoreboard_header_speedrun = [
    // Laps
    uiimage "<grey>textures/modes/speedrunlapped" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]

    // Best lap
    uiimage "<grey>textures/modes/speedrun" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]
]

ui_gameui_scoreboard_player_speedrun = [
    local _time
    _time = (getclientcptime $_player)

    // Laps
    uitext (getclientpoints $_player)

    // Best lap
    uitext (? (> $_time 0) (timestr $_time $scorespeedrunstyle) "-")
]

gameui_scoreboard_mode_specific = [
    caseif (&& [= $gamemode $G_SPEEDRUN] [hasmut $G_M_GSP1]) [
        // Lapped speedrun
        _header_row        = $ui_gameui_scoreboard_header_speedrun
        _player_row        = $ui_gameui_scoreboard_player_speedrun
    ] (= $gamemode $G_SPEEDRUN) [
        // Timed speedrun
        _header_row        = $ui_gameui_scoreboard_header_speedrun
        _player_row        = $ui_gameui_scoreboard_player_speedrun
    ] (= $gamemode $G_EDITING) [
        _header_row = []
        _player_row = []
    ] 1 [
        _header_row        = $ui_gameui_scoreboard_header_standard
        _player_row        = $ui_gameui_scoreboard_player_standard
    ]
]

// 1:<player id>
ui_gameui_scoreboard_player = [
    local _colour
    _colour = (getclientcolour $arg1 $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)

    uiimage (getclientprivtex $arg1) $_colour 0 0.03 0.03
    uitext (getclientname $arg1 0 0)
]

// 1:<group id> 2:<left side>
# ui_gameui_scoreboard_group = [
    _team_name   = (at $T_NAMES $arg1)
    _team_colour = (getteamcolour $arg1)

    uivlist 0 [
        uialign 0 -1

        if $arg1 [
            ui_gameui_group [
                uistyle clampx

                uigroup [
                    uistyle clampx

                    uiclip 0 0.1 0 0.05 [
                        uiimage $glowtex $_team_colour 0 0.2 0.2 [
                            uipropagate [uicolourblend 0.25]
                        ]

                        uiimage $(concatword team $_team_name tex) $_team_colour 0 0.2 0.2 [
                            uipropagate [uicolourblend 0.33]
                        ]
                    ]

                    uifont "titillium/clear/bold" [
                        _team_name = (strupper $(concatword team $_team_name name))

                        uioffset 0 0.04 [
                            uioffset 0.005 0.005 [
                                uicolourtext $_team_name 0 2.5
                            ]
                            uicolourtext $_team_name (modcolourhsv $_team_colour 1 0.3 1) 2.5

                            uipropagate [ uicolourblend 0.8 ]
                        ]
                    ]

                    uispace 0.01 0.01 [
                        uistyle (? #1 arg2 rightbottom leftbottom)

                        local _is_best _colour
                        _is_best = (gameui_score_group_is_best #1 arg1)
                        _colour  = (? $_is_best 0xcccc44 0xaaaaaa)

                        uiimage $glowtex $_colour 0 0.03 0 [
                            uistyle clampxy
                            uipropagate [uicolourblend 0.25]

                            uifont "play/clear/bold" [
                                local _score
                                _score = ($gameui_score_format (getscoretotal (gameui_score_group_get_sorted_index #1 arg1)))

                                uioffset 0.004 0.004 [
                                    uicolourtext $_score 0 1.5
                                ]
                                uicolourtext $_score $_colour 1.5
                            ]
                        ]
                    ]
                ]
            ] [
                p_space_x = 0
                p_space_y = 0
                p_colour = @(tool_colour_add_alpha (modcolour $_team_colour 0.33) 0x44)
            ]
        ]
        ui_gameui_group [
            uitable 0.05 0 [
                uitablecolumn 2
                uifill 0.5

                uitableheader [
                    // Placeholder for player badge
                    uifill

                    // Player name
                    uiimage "<grey>textures/icons/player" 0xffffff 0 0.03 0.03 [
                        uiimageshadow 0.002 0x88010101
                    ]

                    _header_row

                    // Hide ping when offline
                    if $isonline [
                        uiimage "<grey>textures/icons/voices" 0xffffff 0 0.03 0.03 [
                        uiimageshadow 0.002 0x88010101
                    ]
                    ]
                ] [
                    ui_gameui_group [] [
                        p_colour = @(tool_colour_add_alpha (modcolour $_team_colour 0.5) 0x88)
                    ]
                    uiprev [uistyle clampxy]
                ]

                _row_count = 0

                loopscoreboard _player (gameui_score_group_get_sorted_index #1 arg1) [
                    uitablerow [
                        // Player badge/name
                        ui_gameui_scoreboard_player $_player

                        _player_row

                        // Hide ping when offline
                        if $isonline [
                            uitext (getclientping $_player)
                        ]
                    ] [
                        ui_gameui_group [] [
                            p_colour = (? (mod $_row_count 2) 0x44010101 0x44444444)
                        ]
                        uiprev [uistyle clampxy]

                        if (= $_player $playercn) [
                            uiborderedimageclamped $skinshadowtex $_team_colour 0 $ui_texborder $ui_screenborder 0 0
                            uiprev [uistyle clampxy]
                        ]
                    ]

                    _row_count = (+ $_row_count 1)
                ]
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
            p_colour  = 0x44010101
        ]
    ]
]

ui_gameui_scoreboard_spectators = [
    uivlist 0 [
        ui_gameui_group [
            uifill 0.5

            uiimage "<grey>textures/icons/spectator" 0xffffff 0 0.03 0.03 [
                uiimageshadow 0.002 0x88010101
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
        ]

        ui_gameui_group [
            uifill 0.5

            ui_gameui_autoscrollv [
                uigrid 2 0.025 0 [
                    loopscoreboard _spectator -1 [
                        uihlist 0.005 [
                            ui_gameui_scoreboard_player $_spectator
                        ]
                    ]
                ]
            ] [
                p_height = 0.15
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
            p_colour  = 0x44010101
        ]
    ]
]

ui_gameui_scoreboard_music = [
    local _music_artist _music_title
    _music_artist = (getmusic 1)
    _music_title  = (getmusic 2)

    uivlist 0 [
        ui_gameui_group [
            uifill 0.5

            uiimage "<grey>textures/icons/edit/sound" 0xffffff 0 0.03 0.03 [
                uiimageshadow 0.002 0x88010101
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
        ]

        ui_gameui_group [
            uifill 0.5

            ui_gameui_autoscrollh [
                if (&& [!=s $_music_artist ""] [!=s $_music_title ""]) [
                    uihlist 0.005 [
                        uicolourtext $_music_artist 0xcccccc
                        uicolourtext "-" 0x888888
                        uicolourtext $_music_title 0xcccccc
                    ]
                ] [
                    uicolourtext (getmusic 0) 0xcccccc
                ]
            ] [
                p_width = 0.5
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
            p_colour  = 0x44010101
        ]
    ]
]

ui_menu_inhibit_scoreboard = 1
newui "scoreboard" $SURFACE_FOREGROUND [
    ui_menu_current          = "scoreboard"
    ui_menu_close_scoreboard = 0

    if (! (gameui_has_panel)) [
        uiallowinput 0
        uimenu 0
        uiontop 1
        hidecrosshair 1
        gameui_score_update

        gameui_autoscroll = (autoscroll_timer (divf $totalmillis 1000))

        local _header_row _player_row _team _team_name _team_colour _row_count

        gameui_scoreboard_mode_specific

        uicolour 0 0 1 [
            uipropagate [uicolourblend 0]

            uispace 0.05 0.05 [
                uivlist 0.025 [
                    ui_gameui_scoreboard_matchinfo

                    if $numscoregroups [
                        if (hasmut $G_M_FFA) [
                            ui_gameui_scoreboard_group 0 1
                        ] [
                            uihlist 0.025 [
                                ui_gameui_scoreboard_group 1 1
                                ui_gameui_scoreboard_group 2 0
                            ]
                        ]
                    ]

                    if $numspectators [
                        ui_gameui_scoreboard_spectators
                    ]

                    if (getmusic 8) [
                        ui_gameui_scoreboard_music
                    ]
                ]
            ]
        ]
    ]

    ui_menu_current = ""
]
