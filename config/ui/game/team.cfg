gameui_begin_ids

ui_gameui_team_dims = 0.6

// 1:<team> 2:<width> 3:<height> 4:<icon size> 5:<widget id>
ui_gameui_team_button = [
    local _team_name _team_colour _icon _tip _disabled

    if (< $arg1 $T_NEUTRAL) [
        _team_colour = 0xaaaaaa
        _icon        = "textures/modes/demo"
        _tip         = "Spectate"
        _disabled    = $isspectator
    ] [
        _team_name   = (at $T_NAMES $arg1)
        _team_colour = (getteamcolour $arg1)
        _icon        = $(concatword team $_team_name tex)
        _disabled    = (&& [! $isspectator] [= $getclientteam $arg1])

        if (!= $arg1 $T_NEUTRAL) [
            _tip = (concatword "Join " (getteamname $arg1))
        ] [
            _tip = "Join game"
        ]
    ]

    ui_gameui_button [
        p_width     = $arg2
        p_height    = $arg3
        p_colour    = $_team_colour
        p_icon      = $_icon
        p_icon_size = $arg4
        p_tip       = $_tip
        p_disabled  = $_disabled
        p_id        = $arg5
        p_on_click  = [
            caseif (< @arg1 $T_NEUTRAL) [
                spectate 1
            ] (= @arg1 $T_NEUTRAL) [
                spectate 0
            ] 1 [
                team @@arg1
            ]

            gameui_close_all
        ]
    ]
]

# ui_gameui_team = [
    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Pick your team" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uifill 0 0.05

        uivlist 0.05 [
            if (& $mutators (<< 1 $G_M_FFA)) [
                ui_gameui_team_button $T_NEUTRAL 0.4 0.2 0.15 #(gameui_get_id button)
            ] [
                uihlist 0.05 [
                    ui_gameui_team_button $T_ALPHA 0.2 0.2 0.15 #(gameui_get_id button)
                    ui_gameui_team_button $T_OMEGA 0.2 0.2 0.15 #(gameui_get_id button)
                ]
            ]

            ui_gameui_team_button -1 0 0.1 0.08 #(gameui_get_id button)
            uipropchild [ uiclamp 1 1 ]
        ]

        uifill 0 0.025

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff 2
        ] [
            p_width = $gameui_panel_new_size
        ]
    ]

    #(ui_gameui_backbutton)
]
