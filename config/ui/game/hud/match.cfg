game_hud_match_effect_team = 1
game_hud_match_effect_anim = 0

// 1:<group id> 2:<left side>
ui_game_hud_match_team = [
    local _team_name _team_colour _image_size _clip_height _is_best
    _team_name   = (at $T_NAMES $arg1)
    _team_colour = (getteamcolour $arg1)
    _image_size  = (*f $game_hud_largepanel_width 0.15)
    _clip_height = (*f $_image_size 0.5)
    _clip_width  = (*f $_clip_height 4)
    _is_best     = (gameui_score_group_is_best $arg1)

    uiclip $_clip_width $_clip_height 0 (*f $_image_size 1.25) [
        uifill $_clip_width (*f $_image_size 3)

        uiimage $glowtex $_team_colour 0 $_image_size $_image_size [
            uicolourblend 0.33
        ]

        uiimage $(concatword team $_team_name tex) $_team_colour 0 $_image_size $_image_size [
            uicolourblend 0.5
        ]

        if $_is_best [
            _team_colour = (tool_colour_hsvmod_int $_team_colour s 0.4)

            uioffset 0 (*f $_image_size 0.55) [
                uiimage $glowtex $_team_colour 0 (*f $_clip_height 3) $_clip_height [
                    uicolourblend (gameui_anim_pulse 0.25 0.5 1)
                ]
            ]
        ]

        uifont "play/clear/bold" [
            local _score_size _score _shadow_offset
            _score_size = (*f $game_hud_largepanel_text_size 1.1)

            if $_is_best [
                _score_size = (*f $game_hud_largepanel_text_size (gameui_anim_pulse 0.25 1 1.2))
            ]

            _score = ($gameui_score_format (getscoretotal (gameui_score_group_get_sorted_index $arg1)))
            _shadow_offset = (*f $_score_size 0.002)

            uigroup [
                uioffset $_shadow_offset $_shadow_offset [
                    uicolourtext $_score 0 $_score_size
                ]

                if (&& $game_hud_match_effect_anim [= $arg1 $game_hud_match_effect_team]) [
                    local _score_anim _score_anim_size
                    _score_anim      = (gameui_anim_segment $game_hud_match_effect_anim 0.2 1)
                    _score_anim_size = (*f $_score_size (lerpf 3 1 $_score_anim))

                    uitext $_score $_score_anim_size [
                        uicolourblend (lerpf 0 1 $_score_anim)
                    ]
                ]

                uitext $_score $_score_size

                uipropagate [ uicolourblend (? $_is_best 1.0 0.67) ]
            ]
        ]
    ]
]

// Appends "st", "nd", "rd" or "th" to a position number
// 1:<position number>
get_pos_string = [
    local _last_digit _second_last_digit _result
    _result            = $arg1
    _last_digit        = (mod $arg1 10)
    _second_last_digit = (mod (div $arg1 10) 10)

    if (= $_second_last_digit 1) [
        _result = (concatword $arg1 "th")
    ] [
        case $_last_digit 1 [
            _result = (concatword $arg1 "st")
        ] 2 [
            _result = (concatword $arg1 "nd")
        ] 3 [
            _result = (concatword $arg1 "rd")
        ] () [
            _result = (concatword $arg1 "th")
        ]
    ]

    result $_result
]

// 1:<client id> 2:<secondary> 3:<position>
# ui_game_hud_match_client = [
    local _image_size _clip_height _crown_size _colour _icon _score_size
    _image_size  = (*f $game_hud_largepanel_width 0.15)
    _clip_height = (*f $_image_size 0.5)
    _clip_width  = (*f $_clip_height 4)
    _colour      = (? $arg2 0x888888 0xffffff)
    _icon        = (? $arg2 "<grey>textures/icons/action" "<grey>textures/icons/action")
    _score_size  = (*f $game_hud_largepanel_text_size 0.9)

    uiclip $_clip_width $_clip_height 0 (*f $_image_size 1.25) [
        uifill $_clip_width (*f $_image_size 3)

        if $arg2 [
            _image_size = (*f $_image_size 0.9)
            _score_size = (*f $_score_size 0.9)
        ]

        uiimage $glowtex $_colour 0 $_image_size $_image_size [
            uicolourblend 0.33
        ]

        if (= $arg1 (getscoreplayer 0 0)) [
            _crown_size = (*f $_clip_height 1.4 (gameui_anim_pulse 0.25 1 1.2))
            uioffset 0 (*f $_clip_height -0.15) [
                uiimage "<grey>textures/icons/winner" #(hsvtohex [60 0.8 0.8]) 0 $_crown_size $_crown_size [
                    uicolourblend (? $arg2 0.33 0.5)
                    uiimageshadow 0.001 0x444444 0
                ]
            ]
        ] [
            uiimage $_icon $_colour 0 $_image_size $_image_size [
                uicolourblend 0.5
            ]
        ]

        uifont "play/clear/bold" [
            local _score _shadow_offset _shadow_offset_pos _pos

            _score = ($gameui_score_format (getclientpoints $arg1))
            _pos   = (get_pos_string (+ $arg3 1))

            _shadow_offset = (*f $_score_size 0.002 0.7)

            uioffset 0 (*f $_score_size -0.013) [
                uigroup [
                    uioffset $_shadow_offset $_shadow_offset [
                        uicolourtext $_pos 0 (*f $_score_size 0.7)
                    ]

                    uicolourtext $_pos 0x888888 (*f $_score_size 0.7)
                ]
            ]

            _shadow_offset = (*f $_score_size 0.002)

            uioffset 0 (*f $_score_size 0.013) [
                uigroup [
                    uioffset $_shadow_offset $_shadow_offset [
                        uicolourtext $_score 0 $_score_size
                    ]

                    uicolourtext $_score (? $arg2 0xaaaaaa 0xffffff) $_score_size

                    uipropagate [ uicolourblend 0.67 ]
                ]
            ]
        ]
    ]
]

ui_game_hud_match_score_effect = [
    local _team_colour _offset
    _team_colour = (getteamcolour $game_hud_match_effect_team)
    _image_size  = (*f $game_hud_largepanel_width 0.1)
    _offset      = 0

    if (= $game_hud_match_effect_team 1) [
        _offset = (lerpf $game_hud_largepanel_width 0 $game_hud_match_effect_anim)
    ] [
        _offset = (lerpf 0 $game_hud_match_effect_anim $game_hud_largepanel_width)
    ]

    uioffset $_offset 0 [
        uialign -1 -2
        uicroppedimage $glowtex $_team_colour 0 (*f $_image_size 2) (*f $_image_size 0.75) 0 0.3 1 0.35 [
            uicolourblend (*f (gameui_anim_flash $game_hud_match_effect_anim 1 1) 0.75)
        ]
    ]

    _score_shadow_colour = (tool_colour_lerp_int 0xaa000000 $_team_colour $game_hud_match_effect_anim)

    game_hud_match_effect_anim = (animstep $game_hud_match_effect_anim 250 -1)
]

ui_game_hud_match_score = [
    uigroup [
        uifill (*f $game_hud_largepanel_width 0.8) 0 [
            // uispace to compensate of slight overspill when using uirender
            uispace 0 0.0005 [
                uihlist 0 [
                    ui_game_hud_match_team $T_ALPHA 1
                    ui_game_hud_match_team $T_OMEGA 0
                ]
            ]
        ]

        local _score_shadow_colour
        _score_shadow_colour = 0xaa000000

        if $game_hud_match_effect_anim [
            ui_game_hud_match_score_effect
        ]

        ui_gameui_advshadowhoriz [
            p_colour = @_score_shadow_colour
            p_bgblend = 0.2
        ]
    ]
]

ui_game_hud_match_score_ffa = [
    uigroup [
        local _score_shadow_colour _neighbour
        _neighbour = (? $gameui_score_position (- $gameui_score_position 1) (+ $gameui_score_position 1))

        uifill (*f $game_hud_largepanel_width 0.8) 0 [
            // uispace to compensate of slight overspill when using uirender
            uispace 0 0.0005 [
                uihlist 0 [
                    ui_game_hud_match_client $ui_hud_focus 0 $gameui_score_position

                    if (>= (getscoreplayer 0 $_neighbour) 0) [
                        ui_game_hud_match_client (getscoreplayer 0 $_neighbour) 1 $_neighbour
                    ]
                ]
            ]
        ]

        _score_shadow_colour = 0xaa000000

        ui_gameui_advshadowhoriz [
            p_colour = @_score_shadow_colour
            p_bgblend = 0.2
        ]
    ]
]

// 1:<affinity id>
game_hud_match_get_capture_status = [
    _team    = (getcaptureteam $arg1)
    _aff_tex = $flagtex

    caseif (getcapturetaketime $arg1) [
        _warn     = 1
        _aff_flash_tex = $flagtakentex
    ] (getcapturedroptime $arg1) [
        _warn     = 1
        _aff_flash_tex = $flagdroptex
    ]
]

// 1:<affinity id>
game_hud_match_get_dac_status = [
    _team    = (getdefendowner $arg1)
    _aff_tex = $pointtex

    caseif (getdefendenemy $arg1) [
        _warn          = 1
        _aff_flash_tex = $attacktex
        _warn_colour   = (getteamcolour (getdefendenemy $arg1))
    ]
]

// 1:<affinity id>
game_hud_match_get_bomber_status = [
    _team    = (getclientteam (getbomberowner $arg1))
    _aff_tex = $bombtex

    caseif (!= $_team $T_NEUTRAL) [
        _warn          = 1
        _aff_flash_tex = $bombtakentex
    ] (getbomberdroptime $arg1) [
        _warn          = 1
        _aff_flash_tex = $bombdroptex
        _warn_colour   = 0xffffff
    ]
]

// 1:<affinity id> 2:<status cmd>
ui_game_hud_match_affinity = [
    local _team _team_colour _warn_colour _aff_tex _aff_flash_tex _warn _shadow_offset _aff_img_size
    _warn         = 0
    _warn_colour  = 0
    _aff_img_size = $_image_size

    $arg2 $arg1

    _team_colour = (getteamcolour $_team)

    if (&& $_warn [- (mod $totalmillis 1000) (mod $totalmillis 500)]) [
        _aff_tex = $_aff_flash_tex

        if $_warn_colour [
            _team_colour = $_warn_colour
        ]
    ]

    uigroup [
        uifill $_clip_width $_clip_height [
            uifill $_clip_width (*f $game_hud_largepanel_width 0.008) [
                if $_warn [
                    ui_gameui_advshadowhoriz [
                        p_colour  = @(? $_warn_colour $_warn_colour $_team_colour)
                        p_bgblend = 0.67
                    ]

                    uipropagate [ uicolourblend (gameui_anim_pulse 0.5 0 1) ]
                ]
            ]

            if $_warn [
                _aff_img_size = (*f $_aff_img_size (gameui_anim_pulse 0.5 0.8 1))
            ]

            _team_colour = (tool_colour_hsvmod_int $_team_colour s 0.75)

            uiimage $glowtex $_team_colour 0 $_aff_img_size $_aff_img_size [
                uicolourblend 0.33
            ]

            _shadow_offset = (*f $_aff_img_size 0.08)

            uioffset $_shadow_offset $_shadow_offset [
                uiimage $_aff_tex 0 0 $_aff_img_size $_aff_img_size [
                    uicolourblend 0.5
                ]
            ]

            uiimage $_aff_tex $_team_colour 0 $_aff_img_size $_aff_img_size

            if $_warn [
                uipropagate [ uicolourblend (gameui_anim_pulse 0.5 0.5 1) ]
            ] [
                uipropagate [ uicolourblend 0.5 ]
            ]
        ]

        if $arg3 [
            local _text_size
            _text_size     = (*f $game_hud_largepanel_text_size 0.7)
            _shadow_offset = (*f $_text_size 0.003)

            if $_warn [
                _text_size = (*f $_text_size (gameui_anim_pulse 0.5 0.75 1.2))
            ]

            uifont "play/clear/bold" [
                uioffset $_shadow_offset $_shadow_offset [
                    uicolourtext $arg3 0 $_text_size [
                        uicolourblend 0.8
                    ]
                ]

                uicolourtext $arg3 $_team_colour $_text_size
            ]
        ]
    ]
]

ui_game_hud_match_capture = [
    local _num _image_size _clip_width _clip_height
    _num = $getcapturenum
    _image_size  = (*f $game_hud_largepanel_width 0.06)
    _clip_height = (*f $_image_size 1.1)
    _clip_width  = (divf (*f $game_hud_largepanel_width 0.15 2) (+f (*f $_num 0.25) 0.5))

    uifill 0 0 [
        uifill 0 (*f $game_hud_largepanel_width 0.008) [
            uiclamp 1 1

            ui_gameui_advshadowhoriz [
                p_colour  = 0xff000000
                p_bgblend = 0.25
            ]
        ]

        uihlist 0 [
            loopcaptureif 0 0 i [= (getcaptureteam $i) 1] [
                ui_game_hud_match_affinity $i game_hud_match_get_capture_status
            ]

            loopcaptureif 0 0 i [= (getcaptureteam $i) 2] [
                ui_game_hud_match_affinity $i game_hud_match_get_capture_status
            ]
        ]
    ]
]

# ui_game_hud_match_dac = [
    local _num _image_size _clip_width _clip_height
    _num = $getdefendnum
    _image_size  = (*f $game_hud_largepanel_width 0.06)
    _clip_height = (*f $_image_size 1.1)
    _clip_width  = (divf (*f $game_hud_largepanel_width 0.15 2) (+f (*f $_num 0.25) 0.5))

    uifill 0 0 [
        uifill 0 (*f $game_hud_largepanel_width 0.008) [
            uiclamp 1 1

            ui_gameui_advshadowhoriz [
                p_colour  = 0xff000000
                p_bgblend = 0.25
            ]
        ]

        uihlist 0 [
            loopdefend 0 0 i [
                local _label
                _label = (getprintable $i)
                ui_game_hud_match_affinity $i game_hud_match_get_dac_status $_label
            ]
        ]
    ]
]

ui_game_hud_match_bomber = [
    local _num _image_size _clip_width _clip_height
    _num = 0

    loopbomberif 0 0 i [&& [getbomberenabled $i] [= (getbomberteam $i) $T_NEUTRAL]] [
        _num = (+ $_num 1)
    ]

    _image_size  = (*f $game_hud_largepanel_width 0.06)
    _clip_height = (*f $_image_size 1.1)
    _clip_width  = (divf (*f $game_hud_largepanel_width 0.15 2) (+f (*f $_num 0.25) 0.5))

    uifill 0 0 [
        uifill 0 (*f $game_hud_largepanel_width 0.008) [
            uiclamp 1 1

            ui_gameui_advshadowhoriz [
                p_colour  = 0xff000000
                p_bgblend = 0.25
            ]
        ]

        uihlist 0 [
            loopbomberif 0 0 i [&& [getbomberenabled $i] [= (getbomberteam $i) $T_NEUTRAL]] [
                ui_game_hud_match_affinity $i game_hud_match_get_bomber_status
            ]
        ]
    ]
]

ui_game_hud_match_speedrun = [
    uifill $game_hud_largepanel_width 0 [
        ui_gameui_advshadowhoriz [
            p_colour  = 0x88000000
            p_bgblend = 0.5
        ]

        uihlist (*f $game_hud_largepanel_width 0.08) [
            uigrid 2 (*f $game_hud_largepanel_width 0.05) 0 [
                local _text_size
                _text_size = (*f $game_hud_largepanel_text_size 0.6)

                uicolourtext "LAPS" 0x888888 $_text_size
                uitext (getclientpoints $ui_hud_focus) $_text_size

                uicolourtext "BEST" 0x888888 $_text_size
                uitext (timestr (getclientcptime $ui_hud_focus) $scorespeedrunstyle) $_text_size

                uicolourtext "LAST" 0x888888 $_text_size
                uitext (timestr (getclientcplast $ui_hud_focus) $scorespeedrunstyle) $_text_size
            ]

            uihlist (*f $game_hud_largepanel_width 0.01) [
                local _image_size
                _image_size = (*f $game_hud_largepanel_width 0.04)
                uiimage "textures/modes/speedrun" 0xffffff 0 $_image_size $_image_size
                uitext (timestr (getclientcpmillis $ui_hud_focus) $scorespeedrunstyle) $game_hud_largepanel_text_size
            ]
        ]
    ]
]

ui_game_hud_match = [
    local _colour _warn _time_text _time_mode_text
    _colour = (getclientcolour $ui_hud_focus $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)
    _warn   = (&& (getgametimelimit) [|| (!= $gamestate 5) (< $gametimeremain 60000)])

    gameui_score_update

    uivlist 0 [
        uifill 0 (*f $game_hud_largepanel_width 0.0055)

        uifill $game_hud_largepanel_width 0 [
            uifill $game_hud_largepanel_width (*f $game_hud_largepanel_text_size 0.01) [
                ui_gameui_advshadowhoriz [
                    p_colour  = (? $_warn (getclientpulsecolour $ui_hud_focus $PULSE_WARN) 0xaa000000)
                    p_bgblend = 0.5
                ]
            ]

            _time_text      = (timestr (? (getgametimelimit) $gametimeremain $gametimeelapsed) $showtimestyle)
            _time_mode_text = (? (= $gamestate $G_S_PLAYING) (? (getgametimelimit) "REMAINING" "ELAPSED") (strupper (at $G_S_NAMES $gamestate)))

            uifont "play/clear" [
                uioffset 0 (*f $game_hud_largepanel_text_size -0.025) [
                    uispace (*f $game_hud_largepanel_text_size 0.03) 0 [
                        uicolourtext $_time_mode_text 0xaaffffff (*f $game_hud_largepanel_text_size 0.6)
                    ]
                ]
            ]

            uifont "play/clear/bold" [
                local _shadow_offset
                _shadow_offset = (*f $game_hud_largepanel_text_size 0.002)

                uioffset 0 (*f $game_hud_largepanel_text_size -0.001) [
                    uioffset $_shadow_offset $_shadow_offset [
                        uicolourtext $_time_text 0 $game_hud_largepanel_text_size
                    ]

                    uicolourtext $_time_text 0xffffff $game_hud_largepanel_text_size
                ]
            ]
        ]

        if (! (hasmut $G_M_FFA)) [
            uivlist (*f $game_hud_largepanel_width 0.01) [
                ui_game_hud_match_score

                case $gamemode $G_CAPTURE [
                    ui_game_hud_match_capture
                ] $G_DEFEND [
                    ui_game_hud_match_dac
                ] $G_BOMBER [
                    ui_game_hud_match_bomber
                ] $G_SPEEDRUN [
                    ui_game_hud_match_speedrun
                ]
            ]
        ] [
            // if (!= $gamemode $G_EDITING) [
                ui_game_hud_match_score_ffa
            // ]
        ]
    ]
]
