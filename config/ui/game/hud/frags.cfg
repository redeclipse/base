game_hud_def_seqstep GAME_HUD_FRAGS_APPEAR    200
game_hud_def_seqstep GAME_HUD_FRAGS_HOLD      4600
game_hud_def_seqstep GAME_HUD_FRAGS_SHORTHOLD 1600
game_hud_def_seqstep GAME_HUD_FRAGS_FADE      200

game_hud_def_seq GAME_HUD_FRAGS_SEQ [
    GAME_HUD_FRAGS_APPEAR
    GAME_HUD_FRAGS_HOLD
    GAME_HUD_FRAGS_FADE
]

game_hud_def_seq GAME_HUD_FRAGS_SHORT_SEQ [
    GAME_HUD_FRAGS_APPEAR
    GAME_HUD_FRAGS_SHORTHOLD
    GAME_HUD_FRAGS_FADE
]

game_hud_frags_last_fragged      = ""
game_hud_frags_last_fragged_priv = ""
game_hud_frags_last_fragged_anim = 0

game_hud_frags_last_fragged_by      = ""
game_hud_frags_last_fragged_by_priv = ""
game_hud_frags_last_fragged_by_anim = 0

game_hud_frags_special_queue            = []
game_hud_frags_special_cur              = []
game_hud_frags_special_anim             = 0
game_hud_frags_special_action_performed = 0

GAME_HUD_FRAG_SPECIAL_HEADSHOT     = 0
GAME_HUD_FRAG_SPECIAL_REVENGE      = 1
GAME_HUD_FRAG_SPECIAL_DOMINATING   = 2
GAME_HUD_FRAG_SPECIAL_SPREEBREAKER = 3
GAME_HUD_FRAG_SPECIAL_DOUBLEKILL   = 4
GAME_HUD_FRAG_SPECIAL_TRIPLEKILL   = 5
GAME_HUD_FRAG_SPECIAL_MULTIKILL    = 6
GAME_HUD_FRAG_SPECIAL_FIRSTBLOOD   = 7
GAME_HUD_FRAG_SPECIAL_CARNAGE      = 8
GAME_HUD_FRAG_SPECIAL_SLAUGHTER    = 9
GAME_HUD_FRAG_SPECIAL_MASSACRE     = 10
GAME_HUD_FRAG_SPECIAL_BLOODBATH    = 11
GAME_HUD_FRAG_SPECIAL_TEAMINFO     = 12

loadsound S_FRAG_HEADSHOT     "sounds/announcer/frag/headshot"     0.8 1 1 -1 -1 1 1
loadsound S_FRAG_REVENGE      "sounds/announcer/frag/revenge"      0.8 1 1 -1 -1 1 1
loadsound S_FRAG_DOMINATING   "sounds/announcer/frag/dominating"   0.8 1 1 -1 -1 1 1
loadsound S_FRAG_SPREEBREAKER "sounds/announcer/frag/spreebreaker" 0.8 1 1 -1 -1 1 1
loadsound S_FRAG_DOUBLEKILL   "sounds/announcer/frag/doublekill"   0.8 1 1 -1 -1 1 1
loadsound S_FRAG_TRIPLEKILL   "sounds/announcer/frag/triplekill"   0.8 1 1 -1 -1 1 1
loadsound S_FRAG_MULTIKILL    "sounds/announcer/frag/multikill"    0.8 1 1 -1 -1 1 1
loadsound S_FRAG_FIRSTBLOOD   "sounds/announcer/frag/firstblood"   0.8 1 1 -1 -1 1 1
loadsound S_FRAG_CARNAGE      "sounds/announcer/frag/carnage"      0.8 1 1 -1 -1 1 1
loadsound S_FRAG_SLAUGHTER    "sounds/announcer/frag/slaughter"    0.8 1 1 -1 -1 1 1
loadsound S_FRAG_MASSACRE     "sounds/announcer/frag/massacre"     0.8 1 1 -1 -1 1 1
loadsound S_FRAG_BLOODBATH    "sounds/announcer/frag/bloodbath"    0.8 1 1 -1 -1 1 1

game_hud_frag_special_text_getters = [
    [ result "Headshot!"     ]
    [ result "Revenge!"      ]
    [ result "Dominating!"   ]
    [ result "Spreebreaker!" ]
    [ result "Double Kill!"  ]
    [ result "Triple Kill!"  ]
    [ result "Multikill!"    ]
    [ result "First Blood!"  ]
    [ result "Carnage!"      ]
    [ result "Slaughter!"    ]
    [ result "Massacre!"     ]
    [ result "Bloodbath!"    ]
    [ concatword "You are on team " (getteamname $arg1) "!" ]
]

game_hud_frag_special_color_getters = [
    [ pulsecolour $PULSE_HEADSHOT    ]
    [ pulsecolour $PULSE_REVENGE     ]
    [ pulsecolour $PULSE_DOMINATE    ]
    [ pulsecolour $PULSE_BREAK       ]
    [ pulsecolour $PULSE_MULTI       ]
    [ pulsecolour $PULSE_MULTI       ]
    [ pulsecolour $PULSE_MULTI       ]
    [ pulsecolour $PULSE_BLEED       ]
    [ pulsecolour $PULSE_ACHIEVEMENT ]
    [ pulsecolour $PULSE_ACHIEVEMENT ]
    [ pulsecolour $PULSE_ACHIEVEMENT ]
    [ pulsecolour $PULSE_BLOODBATH   ]
    [ getteamcolour $arg1            ]
]

game_hud_frag_special_actions = [
    [ soundbyname S_FRAG_HEADSHOT     ]
    [ soundbyname S_FRAG_REVENGE      ]
    [ soundbyname S_FRAG_DOMINATING   ]
    [ soundbyname S_FRAG_SPREEBREAKER ]
    [ soundbyname S_FRAG_DOUBLEKILL   ]
    [ soundbyname S_FRAG_TRIPLEKILL   ]
    [ soundbyname S_FRAG_MULTIKILL    ]
    [ soundbyname S_FRAG_FIRSTBLOOD   ]
    [ soundbyname S_FRAG_CARNAGE      ]
    [ soundbyname S_FRAG_SLAUGHTER    ]
    [ soundbyname S_FRAG_MASSACRE     ]
    [ soundbyname S_FRAG_BLOODBATH    ]
    [ game_announcer_push (? (= $getclientteam 1) S_ANNCR_ALPHA S_ANNCR_OMEGA) ]
]

game_hud_frag_team_changed_last_team  = -1

game_hud_frag_team_changed_reset = [
    game_hud_frag_team_changed_last_team = -1
]
onevent $CMD_EVENT_MAPLOAD game_hud_frag_team_changed_reset

game_hud_frag_team_changed = [
    local _result

    caseif (hasmut $G_M_FFA) [
        _result = 0
    ] $isspectator [
        _result = 0
    ] 1 [
        _result = (!= $getclientteam $game_hud_frag_team_changed_last_team)
    ]

    game_hud_frag_team_changed_last_team = $getclientteam

    result $_result
]

// 1:<client>
game_hud_frag = [
    if (= $arg1 $ui_hud_focus) [
        game_hud_frags_last_fragged      = "yourself"
        game_hud_frags_last_fragged_priv = ""
    ] [
        game_hud_frags_last_fragged      = (getclientname $arg1)
        game_hud_frags_last_fragged_priv = (getclientprivtex $arg1)
    ]
    game_hud_frags_last_fragged_anim = 1
]

// 1:<client>
game_hud_frag_by = [
    game_hud_frags_last_fragged_by      = (getclientname $arg1)
    game_hud_frags_last_fragged_by_priv = (getclientprivtex $arg1)
    game_hud_frags_last_fragged_by_anim = 1
]

// 1:<type>
game_hud_frag_special = [
    append game_hud_frags_special_queue $arg1
]

game_hud_frag_next_special = [
    if (!=s $game_hud_frags_special_queue []) [
        game_hud_frags_special_cur              = (at $game_hud_frags_special_queue 0)
        game_hud_frags_special_queue            = (sublist $game_hud_frags_special_queue 1)
        game_hud_frags_special_anim             = 1
        game_hud_frags_special_action_performed = 0
    ] [
        game_hud_frags_special_cur = []
    ]
]

// 1:<client name> 2:<client priv> 3:<anim> 4:<fragged by?>
ui_game_hud_frag_message = [
    local _font_size _priv_size _anim_size _anim_appear _anim_hold _anim_fade _blend _text _shadow_offset
    _font_size = (*f $game_hud_status_text_size 1.0)
    _priv_size = (*f $game_hud_status_text_size 0.025)
    _blend     = 1.0

    _anim_appear = @(game_hud_anim_seq_rev $GAME_HUD_FRAGS_SEQ $GAME_HUD_FRAGS_APPEAR [$arg3])
    _anim_hold   = @(game_hud_anim_seq_rev $GAME_HUD_FRAGS_SEQ $GAME_HUD_FRAGS_HOLD   [$arg3])
    _anim_fade   = @(game_hud_anim_seq_rev $GAME_HUD_FRAGS_SEQ $GAME_HUD_FRAGS_FADE   [$arg3])

    _anim_size =                 (lerpf 1.0 0.00001 $_anim_appear)
    _anim_size = (*f $_anim_size (lerpf 1.2 1.0     $_anim_hold))
    _anim_size = (*f $_anim_size (lerpf 1.5 1.2     $_anim_fade))

    _font_size = (*f $_font_size $_anim_size)
    _priv_size = (*f $_priv_size $_anim_size)

    _blend = (*f $_blend (lerpf 1.0 0.0 $_anim_appear))
    _blend = (*f $_blend (lerpf 0.0 1.0 $_anim_fade))

    if $arg4 [
        _text = "You were fragged by "
    ] [
        _text = "You fragged "
    ]

    _shadow_offset = (*f $_font_size 0.0025)

    uihlist 0 [
        uigroup [
            uioffset $_shadow_offset $_shadow_offset [
                uicolourtext (stripcolors $_text) 0 $_font_size
            ]

            uitext $_text $_font_size
        ]

        if (!=s $arg2 "") [
            uigroup [
                uioffset $_shadow_offset $_shadow_offset [
                    uiimage $arg2 0x444444 0 $_priv_size $_priv_size
                ]

                uiimage $arg2 0xffffff 0 $_priv_size $_priv_size
            ]
        ]

        _text = (concatword $arg1 "!")

        uigroup [
            uioffset $_shadow_offset $_shadow_offset [
                uicolourtext (stripcolors $_text) 0 $_font_size
            ]

            uitext $_text $_font_size
        ]

        uipropagate [uicolourblend $_blend]
    ]
]

ui_game_hud_frag_special_message = [
    local _anim_appear _anim_hold _anim_fade _anim_flash _text_getter _text _font_size _blend _colour_getter _colour _shadow_offset

    _font_size = (*f $game_hud_status_text_size 1.5)
    _blend     = 1.0

    _anim_appear = @(game_hud_anim_seq_rev $GAME_HUD_FRAGS_SHORT_SEQ $GAME_HUD_FRAGS_APPEAR    [$game_hud_frags_special_anim])
    _anim_hold   = @(game_hud_anim_seq_rev $GAME_HUD_FRAGS_SHORT_SEQ $GAME_HUD_FRAGS_SHORTHOLD [$game_hud_frags_special_anim])
    _anim_fade   = @(game_hud_anim_seq_rev $GAME_HUD_FRAGS_SHORT_SEQ $GAME_HUD_FRAGS_FADE      [$game_hud_frags_special_anim])
    _anim_flash  = (gameui_anim_segment $_anim_hold 0.95 1.0)

    _font_size = (*f $_font_size (lerpf 1.0 0.00001 $_anim_appear))
    _font_size = (*f $_font_size (lerpf 1.2 1.0     $_anim_hold))
    _font_size = (*f $_font_size (lerpf 1.5 1.2     $_anim_fade))

    _blend = (*f $_blend (lerpf 1.0 0.0 $_anim_appear))
    _blend = (*f $_blend (lerpf 0.0 1.0 $_anim_fade))

    _text_getter = (at $game_hud_frag_special_text_getters $game_hud_frags_special_cur)
    _text        = (_text_getter (getclientteam $ui_hud_focus))

    _colour_getter = (at $game_hud_frag_special_color_getters $game_hud_frags_special_cur)
    _colour        = (_colour_getter (getclientteam $ui_hud_focus))

    _shadow_offset = (*f $_font_size 0.0025)

    uifill 0 0 [
        uioffset $_shadow_offset $_shadow_offset [
            uicolourtext (stripcolors $_text) 0 $_font_size [
                uicolourblend $_blend
            ]
        ]

        uicolourtext $_text $_colour $_font_size [
            uicolourblend $_blend
        ]

        uicolourtext $_text $_colour (*f $_font_size (lerpf 1.5 1.0 $_anim_flash)) [
            uicolourblend (*f $_blend $_anim_flash)
        ]

        uifill (*f $game_hud_largepanel_width (lerpf 1.0 0.0 $_anim_flash)) (*f $_font_size 0.03 (lerpf 0.75 1.0 $_anim_hold)) [
            ui_gameui_advshadowhoriz [
                p_colour  = $_colour
                p_bgblend = (*f $_blend $_anim_flash)
            ]

            uipropagate [ uicolourblend (*f $_anim_hold 0.5) ]
        ]
    ]

    if $game_hud_frags_special_action_performed [] [
        local _action

        _action = (at $game_hud_frag_special_actions $game_hud_frags_special_cur)
        _action (getclientteam $ui_hud_focus)

        game_hud_frags_special_action_performed = 1
    ]
]

ui_game_hud_frags = [
    local _seq_len _short_seq_len
    _seq_len       = (game_hud_anim_seq_len $GAME_HUD_FRAGS_SEQ)
    _short_seq_len = (game_hud_anim_seq_len $GAME_HUD_FRAGS_SHORT_SEQ)

    if (game_hud_frag_team_changed) [
        game_hud_frag_special $GAME_HUD_FRAG_SPECIAL_TEAMINFO
    ]

    uifont "play/clear/bold" [
        uialign 0 -1

        uivlist 0 [
            uifill 0 0.05

            uifill 0 (*f $game_hud_status_width 0.08) [
                if (>f $game_hud_frags_last_fragged_anim 0) [
                    ui_game_hud_frag_message $game_hud_frags_last_fragged $game_hud_frags_last_fragged_priv $game_hud_frags_last_fragged_anim

                    game_hud_frags_last_fragged_anim = (animstep $game_hud_frags_last_fragged_anim $_seq_len -1)
                ]
            ]

            uifill 0 (*f $game_hud_status_width 0.08) [
                if (>f $game_hud_frags_last_fragged_by_anim 0) [
                    ui_game_hud_frag_message $game_hud_frags_last_fragged_by $game_hud_frags_last_fragged_by_priv $game_hud_frags_last_fragged_by_anim 1

                    game_hud_frags_last_fragged_by_anim = (animstep $game_hud_frags_last_fragged_by_anim $_seq_len -1)
                ]
            ]

            uifill 0 (*f $game_hud_status_width 0.16) [
                if (>f $game_hud_frags_special_anim 0) [
                    ui_game_hud_frag_special_message

                    game_hud_frags_special_anim = (animstep $game_hud_frags_special_anim $_short_seq_len -1)
                ] [
                    game_hud_frag_next_special
                ]
            ]
        ]
    ]
]
