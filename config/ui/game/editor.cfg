gameui_begin_ids

// Used for search and naming new maps
gameui_editor_query = ""

// Whether the name check is passed
gameui_editor_name_check = 0

// Fill the map list, sorted by custom maps first
// Assumes the map cinfo is already populated by maps.cfg
gameui_editor_populate_maps = [
    local _map_name _sort_func

    gameui_editor_official_maps = []
    gameui_editor_custom_maps   = []

    loop i (mapcinfo -1) [
        _map_name = (filename (mapcinfo $i 0))

        // Utilize the fact that shipped maps are scanned first, and appear sooner in the list
        if (&& [>= (listfind=s $getmaplist $_map_name) 0] [< (listfind=s $gameui_editor_official_maps $_map_name) 0]) [
            append gameui_editor_official_maps $i
        ] [
            append gameui_editor_custom_maps $i
        ]
    ]

    // Sort function for natural sorting
    _sort_func = [
        > (filemodifystamp (mapcinfo $a 1)) (filemodifystamp (mapcinfo $b 1))
    ]

    // Sort by filename
    gameui_editor_official_maps = (sortlist $gameui_editor_official_maps a b _sort_func)
    gameui_editor_custom_maps   = (sortlist $gameui_editor_custom_maps   a b _sort_func)
]
gameui_editor_populate_maps

// 1:<map index>
ui_gameui_editor_entry_mapshot = [
    uipad 0.02 0 0 0 [
        uialign -1
        uicolour 0 0.2 0.1 [
            uicroppedimage (mapcinfo $arg1 0) 0xffffff 0 0.2 0.1 0 0.25 1 0.75 [
                uifill 0.2 0.1 [
                    uioffset 0 0.06 [
                        uistyle clampxy

                        uicolour 0x66010101 0 0.025 [
                            ui_gameui_shadowhoriz
                        ]
                        uiprev [uistyle clampx]
                    ]
                    uispace 0.01 0.01 [
                        uistyle rightbottom

                        ui_gameui_autoscrollh [
                            uitext (filename (mapcinfo @arg1 0))
                        ] [
                            p_width = 0.18
                        ]
                    ]
                ]
            ]
        ]
    ]
]

// 1:<map index>
# ui_gameui_editor_entry_info = [
    uispace 0.01 0.01 [
        uistyle clampxy

        uistyle lefttop

        uifill 0 0 [
            uistyle clampxy

            uivlist 0 [
                uistyle lefttop

                ui_gameui_autoscrollh [
                    uitext (mapcinfo @arg1 2) 1.3
                ] [
                    p_width = 0.54
                ]

                uipropchild [uialign -1]

                uicolourtext (concatword "r" (+ (mapcinfo $arg1 5 7) 1)) 0xbbbbbb 1
                uiprev [uialign -1 1]
            ]
        ]
    ]
]

// 1:<map index>
# ui_gameui_editor_map_entry = [
    local _bg

    _bg = (? (& $gameui_editor_entry_counter 1) 0x555555 0x888888)

    uiborderedimageclamped $skintex $_bg 0 $ui_texborder $ui_screenborder 0.9 0.1 [
        if $uidrawn [
            uihover [
                gameui_tip      = "Double-click to load"
                gameui_hovering = 1

                uipropagate [uicolourblend 0.66]
            ] [
                uipropagate [uicolourblend 0.33]
            ]

            uidoublepress $arg1 [
                sleep 1 [ // WORKAROUND: Loading screen doesn't show up for some reason unless we sleep
                    gameui_close_all
                    edit (mapcinfo @@arg1 0)
                ]
            ]

            ui_gameui_editor_entry_mapshot $arg1

            uifill (-f 0.9 0.2 0.02) 0 [
                uistyle clampy
                uialign 1

                ui_gameui_editor_entry_info $arg1
            ]
        ]

        ui_gameui_shadow
    ]

    gameui_editor_entry_counter = (+ $gameui_editor_entry_counter 1)
]

// Checks whether a map name is unique and valid
gameui_editor_mapname_check = [
    gameui_editor_name_check = (!=s $gameui_editor_query "")

    loopwhile i (mapcinfo -1) [$gameui_editor_name_check] [
        if (=s (filename (mapcinfo $i 0)) $gameui_editor_query) [
            gameui_editor_name_check = 0
        ]
    ]
]

ui_gameui_editor_new_on_open = [
    gameui_editor_name_check = 0
]

ui_gameui_editor_new_dims = 0.72

# ui_gameui_editor_new = [
    local _anim_smooth _img_size
    _anim_smooth = (-f 1 (*f (-f 1 $gameui_panel_anim) (-f 1 $gameui_panel_anim)))
    _img_size    = (*f $gameui_panel_new_size $_anim_smooth)

    uispace 0 0 [
        uiimage "<grey>textures/icons/editing" 0xcccccc 0 $_img_size $_img_size [
            uipropagate [uicolourblend 0.1]
        ]
    ]

    uivlist 0.025 [
        uistyle clampx

        uicolour 0x55010101 0 0 [
            uistyle clampx

            uitext "Create new map" 2
        ]

        ui_gameui_textinput gameui_editor_query 20 [
            p_prompt    = "[Enter map name here]"
            p_immediate = 1
            p_on_change = [
                gameui_editor_mapname_check
            ]
        ]

        // Error message
        if (&& [!=s $gameui_editor_query ""] [! $gameui_editor_name_check]) [
            uispace 0.01 0.01 [
                uistyle clampx

                uicolour 0x66010101 0 0.025
                uiprev [uistyle clampx]

                uitext "Map name already exists." 1.2
            ]
        ] [
            // Spacer to keep the layout consistent
            uifill 0 0.045
        ]

        ui_gameui_button [
            p_label       = "Create"
            p_width       = 0.2
            p_on_click    = [
                gameui_close_all
                edit $gameui_editor_query
            ]
            p_disabled    = (! $gameui_editor_name_check)
            p_id          = #(gameui_get_id button)
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_editor_on_open = [
    gameui_editor_populate_maps
    gameui_editor_query = ""
]

ui_gameui_editor_dims = 1.2

# ui_gameui_editor = [
    gameui_editor_entry_counter = 0

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Map Editor" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uihlist 0.02 [
            uialign -1 -2

            uispace 0.02 0 [
                uistyle lefttop

                uivlist 0.005 [
                    ui_gameui_button [
                        p_label       = "New Map"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_on_click    = [
                            gameui_editor_query = ""
                            gameui_open ui_gameui_editor_new
                        ]
                        p_id          = #(gameui_get_id button)
                    ]
                ]
            ]

            uivlist 0.02 [
                uihlist 0.02 [
                    uialign -1

                    uitext "Search:"
                    ui_gameui_textinput gameui_editor_query 20 [
                        p_prompt    = "[Enter file name]"
                        p_width     = 0.5
                        p_immediate = 1
                    ]
                ]

                ui_gameui_vscrollarea [
                    uiborderedimageclamped $skintex 0x44010101 0 $ui_texborder $ui_screenborder 0.9 0.65 [
                        uivlist 0 [
                            uistyle lefttop

                            looplist i $gameui_editor_custom_maps [
                                if (>= (strcasestr (filename (mapcinfo $i 0)) $gameui_editor_query) 0) [
                                    ui_gameui_editor_map_entry $i
                                ]
                            ]

                            uicolour 0x33010101 0 0.08 [
                                uistyle clampx

                                uispace 0.01 0.01 [
                                    uistyle clampxy

                                    uicolourtext "Maps in the rotation" 0xcccccc 1.4
                                    uiprev [uialign -1 1]
                                ]

                                ui_gameui_shadow
                            ]

                            looplist i $gameui_editor_official_maps [
                                if (>= (strcasestr (filename (mapcinfo $i 0)) $gameui_editor_query) 0) [
                                    ui_gameui_editor_map_entry $i
                                ]
                            ]
                        ]

                        ui_gameui_shadow
                    ]
                ] [
                    p_width  = 0.9
                    p_height = 0.65
                ]

                ui_gameui_autoscrollh [
                    uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
                ] [
                    p_width = $gameui_panel_new_size
                ]
            ]
        ]
    ]

    #(ui_gameui_backbutton)
]
