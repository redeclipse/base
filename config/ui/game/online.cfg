gameui_online_entry_counter         = 0
gameui_online_connect_desc          = ""
gameui_online_connect_addr          = ""
gameui_online_connect_port          = 0
gameui_online_connect_pswd          = ""
gameui_online_entry_hover           = 0
gameui_online_entry_selected        = -1
gameui_online_entry_selected_prev   = -1
gameui_online_entry_selected_millis = 0
gameui_online_entry_anim            = 0

defvarp gameui_online_show_incompatible 0 0 1

// 1:<server id>
gameui_online_server_sig = [
    concatword (getserver $arg1 0 1) : (getserver $arg1 0 2)
]

// 1:<server id>
gameui_online_ver_check = [
    || $gameui_online_show_incompatible [= (getserver $arg1 1 0) (getversion 1)]
]

gameui_online_update = [
    if (>=f $gameui_panel_anim 1) [
        updateservers
    ]
]

ui_gameui_online_connect_on_close = [
    if (> $connectstatus 0) [
        disconnect
    ]
]

ui_gameui_online_connect_dims = 0.72

ui_gameui_online_connect_on_open = [
    connect $gameui_online_connect_addr $gameui_online_connect_port $gameui_online_connect_pswd
]

# ui_gameui_online_connect = [
    #(gameui_begin_ids)

    if (= $connectstatus 0) [
        gameui_close_all
    ]

    local _text
    _text = ""

    caseif (< $connectstatus 0) [
        _text = "Connection failed"
    ] (= $connectstatus 1) [
        _text = "Connecting..."
    ] (> $connectstatus 1) [
        _text = (format "Connection attempt %1 of %2..." $connectstatus (+ $connectattempts 1))
    ]

    uivlist 0.025 [
        uistyle clampx

        uicolour 0x55010101 0 0 [
            uistyle clampx

            uivlist 0 [
                uihlist 0 [
                    if (> $connectstatus 0) [
                        uiimage $uiloadtex 0xffffff 0 0.05 0.05
                    ]
                    uitext $_text 2
                ]

                ui_gameui_autoscrollh [
                    uicolourtext $gameui_online_connect_desc 0xcccccc
                ] [
                    p_width = $gameui_panel_new_size
                ]
            ]

            ui_gameui_shadowhoriz
        ]

        ui_gameui_prettybutton [
            p_label       = (? (> $connectstatus 0) "Cancel" "Ok")
            p_label_align = 0
            p_width       = 0.2
            p_pad_h_shift = 0
            p_on_click    = [
                gameui_close
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]
    ]
]

ui_gameui_online_private_dims = 0.72

ui_gameui_online_private_on_open = [
    soundbyname S_UI_ERROR
]

# ui_gameui_online_private = [
    #(gameui_begin_ids)

    gameui_online_update

    local _anim_smooth _img_size _password
    _anim_smooth = (-f 1 (*f (-f 1 $gameui_panel_anim) (-f 1 $gameui_panel_anim)))
    _img_size    = (*f $gameui_panel_new_size $_anim_smooth)

    uispace 0 0 [
        uiimage "<grey>textures/servers/locked" #(hsvtohex 0 0.66 1) 0 $_img_size $_img_size [
            uipropagate [uicolourblend 0.1]
        ]
    ]

    uivlist 0.025 [
        uistyle clampx

        uicolour 0x55010101 0 0 [
            uistyle clampx

            uivlist 0 [
                uitext "Server password required" 2
                ui_gameui_autoscrollh [
                    uicolourtext $gameui_online_connect_desc 0xcccccc
                ] [
                    p_width = $gameui_panel_new_size
                ]
            ]

            ui_gameui_shadowhoriz
        ]

        ui_gameui_textinput gameui_online_connect_pswd 24 [
            p_prompt    = "[Enter password here]"
            p_immediate = 1
        ]

        ui_gameui_button [
            p_label       = "Confirm"
            p_width       = 0.2
            p_on_click    = [
                gameui_online_connect 1
            ]
            p_id          = #(gameui_get_id button)
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_online_disconnect_dims = 0.72

ui_gameui_online_disconnect_on_open = [
    sleep 1 [
        soundbyname S_UI_ERROR
    ]
]

# ui_gameui_online_disconnect = [
    #(gameui_begin_ids)

    gameui_online_update

    local _anim_smooth _img_size
    _anim_smooth = (-f 1 (*f (-f 1 $gameui_panel_anim) (-f 1 $gameui_panel_anim)))
    _img_size    = (*f $gameui_panel_new_size $_anim_smooth)

    uispace 0 0 [
        uiimage "<grey>textures/servers/failed" #(hsvtohex 0 0.66 1) 0 $_img_size $_img_size [
            uipropagate [uicolourblend 0.1]
        ]
    ]

    uivlist 0.025 [
        uistyle clampx

        uicolour 0x55010101 0 0 [
            uistyle clampx

            uispace 0 0.01 [
                uivlist 0.01 [
                    uitext "Disconnected from server" 2
                    uicolourtext $disconnectreason 0xcccccc 1.3
                ]
            ]
        ]

        ui_gameui_button [
            p_label       = "Ok"
            p_width       = 0.2
            p_on_click    = [
                gameui_close_all
            ]
            p_id          = #(gameui_get_id button)
        ]
    ]
]

ui_gameui_online_entry_mapshot = [
    uipad 0.02 0 0 0 [
        uialign -1
        uicolour 0 0.2 $_size [
            if (>= $_lastping 0) [
                uitext "?" 4
                uicroppedimage (concatword "maps/" $_mapname) 0xffffff 0 0.2 $_size 0 0.25 1 (divf $_size 0.2) [
                    uispace 0 0.01 [
                        uialign -1 1
                        uistyle clampx

                        uicolour 0x66010101 0 0.025 [
                            ui_gameui_autoscrollh [
                                uialign 1 0
                                uispace 0.01 0 [
                                    uitext $_maptitle
                                ]
                            ] [
                                p_width = 0.18
                            ]

                            ui_gameui_shadowhoriz
                        ]
                        uiprev [uistyle clampx]
                    ]
                ]
            ] [
                uiimage $uiloadtex 0xffffff 0 0.05 0.05
            ]
        ]
    ]
]

gameui_online_entry_info_diff_str = [
    caseif (< $_ver_diff 0) [
        result "Older version"
    ] (> $_ver_diff 0) [
        result "Newer version"
    ]
]

_test_players = (loopconcat i 32 [
    local _len
    _len = (listlen $sv_botmalenames)
    at $sv_botmalenames (rnd (- $_len 1))
])

// 1:<server> 2:<is overlay>
# ui_gameui_online_entry_info = [
    uispace 0.01 0.01 [
        uistyle clampxy

        uistyle lefttop

        uifill 0 0 [
            uistyle clampxy

            uivlist 0 [
                uistyle lefttop

                ui_gameui_autoscrollh [
                    uitext $_desc 1.3
                ] [
                    p_width = 0.54
                ]

                if (&& (!=f $_anim 0) [>= $_lastping 0]) [
                    ui_gameui_autoscrollv [
                        uihlist 0 [
                            uitext (loopconcat i (getserver #1 arg1 2 -1) [getserver #1 arg1 2 $i]) 0.8 [
                                uitextalign -2
                                uitextwrap 0.5

                                uicolourblend (*f $_anim 0.45)
                            ]
                        ]
                    ] [
                        p_width  = 0.5
                        p_height = (lerpf 0.01 0.05 $_anim)
                    ]
                ]

                uipropchild [uialign -1]
            ]

            if (>= $_lastping 0) [
                if $_ver_diff [
                    uihlist 0.01 [
                        uicolourtext "Incompatible" #(hsvtohex 0 0.8 0.5) 1.2

                        uioffset 0 0.003 [
                            uicolourtext (concatword "(" (gameui_online_entry_info_diff_str) ")") #(hsvtohex 0 0.8 0.33) 0.8
                        ]
                    ]
                    uiprev [uialign -1 1]
                ] [
                    uivlist 0 [
                        uicolourtext $_gamemode 0x888888 1.1 [
                            uialign -1 1
                        ]
                        if (!=s $_gamemuts "") [
                            uicolourtext $_gamemuts 0x555555 0.8 [
                                uialign -1 1
                            ]
                        ]
                    ]
                    uiprev [uialign -1 1]

                    uivlist 0 [
                        uialign 1

                        uihlist 0 [
                            uistyle righttop

                            uicolourtext $_nump (? (>= $_nump $_maxp) #(hsvtohex 0 0.8 1) 0x999999) 1.2
                            uiprev [uialign -2 -1]
                            uicolourtext "/" 0xcccccc 1.5
                            uicolourtext $_maxp 0x999999 1.2
                            uiprev [uialign -2 1]
                        ]

                        uihlist 0 [
                            uistyle rightbottom

                            uicolourtext $_ping $_ping_color 1.2
                            uioffset 0 0.003 [
                                uicolourtext " ms" 0xcccccc
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

gameui_begin_ids

// 1:<server> 2:<is overlay> 3:<bg>
# ui_gameui_online_entry = [
    local _sig _bg _size _anim _sel _prevsel

    _sig     = (gameui_online_server_sig $arg1)
    _size    = 0.1
    _sel     = (=s $_sig $gameui_online_entry_selected)
    _prevsel = (=s $_sig $gameui_online_entry_selected_prev)
    _bg      = (? (& $gameui_online_entry_counter 1) 0x555555 0x888888)
    _anim    = 0

    caseif $_sel [
        _anim = (smoothstep $gameui_online_entry_anim)
        _size = (lerpf 0.1 0.15 $_anim)
    ] $_prevsel [
        _anim = (smoothstep (-f 1 $gameui_online_entry_anim))
        _size = (lerpf 0.1 0.15 $_anim)
    ]

    uiborderedimageclamped $skintex $_bg 0 $ui_texborder $ui_screenborder 0.9 $_size [
        if (|| $_sel $uidrawn) [
            uialign -1 -1

            local _desc _ping _ping_color _mapname _maptitle _gamemodeinfo _gamemode _gamemuts _maxp _nump _lastping _stat _stat_icon _stat_color _compatible
            _desc     = (getserver $arg1 0 3)
            _ping     = (getserver $arg1 0 6)
            _mapname  = (getserver $arg1 0 4)
            _maptitle = (getmaptitle $_mapname)
            _gamemode = (modename (getserver $arg1 1 1) (getserver $arg1 1 2))
            _gamemuts = (mutsname (getserver $arg1 1 1) (getserver $arg1 1 2) 0 64)
            _maxp     = (getserver $arg1 1 4)
            _nump     = (getserver $arg1 0 5)
            _lastping = (getserver $arg1 0 7)
            _stat     = (getserver $arg1 0 0)
            _ver_diff = (- (getserver $arg1 1 0) (getversion 1))

            uihover [
                gameui_online_entry_hover = 1

                uipropagate [uicolourblend 0.66]
            ] [
                uipropagate [uicolourblend 0.33]
            ]

            if (! $_sel) [
                uirelease [
                    sleep 1 [
                        gameui_online_entry_selected_millis = $totalmillis
                        gameui_online_entry_selected_prev   = $gameui_online_entry_selected
                        gameui_online_entry_selected        = @_sig

                        soundbyname S_UI_TICK
                        soundbyname S_UI_ANIM2 255 0 2
                    ]
                ]
            ]

            caseif (< $_ping 150) [
                _ping_color = #(hsvtohex 120 0.66 1)
            ] (< $_ping 300) [
                _ping_color = #(hsvtohex 60 0.66 1)
            ] (1) [
                _ping_color = #(hsvtohex 0 0.66 1)
            ]

            if $_ver_diff [
                _stat_icon  = "<grey>textures/servers/unknown"
                _stat_color = #(hsvtohex 15 0.66 0.75)
            ] [
                case $_stat 1 [
                    _stat_icon  = "<grey>textures/servers/locked"
                    _stat_color = #(hsvtohex 60 0.66 0.75)
                ] 2 [
                    _stat_icon  = "<grey>textures/servers/locked"
                    _stat_color = #(hsvtohex 0 0.66 0.75)
                ]
            ]

            ui_gameui_online_entry_mapshot

            uifill (-f 0.9 0.2 0.02) 0 [
                uistyle clampy
                uialign 1

                ui_gameui_online_entry_info $arg1 $_sel

                if $_stat_icon [
                    uipad 0.27 0 0 0 [
                        uicroppedimage $_stat_icon $_stat_color 0 (*f $_size 2) $_size 0 0.25 1 0.5 [
                            uipropagate [uicolourblend 0.25]
                        ]
                    ]
                ]
            ]

            if (!=f $_anim 0) [
                uihlist 0 [
                    uialign 1 1

                    ui_gameui_button [
                        p_label = "Join"
                        p_on_click = [
                            gameui_online_entry_selected = -1
                            if #3 _ver_diff [] [
                                local _addr _port
                                _addr = (getserver #1 arg1 0 1)
                                _port = (getserver #1 arg1 0 2)

                                gameui_online_connect_desc = [#3 _desc]
                                gameui_online_connect_addr = $_addr
                                gameui_online_connect_port = $_port
                                gameui_online_connect_pswd = ""

                                if (= #3 _stat 2) [
                                    gameui_open ui_gameui_online_private
                                ] [
                                    gameui_online_connect
                                ]
                            ]
                        ]
                        p_width  = 0.1
                        p_height = 0.04
                        p_id = #(gameui_get_id button)
                    ]
                    uiprev [
                        uipropagate [
                            uicolourblend $_anim
                        ]
                    ]
                ]
            ]
        ]

        ui_gameui_shadow

        if (!=f $_anim 0) [
            uiborderedimageclamped $skinshadowtex #(hsvtohex 8 0.5 1) 0 $ui_texborder $ui_screenborder 0 0 [
                uiclamp 1 1 1 1
                uicolourblend $_anim
            ]
        ]
    ]
]

ui_gameui_online_dims = 1.2

ui_gameui_online_on_open = [
    gameui_online_entry_selected      = []
    gameui_online_entry_selected_prev = []

]

# ui_gameui_online = [
    gameui_online_entry_counter = 0
    gameui_online_entry_hover   = 0
    gameui_online_update

    local _total_players _total_servers
    _total_players = (getserverplayersif i [= (getserver $i 1 0) (getversion 1)])
    _total_servers = (getservercountif   i [gameui_online_ver_check $i])

    gameui_online_entry_anim = (clampf (divf (-f $totalmillis $gameui_online_entry_selected_millis) 250) 0 1)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Server browser" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uihlist 0.02 [
            uialign -1 -2

            uispace 0.02 0 [
                uistyle lefttop

                uivlist 0.005 [
                    uihlist 0.005 [
                        uiimage "<grey>textures/icons/player" 0xffffff 0 0.025 0.025
                        uitext $_total_players 1.5

                        uifill 0.01

                        uiimage "<grey>textures/servers/list" 0xffffff 0 0.025 0.025
                        uitext $_total_servers 1.5
                    ]

                    ui_gameui_button [
                        p_label       = "Refresh list"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_tip         = "Fetch the latest server list"
                        p_on_click    = [
                            updatefrommaster
                        ]
                        p_id          = #(gameui_get_id button)
                    ]

                    ui_gameui_button [
                        p_label       = "LAN Connect"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_tip         = "Connect to a server on your local network"
                        p_on_click    = [
                            gameui_online_connect_desc = "LAN"
                            gameui_open ui_gameui_online_connect
                            lanconnect
                        ]
                        p_id          = #(gameui_get_id button)
                    ]

                    uifill 0 0.025

                    uihlist 0.01 [
                        uicolourtext "Show incompatible" 0xaaaaaa
                        ui_gameui_compactswitch gameui_online_show_incompatible [
                            p_tip   = "Show incompatible game version servers"
                            p_id    = #(gameui_get_id compactswitch)
                        ]
                    ]
                ]
            ]

            ui_gameui_vscrollarea [
                uiborderedimageclamped $skintex 0x44010101 0 $ui_texborder $ui_screenborder 0.9 0.65 [
                    if (= $_total_servers 0) [
                        uihlist 0 [
                            uiimage $uiloadtex 0xffffff 0 0.05 0.05
                            uitext "Looking for servers..."
                        ]
                    ] [
                        uivlist 0 [
                            uistyle lefttop

                            loopserversif i [gameui_online_ver_check $i] [
                                ui_gameui_online_entry $i
                                gameui_online_entry_counter = (+ $gameui_online_entry_counter 1)
                            ]
                        ]
                    ]

                    ui_gameui_shadow
                ]
            ] [
                p_width  = 0.9
                p_height = 0.65
            ]
        ]

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ] [
            p_width = $gameui_panel_new_size
        ]
    ]

    autosortservers (! $gameui_online_entry_hover)

    #(ui_gameui_backbutton)
]

ui_gameui_online_guidelines_dims = 1.2

ui_gameui_online_guidelines_on_open = [
    ui_gameui_online_guidelines_text = ""
    loadalias ui_gameui_online_guidelines_text "doc/guidelines.txt"

    soundbyname S_UI_ERROR
]

# ui_gameui_online_guidelines = [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Online guidelines" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        ui_gameui_horizgroup [
            uitext "You must agree to these guidelines before playing on this server:"

            uifill 0 0.025

            ui_gameui_vscrollarea [
                ui_gameui_group [
                    uicolourtext $ui_gameui_online_guidelines_text 0xcccccc 1 [
                        uitextwrap 1
                        uitextalign 1
                    ]
                ]
            ] [
                p_height = 0.6
            ]

            uifill 0 0.025

            uihlist 0.1 [
                ui_gameui_button [
                    p_label       = "Disagree"
                    p_width       = 0.2
                    p_colour      = #(hsvtohex 0 0.5 1)
                    p_highlight   = 1
                    p_on_click    = [
                        gameui_close
                    ]
                    p_id          = #(gameui_get_id button)
                ]

                ui_gameui_button [
                    p_label       = "Agree"
                    p_width       = 0.2
                    p_colour      = #(hsvtohex 120 0.5 1)
                    p_highlight   = 1
                    p_on_click    = [
                        connectguidelines 1
                        gameui_close

                        if (!=s $guidelinesaction []) [
                            gameui_close_all
                            do $guidelinesaction
                        ] [
                            gameui_open ui_gameui_online_connect 1
                        ]
                    ]
                    p_id          = #(gameui_get_id button)
                ]
            ]
        ]
    ]
]

gameui_online_prompt_guidelines = [
    if $connectguidelines [] [
        gameui_open ui_gameui_online_guidelines
    ]
]
onevent $CMD_EVENT_GAME_GUIDELINES gameui_online_prompt_guidelines

// 1:<drop ui>
gameui_online_connect = [
    if $connectguidelines [
        gameui_open ui_gameui_online_connect $arg1
    ] [
        set guidelinesaction []
        gameui_open ui_gameui_online_guidelines $arg1
    ]
]

// Server error
gameui_online_disconnect = [
    gameui_open ui_gameui_online_disconnect
]
onevent $CMD_EVENT_GAME_DISCONNECT gameui_online_disconnect
