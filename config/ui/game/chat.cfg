defvarp chatintime 0 250 $varidxmax
defvarp chatouttime 0 15000 $varidxmax
defvarp chatfadetime 0 500 $varidxmax
defvarp chatlines 0 6 $varidxmax
deffvarp chatblend 0 1 1

defvarp chatskin 0 2 4
deffvarp chatskinblend 0.0 0.9 1.0
deffvarp chatskinspace 0 0.001 $fvaridxmax

gameui_chat_msg    = ""
gameui_chat_focus  = 0
gameui_chat_typing = 0
gameui_chat_scroll = 0

// 1:<focused>
ui_gameui_chat_history = [
    local _timeout _lines _max_skip _ratio_displayed _scrollbar_length _scrollbar_maxoffset _scrollbar_offset
    _timeout = (? $arg1 0 $chatouttime)
    _lines   = (? $arg1 50 $chatlines)
    uitarget 0 0 [
        uistyle clampx
        uistyle leftbottom
        uivlist 0 [
            uistyle leftbottom
            uivlist 0 [
                uistyle leftbottom
                loopmessages (- $_lines) 0 message_id [
                    local message_time; message_time = (getmessagemillis $message_id)
                    local message_millis; message_millis = (- $totalmillis $message_time)
                    local message_blend; message_blend = $chatblend
                    local message_growth; message_growth = 1.0
                    caseif (&& $_timeout (> $message_millis $_timeout)) [
                        local message_ctime; message_ctime = (- $message_millis $_timeout)
                        if (&& $chatfadetime (< $message_ctime $chatfadetime)) [
                            message_growth = (-f 1 (divf $message_ctime $chatfadetime))
                            message_blend = (clampf (*f $message_growth $chatblend) 0 1)
                        ] [ message_blend = 0 ]
                    ] (&& $chatintime (< $message_millis $chatintime)) [
                        message_growth = (divf $message_millis $chatintime)
                        message_blend = (clampf (*f $message_growth $chatblend) 0 1)
                    ]
                    if (>f $message_blend 0.0) [
                        local message_clients; message_clients = (getmessagetaggroups $message_id "client")
                        local message_colour; message_colour = 0
                        loopwhile message_clidx $message_clients [! $message_colour] [
                            message_clientid = (getmessagetag $message_id "client" $message_clidx "clientnum")
                            if (= $focusedplayer $message_clientid) [ message_colour = 0x202010 ]
                        ]
                        local message_obj
                        message_obj = [
                            uiimage $infotex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle lefttop; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
                            uitext (getmessagelist $message_id "args" "console") $ui_texttiny [
                                uistyle lefttop
                                uitextalign -1
                                uicolourset (getmessagelist $message_id "args" "colour")
                            ]
                        ]
                        local message_type; message_type = (getmessagelist $message_id "args" "type")

                        cases $message_type "chat" [
                            message_obj = [
                                local message_tex message_thiscol message_action
                                message_action = (getmessagelist $message_id "args" "action")
                                if (& $message_action $SAY_TEAM) [
                                    local message_team
                                    message_team = (getmessagetag $message_id "client" 0 "team")
                                    message_tex = $[team@(at $T_NAMES $message_team)tex]
                                    message_thiscol = $[team@(at $T_NAMES $message_team)colour]
                                ] [
                                    message_tex = $chattex
                                    message_thiscol = 0xFFFFFFFF
                                ]
                                uiimage $message_tex $message_thiscol 0 0 $ui_hud_texttiny [ uistyle lefttop; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
                                local message_txtcol; message_txtcol = $colourwhite
                                local message_preface; message_preface = (getmessagename $message_id "client" 0)
                                if (& $message_action $SAY_WHISPER) [
                                    message_preface = (concat $message_preface "whispers")
                                ]
                                if (& $message_action $SAY_TEAM) [
                                    message_txtcol = (modcolour $message_thiscol 4)
                                ]
                                if (& $message_action $SAY_ACTION) [
                                    message_txtcol = $colourmagenta
                                ] [
                                    message_preface = (concatword $message_preface ":")
                                ]
                                local width
                                uitext $message_preface $ui_texttiny [
                                    uistyle lefttop
                                    uitextalign -1
                                    width = $uilastw
                                ]
                                uitext (getmessagelist $message_id "args" "text") $ui_texttiny [
                                    uistyle leftbottom
                                    uitextalign -1
                                    uicolourset $message_txtcol
                                    uitextwrap (-f 0.475 $ui_hud_texttiny $width (*f $ui_padsmaller 2))
                                ]
                            ]
                        ] "servmsg" [
                            message_obj = [
                                uiimage $infotex 0xFFFFFFFF 0 0 $ui_hud_texttiny [ uistyle lefttop; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
                                uitext (getmessagelist $message_id "args" "console") $ui_texttiny [
                                    uistyle leftbottom
                                    uitextalign -1
                                    uicolourset (getmessagelist $message_id "args" "colour")
                                    uitextwrap (-f 0.475 $ui_hud_texttiny (*f $ui_padsmaller 2))
                                ]
                            ]
                        ] "echomsg" [
                            message_obj = [
                                uiimage $dottex 0xFFFFFFFF 0 0 $ui_hud_texttiny [ uistyle lefttop; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
                                uitext (getmessagelist $message_id "args" "text") $ui_texttiny [
                                    uistyle leftbottom
                                    uitextalign -1
                                    uicolourset (getmessagelist $message_id "args" "colour")
                                    uitextwrap (-f 0.475 $ui_hud_texttiny (*f $ui_padsmaller 2))
                                ]
                            ]
                        ]

                        uitag (concatword "message_" $message_time "_" (getmessageseqid $message_id)) [
                            uipad 0 0 0 (*f $ui_padsmaller $message_growth) [
                                uistyle leftbottom
                                ui_hud_skin (? $message_colour $message_colour 0x80000000) 0 0 [ uistyle leftbottom ] [
                                    uistyle leftbottom
                                    uiclip 0 0 0 0 [
                                        uistyle leftbottom
                                        uispace $ui_padsmall $ui_padsmaller [
                                            uistyle leftbottom
                                            uihlist $ui_padsmall [
                                                uistyle leftmiddle
                                                @message_obj
                                            ]
                                        ]
                                        uiclipoffsetx (*f $uilastwprev (-f 1.0 $message_growth))
                                        uiclipoffsety (*f $uilasthprev (-f 1.0 $message_growth))
                                        uiclipsizew (*f $uilastwprev $message_growth)
                                        uiclipsizeh (*f $uilasthprev $message_growth)
                                    ]
                                ] (*f $chatskinspace $message_growth) $chatskin $chatskinblend
                                uipropagate [ uicolourblend $message_blend ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

// 1:<always show> 2:<width fill>
ui_gameui_chat = [
    if (|| $arg1 $gameui_chat_typing) [
        local _colour

        if (= $gameui_chat_typing 2) [
            _colour = $playerteamcolour
        ] [
            _colour = 0xffffff
        ]

        ui_gameui_group [
            ui_gameui_vscrollarea [
                ui_gameui_chat_history 1
            ] [
                p_width    = 0.4865
                p_height   = 0.15
                p_inverted = 1
            ]
            uiprev [uialign -1 1]

            uifill 0 0.01

            uihlist 0.005 [
                uistyle leftbottom
                uivlist 0 [
                    uistyle leftbottom
                    uiimage "<grey>textures/icons/chat" $_colour 1 0.03 0.03 [
                        uiimageshadow 0.002 0x88010101
                    ]
                    if (= $gameui_chat_typing 2) [
                        uitext "Team:" 0.8
                    ]
                    uifill 0.04
                ]
                uiprev [uialign -2 -1]

                ui_gameui_textinput gameui_chat_msg 42 [
                    p_prompt    = ""
                    p_focus     = $gameui_chat_focus
                    p_text_size = $ui_textsmall
                    p_lines     = 3
                    p_limit     = 100
                    p_on_change = [
                        if (!=s $gameui_chat_msg "") [
                            local say_action; say_action = (=s (at $gameui_chat_msg 0) "/me")
                            local say_type; say_type = (? $say_action "me" "say")
                            local say_text; say_text = (? $say_action (substr $gameui_chat_msg 4) $gameui_chat_msg)
                            if (= $gameui_chat_typing 2) [
                                [@[say_type]team] $say_text
                            ] [
                                [@[say_type]] $say_text
                            ]
                            gameui_chat_msg = ""
                        ]
                        gameui_chat_typing = 0
                    ]
                ]
            ]
        ] [
            p_space_x = 0.01
            p_space_y = 0.01
            p_clamp   = 0
            p_width   = $arg2
            p_colour = 0x44000000
        ]

        if (= $gameui_chat_typing 2) [
            uiprev [
                uiborderedimageclamped $skinshadowtex $_colour 0 $ui_texborder $ui_screenborder 0 0 [
                    uiclamp 1 1 1 1
                ]
            ]
        ]

        if $uianyfocus [] [
            gameui_chat_typing = 0
        ]
    ] [
        ui_gameui_chat_history
    ]
    uiprev [uialign -1 1]

    if $gameui_chat_focus [
        gameui_chat_focus = 0
    ]
]

gameui_chat = [
    gameui_chat_scroll = 0
    gameui_chat_msg    = ""
    gameui_chat_focus  = 1
    gameui_chat_typing = 1
]

gameui_chat_team = [
    if (! (hasmut $G_M_FFA)) [
        gameui_chat
        gameui_chat_typing = 2
    ]
]
