ui_gameui_settings_game_dims = 0.72

# ui_gameui_settings_game = [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Game settings" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uispace 0.05 0 [
            uivlist 0.025 [
                ui_gameui_group [
                    uistyle clampx

                    uigrid 2 0.05 0 [
                        uistyle clampx

                        uicolourtext "First-person" 0xaaaaaa
                        ui_gameui_slider firstpersonfov [
                            p_tip        = "First-person field of vision in degrees"
                            p_id         = #(gameui_get_id slider)
                        ]

                        uicolourtext "Third-person" 0xaaaaaa
                        ui_gameui_slider thirdpersonfov [
                            p_tip        = "Third-person field of vision in degrees"
                            p_id         = #(gameui_get_id slider)
                        ]

                        uicolourtext "Spectator" 0xaaaaaa
                        ui_gameui_slider specfov [
                            p_tip        = "Spectator field of vision in degrees"
                            p_id         = #(gameui_get_id slider)
                        ]

                        uicolourtext "Editor" 0xaaaaaa
                        ui_gameui_slider editfov [
                            p_tip        = "Editing field of vision in degrees"
                            p_id         = #(gameui_get_id slider)
                        ]
                    ]
                ] [
                    p_label = "Field of vision"
                ]

                ui_gameui_group [
                    uistyle clampx

                    uigrid 2 0.05 0 [
                        uistyle clampx

                        uicolourtext "Player brightening" 0xaaaaaa
                        ui_gameui_slider fullbrightmodels [
                            p_tip        = "Brightness boost of the players"
                            p_val_text   = [concatword (toint (round (*f $arg2 100))) "^%" ]
                            p_id         = #(gameui_get_id slider)
                        ]

                        uicolourtext "Speedrun player opacity" 0xaaaaaa
                        ui_gameui_slider playerghostblend [
                            p_tip        = "Opacity of other players in speedrun mode"
                            p_val_text   = [concatword (toint (round (*f $arg2 100))) "^%" ]
                            p_id         = #(gameui_get_id slider)
                        ]
                    ]
                ] [
                    p_label = "Player visibility"
                ]

                uihlist 0 [
                    uistyle clampx

                    ui_gameui_group [
                        uistyle clampx

                        uigrid 2 0.05 0 [
                            uistyle clampx

                            uicolourtext "Auto reload" 0xaaaaaa
                            ui_gameui_compactswitch weapautoreload [
                                p_tip = "Automatically reloads weapons when empty"
                                p_get = [= $arg1 4]
                                p_set = [? $arg1 4 2]
                                p_id  = #(gameui_get_id compactswitch)
                            ]

                            uicolourtext "Auto switch" 0xaaaaaa
                            ui_gameui_compactswitch weapautoswitch [
                                p_tip = "Automatically switches to another weapon when out of ammo"
                                p_id  = #(gameui_get_id compactswitch)
                            ]

                            uicolourtext "Loadout on match start" 0xaaaaaa
                            ui_gameui_compactswitch showloadoutmenu [
                                p_tip = "Show loadout menu on match start"
                                p_id  = #(gameui_get_id compactswitch)
                            ]
                        ]
                    ] [
                        p_label = "Weapon handling"
                    ]

                    uifill

                    ui_gameui_group [
                        uistyle clampx

                        uigrid 2 0.05 0 [
                            uistyle clampx

                            uicolourtext "First person camera" 0xaaaaaa
                            ui_gameui_compactswitch firstpersonbob [
                                p_tip = "First person camera bob animation"
                                p_id  = #(gameui_get_id compactswitch)
                            ]

                            uicolourtext "First person gun sway" 0xaaaaaa
                            ui_gameui_compactswitch firstpersonsway [
                                p_tip = "First person gun sway animation"
                                p_id  = #(gameui_get_id compactswitch)
                            ]
                        ]
                    ] [
                        p_label = "Animation"
                    ]
                    uiprev [uialign -2 -1]
                ]
            ]
        ]

        uifill 0 0.025

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ]
    ]

    #(ui_gameui_backbutton)
]

gameui_settings_controls_buttons = [
    [
        "Movement"
        [
            [ "Forward"  [forward]  ]
            [ "Backward" [backward] ]
            [ "Left"     [left]     ]
            [ "Right"    [right]    ]
            [ "Jump"     [jump]     ]
            [ "Crouch"   [crouch]   ]
            [ "Special"  [special]  ]
        ]
    ]
    [
        "Weapons"
        [
            [ "Primary fire"     [primary]                          ]
            [ "Secondary fire"   [secondary]                        ]
            [ "Reload"           [reload]                           ]
            [ "Next weapon"      [universaldelta 1]                 ]
            [ "Previous weapon"  [universaldelta -1]                ]
            [ "Primary weapon"   [weapon (weapload 0) 1]            ]
            [ "Secondary weapon" [weapon (weapload 1) 1]            ]
            [ "Wheel select"     [game_hud_piemenu_open_weapsel_key]]
            [ "Change loadout"   [gameui_player_show_loadout]       ]
            [ "Drop weapon"      [drop]                             ]
        ]
    ]
    [
        "Interaction"
        [
            [ "Drop affinity" [affinity] ]
            [ "Use/pick up"   [use]      ]
        ]
    ]
    [
        "Communication"
        [
            [ "Chat"            [gameui_chat]                    ]
            [ "Team chat"       [gameui_chat_team]               ]
            [ "Quick chat"      [game_hud_piemenu_open_taunts]   ]
            [ "Quick team chat" [game_hud_piemenu_open_teamchat] ]
        ]
    ]
    [
        "Other"
        [
            [ "Scoreboard"       [showscores]   ]
            [ "Suicide"          [suicide]      ]
            [ "Toggle edit mode" [edittoggle]   ]
            [ "Console"          [inputconsole] ]
        ]
    ]
]

gameui_settings_controls_update_cache = [
    local _action_num
    _action_num = 0

    looplist cat $gameui_settings_controls_buttons [
        looplist action (at $cat 1) [
            [gameui_settings_controls_buttons_cache@_action_num] = (strlower (searchbinds (at $action 1) [] [] " "))
            _action_num = (+ $_action_num 1)
        ]
    ]
]

gameui_settings_controls_unbind_action      = -1
gameui_settings_controls_bind_confirm       = -1
gameui_settings_controls_bind_confirm_key   = ""
gameui_settings_controls_bind_confirm_focus = -1

// 1:<key>
gameui_settings_controls_bind_busy = [
    !=s (getbind $arg1) []
]

// 1:<action> 2:<key>
gameui_settings_controls_bind = [
    bind $arg2 $arg1
    gameui_settings_controls_update_cache
]

// 1:<action num> 2:<key>
gameui_settings_controls_bind_unbind = [
    if (listhas $[gameui_settings_controls_buttons_cache@arg1] (strlower $arg2)) [
        bind $arg2 []
    ]
    gameui_settings_controls_update_cache
]

ui_gameui_settings_controls_entry = [
    local _colour _var _tmp
    _colour = (? (& $_action_num 1) 0x88444444 0x88333333)

    uiborderedimageclamped $skintex $_colour 0 $ui_texborder $ui_screenborder 0 0.04 [
        uistyle clampx

        if $uidrawn [
            _var = [gameui_settings_controls_buttons_cache@_action_num]

            uispace 0.01 0 [
                uialign -1

                uihlist 0 [
                    uialign -1

                    uifill (*f 0.54 0.45) 0 [
                        uialign -1

                        uitext (at $action 0)
                        uiprev [uialign -1]
                    ]

                    uiborderedimageclamped $skintex 0 0 $ui_texborder $ui_screenborder (*f 0.54 0.5) 0.03 [
                        uialign -1

                        uihover [
                            uipropagate [uicolourblend 0.66]
                        ] [
                            uipropagate [uicolourblend 0.33]
                        ]

                        ui_gameui_autoscrollh [
                            uikeycatcher _tmp (*f 0.54 0.5) 0.03 [
                                local _diff_key _no_confirm
                                _diff_key   = (!=s $gameui_settings_controls_bind_confirm_key $_tmp)
                                _no_confirm = (< $gameui_settings_controls_bind_confirm 0)

                                if (&& [|| $_diff_key $_no_confirm] [gameui_settings_controls_bind_busy $_tmp]) [
                                    gameui_settings_controls_bind_confirm       = $_action_num
                                    gameui_settings_controls_bind_confirm_focus = $_action_num
                                    gameui_settings_controls_bind_confirm_key   = $_tmp
                                ] [
                                    gameui_settings_controls_bind_confirm       = -1
                                    gameui_settings_controls_bind_confirm_focus = -1
                                    gameui_settings_controls_bind_confirm_key   = ""
                                    gameui_settings_controls_bind (at $action 1) $_tmp
                                ]
                            ] [
                                caseif $uikeycatcherisfocus [
                                    if (= $gameui_settings_controls_bind_confirm $_action_num) [
                                        uihlist 0 [
                                            uicolourtext "[" 0x888888 0.8
                                            uiaspectimage (concatword $textkeyprefix $gameui_settings_controls_bind_confirm_key) 0xffffff 0 0.02 0.02
                                            uicolourtext " in use, hit again to overwrite, esc to cancel]" 0x888888 0.8
                                        ]
                                    ] [
                                        uicolourtext "[hit key to bind, esc to cancel]" 0x888888 0.9
                                    ]
                                ] (= $_action_num $gameui_settings_controls_unbind_action) [
                                    uicolourtext "[hit key to unbind, esc to cancel]" 0x888888 0.9
                                ] (= $gameui_settings_controls_bind_confirm_focus $_action_num) [
                                    gameui_settings_controls_bind_confirm_focus = -1
                                    uikeycatchersetfocus 1
                                ] 1 [
                                    if (= $gameui_settings_controls_bind_confirm $_action_num) [
                                        gameui_settings_controls_bind_confirm     = -1
                                        gameui_settings_controls_bind_confirm_key = ""
                                    ]

                                    uihlist 0.002 [
                                        looplist key $$_var [
                                            uiaspectimage (concatword $textkeyprefix $key) 0xffffff 0 0.02 0.02
                                        ]
                                    ]
                                ]
                            ]
                        ] [
                            p_width = (*f 0.54 0.5)
                        ]

                        ui_gameui_shadow
                    ]

                    uiborderedimageclamped $skintex 0 0 $ui_texborder $ui_screenborder 0.03 0.03 [
                        uihover [
                            uipropagate [uicolourblend 0.66]
                        ] [
                            uipropagate [uicolourblend 0.33]
                        ]

                        uikeycatcher _tmp 0.03 0.03 [
                            gameui_settings_controls_bind_unbind $_action_num $_tmp
                        ] [
                            if (>= $gameui_settings_controls_unbind_action 0) [
                                if (&& [! $uikeycatcherisfocus] [= $gameui_settings_controls_unbind_action $_action_num]) [
                                    gameui_settings_controls_unbind_action = -1
                                ]
                            ] [
                                if $uikeycatcherisfocus [
                                    gameui_settings_controls_unbind_action = $_action_num
                                ]
                            ]
                        ]

                        uiimage $exittex 0xffffff 0 0.02 0.02

                        ui_gameui_shadow
                    ]
                ]
            ]
        ]
    ]
]

ui_gameui_settings_controls_on_open = [
    gameui_settings_controls_update_cache
    gameui_settings_controls_unbind_action = -1
]

ui_gameui_settings_controls_dims = 0.72

# ui_gameui_settings_controls = [
    #(gameui_begin_ids)

    local _action_num
    _action_num = 0

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Controls" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uivlist 0.025 [
            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "View" 0xaaaaaa
                    ui_gameui_slider sensitivity [
                        p_tip        = "Camera view sensitivity"
                        p_min        = 1
                        p_max        = 20
                        p_squared    = 1
                        p_id         = #(gameui_get_id slider)
                    ]

                    uicolourtext "Zoom multiplier" 0xaaaaaa
                    ui_gameui_slider zoomsensitivity [
                        p_tip        = "Camera zoom sensitivity multiplier"
                        p_min        = 0.1
                        p_max        = 1
                        p_id         = #(gameui_get_id slider)
                    ]

                    uicolourtext "UI" 0xaaaaaa
                    ui_gameui_slider mousesensitivity [
                        p_tip        = "User interface sensitivity"
                        p_min        = 0.25
                        p_max        = 5
                        p_squared    = 1
                        p_id         = #(gameui_get_id slider)
                    ]

                    uicolourtext "Invert mouse" 0xaaaaaa
                    ui_gameui_compactswitch mouseinvert [
                        p_tip      = "Inverts vertical mouse axis"
                        p_id       = #(gameui_get_id compactswitch)
                    ]
                ]
            ] [
                p_label = "Mouse sensitivity"
            ]
        ]

        ui_gameui_vscrollarea [
            uiborderedimageclamped $skintex 0x44010101 0 $ui_texborder $ui_screenborder 0.56 0.5 [
                uivlist 0 [
                    uistyle clampx

                    looplist cat $gameui_settings_controls_buttons [
                        uicolour 0x33010101 0 0.06 [
                            uistyle clampx

                            uispace 0.01 0.01 [
                                uistyle clampxy

                                uicolourtext (at $cat 0) 0xcccccc 1.4
                                uiprev [uialign -1 1]
                            ]

                            ui_gameui_shadow
                        ]

                        uivlist 0 [
                            uistyle clampx
                            uialign -1

                            looplist action (at $cat 1) [
                                ui_gameui_settings_controls_entry
                                _action_num = (+ $_action_num 1)
                            ]
                        ]
                    ]
                ]

                ui_gameui_shadow
            ]
        ] [
            p_width  = 0.56
            p_height = 0.5
        ]

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ]
    ]

    #(ui_gameui_backbutton)
]

deffvarp clipscale 0 1 2 [
    clipoffset (*f (getfvardef clipoffset) (get clipscale))
    clipsize   (*f (getfvardef clipsize)   (get clipscale))
]

ui_gameui_settings_interface_dims = 0.8

# ui_gameui_settings_interface = [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Interface" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uivlist 0.025 [
            ui_gameui_group [
                uistyle clampx

                uihlist 0.025 [
                    uistyle clampx

                    uigrid 2 0.05 0 [
                        uicolourtext "Scale" 0xaaaaaa
                        ui_gameui_slider crosshairscale [
                            p_tip      = "Scale of the crosshair"
                            p_max      = 2
                            p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                            p_width    = 0.27
                            p_id       = #(gameui_get_id slider)
                        ]

                        uicolourtext "Clip scale" 0xaaaaaa
                        ui_gameui_slider clipscale [
                            p_tip      = "Scale of the crosshair ammo clip"
                            p_max      = 1.5
                            p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                            p_width    = 0.27
                            p_id       = #(gameui_get_id slider)
                        ]

                        uicolourtext "Clip opacity" 0xaaaaaa
                        ui_gameui_slider clipblend [
                            p_tip      = "Opacity of the crosshair ammo clip"
                            p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                            p_width    = 0.27
                            p_id       = #(gameui_get_id slider)
                        ]

                        uifill 0 0.015
                        uifill

                        uicolourtext "Show ammo clip" 0xaaaaaa
                        ui_gameui_compactswitch showclips [
                            p_tip = "Show ammo clip around the crosshair"
                            p_id  = #(gameui_get_id compactswitch)
                        ]

                        uifill 0 0.015
                        uifill

                        uicolourtext "Clip animation" 0xaaaaaa
                        ui_gameui_switch clipanims [
                            p_width   = 0.27
                            p_options = [
                                "None"
                                "Simple"
                                "Full"
                            ]
                            p_tip     = "Ammo clip animation style"
                            p_id      = #(gameui_get_id switch)
                        ]

                        uicolourtext "Weapon styling" 0xaaaaaa
                        ui_gameui_switch crosshairweapons [
                            p_width   = 0.27
                            p_options = [
                                "None"
                                "Shape"
                                "Colour"
                                "Shape and colour"
                            ]
                            p_tip     = "Styles crosshairs uniquely for each weapon"
                            p_id      = #(gameui_get_id switch)
                        ]
                    ]

                    uicolour 0x22010101 0.18 0.18 [
                        local _weap _weap_name _colour _crosshair _size
                        _weap      = $getclientweapselect
                        _weap_name = (at $W_NAMES $_weap)

                        if (&& [! $(concatword $_weap_name "ammosub1") 0] [! $(concatword $_weap_name "ammosub2") 0]) [
                            _weap      = 1
                            _weap_name = (at $W_NAMES $_weap)
                        ]

                        if (& $crosshairweapons 1) [
                            _crosshair = $(concatword $_weap_name "crosshairtex")
                        ] [
                            _crosshair = "crosshairs/cross-01"
                        ]

                        if (> $crosshairweapons 1) [
                            _colour = $(concatword $_weap_name "colour")
                        ] [
                            _colour = 0xffffff
                        ]

                        _size = (*f $(concatword $_weap_name "crosshairsize") $crosshairscale)

                        uiimage $_crosshair $_colour 0 $_size $_size
                        if $showclips [
                            uiammoclip $_weap
                        ]

                        ui_gameui_shadow
                    ]

                    uipropchild [uialign -2 -1]
                ]
            ] [
                p_label = "Crosshair"
            ]

            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Scale" 0xaaaaaa
                    ui_gameui_slider game_hud_scale [
                        p_tip = "Scales the size of the in-game HUD"
                        p_min = 0.5
                        p_max = 2.0
                        p_id  = #(gameui_get_id slider)
                    ]

                    uicolourtext "Maximum aspect ratio" 0xaaaaaa
                    ui_gameui_slider game_hud_maxaspect [
                        p_tip = "Maximum aspect ratio of the in-game HUD, limits vertical space taken by the HUD"
                        p_min = 1.0
                        p_max = 4.0
                        p_id  = #(gameui_get_id slider)
                    ]

                    uicolourtext "Padding" 0xaaaaaa
                    ui_gameui_slider ui_padhud [
                        p_tip = "Spaces HUD from the edges of the screen"
                        p_min = 0.01
                        p_max = 0.1
                        p_id  = #(gameui_get_id slider)
                    ]
                ]

                uifill 0 0.01

                uihlist 0.01 [
                    uialign -1 0

                    uicolourtext "Frag feed assists" 0xaaaaaa
                    ui_gameui_compactswitch eventfragassist [
                        p_tip = "Enables showing of assists in the frag feed"
                        p_id  = #(gameui_get_id compactswitch)
                    ]

                    uifill 0.04

                    uicolourtext "Frag feed obituaries" 0xaaaaaa
                    ui_gameui_compactswitch eventobit [
                        p_tip = "Enables showing of textual obituaries in the frag feed"
                        p_id  = #(gameui_get_id compactswitch)
                    ]

                    uifill 0.04

                    uicolourtext "Sway" 0xaaaaaa
                    ui_gameui_compactswitch visorcamvelscale [
                        p_tip = "Enables HUD sway when moving"
                        p_id  = #(gameui_get_id compactswitch)
                    ]
                ]
            ] [
                p_label = "HUD settings"
            ]

            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Match time format" 0xaaaaaa
                    ui_gameui_switch showtimestyle [
                        p_options = [
                            "24.5s"
                            "12:34.567"
                            "12:34.5"
                            "12:34"
                            "12m34s"
                        ]
                        p_tip = "Time formatting of the match timer"
                        p_id  = #(gameui_get_id switch)
                    ]

                    uicolourtext "Framerate" 0xaaaaaa
                    ui_gameui_switch showfps [
                        p_options = [
                            "Off"
                            "Average"
                            "Average, min. and max."
                            "Average and range"
                            "Average and frametime"
                        ]
                        p_tip = "Shows the framerate in the top right corner of the screen"
                        p_id  = #(gameui_get_id switch)
                    ]
                ]

                uifill 0 0.01

                uihlist 0.01 [
                    uialign -1 0

                    uicolourtext "Real time" 0xaaaaaa
                    ui_gameui_compactswitch showrealtime [
                        p_tip = "Display the current real time"
                        p_id  = #(gameui_get_id compactswitch)
                    ]

                    uifill 0.04

                    uicolourtext "Minimap" 0xaaaaaa
                    ui_gameui_compactswitch showminimap [
                        p_tip = "Display the minimap"
                        p_id  = #(gameui_get_id compactswitch)
                    ]

                    uifill 0.04

                    uicolourtext "Performance" 0xaaaaaa
                    ui_gameui_compactswitch timer [
                        p_tip = "Display the performance profiler"
                        p_id  = #(gameui_get_id compactswitch)
                    ]
                ]
            ] [
                p_label = "Information"
            ]
        ]

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_settings_sound_dims = 0.72

# ui_gameui_settings_sound = [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Sound" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uivlist 0.025 [
            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Master" 0xaaaaaa
                    ui_gameui_slider soundmastervol [
                        p_tip      = "Master volume"
                        p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                        p_max      = 1
                        p_id       = #(gameui_get_id slider)
                    ]

                    uicolourtext "Music" 0xaaaaaa
                    ui_gameui_slider soundmusicvol [
                        p_tip      = "Music volume"
                        p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                        p_max      = 1
                        p_id       = #(gameui_get_id slider)
                    ]

                    uicolourtext "Game mix" 0xaaaaaa
                    ui_gameui_slider soundeffectvol [
                        p_tip      = "Game mix (effects and environment) volume"
                        p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                        p_max      = 1
                        p_id       = #(gameui_get_id slider)
                    ]

                    uifill 0 0.01
                    uifill

                    uicolourtext "Game effects" 0xaaaaaa
                    ui_gameui_slider soundeffectevent [
                        p_tip      = "Game effects volume"
                        p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                        p_max      = 1
                        p_id       = #(gameui_get_id slider)
                    ]

                    uicolourtext "Game environment" 0xaaaaaa
                    ui_gameui_slider soundeffectenv [
                        p_tip      = "Environment volume"
                        p_val_text = [concatword (toint (round (*f $arg1 100))) "^%" ]
                        p_max      = 1
                        p_id       = #(gameui_get_id slider)
                    ]

                    uicolourtext "Announcer" 0xaaaaaa
                    ui_gameui_slider game_announcer_volume [
                        p_tip       = "Announcer volume"
                        p_val_text  = [concatword (toint (round (*f $arg1 100))) "^%" ]
                        p_max       = 1
                        p_on_finish = [game_announcer_test]
                        p_id        = #(gameui_get_id slider)
                    ]
                ]
            ] [
                p_label = "Volume"
            ]

            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Real-time effects quality" 0xaaaaaa
                    ui_gameui_switch maxsoundefxslots [
                        p_options = [ "Low" "High" ]
                        p_get     = [ = $arg1 3 ]
                        p_set     = [ at [1 3] $arg1 ]
                        p_tip     = "Processing quality of real-time environment effects such as reverb"
                        p_id      = #(gameui_get_id switch)
                    ]
                ]
            ] [
                p_label = "Sound processing"
            ]

            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Voice variant" 0xaaaaaa
                    ui_gameui_switch game_announcer_variant [
                        p_options   = $game_announcer_variant_prettynames
                        p_tip       = "Choose the voice for the announcer"
                        p_on_change = [game_announcer_test]
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Sound origin" 0xaaaaaa
                    ui_gameui_switch game_announcer_env [
                        p_options = [ "Personal" "Environmental" ]
                        p_tip     = "Pick the sound origin for the announcer"
                        p_id      = #(gameui_get_id switch)
                    ]
                ]
            ] [
                p_label = "Announcer"
            ]

            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Hit sounds" 0xaaaaaa
                    ui_gameui_switch damagesound [
                        p_options   = [ "None" "Tones" "Ticks" "Ticks + Tones" ]
                        p_tip       = "Choose the hit sound style"
                        p_on_change = [
                            if (& $damagesound 1) [soundbyname S_DAMAGE]
                            if (& $damagesound 2) [soundbyname S_DAMAGE_TICK]
                        ]
                        p_id      = #(gameui_get_id switch)
                    ]

                    uicolourtext "Music choice" 0xaaaaaa
                    ui_gameui_switch musictype [
                        p_options = [ "Map defined" "Random" ]
                        p_tip     = "Choose the music for the maps"
                        p_get     = [= $arg1 2 ]
                        p_set     = [? $arg1 2 1]
                        p_id      = #(gameui_get_id switch)
                    ]

                    uifill 0 0.01
                    uifill

                    uicolourtext "Mumble positional audio" 0xaaaaaa
                    ui_gameui_compactswitch mumble [
                        p_tip      = "Positional audio for Mumble voice chat"
                        p_id       = #(gameui_get_id compactswitch)
                    ]
                ]
            ] [
                p_label = "Other"
            ]
        ]

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ]
    ]

    #(ui_gameui_backbutton)
]

// Graphics preset definitions
// Maps temporary parameters to getters, setters and presets
gameui_settings_graphics_preset_defs = [
    [
        gameui_settings_graphics_dispmode
        []
        []
        [
            //                  Windowed  Borderless Fullscreen
            [ fullscreen        0         1          1 ]
            [ fullscreendesktop 0         1          0 ]
        ]
    ]
    [
        gameui_settings_graphics_screenres
        [ listfind=s $gameui_settings_graphics_reslist (concatword $screenw "x" $screenh) ]
        [
            if (>= $gameui_settings_graphics_screenres 0) [
                local _res
                _res = (at $gameui_settings_graphics_reslist $gameui_settings_graphics_screenres)
                _res = (strreplace $_res "x" " ")

                if (!= $screenw (at $_res 0)) [ screenw (at $_res 0) ]
                if (!= $screenh (at $_res 1)) [ screenh (at $_res 1) ]
            ]
        ]
        []
    ]
    [
        gameui_settings_graphics_vsync
        []
        []
        [
            //          Off On Adaptive
            [ vsync     0   1  1 ]
            [ vsynctear 1   0  1 ]
        ]
    ]
    [
        gameui_settings_graphics_maxfps
        [ ? $maxfps (listfind=s $gameui_settings_graphics_refreshlist $maxfps) 0 ]
        [
            if (>= $gameui_settings_graphics_maxfps 0) [
                maxfps (at $gameui_settings_graphics_refreshlist $gameui_settings_graphics_maxfps)
            ]
        ]
        []
    ]
    [
        gameui_settings_graphics_gscale
        [ result $gscale ]
        [ gscale $gameui_settings_graphics_gscale ]
        []
    ]
    [
        gameui_settings_graphics_aa
        []
        []
        [
            //                  Off Low Medium High
            [ msaa              0   0   0      0 ]
            [ fxaa              0   1   0      0 ]
            [ smaa              0   0   1      1 ]
            [ tqaa              0   0   0      1 ]
            [ smaaquality       0   0   2      3 ]
        ]
    ]
    [
        gameui_settings_graphics_textures
        []
        []
        [
            //                  Low  Medium High
            [ bilinear          1    1      1    ]
            [ trilinear         1    1      1    ]
            [ texcompress       2048 4096   0    ]
            [ aniso             0    16     16   ]
            [ texreduce         1    0      0    ]
            [ envmapsize        5    7      10   ]
        ]
    ]
    [
        gameui_settings_graphics_shadows
        []
        []
        [
            //                     Very-low Low   Medium High  Very-high
            [ smsize               10       11    12     13    14     ]
            [ smfilter             0        1     2      3     3      ]
            [ smalpha              0        1     2      2     2      ]
            [ playershadow         0        2     2      2     2      ]
            [ playershadowsqdist   16384    16384 65536  65536 147456 ]
            [ flashlightmax        1        2     4      8     16     ]
            [ flashlightmaxdark    4        8     16     32    64     ]
        ]
    ]
    [
        gameui_settings_graphics_ao
        []
        []
        [
            //            Off Low Medium High
            [ ao          0   1   1      1 ]
            [ aobilateral 2   2   3      5 ]
            [ aotaps      3   3   5      5 ]
            [ aoiter      0   0   0      2 ]
            [ aoreduce    2   2   1      1 ]
        ]
    ]
    [
        gameui_settings_graphics_vol
        []
        []
        [
            //             Off Low Medium High
            [ volumetric   0   1   1      1  ]
            [ volsteps     16  16  32     64 ]
            [ volreduce    2   2   1      1  ]
            [ volbilateral 1   1   2      3  ]
            [ volprefilter 4   4   4      4  ]
        ]
    ]
    [
        gameui_settings_graphics_gi
        []
        []
        [
            //       Low Medium High
            [ rhtaps 12  20     32 ]
        ]
    ]
    [
        gameui_settings_graphics_effects
        []
        []
        [
            //                     Very-low Low   Medium High  Very-high
            [ mapeffects           1        1     2      3     3     ]
            [ grass                0        1     1      1     1     ]
            [ grassstep            4        4     3      2     2     ]
            [ grassdist            512      512   512    1024  1024  ]
            [ windanimdist         800      800   1200   2400  2400  ]
            [ maxparticles         4000     5000  6000   6000  6000  ]
            [ particlewind         0        0     1      1     1     ]
            [ softparticles        0        0     1      1     1     ]
            [ particlewind         0        0     1      1     1     ]
            [ stains               0        1     1      1     1     ]
            [ maxstaintris         4096     4096  4096   8192  8192  ]
            [ stainfade            15000    15000 30000  30000 60000 ]
            [ caustics             0        1     1      1     1     ]
            [ maxfxemitters        500      500   1000   1000  1000  ]
            [ fxdetail             0        1     2      2     2     ]
            [ waterreflect         0        0     1      1     1     ]
            [ weatherdropnumscale  0.25     0.5   1      1     1     ]
            [ compositesize        256      256   512    512   1024  ]
            [ compositeuprate      33       16    16     10    10    ]
            [ compositeruncount    1        1     1      2     3     ]
            [ viewportsize         64       128   256    256   256   ]
            [ viewportuprate       200      100   50     25    10    ]
        ]
    ]
    [
        gameui_settings_graphics_post
        []
        []
        [
            //           Off Low High
            [ bloom      0   1   1  ]
            [ bloomsize  9   9   11 ]
            [ bloomblur  2   2   5  ]
            [ bloomiter  0   0   1  ]
            [ visorglass 0   1   2  ]
        ]
    ]
    [
        gameui_settings_graphics_preset
        []
        []
        [
            //                                  Very-low Low Medium High Very-high
            [ gameui_settings_graphics_aa       0        1   2      3    3 ]
            [ gameui_settings_graphics_textures 0        1   1      2    2 ]
            [ gameui_settings_graphics_shadows  0        1   2      3    4 ]
            [ gameui_settings_graphics_ao       0        1   2      3    3 ]
            [ gameui_settings_graphics_vol      0        1   2      3    3 ]
            [ gameui_settings_graphics_gi       0        0   1      2    2 ]
            [ gameui_settings_graphics_effects  0        1   2      3    4 ]
            [ gameui_settings_graphics_post     0        1   2      2    2 ]
        ]
    ]
]

gameui_settings_graphics_init_default = [
    gameui_settings_graphics_init

    // Set the default medium preset
    gameui_settings_graphics_preset = 2
    gameui_setings_graphics_apply_global
    gameui_settings_graphics_apply
]
onevent $CMD_EVENT_FIRSTRUN gameui_settings_graphics_init_default

// Finds the preset index for the current graphics settings
// 1:<preset_def>
gameui_settings_graphics_find_preset = [
    local _preset_info _num_vars _num_presets _preset _match _var _var_presets _cmp_func
    _preset_info = (at $arg1 3)
    _preset      = -1
    _num_vars    = (listlen $_preset_info)
    _num_presets = (- (listlen (at $_preset_info 0)) 1)

    loopwhile i $_num_presets [< $_preset 0] [
        _match = 1

        loopwhile j $_num_vars [$_match] [
            _var_presets = (at $_preset_info $j)
            _var         = (at $_var_presets 0)

            case (getvartype $_var) $ididxvar [
                _cmp_func = [!=]
            ] $ididxalias [
                _cmp_func = [!=]
            ] $ididxfvar [
                _cmp_func = [!=f]
            ] $ididxsvar [
                _cmp_func = [!=s]
            ] () [
                _cmp_func = [!=]
            ]

            if ($_cmp_func (get $_var) (at $_var_presets (+ $i 1))) [
                _match = 0
            ]
        ]

        if $_match [
            _preset = $i
        ]
    ]

    result $_preset
]

// Applies a preset
// 1:<preset def>
gameui_settings_graphics_apply_preset = [
    local _preset_info _preset _num_vars _var _var_presets _cmp_func _new_val
    _preset_info = (at $arg1 3)
    _preset      = $(at $arg1 0)
    _num_vars    = (listlen $_preset_info)

    // If _preset is -1, it's a custom preset, so we don't need to do anything
    if (> $_preset -1) [
        loop i $_num_vars [
            _var_presets = (at $_preset_info $i)
            _var         = (at $_var_presets 0)
            _new_val     = (at $_var_presets (+ $_preset 1))

            case (getvartype $_var) $ididxvar [
                _cmp_func = [!=]
            ] $ididxalias [
                _cmp_func = [!=]
            ] $ididxfvar [
                _cmp_func = [!=f]
            ] $ididxsvar [
                _cmp_func = [!=s]
            ] () [
                _cmp_func = [!=]
            ]

            if ($_cmp_func (get $_var) $_new_val) [
                set $_var $_new_val
            ]
        ]
    ]
]

// Whether user has changed any graphics settings
gameui_settings_graphics_dirty = 0

// Resets all graphics settings to currently applied values
gameui_settings_graphics_reset = [
    local _presets _getter

    looplist preset_def $gameui_settings_graphics_preset_defs [
        _presets = (at $preset_def 3)

        if (=s $_presets []) [
            _getter = (at $preset_def 1)
            (at $preset_def 0) = (_getter)
        ] [
            (at $preset_def 0) = (gameui_settings_graphics_find_preset $preset_def)
        ]
    ]

    gameui_settings_graphics_dirty = 0
]

// Applies the current graphics settings
gameui_settings_graphics_apply = [
    local _presets _setter _preset _var

    hidechanges 1

    looplist preset_def $gameui_settings_graphics_preset_defs [
        if (!=s (at $preset_def 0) "gameui_settings_graphics_preset") [
            _presets = (at $preset_def 3)

            if (=s $_presets []) [
                _setter = (at $preset_def 2)
                _setter
            ] [
                gameui_settings_graphics_apply_preset $preset_def
            ]
        ]
    ]

    hidechanges 0

    if (> (pendingchanges -1) 0) [
        applychanges
    ]

    gameui_settings_graphics_dirty = 0
]

// Applies the global preset
gameui_setings_graphics_apply_global = [
    local _param
    _param = (listfind i $gameui_settings_graphics_preset_defs [=s (at $i 0) "gameui_settings_graphics_preset"])
    _param = (at $gameui_settings_graphics_preset_defs $_param)

    gameui_settings_graphics_apply_preset $_param
]

gameui_settings_graphics_init = [
    // Populate resolution list

    gameui_settings_graphics_reslist = (enumresolutions)

    // Filter out resultions below w 1024 or h 720
    gameui_settings_graphics_reslist = (listfilter i $gameui_settings_graphics_reslist [
        local _res
        _res = (strreplace $i "x" " ")
        && [>= (at $_res 0) 1024] [>= (at $_res 1) 720]
    ])

    // Populate refresh list

    local _refreshlist _cur_double
    _refreshlist = [
        "30"
        "60"
        "120"
        "144"
    ]

    gameui_settings_graphics_refreshlist = []
    loop i (listlen $_refreshlist) [
        // Limit to maximum supported refresh rate
        if (<= (at $_refreshlist $i) $refreshrate) [
            append gameui_settings_graphics_refreshlist (at $_refreshlist $i)
        ]
    ]

    // Add the current refresh rate if it's not in the list
    if (listhas $gameui_settings_graphics_refreshlist $refreshrate) [] [
        append gameui_settings_graphics_refreshlist $refreshrate
    ]

    // Add double the current refresh rate if it's not in the list and <= 500 Hz
    _cur_double = (* $refreshrate 2)
    if (&& [<= $_cur_double 500] [! (listhas $_refreshlist $_cur_double)]) [
        append _refreshlist $_cur_double
    ]

    gameui_settings_graphics_refreshlist = (sortlist $gameui_settings_graphics_refreshlist a b [ < $a $b ])
    gameui_settings_graphics_refreshlist = (concat "Unlimited" $gameui_settings_graphics_refreshlist)

    gameui_settings_graphics_reset
]

ui_gameui_settings_graphics_on_open = [
    gameui_settings_graphics_init
]

ui_gameui_settings_graphics_dims = [0.72 0.55]

# ui_gameui_settings_graphics = [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Graphics and display" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uivlist 0.025 [
            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Display mode" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_dispmode [
                        p_on_change = [ gameui_settings_graphics_dirty = 1 ]
                        p_options   = [ "Windowed" "Borderless" "Fullscreen" ]
                        p_tip       = "Display mode"
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Display resolution" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_screenres [
                        p_on_change = [ gameui_settings_graphics_dirty = 1 ]
                        p_options   = $gameui_settings_graphics_reslist
                        p_tip       = "Display output resolution"
                        p_custom    = [
                            if $p_disabled [
                                concatword $desktopw "x" $desktoph
                            ] [
                                concatword $screenw "x" $screenh
                            ]
                        ]
                        p_disabled  = (= $gameui_settings_graphics_dispmode 1)
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Vertical sync" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_vsync [
                        p_on_change = [ gameui_settings_graphics_dirty = 1 ]
                        p_options   = [ "Off" "On" "Adaptive" ]
                        p_tip       = "Vertical sync"
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Framerate limit" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_maxfps [
                        p_on_change = [ gameui_settings_graphics_dirty = 1 ]
                        p_options   = $gameui_settings_graphics_refreshlist
                        p_tip       = "Framerate limit"
                        p_get       = [
                            if (< $arg1 0) [
                                result (- (listlen $gameui_settings_graphics_refreshlist) 1)
                            ] [
                                result $arg1
                            ]
                        ]
                        p_custom    = [ result $maxfps ]
                        p_id        = #(gameui_get_id switch)
                    ]

                    uipropchild [uialign -2 0]
                ]

                uifill 0 0.01

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Render resolution" 0xaaaaaa
                    ui_gameui_slider gameui_settings_graphics_gscale [
                        p_on_change  = [ gameui_settings_graphics_dirty = 1 ]
                        p_min        = 25
                        p_max        = 100
                        p_val_format = i
                        p_val_text   = [ concatword (toint $arg1) "^%" ]
                        p_tip        = "Render resolution. Resolution of the text, UI and HUD elements is not affected."
                        p_id         = #(gameui_get_id slider)
                    ]

                    local _res _w _h
                    caseif (= $gameui_settings_graphics_dispmode 1) [
                        _w = $desktopw
                        _h = $desktoph
                    ] (>= $gameui_settings_graphics_screenres 0) [
                        _res = (at $gameui_settings_graphics_reslist $gameui_settings_graphics_screenres)
                        _res = (strreplace $_res "x" " ")
                        _w   = (at $_res 0)
                        _h   = (at $_res 1)
                    ] 1 [
                        _w = $screenw
                        _h = $screenh
                    ]

                    uicolourtext "Effective resolution" 0xaaaaaa
                    uicolourtext (concatword (toint (*f $_w $gameui_settings_graphics_gscale 0.01)) "x" (toint (*f $_h $gameui_settings_graphics_gscale 0.01))) 0xcccccc

                    uicolourtext "Brightness" 0xaaaaaa
                    ui_gameui_slider brightness [
                        p_min        = 0.1
                        p_max        = 3
                        p_val_format = f
                        p_round      = 0.05
                        p_val_text   = [ concatword (toint (round (*f 100 $arg1))) "^%" ]
                        p_id         = #(gameui_get_id slider)
                    ]

                    uipropchild [uialign -2 0]
                ]
            ] [
                p_label = "Display"
            ]

            ui_gameui_group [
                uistyle clampx

                uigrid 2 0.05 0 [
                    uistyle clampx

                    uicolourtext "Preset" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_preset [
                        p_on_change = [
                            gameui_setings_graphics_apply_global
                            gameui_settings_graphics_dirty = 1
                        ]
                        p_options   = [ "Very low" "Low" "Medium" "High" "Very high" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Graphics preset"
                        p_id        = #(gameui_get_id switch)
                    ]

                    uifill 0 0.01
                    uifill

                    uicolourtext "Anti-aliasing" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_aa [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Off" "Low" "Medium" "High" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Anti-aliasing. Smoothes out jagged edges of objects."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Textures" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_textures [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Low" "Medium" "High" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Texture quality. Affects texture resolution, compression and filtering."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Shadows" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_shadows [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Very low" "Low" "Medium" "High" "Very high" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Shadow quality. Affects shadow resolution, filtering and transparency."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Ambient occlusion" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_ao [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Off" "Low" "Medium" "High" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Ambient occlusion. Affects the quality of shadowing around objects."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Global illumination" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_gi [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Low" "Medium" "High" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Global illumination. Affects the quality of bounce lighting."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Volumetric lighting" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_vol [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Off" "Low" "Medium" "High" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Volumetric lighting. Affects the quality of volumetric light scattering."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Effects" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_effects [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Very low" "Low" "Medium" "High" "Very high" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Effects quality. Affects the quality of map detail, particles and stains."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uicolourtext "Post processing" 0xaaaaaa
                    ui_gameui_switch gameui_settings_graphics_post [
                        p_on_change = [
                            gameui_settings_graphics_dirty  = 1
                            gameui_settings_graphics_preset = -1
                        ]
                        p_options   = [ "Off" "Low" "High" ]
                        p_custom    = [ result "Custom" ]
                        p_tip       = "Post processing quality. Affects the resolution and filtering of bloom."
                        p_id        = #(gameui_get_id switch)
                    ]

                    uipropchild [uialign -2 0]
                ]
            ] [
                p_label = "Graphics"
            ]
        ]

        uihlist 0 [
            ui_gameui_button [
                p_label    = "Reset"
                p_tip      = "Reset graphics settings"
                p_disabled = (! $gameui_settings_graphics_dirty)
                p_on_click = [
                    gameui_settings_graphics_reset
                ]
                p_id        = #(gameui_get_id button)
            ]

            ui_gameui_button [
                p_label    = "Apply"
                p_tip      = "Apply graphics settings"
                p_disabled = (! $gameui_settings_graphics_dirty)
                p_on_click = [
                    gameui_settings_graphics_apply
                ]
                p_colour      = #(hsvtohex 8 0.3 1)
                p_highlight   = 1
                p_id        = #(gameui_get_id button)
            ]
        ]

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_settings_dims = [0.5 0.6]

# ui_gameui_settings = [
    #(gameui_begin_ids)

    uivlist 0 [
        uicolour 0x55010101 0 0 [
            uistyle clampx
            ui_gameui_decortext "Settings" [
                p_width = 0.5
            ]
            ui_gameui_shadowhoriz
        ]

        uifill 0 0.05

        ui_gameui_prettybutton [
            p_label    = "Game settings"
            p_tip      = "Gameplay settings"
            p_on_click = [
                gameui_open ui_gameui_settings_game
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label    = "Controls"
            p_tip      = "Keyboard and mouse settings"
            p_on_click = [
                gameui_open ui_gameui_settings_controls
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label    = "Interface"
            p_tip      = "User interface settings"
            p_on_click = [
                gameui_open ui_gameui_settings_interface
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label    = "Sound"
            p_tip      = "Sound and music settings"
            p_on_click = [
                gameui_open ui_gameui_settings_sound
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label    = "Graphics"
            p_tip      = "Graphics and display settings"
            p_on_click = [
                gameui_open ui_gameui_settings_graphics
            ]
            p_id       = #(gameui_get_id prettybutton)
        ]

        uifill 0 0.05

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ]
    ]

    #(ui_gameui_backbutton)
]
