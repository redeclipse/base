ui_gameui_support_about_dims = 0.72

# ui_gameui_support_about [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "About" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        ui_gameui_horizgroup [
            uicolourtext "(C) 2009-2023 Quinton Reeves, Lee Salzman" 0xaaaaaa
            uicolourtext "https://www.redeclipse.net/" #(hsvtohex [8 0.85 0.8])
        ] [
            p_label = "Red Eclipse Engine"
            p_width = 0.4
        ]

        ui_gameui_horizgroup [
            uicolourtext "(C) 2009-2023 Red Eclipse Team" 0xaaaaaa
            uicolourtext "https://www.redeclipse.net/" #(hsvtohex [8 0.85 0.8])
        ] [
            p_label = "Red Eclipse Assets"
            p_width = 0.4
        ]

        ui_gameui_horizgroup [
            uicolourtext "(C) 2014-2023 Quinton Reeves, Lee Salzman, et. al." 0xaaaaaa
            uicolourtext "http://tesseract.gg/" #(hsvtohex [8 0.85 0.8])
        ] [
            p_label = "Tesseract"
            p_width = 0.4
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_support_system_dims = 0.72

# ui_gameui_support_system [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "System information" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        local _os_name
        _os_name = (at ["Windows" "macOS" "Linux/Unix-like" "Other"] (getclientversion (getclientnum) 4))

        ui_gameui_horizgroup [
            uigrid 2 0.02 0 [
                uicolourtext "Operating system" 0xaaaaaa
                uicolourtext $_os_name 0xffffff

                uicolourtext "Bits" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 5) 0xffffff

                uicolourtext "OpenGL version" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 6) 0xffffff

                uicolourtext "OpenGL shader version" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 7) 0xffffff

                uicolourtext "OpenGL renderer version" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 11) 0xffffff

                uicolourtext "Graphics card" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 10) 0xffffff

                uicolourtext "Game branch" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 13) 0xffffff

                uicolourtext "Game build" 0xaaaaaa
                uicolourtext (getclientversion (getclientnum) 14) 0xffffff
            ]
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_support_accounts_dims = 0.9

# ui_gameui_support_accounts [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Player accounts and badges" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        local _accounts
        _accounts = [
            [ "bot"           "A computer-controlled player, used in lieu of" "adequate multiplayer numbers or in singleplayer." ]
            [ "none"          "A non-authenticated human player, without an" "account or any special privileges."                ]
            [ "player"        "A player who has a verified account in the game," "but does not have any special privilages."     ]
            [ "supporter"     "A player who has added to the project by" "participation, contribution or donation."              ]
            [ "moderator"     "A moderator, either local or global, with the" "power to mute and kick players."                  ]
            [ "administrator" "Holds the power to change server variables;" "global admins have access to the global ban list."  ]
            [ "developer"     "A recognised developer of the project;" "holds all powers of lower ranks."                        ]
            [ "founder"       "One of the creators of the project;" "holds all powers of lower ranks."                           ]
        ]

        ui_gameui_horizgroup [
            uifill 0 0.02

            uicolourtext "Players in Red Eclipse may be afforded" 0xaaaaaa 1.2
            uicolourtext "ranks based on their attachment to the community." 0xaaaaaa 1.2

            uifill 0 0.02

            uigrid 2 0.02 0 [
                uiline 0xaaaaaa; uiprev [uistyle clampx]
                uiline 0xaaaaaa; uiprev [uistyle clampx]
                looplist acc $_accounts [
                    uispace 0 0.01 [
                        uiimage (concatword "textures/privs/" (at $acc 0)) 0xffffff 0 0.04 0.04
                    ]
                    uispace 0 0.01 [
                        uivlist 0 [
                            uicolourtext (at $acc 1) 0xaaaaaa 1
                            uicolourtext (at $acc 2) 0xaaaaaa 1
                            uiall [uialign -1]
                        ]
                    ]

                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                ]
            ]
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_support_modes_dims = 1.2

# ui_gameui_support_modes [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Game modes" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        local _gmodes
        _gmodes = [
            [ "demo"       "Demo"               "Play recorded games back (viewable in singleplayer only)."                                   ]
            [ "editing"    "Editing"            "Modify or create maps with the in-game level editor alone or cooperatively."                 ]
            [ "deathmatch" "Deathmatch"         "Kill enemies to achieve the highest possible score during the time limit."                   ]
            [ "capture"    "Capture the Flag"   "Capture the opponent's flag and bring it back to your base, whilst defending your own."      ]
            [ "defend"     "Defend and Control" "Defend capture points from enemies and control the map to gain points."                      ]
            [ "bomber"     "Bomber Ball"        "Pick up the bomb and run it into the enemy's base, or maintain possession of it in hold-bb." ]
            [ "race"       "Race"               "Run a challenging parkour course as quickly as possible, avoiding deadly hazards."           ]
        ]

        ui_gameui_horizgroup [
            uifill 0 0.02

            uicolourtext "Red Eclipse contains five core gameplay" 0xaaaaaa 1.2
            uicolourtext "modes as well as playback and editing utilities." 0xaaaaaa 1.2

            uifill 0 0.02

            uigrid 2 0.02 0 [
                uiline 0xaaaaaa; uiprev [uistyle clampx]
                uiline 0xaaaaaa; uiprev [uistyle clampx]
                looplist gmode $_gmodes [
                    uispace 0 0.01 [
                        uiimage (concatword "textures/modes/" (at $gmode 0)) 0xffffff 0 0.04 0.04
                    ]
                    uispace 0 0.01 [
                        uivlist 0 [
                            uicolourtext (at $gmode 1) 0xffffff 1
                            uicolourtext (at $gmode 2) 0xaaaaaa 1
                            uiall [uialign -1]
                        ]
                    ]

                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                ]
            ]
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_support_weapons_dims = 1.2

# ui_gameui_support_weapons [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Weapons" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        local _weaps _weap_name _weap_colour
        _weaps = [
            [ "sword"   "Quick, short ranged primary along with a slower overhead slash causing bleed."            ]
            [ "shotgun" "High-spread, short range primary with medium-range bleed-inducing lobbed secondary."      ]
            [ "smg"     "High-recoil, high damage medium-ranged primary; secondary capable of splash at range."    ]
            [ "flamer"  "Extra mobility with secondary and high close-range DPS primary which causes burns."       ]
            [ "plasma"  "Blinding, medium range primary fire and cooked area-denial lobbed secondary fire."        ]
            [ "zapper"  "Fast, moderate damage, moderate ROF long range damage with identical scoped secondary."   ]
            [ "rifle"   "Slow firing, heavy hitting damage at any range, with extra power gained by scoping."      ]
            [ "pistol"  "Moderate damage, but capable of fairly high rate of fire and deadly in competent hands."  ]
            [ "mine"    "Capable of causing heavy damage and stun, the mine is tripped by proximity or its laser." ]
            [ "grenade" "Explodes and burns target if hit; secondary sticks to objects but is less powerful."      ]
            [ "claw"    "While capable of damaging opponents, the claw is more useful as a parkour aid."           ]
            [ "rocket"  "The most powerful weapon, a direct hit can instantly kill mutiple opponents."             ]
        ]

        ui_gameui_horizgroup [
            uifill 0 0.02

            uicolourtext "Red Eclipse Weapons" 0xaaaaaa 1.2

            uifill 0 0.02

            ui_gameui_vscrollarea [
                uigrid 2 0.02 0 [
                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                    looplist weap $_weaps [
                        _weap_name   = (getsvardef (concatword (at $weap 0) "longname"))
                        _weap_colour = (getvardef (concatword (at $weap 0) "colour"))

                        uispace 0 0.01 [
                            uiimage $glowtex $_weap_colour 0 0.04 0.04 [
                                uiprop [uicolourblend 0.33]
                            ]
                            uiimage (concatword "textures/weapons/" (at $weap 0)) $_weap_colour 0 0.04 0.04
                        ]
                        uispace 0 0.01 [
                            uivlist 0 [
                                uicolourtext $_weap_name 0xffffff 1
                                uicolourtext (at $weap 1) 0xaaaaaa 1
                                uiall [uialign -1]
                            ]
                        ]

                        uiline 0xaaaaaa; uiprev [uistyle clampx]
                        uiline 0xaaaaaa; uiprev [uistyle clampx]
                    ]
                ]
            ] [
                p_width  = 0.4
                p_height = 0.6
            ]
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_support_scoring_dims = 0.72

# ui_gameui_support_scoring [
    #(gameui_begin_ids)

    uivlist 0.025 [
        uistyle lefttop
        uistyle clampx

        uifill 0 0.05

        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Scoring rules" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        local _scorings _rate _colour
        _scorings = [
            [ "+6"  "Killing a flag carrier" "Killing a bomb carrier"                                                                  ]
            [ "+5"  "Capturing a flag" "Scoring with the bomb"                                                                         ]
            [ "+3"  "Normal opponent kills" "Picking up the flag" "Picking up the bomb"                                                ]
            [ "+1"  "Kill assist" "Getting a streak" "Dominating" "Holding the bomb (awarded each second)" "Ending domination/streaks" ]
            [ "0"   "Indirect team kills via residuals (burn, stun, bleed)"                                                            ]
            [ "-3"  "Direct team kills" "Suicide"                                                                                      ]
            [ "-6"  "Suicide whilst holding grenade" "Suicide with flag/bomb" "Teamkilling flag/bomb holder"                           ]
            [ "-10" "Allowing timer to run out while holding the bomb"                                                                 ]
        ]

        ui_gameui_horizgroup [
            uifill 0 0.02

            uicolourtext "Point values for gameplay activities" 0xaaaaaa 1.2

            uifill 0 0.02

            ui_gameui_vscrollarea [
                uigrid 2 0.02 0 [
                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                    uiline 0xaaaaaa; uiprev [uistyle clampx]
                    looplist scoring $_scorings [
                        _rate = (at $scoring 0)

                        caseif (> $_rate 0) [
                            _colour = 0x88ff88
                        ] (< $_rate 0) [
                            _colour = 0xff8888
                        ] 1 [
                            _colour = 0xffffff
                        ]

                        uispace 0 0.01 [
                            uistyle clampxy
                            uicolourtext $_rate $_colour 2
                        ]
                        uispace 0 0.01 [
                            uistyle clampy

                            uivlist 0 [
                                uistyle clampy

                                looplist i (sublist $scoring 1) [
                                    uihlist 0.01 [
                                        uiimage $bliptex 0x888888 0 0.008 0.008
                                        uicolourtext $i 0xaaaaaa 1
                                    ]
                                ]
                                uiall [uialign -1]
                            ]
                        ]

                        uiline 0xaaaaaa; uiprev [uistyle clampx]
                        uiline 0xaaaaaa; uiprev [uistyle clampx]
                    ]
                ]
            ] [
                p_width  = 0.4
                p_height = 0.6
            ]
        ]
    ]

    #(ui_gameui_backbutton)
]

ui_gameui_support_dims = [0.5 0.6]

# ui_gameui_support [
    #(gameui_begin_ids)

    uivlist 0 [
        uicolour 0x55010101 0 0 [
            uistyle clampx

            ui_gameui_decortext "Support" [
                p_width = $gameui_panel_new_size
            ]
            ui_gameui_shadowhoriz
        ]

        uifill 0 0.05

        ui_gameui_prettybutton [
            p_label       = "About"
            p_tip         = "About Red Eclipse"
            p_on_click    = [
                gameui_open ui_gameui_support_about
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label       = "Player accounts"
            p_tip         = "Information regarding player accounts and badges"
            p_on_click    = [
                gameui_open ui_gameui_support_accounts
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label       = "Game modes"
            p_tip         = "Information regarding game modes and goal"
            p_on_click    = [
                gameui_open ui_gameui_support_modes
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label       = "Weapons"
            p_tip         = "Information regarding weapons"
            p_on_click    = [
                gameui_open ui_gameui_support_weapons
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label       = "Scoring"
            p_tip         = "Score bonuses and penalties"
            p_on_click    = [
                gameui_open ui_gameui_support_scoring
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]

        ui_gameui_prettybutton [
            p_label       = "System info"
            p_tip         = "Gather system information for troubleshooting"
            p_on_click    = [
                gameui_open ui_gameui_support_system
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]

        uifill 0 0.05

        ui_gameui_autoscrollh [
            uicolourtext (? $gameui_hovering $gameui_tip "") 0x88ffffff
        ] [
            p_width = $gameui_panel_new_size
        ]
    ]

    #(ui_gameui_backbutton)
]