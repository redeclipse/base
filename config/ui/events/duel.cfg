event_duel_start = [
    event_image (modetex) 0xFFFFFF
    if (&& (hasmut $G_M_SURVIVOR) (> $event_clients 4)) [
        uitext (concatword "Survivor with ^fs^fc" $event_clients "^fS players") $game_hud_event_text_size [
            uistyle leftmiddle
            uitextalign -1
        ]
    ] [
        local event_extra; event_extra = 0
        loopeventgroups $event_id "client" 0 0 event_client [
            if $event_extra [
                uitext "vs." $game_hud_event_text_size [
                    uistyle leftmiddle
                    uitextalign -1
                    uicolourset $colourgrey
                ]
            ]
            uitext (geteventname $event_id "client" $event_client) $game_hud_event_text_size [
                uistyle leftmiddle
                uitextalign -1
            ]
            event_extra = 1
        ]
    ]
    event_image $arrowrighttex 0xFFFFFF
    local event_desc; event_desc = (? (geteventlist $event_id "args" "sudden") "SUDDEN DEATH" (format "Round %1" (geteventlist $event_id "args" "round")))
    uitext $event_desc $game_hud_event_text_size [
        uistyle leftmiddle
        uitextalign -1
    ]
]

event_duel_draw = [
    event_image (modetex) 0xFFFFFF
    uitext "All players fragged" $game_hud_event_text_size [
        uistyle leftmiddle
        uitextalign -1
    ]
    event_image $deadtex 0xFFFFFF
    uitext "EPIC FAIL" $game_hud_event_text_size [
        uistyle leftmiddle
        uitextalign -1
    ]
]

event_duel_score = [
    event_image (modetex) 0xFFFFFF
    if (&& (hasmut $G_M_SURVIVOR) (! (hasmut $G_M_FFA))) [
        local event_team; event_team = (geteventlist $event_id "args" "winner")
        if (> (geteventtaggroups $event_id "alive") 4) [
            uitext (concat "Team" (getteamname $event_team)) $game_hud_event_text_size [
                uistyle leftmiddle
                uitextalign -1
            ]
            event_image $winnertex 0xFFFFFF
        ] [
            local event_extra; event_extra = 0
            loopeventgroups $event_id "alive" 0 0 event_client [
                if $event_extra [
                    uitext "+" $game_hud_event_text_size [
                        uistyle leftmiddle
                        uitextalign -1
                        uicolourset $colourgrey
                    ]
                ]
                uitext (geteventname $event_id "alive" $event_client) $game_hud_event_text_size [
                    uistyle leftmiddle
                    uitextalign -1
                ]
                event_extra = 1
            ]
            event_image $winnertex 0xFFFFFF
        ]
    ] [
        uitext (geteventname $event_id "alive" 0) $game_hud_event_text_size [
            uistyle leftmiddle
            uitextalign -1
        ]
        event_image $winnertex 0xFFFFFF
    ]
    local event_dead; event_dead = (geteventtaggroups $event_id "dead")
    if (> $event_dead 4) [
        uitext (concatword "^fs^fc" $event_dead "^fS / ^fs^fc" $event_clients "^fS fragged") $game_hud_event_text_size [
            uistyle leftmiddle
            uitextalign -1
        ]
    ] [
        event_extra = 0
        loopeventgroups $event_id "dead" 0 0 event_client [
            if $event_extra [
                uitext "+" $game_hud_event_text_size [
                    uistyle leftmiddle
                    uitextalign -1
                ]
            ]
            uitext (geteventname $event_id "dead" $event_client) $game_hud_event_text_size [
                uistyle leftmiddle
                uitextalign -1
            ]
            event_extra = 1
        ]
    ]
    if (! (hasmut $G_M_INSTAGIB)) [
        local event_flawless; event_flawless = (geteventlist $event_id "args" "flawless")
        event_image $arrowrighttex 0xFFFFFF
        if $event_flawless [
            uitext "Flawless victory!" $game_hud_event_text_size [
                uistyle leftmiddle
                uitextalign -1
            ]
        ] [
            local event_health; event_health = (divf (geteventlist $event_id "args" "health") $damagedivisor)
            if $damageinteger [ event_health = (toint (ceil $event_health)) ] [ event_health = (precf $event_health 1) ]
            uitext (concatword "^fs^fc" $event_health "^fS health left") $game_hud_event_text_size [
                uistyle leftmiddle
                uitextalign -1
            ]
        ]
    ]
    local event_wins; event_wins = (geteventlist $event_id "args" "wins")
    if (> $event_wins 1) [
        event_image $arrowrighttex 0xFFFFFF
        uitext (concatword "^fs^fc" $event_wins "^fS in succession") $game_hud_event_text_size [
            uistyle leftmiddle
            uitextalign -1
        ]
    ]
]
