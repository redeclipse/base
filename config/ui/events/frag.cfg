death_frag_classic = [
    uicolourtext (getdeathlist $death_id "args" "console") $death_concolour $game_hud_event_text_size
]
death_suicide_classic = [ death_frag_classic ]

death_frag_assist_filter = [
    && [
        != $_death_assist 1
    ] [
        != $_death_assist 0
    ] [
        || [hasmut $G_M_FFA] [!= (getdeathtag $death_id "client" 0 "team") (getdeathtag $death_id "client" $_death_assist "team")]
    ] $_death_assist
]

death_frag_assists = [
    loopdeathgroups $death_id "client" 0 0 _death_assist [
        if (death_frag_assist_filter) [
            uitext "+" (*f $game_hud_event_text_size 0.8)
            uioffset 0 (*f $game_hud_event_text_size 0.0012) [
                uitext (getdeathname $death_id "client" $_death_assist) (*f $game_hud_event_text_size 0.8)
            ]
        ]
    ]
]

# death_frag_style_styles = [
    [ "HEADSHOT"      #PULSE_HEADSHOT    ]
    []
    [ "CARNAGE"       #PULSE_ACHIEVEMENT ]
    [ "SLAUGHTER"     #PULSE_ACHIEVEMENT ]
    [ "MASSACRE"      #PULSE_ACHIEVEMENT ]
    [ "BLOODBATH"     #PULSE_ACHIEVEMENT ]
    [ "DOUBLE-KILL"   #PULSE_MULTI       ]
    [ "TRIPLE-KILL"   #PULSE_MULTI       ]
    [ "MULTI-KILL"    #PULSE_MULTI       ]
    [ "REVENGE"       #PULSE_MULTI       ]
    [ "DOMINATING"    #PULSE_MULTI       ]
    [ "FIRST BLOOD"   #PULSE_MULTI       ]
    [ "SPREE BREAKER" #PULSE_MULTI       ]
    [ "PRIZE WINNER"  #PULSE_PRIZE       ]
]

# death_frag_style = [
    local _death_show _death_style_text _death_style_colour
    _death_show = (bitcycle $_death_style 1000)

    uifont "play/clear/bold" [
        uioffset (*f $game_hud_event_text_size 0.01) (*f $game_hud_event_text_size 0.001) [
            loop i #(listlen $death_frag_style_styles) [
                _death_style_text   = (at $death_frag_style_styles $i)
                _death_style_colour = (pulsecolour (at $_death_style_text 1))
                _death_style_text   = (at $_death_style_text 0)

                if (& $_death_style (<< 1 $i)) [
                    uicolourtext $_death_style_text $_death_style_colour (*f $game_hud_event_text_size 0.75) [
                        uicolourblend (? (& $_death_show (<< 1 $i)) 1 0)
                    ]
                ]
            ]
        ]
    ]

    uispace (*f $game_hud_event_image_size 0.125) 0
]

death_frag_normal = [
    local _death_weap _death_style _death_context

    _death_style = (& (^ $FRAG_OBLITERATE) (getdeathlist $death_id "args" "style"))

    if (& (getdeathlist $death_id "args" "actflags") $HIT_PRIZE) [
        _death_style = (| $_death_style (<< 1 13))
    ]

    if $_death_style [ death_frag_style ]

    _death_context  = (getdeathlist $death_id "args" "context")

    _death_weap     = (getdeathlist $death_id "args" "fromweap")
    if (< $_death_weap 0) [
        _death_weap = (getdeathlist $death_id "args" "actweap")
    ]

    uitext (getdeathname $death_id "client" 1) $game_hud_event_text_size

    if $deathfragassist [ death_frag_assists ]

    if (>= $_death_weap) [
        _death_weap = (at $W_NAMES $_death_weap)
        event_image $[@[_death_weap]eventtex] $[@[_death_weap]colour]
    ]

    if (getdeathlist $death_id "args" "burnfunc") [
        event_image $burntex (pulsecolour $PULSE_BURN)
    ]
    if (getdeathlist $death_id "args" "bleedfunc") [
        event_image $bleedtex (pulsecolour $PULSE_BLEED)
    ]
    if (getdeathlist $death_id "args" "shockfunc") [
        event_image $shocktex (pulsecolour $PULSE_SHOCK)
    ]
    if (getdeathlist $death_id "args" "corrodefunc") [
        event_image $corrodetex (pulsecolour $PULSE_CORRODE)
    ]

    if (&& [!=s $_death_context ""] $deathobit) [
        uitext $_death_context $game_hud_event_text_size
    ]

    uitext (getdeathname $death_id "client" 0) $game_hud_event_text_size
]

death_suicide_normal = [
    local _death_weap _death_style _death_context

    _death_style = 0

    if (& (getdeathlist $death_id "args" "actflags") $HIT_PRIZE) [
        _death_style = (| $_death_style (<< 1 13))
    ]

    if $_death_style [ death_frag_style ]

    _death_context  = (getdeathlist $death_id "args" "context")

    uitext (getdeathname $death_id "client" 0) $game_hud_event_text_size

    if (&& [!=s $_death_context ""] $deathobitself) [
        uitext $_death_context $game_hud_event_text_size
    ] [
        event_image $deadtex 0xFFFFFF
    ]
]
