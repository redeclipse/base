//This is the file containing the editing user interfaces, excluding the edit compass.
//Standard padding is $ui_padsmaller vertically and $ui_padbutton horizontally.

// Variables

//for texture ui
setpersist ui_tex_show_all 1
ui_tex_show_all = 1
ui_tex_search_criteria = ""
ui_tex_current_slot = 0

ui_tex_vpalette_types = [
    [   // Pulse colours
        "No palette"
        "Dark fire pulse"
        "Bright fire pulse"
        "Disco pulse"
        "Shock pulse"
        "Bleed pulse"
        "Buff pulse"
        "Warn pulse"
    ] [ // Team colours
        "Team Neutral"
        "Team Alpha"
        "Team Omega"
        "Team Neutral - Enforced"
        "Team Alpha - Enforced"
        "Team Omega - Enforced"
    ] [ // Weapon colours
        "Claw"
        "Pistol"
        "Sword"
        "Shotgun"
        "SMG"
        "Flamer"
        "Plasma"
        "Zapper"
        "Rifle"
        "Grenade"
        "Mine"
        "Rocket"
        "Melee"
        "Claw - Enforced"
        "Pistol - Enforced"
        "Sword - Enforced"
        "Shotgun - Enforced"
        "SMG - Enforced"
        "Flamer - Enforced"
        "Plasma - Enforced"
        "Zapper - Enforced"
        "Rifle - Enforced"
        "Grenade - Enforced"
        "Mine - Enforced"
        "Rocket - Enforced"
        "Melee - Enforced"
    ]
]

ui_tex_vshaderparam_types = [
    "envmin"
    "envscale"
    "gloss"
    "glowcolor"
    "parallaxscale"
    "pulseglowcolor"
    "pulseglowspeed"
    "specscale"
    "triplanarbias"
]

//for liquids ui
ui_water_settings = 0 //tab selection for the liquids ui

//for entities ui
ui_ent_set = 0 //value to set an entity attribute to
ui_ent_tab = 0 //tab selection for the entity menu
ui_ent_find = ""  //type of entity to search for

//for REEE ui (editref)
ui_editref_tab = 0
//entries in editref (by tab)
ui_editref_abc = "aiclip air allfaces alpha ambient ambientscale aodark aomin aoradius aosun atmo atmoblend atmobright atmoclarity atmodensity atmodiskbright atmodisksize atmohaze atmohazefade atmohazefadescale atmoheight atmolight atmolightscale atmoplanetsize blankgeom blendbrushcolour blendpaintmode calclight changeoutline clearblendmap cloudblend cloudbox clip cloudbox cloudfade cloudheight cloudlayer cloudlayercolour cloudscale cloudscrollx cloudscrolly cloudsubdiv copy copyprefab crosshairdistance crosshairdistancex crosshairdistancey"
ui_editref_def = "death decalreset delselwaypoints dropwaypoints dtoutline editcursorblend editcursorsize editcursortex editing editmatblend editmat editmatscale edittoggle entattr entcancel entfind entfindinsel entitysurf entlink entseldecalbox entselmapmodelbox entselradius entset envlayer envlayerblend envlayercolour envmapbb envmapradius fixinsidefaces flip fog fogcolour fogdomecap fogdomeclip fogdomeclouds fogdomecolour fogdomeheight fogdomemax fogdomemin fullbright"
ui_editref_ghi = "getmap gettex gidist giscale glass grasscolour grassheight grassblend gridpower hurt hurtbleeddamage hurtbleeddelay hurtbleedtime hurtburndamage hurtburndelay hurtburntime hurtdamage hurtdelay hurtresidual hurtshockdamage hurtshockdelay hurtshockstun hurtshockstunfall hurtshockstunscale hurtshockstuntime hurtshocktime"
ui_editref_jkl = "ladder lava lavacolour lavafog lavaglowmin lavaglowmax lavaspec lerpangle lerpsubdiv lerpsubdivsize lightbatches lightbatchrects lightbatchstacks lightpassesused lightsoccluded lightsvisible loadwaypoints lse"
ui_editref_mno = "mapauthor mapcrc mapdesc mapenlarge mapname maprevision mapsize maptitle maxmerge minimapheight newent newmap obitdeath obitfall obithurt obitlava obitwater optmats"
ui_editref_pqrs = "paste redo remip replace rotate savemap saveprefab savewaypoints selectbrush selectionsurf sendmap showedittoggles showentdir showstats showtexstats showwaypoints shrinkmap skycolour skylight spinsky spincloudlayer spinclouds sunlightpitch sunlightyaw"
ui_editref_tuv = "texturecull texturerehash undo undomegs valpha vcoastscale vcolour vdelta vlayer voffset vrefract vreset vrotate vangle vscale vscroll vshaderparam vpalette"
ui_editref_wxyz =  "water watercolour waterdeep waterdeepcolour waterdeepfade waterfallcolour waterfallrefract waterfallrefractcolour waterfallspec waterfog waterreflectstep waterrefract waterspec windinterval windspeed windyaw wireframe yawcloudlayer yawclouds yawsky"

//for prefabs ui
ui_prefabs = ""

//UI menus in this file (in order), formatted as menuname@[linenumber]
// textures@100 materials_getbindicon,materials@239,253 liquids_rgbsliders,liquids_horzsliders,liquids_water_body,liquids_lava_body,liquids@372,385,411,474,450
// skybox_entry,skybox_body,skybox@506,519,540 altskybox@544 edit@548 editsettings@574 environment@747 map@923 obituaries@1092 coopedit@1111
// entities@1131 editref_output,editref@1232,1258 prefabrefresh,prefab@1332,1337 mapmodel@1362

// Main textures menu, accessed by [e]-[t] or [f1]
uimenu "textures" "Texture Browser" "" [
    uihlist 0.03125 [
        uialign 0 -1
        uiclamp 1 1 1 1
        uivlist $ui_padsmall [
            uiclamp 1 1 1 1
            uivlist $ui_padsmall [
                uivslotview $ui_tex_current_slot $ui_buttonzw $ui_buttonzw [uioutline 0xC0C0C0; uiclamp- 1 1 1 1]
                ui_tex_current_slot_filename = (gettexname $ui_tex_current_slot)
                while [!= (strstr $ui_tex_current_slot_filename "/") -1] [ui_tex_current_slot_filename = (substr $ui_tex_current_slot_filename (+ 1 (strstr $ui_tex_current_slot_filename "/")))]
                uitext (format "#%1, %2" $ui_tex_current_slot $ui_tex_current_slot_filename) $ui_text [uitextlimit 1]
                uitext (gettexname $ui_tex_current_slot) $ui_texttiny [uitextlimit 1]
            ]
            uivlist $ui_padsmall [
                //These are the v-commands for textures: vscale vrotate vangle voffset vscroll valpha vcolour vrefract vpallete vcoastscale
                // as they are shown off to the left on the texture UI
                uihlist 0 [
                    uitextleft "Shaders:" $ui_textsmall
                    uitextright (format "%1" (getvshadername $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Scale:" $ui_textsmall
                    uitextright (format "%1x" (getvscale $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Offset:" $ui_textsmall
                    uitextright (format "X: %1, Y: %2" @(getvoffset $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Scroll:" $ui_textsmall
                    uitextright (format "X: %1, Y: %2" @(getvscroll $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Rotation:" $ui_textsmall
                    uitextright (format "%1 (%2)" (at ["0 deg" "90 deg" "180 deg" "270 deg" "90 deg transposed" "270 deg transposed" "0 deg transposed" "180 deg transposed"] (getvrotate $ui_tex_current_slot)) (getvrotate $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Angle:" $ui_textsmall
                    uitextright (format "%1" (getvangle $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Alpha:" $ui_textsmall
                    uitextright (format "F: %1, B: %2" @(getvalpha $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uihlist 0 [
                    uitextleft "Colour:" $ui_textsmall
                    uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" @(getvcolour $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" (* (*f (at (getvcolour $ui_tex_current_slot) 0) 255) 1) (* (*f (at (getvcolour $ui_tex_current_slot) 1) 255) 1) (* (*f (at (getvcolour $ui_tex_current_slot) 2) 255) 1)) $ui_textsmall [uitextlimit 1]
                uihlist 0 [
                    uitextleft "Refract:" $ui_textsmall
                    uitextright (format "%1%%" (at (getvrefract $ui_tex_current_slot) 0)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                uitextright (format "^fr%2^fw - ^fg%3^fw - ^fb%4" @(getvrefract $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" (* (*f (at (getvrefract $ui_tex_current_slot) 1) 255) 1) (* (*f (at (getvrefract $ui_tex_current_slot) 2) 255) 1) (* (*f (at (getvrefract $ui_tex_current_slot) 3) 255) 1)) $ui_textsmall [uitextlimit 1]
                uitextleft "Palette:" $ui_textsmall
                uitextright (format "%3 (%1 %2)" @(getvpalette $ui_tex_current_slot) (at (at $ui_tex_vpalette_types (at (getvpalette $ui_tex_current_slot) 0)) (at (getvpalette $ui_tex_current_slot) 1))) $ui_textsmall [uitextlimit 1]
                uihlist 0 [
                    uitextleft "Coast:" $ui_textsmall
                    uitextright (format "%1x" (getvcoastscale $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                    uiclamp 1 1
                ]
                if (!= (getvlayer $ui_tex_current_slot) 0) [ // if the cursor texture is layered (for blendmapping) it will display those informations here (hidden otherwise)
                    uihlist 0 [
                        uitextleft "Layer:" $ui_textsmall
                        ui_tex_current_slot_vlayer_filename = (gettexname (getvlayer $ui_tex_current_slot))
                        while [!= (strstr $ui_tex_current_slot_vlayer_filename "/") -1] [ui_tex_current_slot_vlayer_filename = (substr $ui_tex_current_slot_vlayer_filename (+ 1 (strstr $ui_tex_current_slot_vlayer_filename "/")))]
                        uitextright (format "#%1, %2" (getvlayer $ui_tex_current_slot) $ui_tex_current_slot_vlayer_filename) $ui_textsmall [uitextlimit 1]
                        uiclamp 1 1
                    ]
                ]

                if (!= (getvdetail $ui_tex_current_slot) 0) [// if the cursor texture has vdetail set it will display those informations here (hidden otherwise)
                    uihlist 0 [
                        uitextleft "Detail:" $ui_textsmall
                        ui_tex_current_slot_vdetail_filename = (gettexname (getvdetail $ui_tex_current_slot))
                        while [!= (strstr $ui_tex_current_slot_vdetail_filename "/") -1] [ui_tex_current_slot_vdetail_filename = (substr $ui_tex_current_slot_vdetail_filename (+ 1 (strstr $ui_tex_current_slot_vdetail_filename "/")))]
                        uitextright (format "#%1, %2" (getvdetail $ui_tex_current_slot) $ui_tex_current_slot_vdetail_filename) $ui_textsmall [uitextlimit 1]
                        uiclamp 1 1
                    ]
                ]
                loop i (listlen $ui_tex_vshaderparam_types) [// if the cursor texture has vshaderparam commands set they will be displayed here (hidden otherwise)
                    if (!=s (getvshaderparam $ui_tex_current_slot (at $ui_tex_vshaderparam_types $i)) "") [
                        uihlist 0 [
                            uitextleft (format "%1:" (at $ui_tex_vshaderparam_types $i)) $ui_textsmall
                            uitextright (getvshaderparam $ui_tex_current_slot (at $ui_tex_vshaderparam_types $i)) $ui_textsmall [uitextlimit 1]
                            uiclamp 1 1
                        ]
                    ]
                ]
                uispace 0.115 0.000
                uiclamp 1 1 1 1
            ]
            //option to show modified textures or only unmodified ones (no vcolour vshaderparam vscale etc.)
            uivlist $ui_padsmall [
                uicheckbox "Show variants" $ui_tex_show_all $ui_checksize [ui_tex_show_all = (! $ui_tex_show_all)]
                uiinput ui_tex_search_criteria 20 [] 1 1 "Search textures..." $colourgrey
            ]
        ]
        uihlist $ui_padsmall [
            uiscroll 1.0546875 0.65625 [
                uifill 1.0546875 0.65625 [
                    uigrid 8 0.0078125 0.0078125 [
                        uihover [] [ui_tex_current_slot = (getseltex)]
                        if (= $ui_tex_show_all 1) [
                            looptexmru tex [
                                if (|| (=s $ui_tex_search_criteria "") (!= (strstr (gettexname $tex) $ui_tex_search_criteria) -1)) [
                                    uivslotview $tex 0.125 0.125 [
                                        if (= $tex (getseltex)) [uioutline 0xFF0000; uiclamp- 1 1 1 1] // Outline currently selected texture
                                        uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1; ui_tex_current_slot = $tex]]
                                        uirelease [uiclose "textures"; settex $tex] //this is the part that selects the texture at the cursor
                                    ]
                                ]
                            ]
                        ] [
                            loop slot (numslots) [
                                if (|| (=s $ui_tex_search_criteria "") (!= (strstr (gettexname $slot) $ui_tex_search_criteria) -1)) [
                                    uislotview $slot 0.125 0.125 [
                                        if (= $slot (getseltex)) [uioutline 0xFF0000; uiclamp- 1 1 1 1] // Outline currently selected texture
                                        uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1; ui_tex_current_slot = $slot]]
                                        uirelease [uiclose "textures"; settex (getslottex $slot)] //this is the part that selects the texture at the cursor
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uialign* -1 -1
                ]
            ]
            uivscroll $ui_slidersize 0.65625 3 //scroll bar at the right of the menu
        ]
    ]
]

//grabs an appropriate icon path for given material (with ability to handle binds to "editmat <mat>" and "<mat>")
ui_materials_getbindicon = [ //arg1: material, output: valid key file for material's bind
    caseif (!= -1 (strstr (substr (searcheditbinds (concat "editmat" $arg1) 0 " ") 1) " " )) [ //if there is multiple keys (a space between entries in searcheditbind) for a material, do routine to choose one only
        concatword "textures/keys/" (strlower (substr (searcheditbinds (concat "editmat" $arg1) 0 " ") (+ 2 (strstr (substr (searcheditbinds (concat "editmat" $arg1) 0 " ") 1) " " ) ))) //pick latter of two binds w/ strstr finding space between and then using that index to chop off searcheditbinds input
    ] (!= -1 (strstr (substr (searcheditbinds $arg1 0 " ") 1) " " )) [ //this one searches for multiple binds to <mat> as opposed to "editmat <mat>"
        concatword "textures/keys/" (strlower (substr (searcheditbinds $arg1 0 " ") (+ 2 (strstr (substr (searcheditbinds $arg1 0 " ") 1) " " ) )))
    ] (!=s "" (searcheditbinds $arg1)) [//if there's a single bind to <mat>
        concatword "textures/keys/" (strlower (searcheditbinds $arg1))
    ] (!=s "" (searcheditbinds (concat "editmat" $arg1))) [//if there's a single bind to "editmat <mat>"
        concatword "textures/keys/" (strlower (searcheditbinds (concat "editmat" $arg1)))
    ]
]

// This is the "0" menu for selecting a material.
// UI buttons are coded to ui_rgbw (rgb slider width): .16 units wide and are set to normal button height.
uimenu "materials" "Material Menu" $editingtex [
    uivlist $ui_padbutton [
        uihlist $ui_padbutton [
            uivlist $ui_padbutton [
                uihlist $ui_padbutton [
                    uichecktextbutton "AI Clip" (ui_materials_getbindicon "aiclip") 0 $ui_rgbw $ui_buttonh [
                        aiclip
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "Death" (ui_materials_getbindicon "death") 0 $ui_rgbw $ui_buttonh [
                        death
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "Ladder" (ui_materials_getbindicon "ladder") 0 $ui_rgbw $ui_buttonh [
                        ladder
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ]
                uihlist $ui_padbutton [
                    uichecktextbutton "Lava" (ui_materials_getbindicon "lava") 0 $ui_rgbw $ui_buttonh [
                        lava
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "Clip" (ui_materials_getbindicon "clip") 0 $ui_rgbw $ui_buttonh [
                        clip
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "Noclip" (ui_materials_getbindicon "noclip") 0 $ui_rgbw $ui_buttonh [
                        noclip
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ]
                uihlist $ui_padbutton [
                    uichecktextbutton "Air (none)" (ui_materials_getbindicon "air") 0 $ui_rgbw $ui_buttonh [
                        air
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "Alpha" (ui_materials_getbindicon "alpha") 0 $ui_rgbw $ui_buttonh [
                        alpha
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "Water" (ui_materials_getbindicon "water") 0 $ui_rgbw $ui_buttonh [
                        water
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ]
                uihlist $ui_padbutton [
                    uifill $ui_rgbw $ui_buttonh
                    uichecktextbutton "Hurt" (ui_materials_getbindicon "hurt") 0 $ui_rgbw $ui_buttonh [
                        hurt
                        uiclose "materials"
                    ] $colourwhite $ui_default
                    uichecktextbutton "No GI" (ui_materials_getbindicon "nogi") 0 $ui_rgbw $ui_buttonh [
                        nogi
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ]
            ]
            uibar 0 1
            uivlist $ui_padbutton [ //glass entries
                loop i 4 [
                    i = (+ $i 1) //want 1-2-3-4 and not 0-1-2-3
                    if (= 1 $i) [
                        uichecktextbutton (concat "Glass" $i) (ui_materials_getbindicon "glass" ) 0 $ui_rgbw $ui_buttonh [
                            glass
                            uiclose "materials"
                        ] $colourwhite $ui_default
                    ] [
                        uichecktextbutton (concat "Glass" $i) (ui_materials_getbindicon (concatword "glass" $i)) 0 $ui_rgbw $ui_buttonh [
                            @(concatword "glass" $i)
                            uiclose "materials"
                        ] $colourwhite $ui_default
                    ]
                ]
            ]
        ]
        uibar 1 0
        uihlist $ui_padbutton [ //water entries
            loop i 4 [
                i = (+ $i 1) //want 1-2-3-4 and not 0-1-2-3
                if (= 1 $i) [
                    uichecktextbutton (concat "Water" $i) (ui_materials_getbindicon "water" ) 0 $ui_rgbw $ui_buttonh [
                        water
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ] [
                    uichecktextbutton (concat "Water" $i) (ui_materials_getbindicon (concatword "water" $i)) 0 $ui_rgbw $ui_buttonh [
                        @(concatword "water" $i)
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ]
            ]
        ]
        uibar 1 0
        uihlist $ui_padbutton [ //lava entries
            loop i 4 [
                i = (+ $i 1) //want 1-2-3-4 and not 0-1-2-3
                if (= 1 $i) [
                    uichecktextbutton (concat "Lava" $i) (ui_materials_getbindicon "lava" ) 0 $ui_rgbw $ui_buttonh [
                        lava
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ] [
                    uichecktextbutton (concat "Lava" $i) (ui_materials_getbindicon (concatword "lava" $i)) 0 $ui_rgbw $ui_buttonh [
                        @(concatword "lava" $i)
                        uiclose "materials"
                    ] $colourwhite $ui_default
                ]
            ]
        ]
    ]
]


//This is the liquid settings UI. The first four tabs correspond to each water variant; the two variants (normal and alt) are side-by-side inside each tab.
//The latter four tabs correspond to each lava variant, arranged in a similar manner.
//The content in each tab is defined within ui_liquids_water_body and ui_liquids_lava_body, which use ui_liquids_row_rgbsliders/horzsliders to create their entries.

//defines a table row containing a pair (normal, alt) of rgb sliders, for colour vars
ui_liquids_row_rgbsliders = [ //arg1: liquid # (water, water2, water3, water4...) arg2: description arg3: var arg4: liquid
    uitablerow [
        uihlist $ui_padbutton [
            uitextleft $arg2 //arg2: slider description
            uifill $ui_checksize
            uialign 1 0 //all legend text is aligned rightwards for this ui
            uirgbsliders (concatword $arg4 (if (= $arg1 1) [] [$arg1]) $arg3 ) //concats liquid type (water, lava) with liquid number (omitted if liquid# is 1) and the specific var
            uirgbsliders (concatword $arg4 (if (= $arg1 1) [] [$arg1]) $arg3 alt) //ditto except with alt appended to the end (alt slider)
        ]
    ]
]

//defines a table row containing a pair (normal, alt) of horizontal sliders, for float vars
ui_liquids_row_horzsliders = [ //arg1: liquid # (water, water2, water3, water4...) arg2: description arg3: var arg4: liquid arg5: lower-bound arg6 : upper-bound arg7: step-size
    uitablerow [
        uihlist $ui_padbutton [
            uitextleft $arg2 //arg2: slider description
            uifill $ui_checksize
            uialign 1 0
            ui_liquids_row_var = (concatword $arg4 (if (= $arg1 1) [] [$arg1]) $arg3 ) // concats liquid type (water, lava) with liquid number (omitted if liquid# is 1) and the specific var (can't pass these args to within uihorzslider)
            //normal mapvariant slider
            uihorzslider (concatword $arg4 (if (= $arg1 1) [] [$arg1]) $arg3) $arg5 $arg6 $arg7 $ui_rgbw 0.025 [ //arg5/6/7 are lower/upper/stepsize of the slider
                uivlist 0 [
                     uifill 0.050 0
                     uitext $@ui_liquids_row_var //slider desc text
                ]
            ]
            //alt mapvariant slider
            uihorzslider (concatword $arg4 (if (= $arg1 1) [] [$arg1]) $arg3 alt) $arg5 $arg6 $arg7 $ui_rgbw 0.025 [ //arg5/6/7 are lower/upper/stepsize of the slider
                uivlist 0 [
                     uifill 0.050 0
                     uitext $(concatword $ui_liquids_row_var alt) //slider desc text
                ]
            ]
        ]
    ]
]

//organization of table row entries within the water tabs
ui_liquids_water_body = [//arg1: liquid # (water, water2, water3, water4...)
    uitablerow [
        uihlist $ui_buttonzw [
            uitextright "Normal"
            uitextright "Alt"
        ]
    ]
    ui_liquids_row_rgbsliders $arg1 "Water Colour" "colour" "water"
    ui_liquids_row_rgbsliders $arg1 "Deep Colour" "deepcolour" "water"
    ui_liquids_row_rgbsliders $arg1 "Deep Fade" "deepfade" "water"
    ui_liquids_row_rgbsliders $arg1 "Waterfall Colour" "fallcolour" "water"
    ui_liquids_row_rgbsliders $arg1 "Refract Colour" "refractcolour" "water"
    ui_liquids_row_rgbsliders $arg1 "Waterfall Refract Colour" "colour" "water"
    uitablerow [ uibar 1 0]
    ui_liquids_row_horzsliders $arg1 "Water Fog" "fog" "water" 0 600 5
    ui_liquids_row_horzsliders $arg1 "Deep Water Brightness" "deep" "water" 0 1000 10
    ui_liquids_row_horzsliders $arg1 "Water Refraction" "refract" "water" 0 5 .05
    ui_liquids_row_horzsliders $arg1 "Waterfall Refraction" "fallrefract" "water" 0 5 .05
    ui_liquids_row_horzsliders $arg1 "Water Specularity" "spec" "water" 0 200 5
    ui_liquids_row_horzsliders $arg1 "Waterfall Specularity" "fallspec" "water" 0 200 5
]

//organization of table row entries within the lava tabs
ui_liquids_lava_body = [//arg1: liquid # (lava, lava2, lava3, lava4...)
    uitablerow [
        uihlist $ui_buttonzw [
            uitextright "Normal"
            uitextright "Alt"
        ]
    ]
    ui_liquids_row_rgbsliders $arg1 "Lava Colour" "colour" "lava"
    uitablerow [ uibar 1 0]
    ui_liquids_row_horzsliders $arg1 "Lava Fog" "fog" "lava" 0 600 5
    ui_liquids_row_horzsliders $arg1 "Lava Glow Minimum" "glowmin" "lava" 0 2 .1
    ui_liquids_row_horzsliders $arg1 "Lava Glow Maximum" "glowmax" "lava" 0 2 .1
    ui_liquids_row_horzsliders $arg1 "Lava Specularity" "spec" "lava" 0 200 5
]

//actual liquids UI now that all the interesting stuff has been defined
uimenu "liquids" "Liquid Settings" $editingtex [
    hidechanges 1
    uihlist 0 [
        uiclamp 1 1 1 1
        // side tab, in settings menu
        uivtab (+f $ui_slidersize 0.48) 0.4 $ui_tabw $ui_tabh ui_water_settings [
            0 "Water 1"        []
            1 "Water 2"        []
            2 "Water 3"        []
            3 "Water 4"        []
            4 "Lava 1"        []
            5 "Lava 2"        []
            6 "Lava 3"        []
            7 "Lava 4"        []
        ] [
            uiscroll .8 0.4 [
                uialign -1 0
                uifill .3 .4 [
                    uialign 0 -1
                    uipad .01 .023 .01 .01 [//padding to keep the scroll bar from intersecting the sliders and stuff
                        uialign 0 -1
                        uiclamp 1 1
                        uitable 0 $ui_padsmaller [
                            uialign -1 -1
                            uiclamp 1 1
                            case $ui_water_settings 0 [ //Water 1
                                ui_liquids_water_body 1 //tells ui_liquids_water_body to create content for water1 (& water2, 3, 4 in other tabs)
                            ] 1 [ //Water 2
                                ui_liquids_water_body 2
                            ] 2 [ //Water 3
                                ui_liquids_water_body 3
                            ] 3 [ //Water 4
                                ui_liquids_water_body 4
                            ] 4 [ //Lava 1
                                ui_liquids_lava_body 1  //tells ui_liquids_lava_body to create content for lava1 (& lava2, 3, 4 in other tabs)
                            ] 5 [ //Lava 2
                                ui_liquids_lava_body 2
                            ] 6 [ //Lava 3
                                ui_liquids_lava_body 3
                            ] 7 [ //Lava 4
                                ui_liquids_lava_body 4
                            ]
                        ]
                    ]
                ]
            ]
            uihlist 0 [uivscroll $ui_slidersize 0.4 3; uialign 1 0; uiclamp 0 1]
        ]
    ]
]

//This is the normal skybox menu {forcemapvariant 1}
//All skyboxes here and in uimenu altskybox are hard coded and any new skyboxes need to be added here in order to show up.
//Follow the argument description for ui_skybox_entry for info on how to add entries. Each ui_skybox_entry is designed for two skyboxes.
//The UI closes after selecting each option. This can be changed by removing the >uiclose @arg5 line from each button.
//ui_skybox_entry describes individual rows
ui_skybox_entry = [//arg1: desc1 arg2: path1 arg3: desc2 arg4: path2 arg5: ui to close arg6:path to icon1 arg7:path to icon2
    uihlist $ui_padbutton [ // making each row contain two entries
        uichecktextbutton $arg1 $arg6 0 $ui_buttonw $ui_buttonh [
            @arg5 = $arg2
            uiclose @arg5
        ] $colourwhite $ui_default
        uichecktextbutton $arg3 $arg7 0 $ui_buttonw $ui_buttonh [
            @arg5 = $arg4
            uiclose @arg5
        ] $colourwhite $ui_default
    ]
]
//contents of the skybox uis (done so that the same code can cover both the normal and alt cases)
ui_skybox_body = [//arg1: ui to close
    uivlist $ui_padsmaller [
        uichecktextbutton "None" "" 0 $ui_buttonw $ui_buttonh [ //leaving the no-skybox option in its own row (rest are 2 entry rows)
            skybox = ""
            uiclose $arg1
        ] $colourwhite $ui_default
        ui_skybox_entry "Arch3" "elyvisions/arch3" "Cave3" "elyvisions/cave3" $arg1 "elyvisions/arch3_lf" "elyvisions/cave3_up"
        ui_skybox_entry "Clouds" "skyboxes/clouds2" "Dark" "elyvisions/dark" $arg1 "skyboxes/clouds2_up" "elyvisions/dark_rt"
        ui_skybox_entry "Gradient" "skyboxes/gradient"  "Grey" "skyboxes/grey" $arg1 "skyboxes/gradient_up" "skyboxes/grey_up"
        ui_skybox_entry "Hot" "elyvisions/hot" "Interstellar" "skyboxes/interstellar" $arg1 "elyvisions/hot_rt" "skyboxes/interstellar_up"
        ui_skybox_entry "Miramar" "skyboxes/miramar" "Rainbow" "elyvisions/rainbow" $arg1 "skyboxes/miramar_up" "elyvisions/rainbow_rt"
        ui_skybox_entry "Sh" "elyvisions/sh" "Skyast" "elyvisions/skyast" $arg1 "elyvisions/sh_up" "elyvisions/skyast_up"
        ui_skybox_entry "Skyhigh" "skyboxes/skyhigh" "Skyhsky" "elyvisions/skyhsky" $arg1 "skyboxes/skyhigh_up" "elyvisions/skyhsky_rt"
        ui_skybox_entry "Skylump" "skyboxes/skylump" "Skylump2" "skyboxes/skylump2" $arg1 "skyboxes/skylump_up" "skyboxes/skylump2_up"
        ui_skybox_entry "Skype" "elyvisions/skype" "Sp2" "elyvisions/sp2" $arg1 "elyvisions/skype_up" "elyvisions/sp2_up"
        ui_skybox_entry "Sp3" "elyvisions/sp3" "Stars" "skyboxes/stars" $arg1 "elyvisions/sp3_rt" "skyboxes/stars"
        ui_skybox_entry "Stormydays" "skyboxes/stormydays" "Sunset" "skyboxes/sunsetflat" $arg1 "skyboxes/stormydays_up" "skyboxes/sunsetflat_up"
        ui_skybox_entry "Tron" "elyvisions/tron" "Yellow" "skyboxes/yellow" $arg1 "elyvisions/tron_up" "skyboxes/yellow_up"
    ]
]
//This is the skybox menu, which just tells ui_skybox_body to execute with "skybox" as its exit command
uimenu "skybox" "Choose Skybox" $questiontex [
    ui_skybox_body "skybox"
]
//This is the alternate skybox menu for {forcemapvariant 2}. It is identical to the ordinary skybox menu except that it changes skyboxalt.
uimenu "skyboxalt" "Choose Alt Skybox" $questiontex [
    ui_skybox_body "skyboxalt"
]
//This is the edit menu as accessed by f3 while in edit mode
uimenu "edit" "Edit Menu" $editingtex [
    uivlist $ui_padsmaller [
        uichecktextbutton "Edit Settings" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "editsettings"
        ] $colourwhite $ui_default
        uichecktextbutton "Environment Settings" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "environment"
        ] $colourwhite $ui_default
        uichecktextbutton "Map Settings" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "map"
        ] $colourwhite $ui_default
        uichecktextbutton "Textures" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "textures"
        ] $colourwhite $ui_default
        uichecktextbutton "Multiplayer Settings" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "coopedit"
        ] $colourwhite $ui_default
        uichecktextbutton "Entity Menu" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "entities"
        ] $colourwhite $ui_default
        uichecktextbutton "The REEE" "" 0 $ui_buttonw $ui_buttonh [
            uiopen "editref"
        ] $colourwhite $ui_default
    ]
]
// This is the menu accessed by [e]-[d] while in edit mode, for editing UI settings
uimenu "editsettings" "Edit Settings" $editingtex [
    uifill 0.38 0.4 //ui size
    uivlist  $ui_padbutton [
        uialign -1 -1
        uiclamp 1 1
        uivlist $ui_padsmaller [
            //cube painting size, in powers of 2; 10 is a standard newmap, so slider is limited to 9. Hotkey for this in edit would be [G] + scroll
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitext "Grid Power"
                uialign -1 0 // text is aligned rightwards, bumped up against the sliders for gridpower and undomegs
                uihorzslider "gridpower" 0 9 1 $ui_buttonzw 0.025 [//all the sliders in the edit menus are .2 ui-units across unless labeled otherwise
                    uivlist 0 [
                        uifill 0.050 0//centering the text inside the slider so things don't look dumb (all sliders are configured like this unless labeled otherwise)
                        uitext $gridpower
                    ]
                ]
                uialign 1 0 //whole hlist is aligned rightwards (most sliders will be aligned this way; most toggles will be aligned leftwards)
            ]
            //undo cache size in megabytes, 1 to 100mb
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Undo Cache Size"
                uialign -1 0
                uihorzslider "undomegs" 1 100 1 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $undomegs
                    ]
                ]
                uialign 1 0
            ]
            //options for edit display & controls -- float speed, float coast, edit FOV
            uibar 1 0
            //float speed while spectating or editing
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitext "Float Speed"
                uialign -1 0
                uihorzslider "floatspeed" 50 1000 10 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $floatspeed
                    ]
                ]
                uialign 1 0
            ]
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitext "Float Coast"
                uialign -1 0
                uihorzslider "floatcoast" 1 20 1 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $floatcoast
                    ]
                ]
                uialign 1 0
            ]
            // field of view in edit mode
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitext "Edit Field of View"
                uialign -1 0 // text is aligned rightwards, bumped up against the sliders for gridpower and undomegs
                uihorzslider "editfov" 1 179 1 $ui_buttonzw 0.025 [//all the sliders in the edit menus are .2 ui-units across unless labeled otherwise
                    uivlist 0 [
                        uifill 0.050 0//centering the text inside the slider so things don't look dumb (all sliders are configured like this unless labeled otherwise)
                        uitext $editfov
                    ]
                ]
                uialign 1 0 //whole hlist is aligned rightwards (most sliders will be aligned this way; most toggles will be aligned leftwards)
            ]
            uibar 1 0
            uihlist $ui_padbutton [ // determines which variant is displayed: normal or alt (day or night)
                uifill $ui_checksize
                uitextleft "Map Variant"
                uialign -1 0
                uiselect $ui_buttonzw 0.025 "forcemapvariant" [
                    0   "Random"             []
                    1   "Normal"             []
                    2   "Alternate"          []
                ] [uialign 1 0]
                uialign 1 0
            ]
            uihlist $ui_padbutton [ // this determines how music plays in edit mode, NOT the map's actual music
                uifill $ui_checksize
                uitextleft "Map Music"
                uialign -1 0
                uiselect $ui_buttonzw 0.025 "musicedit" [
                    0   "Off"             []
                    3   "By Map"          []//note that option 3 is used here because musicedit 1 seems to shuffle (?)
                    2   "Random/Loop"     []
                    5   "Random/Shuffle"  []
                ] [uialign 1 0]
                uialign 1 0
            ]
            uibar 1 0
            // these are the togglable editing settings: entediting, entselsnap, allfaces, showmat, wireframe, fullbright, outline, crosshairdistance
            uihlist $ui_padbutton [
                uicheckbox "Entity Editing" $entediting $ui_checksize [entediting = (! $entediting)]
                uialign -1 0 // text is aligned leftwards, bumped up against the buttons for the toggleables
            ]
            uihlist $ui_padbutton [
                uicheckbox "Entity Grid Snap" $entselsnap $ui_checksize [entselsnap = (! $entselsnap)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "All Face Texturing" $allfaces $ui_checksize [allfaces = (! $allfaces)]
                uialign -1 0
            ]
            uibar 1 0
            uihlist $ui_padbutton [
                uicheckbox "Move With Selection Push" $selectionsurf $ui_checksize [selectionsurf = (! $selectionsurf)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Move With Entities" $entitysurf $ui_checksize [entitysurf = (! $entitysurf)]
                uialign -1 0
            ]
            uibar 1 0
            uihlist $ui_padbutton [
                uicheckbox "Show Material Volumes" $showmat $ui_checksize [showmat = (! $showmat)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Wireframe" $wireframe $ui_checksize [wireframe = (! $wireframe)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Fullbright" $fullbright $ui_checksize [fullbright = (! $fullbright)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Blank Geometry" $blankgeom $ui_checksize [blankgeom = (! $blankgeom)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Geometry Outline" $outline $ui_checksize [outline = (! $outline)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Depth Test Outlines" $dtoutline $ui_checksize [dtoutline = (! $dtoutline)]
                uialign -1 0
            ]
            uibar 1 0
            uispace 0
            uihlist $ui_padbutton [
                uicheckbox "Show Distance on Crosshair" $crosshairdistance $ui_checksize [crosshairdistance = (! $crosshairdistance)]
                uialign -1 0
            ]
            uibar 1 0
            uihlist $ui_padbutton [
                uicheckbox "Show Editing Info" $showstats $ui_checksize [showstats = (! $showstats)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Show Texture Info" $showtexstats $ui_checksize [showtexstats = (! $showtexstats)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Show Edit Toggles" $showedittoggles $ui_checksize [showedittoggles = (! $showedittoggles)]
                uialign -1 0
            ]
            uibar 1 0
            uihlist $ui_padbutton [
                uichecktextbutton "Customize Keybinds" "" 0 $ui_buttonw $ui_buttonzh [
                    uiopen "binds"
                ] $colourwhite $ui_default
            ]
        ]
    ]
]
//Map environment settings, accessed by [e]-[w] in the edit compass menu
uimenu "environment" "Environment Settings" $editingtex [
    uifill 0.38 0.4 //size of ui
    uivlist  $ui_padbutton [
        uialign -1 -1
        uiclamp 1 1
        uivlist $ui_padsmaller [
            //title bar
            uihlist $ui_padbutton [
                uitextleft "Normal                         "
                uialign -1 0
                uitextright " Alt"
                uialign 1 0
            ]
            //fog distance sliders for normal and alt modes
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextright "Fog Distance " // the extra space is a kludge for alignment (fix)
                uialign -1 0
                uifill $ui_checksize
                uihorzslider "fog" 16 10000 10 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $fog
                    ]
                ]
                uialign 1 0
                uihorzslider "fogalt" 16 10000 10 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $fogalt
                    ]
                ]
                uialign 1 0
            ]
            //fog colour sliders, normal and alt
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Fog Colour"
                uialign -1 0
                uifill $ui_checksize
                uirgbsliders fogcolour
                uirgbsliders fogcolouralt
                uialign 1 0
            ]
            uibar 1 0
            //slider multiplier for sunlight, normal and alt
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Sunlight Scale"
                uialign -1 0
                uifill $ui_checksize
                uihorzslider "sunlightscale" 0 16 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightscale
                    ]
                ]
                uialign 1 0
                uihorzslider "sunlightscalealt" 0 16 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightscalealt
                    ]
                ]
                uialign 1 0
            ]
            // controls the height of the sun in the sky, from 0 (horizon) to 90 (noon), normal and alt sliders
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Sunlight Pitch"
                uialign -1 0
                uifill $ui_checksize
                uihorzslider "sunlightpitch" 0 90 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightpitch
                    ]
                ]
                uialign 1 0
                uihorzslider "sunlightpitchalt" 0 90 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightpitchalt
                    ]
                ]
                uialign 1 0
            ]
            //controls the direction the sun faces; normal and alt sliders
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Sunlight Yaw"
                uialign -1 0
                uifill $ui_checksize
                uihorzslider "sunlightyaw" 0 360 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightyaw
                    ]
                ]
                uialign 1 0
                uihorzslider "sunlightyawalt" 0 360 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightyawalt
                    ]
                ]
                uialign 1 0
            ]
            //sunlight colour sliders for normal and alt
            uihlist $ui_padbutton [
                uitextleft "Sunlight Colour"
                uifill $ui_checksize
                uirgbsliders sunlight
                uirgbsliders sunlightalt
                uialign 1 0
            ]
            uibar 1 0
            //ambient colour (total shadow) sliders for normal and alt
            uihlist $ui_padbutton [
                uitextleft "Ambient Colour"
                uifill $ui_checksize
                uirgbsliders ambient
                uirgbsliders ambientalt
                uialign 1 0
            ]
            uibar 1 0
            //skybox colour sliders
            uihlist $ui_padbutton [
                uitextleft "Skybox Colour "
                uifill $ui_checksize
                uirgbsliders skycolour
                uirgbsliders skycolouralt
                uialign 1 0
            ]
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Skybox Yaw"
                uialign -1 0
                uifill $ui_checksize
                uihorzslider "yawsky" 0 360 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $yawsky
                    ]
                ]
                uialign 1 0
                uihorzslider "yawskyalt" 0 360 1 $ui_rgbw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $yawskyalt
                    ]
                ]
                uialign 1 0
            ]
            //sends you to the skybox uis so you can pick one
            uihlist $ui_padbutton [
                uialign -1 0
                uichecktextbutton "Normal Skybox..." "" 0 $ui_rgbw .025 [
                    uiopen "skybox"
                ] $colourwhite $ui_default
                uichecktextbutton "Alt Skybox..." "" 0 $ui_rgbw .025 [
                    uiopen "skyboxalt"
                ] $colourwhite $ui_default
                uialign 1 0
            ]
            uihlist $ui_padbutton [
                uialign -1 0
                uichecktextbutton "Liquid Settings" "" 0 .328 .025 [
                    uiopen "liquids"
                ] $colourwhite $ui_default
                uialign 1 0
            ]
        ]
    ]
]
//Map menu, accessed by [e]-[a] in the editing compass menu
uimenu "map" "Map Settings" $editingtex [
    uifill 0.38 0.4 //size of ui
    uivlist  $ui_padbutton [
        uialign -1 -1
        uiclamp 1 1
        uivlist $ui_padsmaller [
            //preview picture
            uihlist $ui_padbutton [
                uivlist $ui_padbutton [
                    if (= 1 (maprevision)) [ //if map is new (on r1) then show icon instead of blanking
                        uihlist 0 [
                            uifill .03 0.01
                            uiimage "textures/icon" $ui_default 0 .18 .18
                            uifill .03 0.01
                        ]
                    ] [
                        uiimage $mapname $ui_default 0 .24 .18
                    ]
                ]
                uivlist $ui_padbutton [
                    //file settings: mapname maptitle mapauthor savemap
                    uihlist $ui_padbutton [
                        uialign -1 0
                        uifill $ui_checksize
                        uitextleft "File Settings"
                    ]
                        uihlist $ui_padbutton [
                        uialign 1 0
                        uifill $ui_checksize
                        uitextleft "Map Name"
                        uiinput "mapname" 17 [] 1 0 [] () "mapname"
                    ]
                    uihlist $ui_padbutton [
                        uialign 1 0
                        uifill $ui_checksize
                        uitextleft "Map Title"
                        uiinput "maptitle" 17 [] 1 0 [] () "maptitle"
                    ]
                    uihlist $ui_padbutton [
                        uialign 1 0
                        uifill $ui_checksize
                        uitextleft "Map Author"
                        uiinput "mapauthor" 17 [] 1 0 [] () "mapauthor"
                    ]
                    uihlist $ui_padbutton [
                        uifill $ui_checksize
                        uichecktextbutton "Save Map" "" 0 .3 .025 [
                            savemap
                        ] $colourwhite $ui_default
                    ]
                ]
            ]
            uibar 1 0
            //lerp settings: lerpangle lerpsubdiv lerpsubdivsize
            uihlist $ui_padbutton [
                uialign -1 0
                uifill $ui_checksize
                uitextleft "Settings for Normalmap Interpolation"
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uifill $ui_checksize
                uitextleft "Interpolation Angle"
                uihorzslider "lerpangle" 0 180 1 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $lerpangle
                    ]
                ]
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uifill $ui_checksize
                uitextleft "Interpolation Subdivisions"
                uihorzslider "lerpsubdiv" 0 4 1 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $lerpsubdiv
                    ]
                ]
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uifill $ui_checksize
                uitextleft "Interpolation Subdiv Size"
                uihorzslider "lerpsubdivsize" 4 128 1 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $lerpsubdivsize
                    ]
                ]
            ]
            uibar 1 0
            //waypoints
            uihlist $ui_padbutton [
                uialign -1 0
                uifill $ui_checksize
                uitextleft "Waypoints"
            ]
            uihlist $ui_padbutton [
                uicheckbox "Show Waypoints" $showwaypoints $ui_checksize [showwaypoints = (! $showwaypoints)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Drop Waypoints" $dropwaypoints $ui_checksize [dropwaypoints = (! $dropwaypoints)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uichecktextbutton "Load" "" 0 .14 .025 [
                    loadwaypoints
                ] $colourwhite $ui_default
                uichecktextbutton "Save" "" 0 .14 .025 [
                    savewaypoints
                ] $colourwhite $ui_default
                uichecktextbutton "Clear" "" 0 .14 .025 [
                    clear waypoints
                ] $colourwhite $ui_default
                uichecktextbutton "Clear in Sel" "" 0 .14 .025 [
                    clear waypoints
                ] $colourwhite $ui_default
            ]
            uibar 1 0
            uihlist $ui_padbutton [
                uialign -1 0
                uifill $ui_checksize
                uitextleft "Misc. Map Settings"
            ]
            //waterlod slider, for water detail (larger maps may prefer setting to 0)
            uihlist $ui_padbutton [
                uialign 1 0 // text is aligned rightwards, bumped up against the sliders in this ui
                uifill $ui_checksize
                uitextleft "Water LOD"
                uihorzslider "waterlod" 0 3 1 $ui_buttonzw 0.025 [
                    uifill 0.050 0
                    uitext (format "^f[%1]%2^%" (rgbtohex @(hsltorgb (*f (- 3 $waterlod) 36) 1 0.5)) $waterlod)
                ]
            ]
            //aggressiveness of remipper, at the expense of occlusion
            uihlist $ui_padbutton [
                uialign 1 0
                uifill $ui_checksize
                uitextleft "Maximum Merge Size"
                uihorzslider "maxmerge" 0 12 1 $ui_buttonzw 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $maxmerge
                    ]
                ]
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uifill $ui_checksize
                uitextleft "Sky Texture Behavior"
                uiselect $ui_buttonzw 0.025 "skytexture" [
                    0 "Transparent"     []
                    1 "Render Skybox"   []
                ] [uialign 1 0]
            ]
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uichecktextbutton "Customize Obituaries" "" 0 .56 .025 [
                    uiopen obituaries
                ] $colourwhite $ui_default
            ]
        ]
    ]
]
//Obituaries menu, accessed from the map ui [e] -> [a]
uimenu "obituaries" "Customize Obituaries" $editingtex [
    uifill 0.38 0.3 //size of ui
    uivlist  $ui_padsmaller [
        uialign -1 -1
        uiclamp 1 1
        uivlist 0 [
            //edit player opacity, for other editors in coop edit
            looplist i [ fall death hurt water water2 water3 water4 lava lava2 lava3 lava4] [
                uihlist $ui_padbutton [
                    uialign 1 0
                    uifill $ui_checksize
                    uitextleft (concat $i "obituary")
                    uiinput (concatword "obit" $i) 36 [] 1 0 [] () (concatword "obit" $i)
                ]
            ]
        ]
    ]
]
//Multiplayer menu, accessed by [e]-[q] in the editing compass menu
uimenu "coopedit" "Multiplayer Settings" $editingtex [
    uifill 0.38 0.4 //size of ui
    uivlist  $ui_padsmaller [
        uialign -1 -1
        uiclamp 1 1
        uivlist 0 [
            //edit player opacity, for other editors in coop edit
            uihlist $ui_padbutton [
                uialign 1 0 // text is aligned rightwards, bumped up against the sliders in this ui
                uifill $ui_checksize
                uitextleft "Player Opacity"
                uihorzslider "playereditblend" 0 1 .125 $ui_buttonzw 0.025 [
                    uifill 0.050 0
                    uitext ($playereditblend)
                ]
            ]
        ]
    ]
]
//entity editing, selection, and creation menu (default compass bind [e] -> [f])
uimenu "entities" "Find and Edit Entities" $editingtex [
    uifill 0.3 0.48 //size of ui
    uivtab (+f $ui_slidersize 0.32) 0.48 $ui_tabw $ui_tabh ui_ent_tab [
        0 "New"      []
        1 "Find"     []
        2 "Edit"     []
    ]
    case $ui_ent_tab 0 [ //newent tab
        uivlist $ui_padbutton [
            uitext "Create New Entity"
            uihlist $ui_padbutton [
                uifill $ui_doubleh
                uichecktextbutton "mapmodel" "" 0 $ui_buttonzw .02 [
                    showui "mapmodel"
                    uiclose "entities"
                ] $colourwhite $ui_default
            ]
            looplist j [light lightfx affinity playerstart envmap sound teleport trigger pusher particles weapon checkpoint] [ //creates entry for each type
                uihlist $ui_padbutton [
                    uifill $ui_doubleh
                    uichecktextbutton $j "" 0 $ui_buttonzw .02 [
                        newent $j
                        uiclose "entities"
                    ] $colourwhite $ui_default
                ]
            ]
            uialign 0 0
        ]
    ] 1 [ //find ents tab
        uivlist $ui_padbutton [
            uitext "Find Entities"
            uihlist $ui_padbutton [ //sets ui_ent_find for use in entfind and entfindinsel buttons
                    uifill $ui_doubleh
                    uitext "Find:"
                    uiinput "ui_ent_find" 11 [] 1 0 [] () "ui_ent_find"
                ]
            uihlist $ui_padbutton [
                uifill $ui_doubleh
                uichecktextbutton "Find Entities" "" 0 $ui_buttonzw 0.02 [
                    entfind $ui_ent_find
                ] $colourwhite $ui_default
            ]
            uihlist $ui_padbutton [
                uifill $ui_doubleh
                uichecktextbutton "Find Ents in Sel" "" 0 $ui_buttonzw .02 [
                    entfind $ui_ent_find
                ] $colourwhite $ui_default
            ]
             uihlist $ui_padbutton [
                uifill $ui_doubleh
                uichecktextbutton "Clear Selection" "" 0 $ui_buttonzw .02 [
                    entcancel
                ] $colourwhite $ui_default
            ]
            uialign 0 0
        ]

    ] 2 [  //edit entities tab
        if (= (enthavesel) 1) [ //only show setvalue + ent entries if an ent is selected
            uivlist  $ui_padsmaller [
                uihlist $ui_padbutton [
                    uitext "Set Value"
                    uiinput "ui_ent_set" 5 [] 1 0 [] () "ui_ent_set" //value to set entities to
                ]
                loop i 20 [  //20 may be high here, but more ent attributes may be added in the future
                    if (!=s (entattr $i) "") [ //if entattr != a blank string (there is a valid attr) then show the hlist below
                        uihlist $ui_padbutton [
                            uifill $ui_doubleh
                            ui_entities_index@i = (entattr $i) // variable ui_entities_index1,2,3 for attributes 1,2,3 etc.
                            uitext (getentattr (indexof $enttypelist (enttype)) $i 1) //gets the entitytypelist name for each attribute number
                            uichecktextbutton "+" "" 0 .012 .02 [ //adds 1 to entity attribute
                                entprop $i 1
                            ] $colourwhite $ui_default
                            uialign 1 1
                            uichecktextbutton "-" "" 0 .012 .02 [ //subtracts 1 from entity attribute
                                entprop $i -1
                            ] $colourwhite $ui_default
                            uifield [ui_entities_index@i] 4 [] 1 0 //show current value for attribute
                            uichecktextbutton "Set" "" 0 0.02 0.02 [ //sets value to attribute from Set Value box
                                entattr $i $ui_ent_set
                            ]
                        ]
                    ]
                ]
            ]
        ] [ //else display "no ent selected" message
            uivlist $ui_padsmaller [
                uihlist $ui_padbutton [
                    uifill $ui_doubleh
                    uitext "No entity selected"
                ]
                uihlist $ui_padsmaller [
                    uifill $ui_doubleh
                    uitext "Select an entity to edit"
                ]
            ]
        ]
    ]
]
//body of editref: the part that actually grabs the entries and displays them
//arg1: list of commands to create entries for
ui_editref_output = [
    looplist ui_editref_entry $arg1 [ //loops through argument to create a desc for each command
        uitablerow [
            uivlist 0 [
                if (!=s (getvardesc (concatword $ui_editref_entry alt)) "" ) [
                    uitextleft (format "%1[alt]" $ui_editref_entry ) //append "[alt]" to the end if command is normal/alt
                ] [
                    uitextleft $ui_editref_entry
                ]
                uifill .22 $ui_padtiny
                loop ui_editref_fields (getvarfields $ui_editref_entry) [
                    uitextleft (getvarfields $ui_editref_entry $ui_editref_fields) $ui_textsmall
                ]
                if (!=s (dobindsearch $ui_editref_entry edit) "") [
                    uitextleft (dobindsearch $ui_editref_entry edit)
                ]
            ]
            uivlist $ui_padsmaller [
                uitextleft (getvardesc $ui_editref_entry)
                uialign -1 0
            ]
        ]
        uibar 1 0
    ]
]
//editing reference: Red Eclipse Editing Encyclopedia
uimenu "editref" "The REEE" $questiontex [
    hidechanges 1
    uihlist 0 [
        uiclamp 1 1 1 1
        // side tab, editref menu
        uivtab .85 0.5 $ui_tabw $ui_tabh ui_editref_tab [ //tabs in left part of UI
            0 "About" []
            1 "ABC"   []
            2 "DEF"   []
            3 "GHI"   []
            4 "JKL"   []
            5 "MNO"   []
            6 "PQRS"  []
            7 "TUV"   []
            8 "WXYZ"  []
        ] [
            uiscroll 0.8 0.5 [
                uialign -1 0
                uifill 0.8 0.5 [
                    uialign 0 -1
                    uipad $ui_padbutton $ui_padbutton $ui_padbutton $ui_padbutton [
                        uialign 0 -1
                        uiclamp 1 1
                        uitable 0 $ui_padsmall [
                            uialign -1 -1
                            uiclamp 1 1
                            case $ui_editref_tab 0 [ //title "about" page
                                uitablerow [
                                    uitext "The Red Eclipse Editing Encyclopedia (REEE)" 1.5
                                ]
                                uitablerow [
                                    uivlist $ui_padsmaller [
                                        uitext "The Red Eclipse Editing Encyclopedia (REEE) is the primary edit command reference."
                                        uitext "Commands with alt modes have [alt] appended to their names."
                                        uitext "This UI is incomplete and entries are in progress."
                                        uibar 1 0
                                    ]
                                ]
                                uitablerow [
                                    uivlist $ui_padsmaller [
                                        uitext "Entries listed are all edit commands, used by entering the console with [/]"
                                        uitext "and typing the desired command and its arguments."
                                        uifill $ui_checksize $ui_checksize
                                        uitext "Entry fields are listed below the command; descriptions to the right."
                                    ]
                                ]
                            //go through and execute the entry maker (ui_editref_output, immediately above this ui) for each var
                            ] 1 [ //ABC
                                ui_editref_output $ui_editref_abc
                            ] 2 [ // DEF
                                ui_editref_output $ui_editref_def
                            ] 3 [ // GHI
                                ui_editref_output $ui_editref_ghi
                            ] 4 [ // JKL
                                ui_editref_output $ui_editref_jkl
                            ] 5 [ // MNO
                                ui_editref_output $ui_editref_mno
                            ] 6 [ // PQRS
                                ui_editref_output $ui_editref_pqrs
                            ] 7 [ // TUV
                                ui_editref_output $ui_editref_tuv
                            ] 8 [ // WXYZ
                                ui_editref_output $ui_editref_wxyz
                            ]
                        ]
                    ]
                ]
            ]
            uihlist 0 [uivscroll $ui_slidersize 0.5 3; uialign 1 0; uiclamp 0 1] //scrollbar
        ]
    ]
]

//loops through prefab file names for prefab menu open routine
ui_prefab_refresh = [
    ui_prefabs = ""
    loopfiles i "prefab" "obr" [append ui_prefabs $i]
]
//prefab menu: shows a nice list of prefabs saved to disk to choose from, loads a new list at ui open time
uimenu "prefab" "Get Prefabs" $questiontex [
    uihlist $ui_padsmaller [
        uiscroll .3 0.4 [
            uialign -1 0
            uifill .3 0.4 [
                uialign 0 -1
                uipad $ui_padbutton $ui_padbutton $ui_padbutton $ui_padbutton [
                    uialign 0 -1
                    uiclamp 1 1
                    uivlist $ui_padsmaller [
                        looplist i $ui_prefabs [
                            uitextexec $i [
                                copyprefab $i
                                uiclose "prefab"
                            ] [] [] [] [] 0 $ui_textsmaller
                        ]
                    ]
                ]
            ]
        ]
        uihlist 0 [uivscroll $ui_slidersize 0.4 3; uialign 1 0; uiclamp 0 1]
    ]
] [ui_prefab_refresh]//on open

//mapmodel menu: shows a nice list of mapmodels to choose from
uimenu "mapmodel" "Get a Mapmodel" $questiontex [
    uihlist $ui_padsmaller [
        uiscroll .3 0.4 [
            uialign -1 0
            uifill .3 0.4 [
                uialign 0 -1
                uipad $ui_padbutton $ui_padbutton $ui_padbutton $ui_padbutton [
                    uialign 0 -1
                    uiclamp 1 1
                    uivlist $ui_padsmaller [
                        loop i (mapmodelindex) [
                            uitextexec (mapmodelindex $i) [
                                newent "mapmodel" $i
                                uiclose "mapmodel"
                            ] [] [] [] [] 0 $ui_textsmaller
                        ]
                    ]
                ]
            ]
        ]
        uihlist 0 [uivscroll $ui_slidersize 0.4 3; uialign 1 0; uiclamp 0 1]
    ]
]
