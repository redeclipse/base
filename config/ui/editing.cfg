setpersist ui_tex_show_all 1
ui_tex_show_all = 1
ui_tex_search_criteria = ""
ui_tex_current_slot = 0

uimenu "textures" "Texture Browser" "" [ // Main textures menu, accessed by [e]-[t] or [f1]
    uihlist 0.03125 [
        uialign 0 -1
        uiclamp 1 1 1 1

        uivlist $ui_padsmall [
            uiclamp 1 1 1 1

            uivlist $ui_padsmall [
                uivslotview $ui_tex_current_slot 0.2 0.2 [uioutline 0xC0C0C0; uiclamp- 1 1 1 1]

                ui_tex_current_slot_filename = (gettexname $ui_tex_current_slot)

                while [!= (strstr $ui_tex_current_slot_filename "/") -1] [ui_tex_current_slot_filename = (substring $ui_tex_current_slot_filename (+ 1 (strstr $ui_tex_current_slot_filename "/")))]

                uitext (format "#%1, %2" $ui_tex_current_slot $ui_tex_current_slot_filename) $ui_text [uitextlimit 1]
                uitext (gettexname $ui_tex_current_slot) $ui_texttiny [uitextlimit 1]
            ]

            uivlist $ui_padsmall [

                ui_tex_vpalette_types = [
                    [   // Pulse colours
                        "No palette"
                        "Dark fire pulse"
                        "Bright fire pulse"
                        "Disco pulse"
                        "Shock pulse"
                        "Bleed pulse"
                        "Buff pulse"
                        "Warn pulse"
                    ] [ // Team colours
                        "Team Neutral"
                        "Team Alpha"
                        "Team Omega"
                        "Team Kappa"
                        "Team Sigma"
                        "Team Neutral - Enforced"
                        "Team Alpha - Enforced"
                        "Team Omega - Enforced"
                        "Team Kappa - Enforced"
                        "Team Sigma - Enforced"
                    ] [ // Weapon colours
                        "Claw"
                        "Pistol"
                        "Sword"
                        "Shotgun"
                        "SMG"
                        "Flamer"
                        "Plasma"
                        "Zapper"
                        "Rifle"
                        "Grenade"
                        "Mine"
                        "Rocket"
                        "Melee"
                        "Claw - Enforced"
                        "Pistol - Enforced"
                        "Sword - Enforced"
                        "Shotgun - Enforced"
                        "SMG - Enforced"
                        "Flamer - Enforced"
                        "Plasma - Enforced"
                        "Zapper - Enforced"
                        "Rifle - Enforced"
                        "Grenade - Enforced"
                        "Mine - Enforced"
                        "Rocket - Enforced"
                        "Melee - Enforced"
                    ]
                ]

                ui_tex_vshaderparam_types = [
                    "envmin"
                    "envscale"
                    "gloss"
                    "glowcolor"
                    "parallaxscale"
                    "pulseglowcolor"
                    "pulseglowspeed"
                    "specscale"
                    "triplanarbias"
                ]
                //These are the v-commands for textures: vscale vrotate voffset vscroll valpha vcolour vrefract vpallete vcoastscale
                // as they are shown off to the left on the texture UI
                uihlist 0 [
                    uitextleft "Scale:" $ui_textsmall
                    uitextright (format "%1x" (getvscale $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Rotation:" $ui_textsmall
                    uitextright (format "%1 (%2)" (at ["0 deg" "90 deg" "180 deg" "270 deg" "X Mirror" "Y Mirror"] (getvrotate $ui_tex_current_slot)) (getvrotate $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Offset:" $ui_textsmall
                    uitextright (format "X: %1, Y: %2" @(getvoffset $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Scroll:" $ui_textsmall
                    uitextright (format "X: %1, Y: %2" @(getvscroll $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Alpha:" $ui_textsmall
                    uitextright (format "F: %1, B: %2" @(getvalpha $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]

                uihlist 0 [
                    uitextleft "Colour:" $ui_textsmall
                    uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" @(getvcolour $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]
                uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" (* (*f (at (getvcolour $ui_tex_current_slot) 0) 255) 1) (* (*f (at (getvcolour $ui_tex_current_slot) 1) 255) 1) (* (*f (at (getvcolour $ui_tex_current_slot) 2) 255) 1)) $ui_textsmall [uitextlimit 1]

                uihlist 0 [
                    uitextleft "Refract:" $ui_textsmall
                    uitextright (format "%1%%" (at (getvrefract $ui_tex_current_slot) 0)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]
                uitextright (format "^fr%2^fw - ^fg%3^fw - ^fb%4" @(getvrefract $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]
                uitextright (format "^fr%1^fw - ^fg%2^fw - ^fb%3" (* (*f (at (getvrefract $ui_tex_current_slot) 1) 255) 1) (* (*f (at (getvrefract $ui_tex_current_slot) 2) 255) 1) (* (*f (at (getvrefract $ui_tex_current_slot) 3) 255) 1)) $ui_textsmall [uitextlimit 1]

                uitextleft "Palette:" $ui_textsmall
                uitextright (format "%3 (%1 %2)" @(getvpalette $ui_tex_current_slot) (at (at $ui_tex_vpalette_types (at (getvpalette $ui_tex_current_slot) 0)) (at (getvpalette $ui_tex_current_slot) 1))) $ui_textsmall [uitextlimit 1]

                uihlist 0 [
                    uitextleft "Coast:" $ui_textsmall
                    uitextright (format "%1x" (getvcoastscale $ui_tex_current_slot)) $ui_textsmall [uitextlimit 1]

                    uiclamp 1 1
                ]

                if (!= (getvlayer $ui_tex_current_slot) 0) [ // if the cursor texture is layered (for blendmapping) it will display those informations here (hidden otherwise)
                    uihlist 0 [
                        uitextleft "Layer:" $ui_textsmall

                        ui_tex_current_slot_vlayer_filename = (gettexname (getvlayer $ui_tex_current_slot))
                        while [!= (strstr $ui_tex_current_slot_vlayer_filename "/") -1] [ui_tex_current_slot_vlayer_filename = (substring $ui_tex_current_slot_vlayer_filename (+ 1 (strstr $ui_tex_current_slot_vlayer_filename "/")))]
                        uitextright (format "#%1, %2" (getvlayer $ui_tex_current_slot) $ui_tex_current_slot_vlayer_filename) $ui_textsmall [uitextlimit 1]

                        uiclamp 1 1
                    ]
                ]

                if (!= (getvdetail $ui_tex_current_slot) 0) [// if the cursor texture has vdetail set it will display those informations here (hidden otherwise)
                    uihlist 0 [
                        uitextleft "Detail:" $ui_textsmall

                        ui_tex_current_slot_vdetail_filename = (gettexname (getvdetail $ui_tex_current_slot))
                        while [!= (strstr $ui_tex_current_slot_vdetail_filename "/") -1] [ui_tex_current_slot_vdetail_filename = (substring $ui_tex_current_slot_vdetail_filename (+ 1 (strstr $ui_tex_current_slot_vdetail_filename "/")))]
                        uitextright (format "#%1, %2" (getvdetail $ui_tex_current_slot) $ui_tex_current_slot_vdetail_filename) $ui_textsmall [uitextlimit 1]

                        uiclamp 1 1
                    ]
                ]

                loop i (listlen $ui_tex_vshaderparam_types) [// if the cursor texture has vshaderparam commands set they will be displayed here (hidden otherwise)
                    if (!=s (getvshaderparam $ui_tex_current_slot (at $ui_tex_vshaderparam_types $i)) "") [
                        uihlist 0 [
                            uitextleft (format "%1:" (at $ui_tex_vshaderparam_types $i)) $ui_textsmall
                            uitextright (getvshaderparam $ui_tex_current_slot (at $ui_tex_vshaderparam_types $i)) $ui_textsmall [uitextlimit 1]

                            uiclamp 1 1
                        ]
                    ]
                ]


                uispace 0.115 0.000
                uiclamp 1 1 1 1
            ]
            //option to show modified textures or only unmodified ones (no vcolour vshaderparam vscale etc.)
            uivlist $ui_padsmall [
                uicheckbox "Show variants" $ui_tex_show_all $ui_checksize [ui_tex_show_all = (! $ui_tex_show_all)]
                uiinput ui_tex_search_criteria 20 [] 1 1 "Search textures..." $colourgrey
            ]
        ]

        uihlist $ui_padsmall [
            uiscroll 1.0546875 0.65625 [
                uifill 1.0546875 0.65625 [
                    uigrid 8 0.0078125 0.0078125 [
                        uihover [] [ui_tex_current_slot = (getseltex)]
                        if (= $ui_tex_show_all 1) [
                            looptexmru tex [
                                if (|| (=s $ui_tex_search_criteria "") (!= (strstr (gettexname $tex) $ui_tex_search_criteria) -1)) [
                                    uivslotview $tex 0.125 0.125 [
                                        if (= $tex (getseltex)) [uioutline 0xFF0000; uiclamp- 1 1 1 1] // Outline currently selected texture
                                        uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1; ui_tex_current_slot = $tex]]
                                        uirelease [uiclose "textures"; settex $tex] //this is the part that selects the texture at the cursor
                                    ]
                                ]
                            ]
                        ] [
                            loop slot (numslots) [
                                if (|| (=s $ui_tex_search_criteria "") (!= (strstr (gettexname $slot) $ui_tex_search_criteria) -1)) [
                                    uislotview $slot 0.125 0.125 [
                                        if (= $slot (getseltex)) [uioutline 0xFF0000; uiclamp- 1 1 1 1] // Outline currently selected texture
                                        uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1; ui_tex_current_slot = $slot]]
                                        uirelease [uiclose "textures"; settex (getslottex $slot)] //this is the part that selects the texture at the cursor
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uialign* -1 -1
                ]
            ]
            uivscroll $ui_slidersize 0.65625 3 //scroll bar at the right of the menu
        ]
    ]
]

uimenu "edit" "Edit Settings" "<grey>textures/icons/editing" [// This is the menu accessed by [e]-[d] while in edit mode
    uifill 0.38 0.4 //ui size
    uivlist  $ui_padbutton [
        uialign -1 -1
        uiclamp 1 1
        uivlist 0 [
            //cube painting size, in powers of 2; 10 is a standard newmap, so slider is limited to 9. Hotkey for this in edit would be [G]
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitext "Grid Power"
                uialign -1 0 // text is aligned rightwards, bumped up against the sliders for gridpower and undomegs
                uihorzslider "gridpower" 0 9 1 0.2 0.025 [//all the sliders in the edit menus are .2 ui-units across unless labeled otherwise
                    uivlist 0 [
                        uifill 0.050 0//centering the text inside the slider so things don't look dumb (all sliders are configured like this unless labeled otherwise)
                        uitext $gridpower
                    ]
                ]
                uialign 1 0 //whole hlist is aligned rightwards (most sliders will be aligned this way; most toggles will be aligned leftwards)
            ]
            //undo cache size in megabytes, 1 to 100mb
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Undo Cache Size"
                uialign -1 0
                uihorzslider "undomegs" 1 100 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $undomegs
                    ]
                ]
                uialign 1 0
            ]
            // these are the togglable editing settings: entediting, entselsnap, allfaces, showmat, wireframe, fullbright, outline, crosshairdistance
            uihlist $ui_padbutton [
                uicheckbox "Entity Editing" $entediting $ui_checksize [entediting = (! $entediting)]
                uialign -1 0 // text is aligned leftwards, bumped up against the buttons for the toggleables
            ]
            uihlist $ui_padbutton [
                uicheckbox "Entity Grid Snap" $entselsnap $ui_checksize [entselsnap = (! $entselsnap)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "All Face Texturing" $allfaces $ui_checksize [allfaces = (! $allfaces)]
                uialign -1 0
            ]
            uibar 1 0
            uihlist $ui_padbutton [
                uicheckbox "Show Material Volumes" $showmat $ui_checksize [showmat = (! $showmat)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Wireframe" $wireframe $ui_checksize [wireframe = (! $wireframe)]
                uialign -1 0
            ]
            uihlist $ui_padbutton [
                uicheckbox "Fullbright" $fullbright $ui_checksize [fullbright = (! $fullbright)]
                uialign -1 0
            ]
             
            uihlist $ui_padbutton [
                uicheckbox "Geometry Outline" $outline $ui_checksize [outline = (! $outline)]
                uialign -1 0
            ]
            uibar 1 0
            uispace 0
            uihlist $ui_padbutton [
                uicheckbox "Show Distance on Crosshair" $crosshairdistance $ui_checksize [crosshairdistance = (! $crosshairdistance)]
                uialign -1 0
            ]
        ]
    ]
]

uimenu "environment" "Environment Settings" "<grey>textures/icons/editing" [ //Map environment settings, accessed by [e]-[w] in the edit compass menu
    uifill 0.38 0.4 //size of ui
    uivlist  $ui_padbutton [
        uialign -1 -1
        uiclamp 1 1
        uivlist 0 [
            //title bar 
            uihlist $ui_padbutton [
                uitextleft "Normal                         "
                uialign -1 0
                uitextright " Alt"
                uialign 1 0        
            ]
            //fog distance sliders for normal and alt modes
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextright "Fog Distance "
                uialign -1 0 
                uihorzslider "fog" 16 10000 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $fog
                    ]
                ] 
                uialign 1 0
                uihorzslider "fogalt" 16 10000 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $fogalt
                    ]
                ]
                uialign 1 0 
            ]
            //slider multiplier for sunlight, normal and alt
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Sunlight Scale"
                uialign -1 0 
                uihorzslider "sunlightscale" 0 16 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightscale
                    ]
                ]
                uialign 1 0
                uihorzslider "sunlightscalealt" 0 16 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightscalealt
                    ]
                ]
                uialign 1 0
            ] 
            // controls the height of the sun in the sky, from 0 (horizon) to 90 (noon), normal and alt sliders
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Sunlight Pitch"
                uialign -1 0      
                uihorzslider "sunlightpitch" 0 90 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightpitch
                    ]
                ]
                uialign 1 0
                uihorzslider "sunlightpitchalt" 0 90 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightpitchalt
                    ]
                ]
                uialign 1 0
            ]
            //controls the direction the sun faces; normal and alt sliders
            uihlist $ui_padbutton [
                uifill $ui_checksize
                uitextleft "Sunlight Yaw"
                uialign -1 0         
                uihorzslider "sunlightyaw" 0 360 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightyaw
                    ]
                ]
                uialign 1 0
                uihorzslider "sunlightyawalt" 0 360 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $sunlightyawalt
                    ]
                ]
                uialign 1 0
            ]
            //sunlight colour sliders for normal and alt
            uihlist $ui_padbutton [            
                uitextleft "Sunlight Colour"
                uifill $ui_checksize
                uirgbsliders sunlight
                uirgbsliders sunlightalt
            
            ]
            //ambient colour (total shadow) sliders for normal and alt 
            uihlist $ui_padbutton [               
                uitextleft "Ambient Colour"
                uifill $ui_checksize
                uirgbsliders ambient
                uirgbsliders ambientalt            
            ]
        ]
    ]
]

uimenu "map" "Map Settings" "<grey>textures/icons/editing" [ //Map menu, accessed by [e]-[a] in the editing compass menu
    uifill 0.38 0.4 //size of ui
    uivlist  $ui_padbutton [
        uialign -1 -1
        uiclamp 1 1
        uivlist 0 [
            //waterlod slider, for water detail (larger maps may prefer setting to 0)
            uihlist $ui_padbutton [
                uialign 1 0 // text is aligned rightwards, bumped up against the sliders in this ui
                uifill $ui_checksize
                uitextleft "Water LOD"
                uihorzslider "waterlod" 0 3 1 0.2 0.025 [
                    uifill 0.050 0
                    uitext (format "^f[%1]%2^%" (rgbtohex @(hsltorgb (*f (- 3 $waterlod) 36) 1 0.5)) $waterlod)
                ]
            ]
            //aggressiveness of remipper, at the expense of occlusion 
            uihlist $ui_padbutton [
                uialign 1 0
                uifill $ui_checksize
                uitextleft "Maximum Merge Size"
                uihorzslider "maxmerge" 0 12 1 0.2 0.025 [
                    uivlist 0 [
                        uifill 0.050 0
                        uitext $maxmerge
                    ]
                ] 
            ]
        ]
    ]
]
