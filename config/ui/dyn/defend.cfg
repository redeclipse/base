deffvarp defenduiblend $fvaridxnonzero 1 1

ui_defend_dynui = [
    local _opacity _owner _owners _enemy _enemies _converted _points _occupied _name _seq _colour _text _disptime _ecolour

    uiallowinput 0
    uimenu 0
    uiontop 1
    uizindex -1 // draw behind other stuff on this surface
    uistyle centertop

    _opacity = 1.0

    _owner = (getdefendowner $uiarg1)
    _owners = (getdefendowners $uiarg1)
    _enemy = (getdefendenemy $uiarg1)
    _enemies = (getdefendenemies $uiarg1)

    _converted = (getdefendconverted $uiarg1)
    _points = (getdefendpoints $uiarg1)
    _occupied = (getdefendoccupied $uiarg1)
    _name = (getdefendname $uiarg1)
    _seq = (getprintable $uiarg1)

    _colour = (getteamcolour $_owner)

    if $_owner [
        _text = (getteamname $_owner)
        if $_enemy [
            _ecolour = (getteamcolour $_enemy)
            _colour = (skewcolour $_colour $_ecolour (-f 1.0 $_occupied))
            _text = (format "%1 v %2" $_text (getteamname $_enemy))
        ]
    ] [
        if $_enemy [
            _ecolour = (getteamcolour $_enemy)
            _colour = (skewcolour $_colour $_ecolour (-f 1.0 $_occupied))
            _text = (getteamname $_enemy)
        ] [
            _text = (getteamname $_owner)
        ]
    ]

    uivlist 0.00125 [
        uistyle centermiddle

        uitext $_enemies
        uitext $_enemy

        uihlist 0 [
            if $_enemy [
                uicolour (getteamcolour $_owner) (*f 0.02 (-f 1.0 $_occupied)) 0.002 [ uicolourblend 0.65 ]
                uicolour (getteamcolour $_enemy) (*f 0.02 $_occupied) 0.002 [ uicolourblend 0.65 ]
            ] [
                uicolour (getteamcolour $_owner) (*f 0.02 $_occupied) 0.002 [ uicolourblend 0.65 ]
            ]
        ]
        uivlist 0 [
            uiborderedimageclamped $skinalphatex 0x60000000 0 $ui_texborder $ui_screenborder 0 0 [
                uiborderedimageclamped $skinshadowtex $_colour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.003 [
                        uifont $textfontoutline [
                            uivlist 0 [
                                uitext (concatword $_name " (^fs^fy" $_seq "^fS)") 0.5
                                uitext $_text 0.5
                            ]
                        ]
                    ]
                ]
            ]

            uiimage $pointsharptex $_colour 0 0.0075 0.0075 [ uicolourblend 0.35 ]
        ]
    ]

    uipropagate [ uicolourblend (*f $defenduiblend $_opacity) ]
]

dynui defend [
    ui_defend_dynui
]

