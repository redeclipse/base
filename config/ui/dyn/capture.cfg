deffvarp captureuiblend $fvaridxnonzero 1 1

ui_capture_dynui_affinity = [
    uihint $_curtime

    _offset = (- $lastmillis $_curtime)
    _show = 1

    if (< $_offset $uihintintime) [
        _skew = (divf $_offset $uihintintime)
    ] [
        _offset = (- $_offset $uihintintime)

        if (> $_offset $uihintholdtime) [
            _offset = (- $_offset $uihintholdtime)

            if (< $_offset $uihintouttime) [
                _skew = (-f 1.0 (divf $_offset $uihintouttime))
            ] [
                _show = 0
            ]
        ] [
            _skew = 1.0
        ]
    ]

    _size = (+f $_size $_skew)
]

ui_capture_dynui = [
    local _droptime _owner _taken _team _colour _text _disptime _bordercolour _size _hasaffinity _show _curtime _offset _skew _yaw _dir

    uiallowinput 0
    uimenu 0
    uiontop 1
    uizindex -1 // draw behind other stuff on this surface
    uistyle centertop

    _droptime = (getcapturedroptime $uiarg1)
    _owner = (getcaptureowner $uiarg1)
    _taken = (|| $_droptime [>= $_owner 0])

    _team = (getcaptureteam $uiarg1)
    _colour = (getteamcolour $_team)
    _text = (getteamname $_team)

    _bordercolour = (modcolour $_colour 0.25)
    _size = 1.0
    _hasaffinity = (&& [getclienthasaffinity $focusedplayer] [= (getclientteam $focusedplayer) $_team])

    _show = 0

    if $_hasaffinity [
        _curtime = 0

        loopcaptureif 0 0 _iter [= (getcaptureowner $_iter) $focusedplayer] [
            _curtime = (max $_curtime (getcapturetaketime $_iter))
        ]

        if $_curtime [
            ui_capture_dynui_affinity
        ]
    ]

    uiradar 1 0 0 0 0 [
        uiradarshape 1

        uispace (*f 0.005 $_size) (*f 0.005 $_size) [
            uivlist 0 [
                uiborderedimageclamped $skinshadowtex $_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.003 [
                        uifont $textfontoutline [
                            uivlist 0 [
                                uitext $_text (*f 0.5 $_size)
                                uitext (? $_hasaffinity "GOAL" (? $_taken "TAKEN" "SAFE")) (*f 0.5 $_size) [
                                    if $_hasaffinity [ uicolourset (pulsecolour $PULSE_DISCO) ]
                                ]
                            ]
                        ]
                    ]
                ]
                if (! $_show) [
                    uiimage $pointsharptex $_bordercolour 0 0.0075 0.0075 [ uicolourblend 0.35 ]
                ]
            ]
        ]

        if $_show [
            _dir = (getcaptureradardir $uiarg1 1)
            _yaw = (clampf (modf (-f 360 (+f $_dir 180)) 360) 90 270)

            uiradarblip $arrowsharptex (pulsecolour $PULSE_DISCO) $_yaw $_yaw 2 (*f 0.01 $_size) (*f 0.01 $_size)
        ]
    ]

    uipropagate [ uicolourblend $captureuiblend ]
]

dynui capture [
    ui_capture_dynui
] [] [] [
    result (&& [getclienthasaffinity $focusedplayer] [= (getclientteam $focusedplayer) (getcaptureteam $uiarg1)])
]

deffvarp captureflaguiblend $fvaridxnonzero 1 1

ui_captureflag_dynui = [
    local _taken _team _colour _text _disptime _opacity _bordercolour _offtime

    uiallowinput 0
    uimenu 0
    uiontop 1
    uizindex -1 // draw behind other stuff on this surface
    uistyle centermiddle

    _taken = (&& [|| [getcapturedroptime $uiarg1] [>= (getcaptureowner $uiarg1) 0]])
    _team = (getcaptureteam $uiarg1)
    _colour = (getteamcolour $_team)
    _text = (getteamname $_team)
    _opacity = 1.0

    _bordercolour = (modcolour $_colour 0.5)

    _disptime = (getcapturedisptime $uiarg1)
    if $_disptime [
        _offtime = (- $lastmillis $_disptime)
        if (< $_offtime 1000) [
            _opacity = (divf $_offtime 1000)
        ]
    ]

    uivlist 0 [
        uiborderedimageclamped $skinshadowtex $_colour 0 $ui_texborder $ui_screenborder 0 0 [
            uicolourblend 0.5
            uispace 0.003 0.003 [
                uifont $textfontoutline [
                    uivlist 0 [
                        uitext $_text 0.75
                        uitext "FLAG" 0.5
                    ]
                ]
            ]
        ]

        uiimage $pointsharptex $_colour 0 0.0075 0.0075 [ uicolourblend 0.35 ]
    ]
    uipropagate [ uicolourblend (*f $captureflaguiblend $_opacity) ]
]

dynui captureflag [
    ui_captureflag_dynui
] [] [] [
    result (|| [getcapturedroptime $uiarg1] [>= (getcaptureowner $uiarg1) 0])
]
