defvar entityitemuihint 0 0 1
defvar entityitemuisuper 0 1 1
deffvar entityitemuiblend 0 1.0 1
deffvar entityitemuiinactive 0 0.5 1

ui_entityitem_dynui = [
    local _name _attr1 _enter _ready _colour _alt _canuse

    _name = (getentinfo $_type 1)
    _attr1 = (getentity $_ent 1 0 1)

    _ready = (? (>= $_idx 0) (getclientactitemready $focusedplayer $_idx) 0)
    _name = (at $W_NAMES $_attr1)
    _colour = $[@[_name]colour]

    if $_ready [
        _blend = $entityitemuiblend
        uiontop (|| (= $_idx 0) (= $_dir -1))
        uizindex (? (= $_idx 0) -1 -2)
        uihint (? $entityitemuihint (getclientactitementer $focusedplayer $_idx) 0)
    ] [
        if (&& $entityitemuisuper [>= $_attr1 $W_SUPER] [< $_attr1 $W_ALL] [!= $_dir 1] [< (- $uilastmillis $_millis) $uihinttime]) [
            _blend = $entityitemuiblend
            uiontop 1
            uizindex 0
            uihint $_millis
        ] [
            _blend = $entityitemuiinactive
            uiontop (= $_dir -1)
            uizindex -3
            uihint (? $entityitemuihint $_enter 0)
        ]
    ]

    _alt = $_blend

    case $_dir -1 [
        _colour = (mulcolour $_colour (skewcolour (pulsecolour $PULSE_FLASH 50) 0xFFFFFF $_spawn))
        _spawn = (*f $_spawn 2.0)
        if (>f $_spawn 1.0) [
            _size = (+f $_size (-f 2.0 $_spawn))
        ] [
            _size = (*f $_spawn 2.0)
        ]
    ] 1 [
        _size = $_spawn
    ] 0 [
        if (>=f $_spawnlevel 0.0) [
            _colour = (mulcolour $_colour (skewcolour 0xFFFFFF (pulsecolour $PULSE_FLASH 50) $_spawnlevel))
        ]
    ]

    _canuse = (getclientcanuse $focusedplayer $_ent)
    _colour = (skewcolour 0x000000 $_colour (? $_canuse $showentavailable $showentunavailable))
    _blend = (*f $_blend (? $_canuse $showentavailable $showentunavailable))
    _alt = (*f $_alt (? $_canuse $showentavailable $showentunavailable))

    uivlist $ui_padsmaller [
        if (!=f $_spawn -1.0) [
            uivlist $ui_padsmaller [
                if $_ready [
                    uiborderedimageclamped $skinalphatex 0x60000000 0 $ui_texborder $ui_screenborder 0 0 [
                        uiborderedimageclamped $skinshadowtex (modcolour $_colour 0.5) 0 $ui_texborder $ui_screenborder 0 0 [
                            uispace $ui_padtiny $ui_padtiny [
                                uivlist $ui_padtiny [
                                    if $_canuse [
                                        uitext "^f{=use}" (*f 0.35 $_size)
                                    ]
                                    uitext $[@[_name]longname] (*f 0.15 $_size)
                                ]
                            ]
                        ]
                    ]
                ]

                uiimage (getenttex $_ent) $_colour 0 (*f 0.0125 $_size) (*f 0.0125 $_size)

                case $_dir -1 [
                    if (>f $_spawn 1.0) [
                        _blend = (+f $_blend (*f (-f 1.0 $_blend) (-f 2.0 $_spawn)))
                    ] [
                        _blend = $_spawn
                    ]
                ] 1 [
                    _blend = (*f $_blend $_spawn)
                ]

                uipropagate [ uicolourblend $_blend ]
            ]
        ]

        uicolour $_colour 0.02 0.004 [
            uicolour $_colour (? (>=f $_spawnlevel 0.0) (*f 0.02 $_spawnlevel) 0.02) 0.004 [ uicolourblend (*f $_alt 0.75) ]
            uicolourblend (*f $_alt 0.25)
        ]
        uiimage $pointsharptex $_colour 0 0.01 0.01 [ uicolourblend (*f $_alt 0.75) ]
    ]
]

dynui entityitem [
    local _ent _type _idx _millis _blend _dir _spawn _size _spawnlevel

    uistyle centertop

    _ent = $uiarg1
    _type = (getentity $_ent 0)
    _idx = (getclientactitemidx $focusedplayer $ACTITEM_ENT $_ent)
    _millis = (getentity $_ent 3 1)

    _dir = 0
    _spawn = 1.0
    if (getentity $_ent 3 0) [
        _size = (-f $uilastmillis (getentity $_ent 3 1))
        if (< $_size $itemfadetime) [
            _dir = -1
            _spawn = (clampf (divf $_size $itemfadetime) 0.0 1.0)
        ]
    ] [
        _size = (-f $uilastmillis (getentity $_ent 3 2))
        if (< $_size $itemfadetime) [
            _spawn = (clampf (-f 1.0 (divf $_size $itemfadetime)) 0.0 1.0)
            _dir = 1
        ] [
            _spawn = -1.0
        ]
    ]
    _spawnlevel = (clampf (getentity $_ent 3 8) -1.0 1.0)
    _size = 1.0

    ui_entityitem_dynui
]

dynui entityproj [
    local _ent _type _idx _millis _blend _dir _spawn _size _spawnlevel

    uistyle centertop

    _ent = (getprojid $uiarg1 $PROJ_ENTITY)
    _type = (getentity $_ent 0)
    _idx = (getclientactitemidx $focusedplayer $ACTITEM_PROJ -1 $uiarg1)
    _millis = (getprojspawntime $uiarg1 $PROJ_ENTITY)

    _dir = (getprojfadedir $uiarg1 $PROJ_ENTITY)
    _spawn = (getprojfade $uiarg1 $PROJ_ENTITY 0 0)
    _size = 1.0
    _spawnlevel = -1.0

    ui_entityitem_dynui
]

defvar entityedituilevel 0 0 2
defvar entityedituinumber 0 1 1
deffvarp entityedituiblend 0 0.5 1.0
deffvarp entityedituiselblend 0 0.75 1.0
deffvarp entityedituihoverblend 0 1.0 1.0

ui_entityedit_dynui_full = [
    uicolour 0x80000000 0 0 [
        uitable 0 0 [
            loop i (getentity $uiarg1 1) [
                _aname = (getentattr $_type $i $_attr1)
                if (!=s $_aname "") [
                    uitablerow [
                        uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 0.25]
                        uispace $ui_padsmall $ui_padtiny [uitext $_aname 0.25]
                        uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiarg1 1 $i) 0.25]
                    ]
                ]
            ]
        ]
    ]
]

ui_entityedit_dynui = [
    local _name _attr1 _group_hover _selected _hovering _full

    _name = (getentinfo $_type 1)
    _attr1 = (getentity $uiarg1 1 0)
    _blend = $entityedituiblend

    _group = (entgrouppos $uiarg1)
    _hover = (enthoverpos $uiarg1)

    _selected = (>= $_group 0)
    _hovering = (= $_hover 0)

    uiontop (|| $_hovering $_selected)
    uizindex (? $_selected -1 (? $_hovering -2 -3))

    _full = (> $entityedituilevel (? (|| $_selected $_hovering) 0 1))

    if $_selected [ _blend = (maxf $_blend $entityedituiselblend) ]
    if $_hovering [ _blend = (maxf $_blend $entityedituihoverblend) ]

    uivlist 0 [
        uiborderedimageclamped $skinalphatex (? $_selected 0x60000000 0x20000000) 0 $ui_texborder $ui_screenborder 0 0 [
            uiborderedimageclamped $skinshadowtex 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
                uispace $ui_padnormal $ui_padsmall [
                    uivlist $ui_padsmall [
                        if (&& (= $_group 0) (=s $_name "Camera")) [
                            uiviewport $uiname (getentity $uiarg1 4 1) (getentity $uiarg1 4 2) (getentity $uiarg1 4 3) (getentity $uiarg1 4 4) (getentity $uiarg1 4 5) 0.08 (? (= $_attr1 1) 0.08 0.045) [

                                if $_hovering [
                                    uiviewportuprate $viewportuprate
                                    uiviewportwidth $viewportsize
                                ] [
                                    uiviewportuprate (* $viewportuprate 2)
                                    uiviewportwidth (div $viewportsize 2)
                                ]

                                uiviewportheight (? (= $_attr1 1) $uiviewportwidth (div (* $uiviewportwidth 9) 16))
                                
                                if (= $_attr1 1) [
                                    if (getentity $uiarg1 1 11) [
                                        uiviewportfov (getentity $uiarg1 1 11)
                                    ] [
                                        uiviewportfov 90
                                    ]
                                ] [
                                    uiviewportfov $specfov
                                ]
                            ]
                        ]
                        
                        uihlist $ui_padsmall [
                            uiimage (? $_selected $startex $dottex) 0xFFFFFFFF 0 0.0075 0.0075
                            uifont $textfontoutline [
                                uihlist $ui_padsmall [
                                    uitext $_name 0.5
                                    if $entityedituinumber [ uitext (concatword "(" $uiarg1 ")") 0.5 [uicolourset $colouryellow] ]
                                ]
                            ]
                        ]

                        if $_full [
                            ui_entityedit_dynui_full
                        ]
                    ]
                ]
            ]
        ]

        uiimage $pointsharptex 0xff000000 0 0.0075 0.0075 [ uicolourblend $_blend ]
    ]
]

dynui entityedit [
    local _type _blend

    _type = (getentity $uiarg1 0)

    if (&& $_type [isediting] [! $editinhibit]) [
        uistyle centertop

        ui_entityedit_dynui

        uipropagate [ uicolourblend $_blend ]
    ] [
        uivisible 0
    ]
]
