defvarp playeruihealth 0 1 1
defvarp playeruidamage 0 1 1
defvarp playeruidamagenumbers 0 0 1
defvarp playeruitargeting 0 2 2
deffvarp playeruiblend $fvaridxnonzero 1 1

ui_player_dynui_flash = [
    _modtime = (divf (mod $lastmillis 1000) 500)
    if (>f $_modtime 1.0) [ _modtime = (-f 2.0 $_modtime) ]

    caseif $_healthcrit [
        _bordercolour = (skewcolour (modcolour (getclientpulsecolour $uiarg1 $PULSE_ALERT) 0.75) $_bordercolour $_modtime)
    ] $_buffing [
        _bordercolour = (skewcolour (modcolour (getclientpulsecolour $uiarg1 $PULSE_BUFF) 0.5) $_bordercolour $_modtime)
    ]

    if (> $_isprize 0) [
        _colour = (getclientpulsecolour $uiarg1 $PULSE_READY)
        _bordercolour = (skewcolour $_colour $_bordercolour $_modtime)
    ]
]

ui_player_dynui_health = [
    if (getclientconopen $uiarg1) [
        uiimage $chattex $_colour 0 0.02 0.02 [
            uistyle centermiddle
            uiimageshadow 0.001 0x88010101
            uicolourblend 0.75
        ]
    ]

    if $playeruihealth [
        _healthcolour = (? (>f $_healthlevel 1.0) (skewcolour $colourgreen $colourcyan (-f $_healthlevel 1.0)) (skewcolour $colourred $colourgreen $_healthlevel))
        if $_healthregenerating [
            _healthcolour = (skewcolour (getclientpulsecolour $uiarg1 $PULSE_HEALTH) $_healthcolour $_modtime)
        ]

        uicolour $_healthcolour (*f 0.02 $_healthlevel) 0.004 [ uicolourblend 0.65 ]
    ]
]

# ui_player_dynui_damage = [
    loopdamagerevif 0 0 _curdamage [&& (= (getdamageclient $_curdamage) $uiarg1) (= (getdamagefrom $_curdamage) $focusedplayer)] [
        _totaldamage = (getdamageamt $_curdamage)
        _damagelevel = (clampf (divf $_totaldamage $_spawnhealth) 0.0 2.0)

        _damagecolour = (skewcolour $colourdarkred (getclientpulsecolour $uiarg1 (at [
            #PULSE_WARN
            #PULSE_BURN
            #PULSE_BLEED
            #PULSE_SHOCK
            #PULSE_CORRODE
        ] (getdamagetype $_curdamage))) 0.5)

        _damageready = (getdamageready $_curdamage)

        if $_damageready [
            _damagetime = (- $totalmillis $_damageready)
            _damagelength = (div (getdamagelength $_curdamage) 2)
            if (> $_damagetime $_damagelength) [
                _damageoffset = (- $_damagetime $_damagelength)
                _damageblend = (-f 1.0 (divf $_damageoffset $_damagelength))
            ] [
                _damageblend = 1.0
            ]
        ] [
            _damageblend = 1.0
        ]

        uicolour $_damagecolour (*f 0.02 $_damagelevel $_damageblend) 0.004 [ uicolourblend (*f 0.65 $_damageblend) ]
    ]
]

ui_player_dynui = [
    local _state _name _team _colour _opacity _health _spawnhealth _healthregen _healthregenlast _healthregenerating _isprize _damagelevel _damagecrit _bordercolour _modtime _healthcolour _totaldamage _damagecolour _damagelevel _damageblend _damageready _damagetime _damagelength _damageoffset _prize

    _state = (getclientstate $uiarg1)
    _team = (getclientteam $uiarg1)
    _colour = (getclientcolour $uiarg1 $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)
    _opacity = (*f (getclientopacity $uiarg1) (getdarkness $DARK_HALO))
    _isprize = (getclientisprize $uiarg1)

    uiontop (> $_isprize 0)
    uizindex -2 // draw behind other stuff on this surface
    uistyle centertop

    uivlist 0.00125 [
        uistyle centermiddle

        _name = (getclientname $uiarg1 0 0)
        _health = (getclienthealth $uiarg1)
        _spawnhealth = (getclientspawnhealth $uiarg1)
        _healthregen = (getclientregen $uiarg1)
        _healthregenlast = (- $lastmillis $_healthregen)
        _healthregenerating = (<= $_healthregenlast $regentime)
        _buffing = (getclientbuffing $uiarg1)
        _healthlevel = (clampf (? (= $_state $CS_DEAD) 0.0 (divf $_health $_spawnhealth)) 0.0 2.0)
        _healthcrit = (|| [= $_state $CS_DEAD] [&& [= $_state $CS_ALIVE] [<=f $_healthlevel $damagecritical]])
        case $_isprize 1 [
            _prize = (getclienthasprize $uiarg1)
        ] 2 [
            _prize = 3
        ] 3 [
            _prize = $revengeprize
        ] () [
            _prize = 0
        ]
        _prize = (? (> $_prize 0) (getattrmap (+ $_prize (- $W_GRENADE 1))) $_prize)

        _bordercolour = $_colour
        if (= $_state $CS_ALIVE) [
            ui_player_dynui_flash
        ]

        uivlist 0.003 [
            if (|| (= $_state $CS_DEAD) (= $_state $CS_WAITING)) [
                uiimage $deadtex $_colour 0 0.025 0.025 [
                    uistyle centermiddle
                    uicolourblend 0.75
                ]

                if $playeruidamage [
                    uihlist 0 [ ui_player_dynui_damage ]
                ]
            ] [
                caseif (> $_prize 0) [
                    uiimage (getweaptex $_prize) (skewcolour $[@(at $W_NAMES $_prize)colour] (getclientpulsecolour $uiarg1 $PULSE_PRIZE) (divf (mod $uilastmillis 500) 2000)) 0 0.015 0.015 [
                        uistyle centermiddle
                        uicolourblend 0.75
                    ]
                ] (< $_prize 0) [
                    uiimage $questiontex (skewcolour (getclientpulsecolour $uiarg1 $PULSE_WARN) (getclientpulsecolour $uiarg1 $PULSE_PRIZE) (divf (mod $uilastmillis 500) 2000)) 0 0.015 0.015 [
                        uistyle centermiddle
                        uicolourblend 0.75
                    ]
                ]

                uihlist 0 [
                    if (! (hasmut $G_M_INSTAGIB)) [ ui_player_dynui_health ]

                    if $playeruidamage [
                        ui_player_dynui_damage
                    ]
                ]
            ]
        ]

        uivlist 0 [
            uiborderedimageclamped $skinalphatex 0x60000000 0 $ui_texborder $ui_screenborder 0 0 [
                uiborderedimageclamped $skinshadowtex $_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.0 [
                        uifont $textfontoutline [ uitext $_name 0.5 [ uicolourset $_colour ] ]
                    ]
                ]
            ]

            uiimage $pointsharptex $_bordercolour 0 0.0075 0.0075 [ uicolourblend 0.35 ]
        ]
    ]

    uipropagate [ uicolourblend (*f $playeruiblend $_opacity) ]
]

dynui player [
    ui_player_dynui
]

# ui_playeroverlay_dynui_damage = [
    loopdamagerevif 0 0 _curdamage [&& (= (getdamageclient $_curdamage) $uiarg1) (= (getdamagefrom $_curdamage) $focusedplayer)] [
        _totaldamage = (getdamageamt $_curdamage)
        _damagelevel = (clampf (divf $_totaldamage $_spawnhealth) 0.0 2.0)

        _damagecolour = (? $_curdamage (skewcolour (modcolour $colourwhite $_damagelevel) (getclientpulsecolour $uiarg1 (at [
            #PULSE_WARN
            #PULSE_BURN
            #PULSE_BLEED
            #PULSE_SHOCK
            #PULSE_CORRODE
        ] (getdamagetype $_curdamage))) 0.75) $colourwhite)

        _damageready = (getdamageready $_curdamage)

        if $_damageready [
            _damagetime = (- $totalmillis $_damageready)
            _damagelength = (getdamagelength $_curdamage)
            _damageoffset = (getdamagedelay $_curdamage)
            _damagelength = (- $_damagelength $_damageoffset)
            if (> $_damagetime $_damagelength) [
                _damageblend = (-f 1.0 (divf (- $_damagetime $_damagelength) $_damageoffset))
            ] [
                _damageblend = 1.0
            ]
        ] [
            _damageready = (getdamagemillis $_curdamage)
            if $_damageready [
                _damagetime = (- $totalmillis $_damageready)
                _damageoffset = (getdamagedelay $_curdamage)
                if (> $_damagetime $_damageoffset) [
                    _damageblend = 1.0
                ] [
                    _damageblend = (divf $_damagetime $_damageoffset)
                ]
            ] [
                _damageblend = 0.0
            ]
        ]

        _totaldamage = (divf $_totaldamage $damagedivisor)
        if $damageinteger [ _totaldamage = (toint (ceil $_totaldamage)) ] [ _totaldamage = (precf $_totaldamage 1) ]

        if (>f $_damageblend 0.0) [
            uiborderedimageclamped $skinalphatex (modcolour $_colour 0.125) 0 $ui_texborder $ui_screenborder 0 0 [
                uistyle righttop
                uicolourblend 0.125
                uiborderedimageclamped $skinshadowtex (modcolour $_colour 0.25) 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.25
                    uispace 0.001 0.0 [

                        uitext $_totaldamage (*f 0.65 $_damageblend) [
                            uicolourset $_damagecolour
                            uicolourblend $_damageblend
                        ]
                    ]
                ]
            ]
        ]
    ]
]

# ui_playeroverlay_dynui_targeting = [
    caseif (&& [= $gamemode $G_BOMBER] [= $bombercurtarget $uiarg1]) [
        uirender "targeting" 0.15 0.15 [
            uistyle centermiddle
            uicolourset (skewcolour $colourwhite (getclientpulsecolour $uiarg1 $PULSE_WARN) 0.5)
        ]
    ] (&& [= $playeruitargeting 2] [getclienthasaffinity $uiarg1]) [
        uirender "targeting" 0.15 0.15 [
            uistyle centermiddle
            uicolourset (skewcolour $colourwhite (getclientpulsecolour $uiarg1 $PULSE_ALERT) (? (!= (getclientteam $focusedplayer) (getclientteam $uiarg1)) 0.9 0.25))
            uicolourblend 0.75
        ]
    ]
]

ui_playeroverlay_dynui = [
    local _colour _opacity _spawnhealth _damagelevel _totaldamage _damagecolour _damagelevel _damageblend _damageready _damagetime _damagelength _damageoffset

    _colour = (getclientcolour $uiarg1 $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)
    _opacity = (*f (getclientopacity $uiarg1) (getdarkness $DARK_HALO))

    uiontop 1
    uizindex -1 // draw behind other stuff on this surface
    uistyle centermiddle

    uifill 0.15 0.15

    _name = (getclientname $uiarg1 0 0)
    _spawnhealth = (getclientspawnhealth $uiarg1)

    if $playeruitargeting [ ui_playeroverlay_dynui_targeting ]

    if (&& $playeruidamagenumbers [getdamagemerges $focusedplayer $uiarg1]) [
        uipad 0 0.025 0 0 [
            uistyle righttop
            uifont $textfontoutline [
                uistyle righttop
                uivlist 0.001 [
                    uistyle righttop
                    ui_playeroverlay_dynui_damage
                ]
            ]
        ]
    ]

    uipropagate [ uicolourblend (*f $bomberuiblend $_opacity) ]
]


dynui playeroverlay [
    ui_playeroverlay_dynui
]
