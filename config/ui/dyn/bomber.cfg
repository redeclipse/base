deffvarp bomberuiblend $fvaridxnonzero 1 1

ui_bomber_dynui_affinity = [
    uihint $_curtime
    _show = 1
    _offset = (- $lastmillis $_curtime)

    if (< $_offset $uihintintime) [
        _skew = (divf $_offset $uihintintime)
    ] [
        _offset = (- $_offset $uihintintime)

        if (> $_offset $uihintholdtime) [
            _offset = (- $_offset $uihintholdtime)

            if (< $_offset $uihintouttime) [
                _skew = (-f 1.0 (divf $_offset $uihintouttime))
            ] [
                _show = 0
            ]
        ] [
            _skew = 1.0
        ]
    ]

    _size = (+f $_size $_skew)
]

ui_bomber_dynui = [
    local _opacity _team _colour _text _disptime _hasaffinity _size _offtime _show _curtime _offset _skew _yaw _dir
    _opacity = 1.0

    uiontop 1
    uizindex -1 // draw behind other stuff on this surface
    uistyle centertop

    _team = (getbomberteam $uiarg1)
    if $_team [
        _colour = (getteamcolour $_team)
        _text = (getteamname $_team)
    ] [
        _colour = (pulsecolour $PULSE_DISCO)
        _text = "BOMB"
    ]

    _hasaffinity = (&& [getclienthasaffinity $focusedplayer] $_team [!= (getclientteam $focusedplayer) $_team])

    _disptime = (getbomberdisptime $uiarg1)
    if $_disptime [
        _offtime = (- $lastmillis $_disptime)
        if (< $_offtime 1000) [
            _opacity = (divf $_offtime 1000)
        ]
    ]

    _show = 0

    if $_hasaffinity [
        _curtime = 0
        loopbomberif 0 0 _iter [= (getbomberowner $_iter) $focusedplayer] [
            _curtime = (max $_curtime (getbombertaketime $_iter))
        ]

        if $_curtime [
            ui_bomber_dynui_affinity
        ]
    ]

    uiradar 1 0 0 0 0 [
        uiradarshape 1

        uispace (*f 0.005 $_size) (*f 0.005 $_size) [
            uivlist 0 [
                uiborderedimageclamped $skinalphatex 0x60000000 0 $ui_texborder $ui_screenborder 0 0 [
                    uiborderedimageclamped $skinshadowtex $_colour 0 $ui_texborder $ui_screenborder 0 0 [
                        uispace 0.003 0.003 [
                            uifont $textfontoutline [
                                uivlist 0 [
                                    uitext $_text (*f 0.4 $_size) [ if (! $_team) [ uicolourset $_colour ] ]
                                    if $_team [
                                        uitext (? (!= (getclientteam $focusedplayer) $_team) "GOAL" "BASE") (*f 0.25 $_size) [
                                            if $_hasaffinity [ uicolourset (pulsecolour $PULSE_DISCO) ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                if (! $_show) [ uiimage $pointsharptex $_colour 0 0.0075 0.0075 [ uicolourblend 0.35 ] ]
            ]
        ]

        if $_show [
            _dir = (getbomberradardir $uiarg1 1)
            _yaw = (clampf (modf (-f 360 (+f $_dir 180)) 360) 90 270)

            uiradarblip $arrowtex (pulsecolour $PULSE_DISCO) $_yaw $_yaw 2 (*f 0.01 $_size) (*f 0.01 $_size)
        ]
    ]

    uipropagate [ uicolourblend (*f $bomberuiblend $_opacity) ]
]

dynui bomber [
    ui_bomber_dynui
] [] [] [] [|| [! (getbomberteam $uiarg1)] (&& [getclienthasaffinity $focusedplayer] (getbomberteam $uiarg1) [!= (getclientteam $focusedplayer) (getbomberteam $uiarg1)])]
