ui_tooltip_body = ""
ui_tooltip_time = 0
ui_tooltip_last = 0
ui_tooltip_skip = 0

uihidetip = [
    hideui "tooltip"
    ui_tooltip_body = ""
    ui_tooltip_time = 0
    ui_tooltip_last = 0
    ui_tooltip_skip = 0
]

uistarttip = [
    ui_tooltip_body = $arg1
    ui_tooltip_last = (getmillis 1)
    ui_tooltip_time = $ui_tooltip_last
    ui_tooltip_skip = $arg2
]

uiskiptip = [
    ui_tooltip_skip = $arg1
]

uitipmenu "tooltip" [
    uivlist 0 [[ui_tip_@ui_tooltip_body]]
    ui_tooltip_frame = (- (getmillis 1) $ui_tooltip_last)
    if (|| (= $ui_tooltip_time 0) (> $ui_tooltip_frame (? (> $ui_tooltip_skip 1) $ui_tooltipwait))) [
        ui_tooltip_alpha = (-f 1 (divf (- $ui_tooltip_frame (? (> $ui_tooltip_skip 1) $ui_tooltipwait)) $ui_tooltipblend))
        if (>f $ui_tooltip_alpha 0) [uichangeblends $ui_tooltip_alpha] [
            uichangeblends 0
            case $ui_tooltip_skip 0 [uihidetip] 1 [uiskiptip 2] 3 [uiskiptip 0] 4 [uihidetip]
        ]
    ] [
        ui_tooltip_alpha = (divf (- (getmillis 1) $ui_tooltip_time (? $ui_tooltip_skip 0 $ui_tooltipdelay)) $ui_tooltipblend)
        if (<=f $ui_tooltip_alpha 1) [uichangeblends $ui_tooltip_alpha] [
            uichangeblends 1
            case $ui_tooltip_skip 3 [uiskiptip 4] 4 [if (> (- (getmillis 1) $ui_tooltip_time $ui_tooltipdelay) $ui_tooltipblend) [uiskiptip 0]]
        ]
    ]
]

uitooltip = [
    if (&& (!=s $arg1 "") (!=s $ui_menu_current "") [= $[ui_menu_close_@ui_menu_current] 0]) [
        uihover [
            if (&& (!=s (getalias [ui_tip_@@arg1]) "") (!=s $ui_tooltip_body $arg1)) [
                if (uivisible "tooltip") [
                    case $ui_tooltip_skip 0 [
                        if (> (- (getmillis 1) $ui_tooltip_last $ui_tooltipdelay) $ui_tooltipblend) [uistarttip $arg1 0] [uiskiptip 1]
                    ] 2 [uistarttip $arg1 3] 4 [uiskiptip 0]
                ] [uistarttip $arg1 0]
            ] [
                if $ui_tooltip_time [
                    ui_tooltip_last = (getmillis 1)
                    if (! (uivisible "tooltip")) [showui "tooltip"]
                ]
            ]
        ]
    ]
]
