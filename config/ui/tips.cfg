uitipreset = [
    ui_tooltip_body = ""
    ui_tooltip_time = 0
    ui_tooltip_last = 0
]
uitipreset

uitip = [
    if (&& (!=s $arg1 "") (!=s (getalias [ui_tip_@arg1]) "")) [
        uihover [
            if (!=s $ui_tooltip_body $arg1) [
                ui_tooltip_body = $arg1
                if $ui_tooltip_time [
                    ui_tooltip_time = (- $totalmillis (- $ui_tooltipblend (- $totalmillis $ui_tooltip_last)))
                ] [
                    ui_tooltip_time = $totalmillis
                ]
            ]
            ui_tooltip_last = $totalmillis
            if (! (uitest "tooltip" $SURFACE_FOREGROUND)) [ showui "tooltip" $SURFACE_FOREGROUND ]
        ]
    ]
]

newui "tooltip" $SURFACE_FOREGROUND [
    uiontop 1
    uiwinstyle $WINSTYLE_TOOLTIP
    uizindex 1
    uivlist 0 [
        uiborderedimageclamped $skinalphatex 0x000000 0 $ui_texborder $ui_screenborder 0 0 [
            uiborderedimageclamped $skinshadowtex 0x202020 0 $ui_texborder $ui_screenborder 0 0 [
                uispace $ui_padnormal $ui_padsmall [
                    uivlist 0 [[ui_tip_@ui_tooltip_body]]
                    local tooltip_frame; tooltip_frame = (- $totalmillis $ui_tooltip_last)
                    if (|| (= $ui_tooltip_time 0) (> $tooltip_frame $ui_tooltipdelay)) [
                        local tooltip_since
                        tooltip_since = (-f $tooltip_frame $ui_tooltipdelay)
                        if (> $tooltip_since $ui_tooltipblend) [
                            hideui "tooltip" $uisurfacetype
                        ] [
                            uiproproot [ uicolourblend (-f 1 (divf $tooltip_since $ui_tooltipblend)) ]
                        ]
                    ] [
                        local tooltip_since; tooltip_since = (- $totalmillis $ui_tooltip_time)
                        if (< $tooltip_since $ui_tooltipblend) [
                            uiproproot [ uicolourblend (divf $tooltip_since $ui_tooltipblend) ]
                        ]
                    ]
                ]
            ]
        ]
    ]
] [] [ uitipreset ]
