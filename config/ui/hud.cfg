defvarp darkhud 0 2 2
defvarp weaponhud 0 1 1

defvarp showfps 0 0 4
defvarp showstats 0 1 2
defvarp showtexstats 0 1 1
defvarp showedittoggles 0 1 1
defvarp showvelocity 0 1 1
defvarp showweight 0 1 1
defvarp showmovecount 0 1 1
defvarp showtimestyle 0 3 4
defvarp showrealtime 0 0 1

defvarp showminimap 0 1 1
deffvarp minimapdist 0 384 $fvaridxmax
deffvarp minimapdistblend 0 500 $fvaridxmax
deffvarp minimapoffset 0 0.025 1
deffvarp minimapborder 0 0.1 1
deffvarp minimapblipsize 0 0.009 1
deffvarp minimapsmallblipsize 0 0.006 1
defvarp minimapplayernames 0 0 1
defvarp minimapcompass 0 1 1

defvarp showradar 0 1 1
deffvarp radardist 0 384 $fvaridxmax
deffvarp radardistblend 0 500 $fvaridxmax
deffvarp radaroffset 0 0.155 1
deffvarp radarblipsize 0 0.0165 1
deffvarp radarsmallblipsize 0 0.01 1
defvarp radarplayernames 0 0 1
deffvarp radarsize 0 0.35 2
defvarp radaraspect 0 0 1

defsvarp radarskintex "<grey>textures/hud/radar"

deffvarp ui_hud_sizew $fvaridxnonzero 0.075 $fvaridxmax
deffvarp ui_hud_sizeh $fvaridxnonzero 0.15 $fvaridxmax
deffvarp ui_hud_bar $fvaridxnonzero 0.14 $fvaridxmax
deffvarp ui_hud_barw $fvaridxnonzero 0.05 $fvaridxmax
deffvarp ui_hud_barh $fvaridxnonzero 0.1 $fvaridxmax
deffvarp ui_hud_bar2 $fvaridxnonzero 0.025 $fvaridxmax
deffvarp ui_hud_top $fvaridxnonzero 0.03 $fvaridxmax
deffvarp ui_hud_mtrw $fvaridxnonzero 0.06 $fvaridxmax
deffvarp ui_hud_mtrh $fvaridxnonzero 0.02 $fvaridxmax

defsvarp ui_hud_statenames [ALIVE DEAD EDIT SPEC WAIT]
defsvarp ui_hud_statetexs [playertex deadtex editingtex spectatortex waitingtex]
defsvarp ui_hud_statetex2 [playerbliptex deadtex playerbliptex spectatortex deadtex]
defsvarp ui_hud_timecols [red red red red red green yellow orange orange]

ui_hud_init = [
    refreshenginestats
    ui_hud_clientnum = (getplayercn)
    ui_hud_focus = (getclientfocused)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_health = (getclienthealth $ui_hud_focus)
    ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
    if (& (mutators) $mutsbitinstagib) [
        ui_hud_damagelevel = 1
        ui_hud_damagecrit = (= $ui_hud_state 1)
    ] [
        ui_hud_damagelevel = (? (= $ui_hud_state 1) 0.0 (divf $ui_hud_health $ui_hud_spawnhealth))
        ui_hud_damagecrit = (|| (= $ui_hud_state 1) (&& (= $ui_hud_state 0) (<=f $ui_hud_damagelevel $damagecritical)))
    ]
    ui_hud_carryaffinity = (getclientcarryaffinity $ui_hud_focus)
    ui_hud_colour = (getteamcolour $ui_hud_team)
    caseif (= $darkhud 0) [
        ui_hud_colour1 = (| $ui_hud_transor (modcolour $ui_hud_colour 0.2))
        ui_hud_colour2 = (modcolour $ui_hud_colour 0.75)
        ui_hud_colour3 = (| $ui_hud_transor (modcolour $ui_hud_colour 0.6))
        if (<= $ui_hud_state 1) [
            ui_hud_colour4 = (| $ui_hud_transor (skewcolour 0xFF0000 (modcolour $ui_hud_colour 0.2) $ui_hud_damagelevel))
        ] [
            ui_hud_colour4 = (| $ui_hud_transor (modcolour $ui_hud_colour 0.2))
        ]
    ] (= $darkhud 1) [
        ui_hud_colour1 = (| $ui_hud_transor $ui_dark)
        ui_hud_colour2 = (modcolour $ui_hud_colour 0.75)
        ui_hud_colour3 = (| $ui_hud_transor (modcolour $ui_hud_colour 0.25))
        if (<= $ui_hud_state 1) [
            ui_hud_colour4 = (| $ui_hud_transor (skewcolour 0xFF0000 $ui_darkhi $ui_hud_damagelevel))
        ] [
            ui_hud_colour4 = (| $ui_hud_transor $ui_darkhi)
        ]
    ] (= $darkhud 2) [
		ui_hud_colour1 = (| $ui_hud_transor $ui_dark)
        ui_hud_colour2 = (modcolour $ui_hud_colour 0.2)
        ui_hud_colour3 = (| $ui_hud_transor (modcolour $ui_hud_colour 0.25))
        if (<= $ui_hud_state 1) [
            ui_hud_colour4 = (| $ui_hud_transor (skewcolour 0xFF0000 $ui_darkhi $ui_hud_damagelevel))
        ] [
            ui_hud_colour4 = (| $ui_hud_transor $ui_darkhi)
        ]
    ]
    ui_hud_colourw = (| $ui_hud_transor $colourwhite)
    ui_hud_commandmillis = (getcommandmillis)
    ui_hud_commandopen = (> $ui_hud_commandmillis 0)
    ui_hud_showhud = (&& (! (getprogressing)) (|| $ui_hud_commandopen $showhud))
    ui_hud_sizet = (-f $ui_hud_sizew $ui_screenborder $ui_screenborder)
]

ui_hud_residuals = [
    ui_hud_residualw = (-f (divf (-f $ui_hud_sizet (*f $ui_screenborder 2)) 3) (*f $ui_padtiny 2))
    uihlist 0 [
        uialign -1 0
        if (getclientshocking $ui_hud_focus) [
            uihudtab $ui_hud_residualw 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                uispace $ui_padtiny $ui_padtiny [
                    uiimage "textures/icons/shock" (getclientrescolour $ui_hud_focus $pulseidxshock) 0 $ui_hud_residualw $ui_hud_residualw
                ]
            ]
        ]
        if (getclientbleeding $ui_hud_focus) [
            uihudtab $ui_hud_residualw 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                uispace $ui_padtiny $ui_padtiny [
                    uiimage "textures/icons/bleed" (getclientrescolour $ui_hud_focus $pulseidxbleed) 0 $ui_hud_residualw $ui_hud_residualw
                ]
            ]
        ]
        if (getclientburning $ui_hud_focus) [
            uihudtab $ui_hud_residualw 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                uispace $ui_padtiny $ui_padtiny [
                    uiimage "textures/icons/burn" (getclientrescolour $ui_hud_focus $pulseidxburn) 0 $ui_hud_residualw $ui_hud_residualw
                ]
            ]
        ]
    ]
]

ui_hud_healthbar = [
    uihudskin $ui_hud_sizew (? $ui_hud_state $ui_hud_sizew $ui_hud_bar) $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uivlist 0 [
            uifill $ui_padnormal $ui_padnormal
            uialign 0 1
            ui_hud_health = (getclienthealth $ui_hud_focus)
            if (& (mutators) $mutsbitinstagib) [
                ui_hud_healthtext = "INSTA"
                ui_hud_healthcol1 = $colouryellow
                ui_hud_healthcol2 = $colourred
                ui_hud_healthcol3 = $colourpurple
            ] [
                ui_hud_healthtext = (divf $ui_hud_health $damagedivisor)
                if $damageinteger [ui_hud_healthtext = (toint (ceil $ui_hud_healthtext))]
                ui_hud_healthcol1 = $colourgreen
                ui_hud_healthcol2 = $colouryellow
                ui_hud_healthcol3 = $colourred
            ]
            ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
            ui_hud_regen = (getclientregen $ui_hud_focus)
            ui_hud_healthpart = (divf $ui_hud_health $ui_hud_spawnhealth)
            uigroup [
                uialign 0 1
                uivmeter $ui_hud_healthpart $ui_hud_barw $ui_hud_barh $ui_hud_healthcol1 $ui_hud_colour2 [@ui_hud_healthcol2 @ui_hud_healthcol3] [
                    if [@@ui_hud_regen] [
                        ui_hud_regenlast = (- (getmillis) [@@ui_hud_regen])
                        if (<= $ui_hud_regenlast $regentime) [
                            uicolourrotate (divf $ui_hud_regenlast $regentime)
                        ]
                    ]
                ]
                if (getclientbuffing $ui_hud_focus) [
                    uiimage "textures/icons/buff" (getclientrescolour $ui_hud_focus $pulseidxbuff) 0 $ui_hud_barw $ui_hud_barw [uialign 0 1]
                ]
            ]
            caseif (>f $ui_hud_healthpart 1.0) [
                uitext $ui_hud_healthtext $ui_textlarge [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxbuff); uialign 0 1]
            ] (<=f $ui_hud_healthpart 0.25) [
                uitext $ui_hud_healthtext $ui_textlarge [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
            ] () [uitext $ui_hud_healthtext $ui_textlarge [uitextlimit 1; uialign 0 1]]
        ]
    ]
]

ui_hud_impulsebar = [
    if (&& $impulsemeter (getclientallowimpulse $ui_hud_focus)) [
        uihudskin $ui_hud_sizew (? $ui_hud_state $ui_hud_sizew $ui_hud_bar) $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
            uivlist 0 [
                uifill $ui_padnormal $ui_padnormal
                uialign 0 1
                ui_hud_impulse = (getclientimpulsemeter $ui_hud_focus)
                ui_hud_impulseregen = (getclientimpulselast $ui_hud_focus)
                ui_hud_impulseamt = 100
                ui_hud_impulsepart = 1.0
                if (> $ui_hud_impulse 0) [
                    ui_hud_impulsepart = (clampf (-f 1 (divf $ui_hud_impulse $impulsemeter)) 0.0 1.0)
                    ui_hud_impulseamt = (ceil (*f $ui_hud_impulsepart 100))
                ]
                uigroup [
                    uialign 0 1
                    uivmeter $ui_hud_impulsepart $ui_hud_barw $ui_hud_barh $colourpurple $ui_hud_colour2 [@colourcyan @colourblue] [if [@@ui_hud_impulseregen] [uicolourrotate (divf [@@ui_hud_impulseregen] 1000)]]
                ]
                if (<=f $ui_hud_impulsepart 0.25) [
                    uitext $ui_hud_impulseamt $ui_textlarge [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                ] [uitext $ui_hud_impulseamt $ui_textlarge [uitextlimit 1; uialign 0 1]]
            ]
        ]
    ]
]

ui_hud_leftbar = [
    if (= $ui_hud_state 0) [
        uihlist $ui_padsmall [
            uivlist 0 [
                uialign -1 1
                ui_hud_residuals
                ui_hud_healthbar
            ]
            uivlist 0 [
                uialign -1 1
                ui_hud_impulsebar
            ]
        ]
    ]
]

ui_hud_captureinventory = [
    loopcapture 0 0 ui_hud_flag [
        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
        ui_hud_flagname = (at $teamnames $ui_hud_flagteam)
        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
        ui_hud_flagcol1 = (modcolour $ui_hud_flagcol 0.75)
        //ui_hud_flagcol2 = (modcolour $ui_hud_flagcol 0.5)
        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
        //ui_hud_flaglast = (getcapturelastowner $ui_hud_flag)
        ui_hud_flagpart = 0.0
        if $ui_hud_flagdrop [
            ui_hud_flagoffs = (- (getmillis) $ui_hud_flagdrop)
            ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
        ]
        uivlist 0 [
            uihudtab $ui_hud_sizet $ui_hud_top (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                caseif (>= $ui_hud_flagowner 0) [
                   uispace $ui_padtiny $ui_padtiny [uitext (getclientname $ui_hud_flagowner 2 0) $ui_text [uitextlimit 1; uialign 0 0]]
                ] (>f $ui_hud_flagpart 0) [
                    uihmeter $ui_hud_flagpart $ui_hud_mtrw $ui_hud_mtrh $ui_hud_flagcol1 $ui_hud_colour2 [@ui_hud_flagcol]
                ] () [
                   uispace $ui_padtiny $ui_padtiny [uitext (concatword "^f(" $[team@[ui_hud_flagname]tex] ")" $[team@[ui_hud_flagname]name]) $ui_textsmall [uitextlimit 1; uicolourset $ui_hud_flagcol; uialign 0 0]]
                ]
            ]
            uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_flagowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $flagtex $ui_hud_flagcol 0 $ui_hud_barw $ui_hud_barw [
                        uialign 0 0
                        caseif (>= $ui_hud_flagowner 0) [
                            ui_hud_flagownerteam = (getclientteam $ui_hud_flagowner)
                            ui_hud_flagownername = (at $teamnames $ui_hud_flagownerteam)
                            uiimage $flagtakentex $[team@[ui_hud_flagownername]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]
                        ] (> $ui_hud_flagdrop 0) [
                            uiimage $flagdroptex $colourcyan 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]
                        ] () [uiimage $pointtex $ui_hud_flagcol 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]]
                    ]
                    caseif (>= $ui_hud_flagowner 0) [
                        caseif (= $ui_hud_flagowner $ui_hud_focus) [
                            uitext "HOLDING" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                        ] (= (getclientteam $ui_hud_flagowner) $ui_hud_team) [
                            uitext "RECLAIMED" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                        ] () [
                            uitext "STOLEN" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                        ]
                    ] (> $ui_hud_flagdrop 0) [
                        uitext "DROPPED" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxdisco); uialign 0 1]
                    ] () [uitext "SAFE" $ui_textbig [uitextlimit 1; uicolourset $colourgreen; uialign 0 1]]
                ]
            ]
        ]
    ]
]

ui_hud_defendinventory = [
    loopdefend 0 0 ui_hud_point [
        ui_hud_pointowner = (getdefendowner $ui_hud_point)
        ui_hud_pointname1 = (at $teamnames $ui_hud_pointowner)
        ui_hud_pointenemy = (getdefendenemy $ui_hud_point)
        ui_hud_pointname2 = (at $teamnames $ui_hud_pointenemy)
        ui_hud_pointname = (getdefendname $ui_hud_point)
        ui_hud_pointcol = $[team@[ui_hud_pointname1]colour]
        ui_hud_pointcol1 = (modcolour $ui_hud_pointcol 0.75)
        ui_hud_pointcol2 = (? (&& (= $ui_hud_pointowner $teamidxneutral) (!= $ui_hud_pointenemy $teamidxneutral)) $[team@[ui_hud_pointname2]colour] $[team@[ui_hud_pointname1]colour])
        ui_hud_pointcol3 = (modcolour $ui_hud_pointcol2 0.75)
        //ui_hud_pointcol2 = (modcolour $ui_hud_pointcol 0.5)
        //ui_hud_pointowners = (getdefendowners $ui_hud_point)
        //ui_hud_pointenemies = (getdefendenemies $ui_hud_point)
        //ui_hud_pointconverted = (getdefendconverted $ui_hud_point)
        ui_hud_pointoccupied = (getdefendoccupied $ui_hud_point)
        ui_hud_pointinside = (getdefendinside $ui_hud_point $ui_hud_focus 1)
        ui_hud_pointpart = 0.0
        if $ui_hud_pointenemy [
            ui_hud_pointpart = $ui_hud_pointoccupied
        ]
        uivlist 0 [
            uihudtab $ui_hud_sizet $ui_hud_top (? $ui_hud_pointinside $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                caseif (&& (>f $ui_hud_pointpart 0) (<f $ui_hud_pointpart 1)) [
                    uihmeter $ui_hud_pointpart $ui_hud_mtrw $ui_hud_mtrh $ui_hud_pointcol3 $ui_hud_colour2 [@ui_hud_pointcol2]
                ] $ui_hud_pointowner [
                   uispace $ui_padtiny $ui_padtiny [uitext (concatword "^f(" $[team@[ui_hud_pointname1]tex] ")" $[team@[ui_hud_pointname1]name]) $ui_textsmall [uitextlimit 1; uicolourset $ui_hud_pointcol; uialign 0 0]]
                ] () [
                   uispace $ui_padtiny $ui_padtiny [uitext (concatword "^f(" $pointtex ")" $[team@[ui_hud_pointname1]name]) $ui_textsmall [uitextlimit 1; uicolourset $ui_hud_pointcol; uialign 0 0]]
                ]
            ]
            uihudskin $ui_hud_sizew $ui_hud_sizew (? $ui_hud_pointinside $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                uivlist 0 [
                    uialign 0 -1
                    uiimage $pointtex $ui_hud_pointcol 0 $ui_hud_barw $ui_hud_barw [
                        uialign 0 0
                        caseif $ui_hud_pointenemy [
                            uiimage $attacktex $[team@[ui_hud_pointname2]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]
                        ] $ui_hud_pointowner [
                            uiimage $[team@[ui_hud_pointname1]tex] $[team@[ui_hud_pointname1]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]
                        ]
                        uispace 0 $ui_padtiny [
                            uialign 0 0
                            uitext $ui_hud_pointname $ui_textsmaller [uitextwrap 0.05]
                        ]
                    ]
                    caseif (|| $ui_hud_pointenemy $ui_hud_pointowner) [
                        caseif (&& $ui_hud_pointinside $ui_hud_pointenemy (= $ui_hud_pointenemy $ui_hud_team)) [
                            uitext "ATTACKING" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                        ] (<f $ui_hud_pointoccupied 1) [
                            uitext "SECURING" $ui_textbig [uitextlimit 1; uicolourset (? (= $ui_hud_pointenemy $ui_hud_team) $colourgreen $colouryellow); uialign 0 1]
                        ] () [
                            uitext "SECURED" $ui_textbig [uitextlimit 1; uicolourset (? (= $ui_hud_pointowner $ui_hud_team) $colourgreen $colourorange); uialign 0 1]
                        ]
                    ] () [uitext "OPEN" $ui_textbig [uitextlimit 1; uicolourset $colourcyan; uialign 0 1]]
                ]
            ]
        ]
    ]
]

ui_hud_bomberinventory = [
    loopbomber 0 0 ui_hud_bomb [
        if (&& (getbomberenabled $ui_hud_bomb) (= (getbomberteam $ui_hud_bomb) $teamidxneutral)) [
            //ui_hud_bombteam = (getbomberteam $ui_hud_bomb)
            ui_hud_bombcol = (getclientrescolour $ui_hud_focus $pulseidxdisco)
            ui_hud_bombcol1 = (modcolour $ui_hud_bombcol 0.75)
            //ui_hud_bombcol2 = (modcolour $ui_hud_bombcol 0.5)
            ui_hud_bombdrop = (getbomberdroptime $ui_hud_bomb)
            ui_hud_bombtake = (getbombertaketime $ui_hud_bomb)
            ui_hud_bombowner = (getbomberowner $ui_hud_bomb)
            //ui_hud_bomblast = (getbomberlastowner $ui_hud_bomb)
            //ui_hud_bombtarget = (getbombertarget $ui_hud_bomb)
            ui_hud_bombpart = 0.0
            caseif $ui_hud_bombdrop [
                ui_hud_bomboffs = (- (getmillis) $ui_hud_bombdrop)
                ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bomberresetdelay))
            ] $ui_hud_bombtake [
                ui_hud_bomboffs = (- (getmillis) $ui_hud_bombtake)
                ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bombercarrytime))
            ]
            uivlist 0 [
                uihudtab $ui_hud_sizet $ui_hud_top (? (= $ui_hud_bombowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    caseif (>f $ui_hud_bombpart 0) [
                        uihmeter $ui_hud_bombpart $ui_hud_mtrw $ui_hud_mtrh $ui_hud_bombcol1 $ui_hud_colour2 [@ui_hud_bombcol]
                    ] (>= $ui_hud_bombowner 0) [
                       uispace $ui_padtiny $ui_padtiny [uitext (getclientname $ui_hud_bombowner 2 0) $ui_text [uitextlimit 1; uialign 0 0]]
                    ] () [
                        uispace $ui_padtiny $ui_padtiny [uitext "Bomb" $ui_text [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxdisco); uialign 0 0]]
                    ]
                ]
                uihudskin $ui_hud_sizew $ui_hud_sizew (? (= $ui_hud_bombowner $ui_hud_focus) $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    uivlist 0 [
                        uialign 0 -1
                        uiimage $bombtex $ui_hud_bombcol 0 $ui_hud_barw $ui_hud_barw [
                            uialign 0 0
                            caseif (>= $ui_hud_bombowner 0) [
                                ui_hud_bombownerteam = (getclientteam $ui_hud_bombowner)
                                ui_hud_bombownername = (at $teamnames $ui_hud_bombownerteam)
                                uiimage $bombtakentex $[team@[ui_hud_bombownername]colour] 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]
                            ] (> $ui_hud_bombdrop 0) [
                                uiimage $bombdroptex $colourcyan 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]
                            ] () [uiimage $pointtex $ui_hud_bombcol 0 $ui_hud_bar2 $ui_hud_bar2 [uialign -1 1]]
                        ]
                        caseif (>= $ui_hud_bombowner 0) [
                            caseif (= $ui_hud_bombowner $ui_hud_focus) [
                                uitext "HOLDING" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                            ] (= (getclientteam $ui_hud_bombowner) $ui_hud_team) [
                                uitext "CLAIMED" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                            ] () [
                                uitext "STOLEN" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                            ]
                        ] (> $ui_hud_bombdrop 0) [
                            uitext "DROPPED" $ui_textbig [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxdisco); uialign 0 1]
                        ] () [uitext "READY" $ui_textbig [uitextlimit 1; uicolourset $colourgreen; uialign 0 1]]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_raceinventory = [
    if (|| (= $ui_hud_team $teamidxalpha) (! (& (mutators) $mutsbitgsp3))) [
        ui_hud_racetime = (getclientcptime $ui_hud_focus)
        ui_hud_racemillis = (getclientcpmillis $ui_hud_focus)
        ui_hud_racelast = (getclientcplast $ui_hud_focus)
        ui_hud_racepoints = (getclientpoints $ui_hud_focus)
        uihlist 0 [
            uivlist 0 [
                uihudtab $ui_hud_sizew $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    uispace $ui_padtiny $ui_padtiny [uitext Laps $ui_text [uitextlimit 1; uialign 0 0]]
                ]
                uihudskin $ui_hud_sizew $ui_hud_mtrw $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    uispace $ui_padtiny $ui_padtiny [uitext $ui_hud_racepoints $ui_textlarge [uitextlimit 1; uialign 0 0]]
                ]
            ]
            uivlist 0 [
                uihudskin $ui_buttonzw $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    uispace $ui_padtiny $ui_padtiny [
                        uihlist 0 [
                            uitext "Current: " $ui_text [uitextlimit 1; uialign 0 0]
                            uitext (timestr $ui_hud_racemillis $scoreracestyle) $ui_text [uitextlimit 1; uialign 0 0]
                        ]
                    ]
                ]
                uihudskin $ui_buttonzw $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    uispace $ui_padtiny $ui_padtiny [
                        uihlist 0 [
                            uitext "Best: " $ui_text [uitextlimit 1; uialign 0 0]
                            uitext (timestr $ui_hud_racetime $scoreracestyle) $ui_text [uitextlimit 1; uialign 0 0]
                        ]
                    ]
                ]
                uihudskin $ui_buttonzw $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                    uispace $ui_padtiny $ui_padtiny [
                        uihlist 0 [
                            uitext "Last: " $ui_text [uitextlimit 1; uialign 0 0]
                            uitext (timestr $ui_hud_racelast $scoreracestyle) $ui_text [uitextlimit 1; uialign 0 0]
                        ]
                    ]
                ]
            ]
            if (&& (ischallengemap) (! (isonline)) ) [
                uivlist 0 [
                    uihudtab $ui_hud_sizew $ui_hud_top $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                        uispace $ui_padtiny $ui_padtiny [uitext Level $ui_text [uitextlimit 1; uialign 0 0]]
                    ]
                    uihudskin $ui_hud_sizew $ui_hud_mtrw $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
                        uispace $ui_padtiny $ui_padtiny [uitext (substr $mapname 16) $ui_textlarge [uitextlimit 1; uialign 0 0]]
                    ]
                ]
                if (getclientpoints (getclientnum)) [
                    showui challenges
                ]
            ]
        ]
    ]
]

ui_hud_showstats = [
    refreshenginestats
    uihudskin (*f $ui_hud_sizew 3.75) 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uialign -1 1
        uispace $ui_padsmall $ui_padsmall [
            uialign -1 1
            uivlist 0 [
                uialign -1 1
                uitext (format "^f4pos:^f7%1,%2,%3 ^f4yaw:^f7%4 ^f4pitch:^f7%5" (getenginestat 21) (getenginestat 22) (getenginestat 23) (getenginestat 24) (getenginestat 25)) $ui_textsmaller [uialign -1 1]
                if (= $ui_hud_state 2) [
                    uitext (format "^f4sel:^f7%1,%2,%3 ^f4vol:^f7%4x%5x%6 ^f7(%7) ^f4mat:^f7%8" (getenginestat 27) (getenginestat 28) (getenginestat 29) (getenginestat 30) (getenginestat 31) (getenginestat 32) (concatword (getenginestat 33) "," (getenginestat 34) "," (getenginestat 35) "," (getenginestat 36)) (getenginestat 38)) $ui_textsmaller [uialign -1 1]
                    uitext (format "^f4cube:^f7%1%2 ^f4corner:^f7%3 ^f4orient:^f7%4 ^f4grid:^f7%5" (? (< (getenginestat 37) 0) "1/" "") (abs (getenginestat 37)) (getenginestat 39) (getenginestat 40) (getenginestat 41)) $ui_textsmaller [uialign -1 1]
                ]
                uitext (format "^f4ents(in sel):^f7%1(%2) ^f4wp:^f7%3 ^f4pvs:^f7%4" (getenginestat 14) (getenginestat 15) (getenginestat 16) (getenginestat 17)) $ui_textsmaller [uialign -1 1]
                uitext (format "^f4ond:^f7%1 ^f4va:^f7%2 ^f4gl:^f7%3(%4) ^f4oq:^f7%5" (getenginestat 6) (getenginestat 7) (getenginestat 8) (getenginestat 9) (getenginestat 10)) $ui_textsmaller [uialign -1 1]
                uitext (format "^f4wtr:^f7%1k(%2%%) ^f4wvt:^f7%3k(%4%%) ^f4evt:^f7%5k ^f4eva:^f7%6k" (getenginestat 0) (getenginestat 1) (getenginestat 2) (getenginestat 3) (getenginestat 4) (getenginestat 5)) $ui_textsmaller [uialign -1 1]
            ]
        ]
    ] [uialign -1 1]
]
//texture readouts for editing
ui_hud_texstats = [
    uihudskin (*f $ui_hud_sizew 3.75) 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uialign -1 1
        uispace $ui_padsmall $ui_padsmall [
            uialign -1 1
            uivlist 0 [
                if (> (getseltex) 0) [ //if there is a valid nonsky texture selected, show the info on it
                    uialign -1 1
                    uitext (format "^f4texture:^f7 %1 (#%2)" (gettexname (getseltex)) (getseltex) ) $ui_textsmaller [uialign -1 1]
                    uitext (format "^f4shaders:^f7 %1" (getvshadername (getseltex))) $ui_textsmaller [uialign -1 1]
                    if (> (getvlayer (getseltex)) 0) [
                        uitext (format "^f4vlayer:^f7 %1 (#%2)" (gettexname (getvlayer (getseltex))) (getvlayer (getseltex)) ) $ui_textsmaller [uialign -1 1]
                    ]
                    uitext (format "^f4scale:^f7 %1 ^f4res:^f7 %2 ^f4x ^f7%3" (getvscale (getseltex)) (getvteximgxs (getseltex)) (getvteximgys (getseltex))) $ui_textsmaller [uialign -1 1]
                    uitext (format "^f4colour:^f7 %1 ^f4palette:^f7 %2 " (getvcolour (getseltex)) (getvpalette (getseltex))) $ui_textsmaller [uialign -1 1]
                    uitext (format "^f4offset:^f7 %1 ^f4scroll:^f7 %2 ^f4rotate:^f7 %3" (getvoffset (getseltex)) (getvscroll (getseltex)) (getvrotate (getseltex))) $ui_textsmaller [uialign -1 1]
                    uitext (format "^f4alpha:^f7 %1 ^f4refract:^f7 %2" (getvalpha (getseltex)) (getvrefract (getseltex)) ) $ui_textsmaller [uialign -1 1]
                ] [ //else: display no texture message
                    uihlist $ui_padsmall [
                        uitext (format "^f4No texture selected") $ui_textsmaller
                        if (> (getenginestat 37) 1) [ //enginestat 37 (cubes in sel) to determine whether to announce the problem is that selection contains too many cubes
                            uitext "^f4(multiple cubes in sel)" $ui_textsmaller
                        ] [] //no else
                    ]
                ]
                loop i (listlen $ui_tex_vshaderparam_types) [// if the cursor texture has vshaderparam commands set they will be displayed here (hidden otherwise) (note ui_tex_vshaderparam_types is defined in editing.cfg)
                    if (!=s (getvshaderparam (getseltex) (at $ui_tex_vshaderparam_types $i)) "") [
                        uitextleft (format "^f4%1:^f7 %2" (at $ui_tex_vshaderparam_types $i) (getvshaderparam (getseltex) (at $ui_tex_vshaderparam_types $i)) ) $ui_textsmaller
                    ] [] //no else
                ]
            ]
        ] [uialign -1 1]
    ]
]

ui_hud_edittoggles = [
    uihlist 0 [
        uialign -1 1
        uivlist 0 [
            looplist ui_hud_edittoggles_index ["allfaces" "blankgeom" "dtoutline" "entediting" "entselsnap" "fullbright" "outline" "showmat" "wireframe"  ] [
                uitext (format "^f4%1 " $ui_hud_edittoggles_index) $ui_textsmaller [uialign -1 1]
            ]
        ]
        uivlist 0 [
            looplist ui_hud_edittoggles_index ["allfaces" "blankgeom" "dtoutline" "entediting" "entselsnap" "fullbright" "outline" "showmat" "wireframe"  ] [
                do [
                    if $@ui_hud_edittoggles_index [
                        uitext "^f9ON" $ui_textsmaller [uialign 1 1]
                    ] [
                        uitext "^f6OFF" $ui_textsmaller [uialign 1 1]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_leftinventory = [
    uivlist $ui_padtiny [
        uialign -1 1
        if (> $showstats (? (= (gamemode) $modeidxediting) 0 1)) [
            if (= $ui_hud_state 2) [
                if $showedittoggles [
                    ui_hud_edittoggles
                    uialign -1 1
                ]
                if $showtexstats [
                    uihlist 0 [
                        ui_hud_texstats
                        uialign -1 1
                    ]
                ]
            ] []
            uihlist 0 [
                ui_hud_showstats
                uialign -1 1
            ]
        ]
        uihlist $ui_padtiny [
            uialign -1 1
            case (gamemode) $modeidxcapture [
                ui_hud_captureinventory
            ] $modeidxdefend [
                ui_hud_defendinventory
            ] $modeidxbomber [
                ui_hud_bomberinventory
            ] $modeidxrace [
                if (= $ui_hud_state 0) [
                    ui_hud_raceinventory
                ]
            ]
        ]
    ]
]

ui_hud_showfps = [
    if $showfps [
        uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
            uispace $ui_padtiny $ui_padtiny [
                uivlist 0 [
                    doif (>= $showfps 1) [ //show fps regardless of setting (as long as fps readout enabled)
                        uitext (concat (getenginestat 11) "fps") $ui_text [uitextlimit 1]
                    ] (= $showfps 2) [//show +[maximum]-[minimum] below
                        uitext (concatword "+" (getenginestat 12) "-" (getenginestat 13)) $ui_textsmall [uitextlimit 1]
                    ] (= $showfps 3) [//show range: (avg-worst) "-" (avg+best)
                        uitext (concatword (- (getenginestat 11) (getenginestat 13)) "-" (+ (getenginestat 11) (getenginestat 12)) ) $ui_textsmall [uitextlimit 1]
                    ] (= $showfps 4) [//show frametimes: average and worst
                        uitext (concatword "Avg: " (div 1000 (getenginestat 11)) "ms" ) $ui_textsmaller
                        uitext (concatword "Max: " (div 1000 (- (getenginestat 11) (getenginestat 13)) ) "ms" ) $ui_textsmaller
                    ]
                ]
            ]
        ] [uialign 1 1]
    ]
]

ui_hud_timers = [
    ui_hud_timers_type = (updatetimers)
    if $ui_hud_timers_type [
        uihudskin 0 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
            uispace $ui_padtiny $ui_padtiny [
                uivlist 0 [
                    if $frametimer [
                        uispace $ui_padtiny $ui_padtiny [
                            uivlist 0 [
                                uitext "Frame Time" $ui_texttiny [uicolourset $colouryellow]
                                uitext (concat (gettimer -1 1) "ms") $ui_texttiny [uitextlimit 1]
                            ]
                        ]
                    ]
                    if $timer [
                        ui_hud_timers_count = (gettimer -1 -1)
                        loop i $ui_hud_timers_count [
                            if (gettimer $i -2) [] [
                                uispace $ui_padnormal $ui_padtiny [
                                    uivlist 0 [
                                        uitext (gettimer $i 0) $ui_texttiny [uicolourset $colouryellow]
                                        uitext (concat (precf (gettimer $i 6) 2) "ms" (? (gettimer $i 1) "(gpu)" "(cpu)")) $ui_texttiny
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_weight = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                uitext "WEIGHT" $ui_text [uitextlimit 1; uicolourset $colouryellow]
                uitext (getclientweight $ui_hud_focus) $ui_textlarge [uitextlimit 1]
            ]
        ]
    ] [uialign 1 1]
]

ui_hud_velocity = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                uitext "SPEED" $ui_text [uitextlimit 1; uicolourset $colourgreen]
                uitext (getenginestat 20) $ui_textlarge [uitextlimit 1]
            ]
        ]
    ] [uialign 1 1]
]

ui_hud_movecount = [
    uihudskin $ui_hud_sizew 0 $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                uitext "MOVES" $ui_text [uitextlimit 1; uicolourset $colourcyan]
                if (= $impulsestyle 3) [
                    uitext "8" $ui_textlarge [
                        uitextrotate 1
                        uitextlimit 1
                    ]
                ] [
                    ui_hud_moves = (- $impulsecount (getclientimpulsecount $ui_hud_focus))
                    uitext $ui_hud_moves $ui_textlarge [
                        uitextlimit 1
                        if (<= $ui_hud_moves 1) [uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn)]
                    ]
                ]
            ]
        ]
    ] [uialign 1 1]
]

ui_hud_rightbar = [
    uivlist $ui_padsmall [
        uialign 1 1
        uihlist $ui_padtiny [
            uialign 1 1
            ui_hud_showfps
        ]
        if (= $ui_hud_state 0) [
            uihlist $ui_padtiny [
                uialign 1 1
                if $showmovecount [uivlist 0 [ui_hud_movecount]]
            ]
            uihlist $ui_padtiny [
                uialign 1 1
                if $showweight [uivlist 0 [ui_hud_weight]]
                if $showvelocity [uivlist 0 [ui_hud_velocity]]
            ]
        ]
        if $showminimap [ui_hud_minimap]
    ]
]

ui_hud_weapdisplay = [
    ui_hud_weapidx = (at $weapname $arg1)
    ui_hud_weapstate = (getclientweapstate $ui_hud_focus $arg1)
    ui_hud_weapammo = (getclientweapammo $ui_hud_focus $arg1)
    ui_hud_weapstore = (getclientweapstore $ui_hud_focus $arg1)
    ui_hud_weapmax = (+ $[@[ui_hud_weapidx]ammostore] $[@[ui_hud_weapidx]ammoclip])
    ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
    ui_hud_weapcol1 = (modcolour $ui_hud_weapcol 0.75)
    ui_hud_weapcol2 = (modcolour $ui_hud_weapcol 0.5)
    ui_hud_weappart = 1.0
    ui_hud_weappart = (divf (+ $ui_hud_weapstore $ui_hud_weapammo) $ui_hud_weapmax)
    uivlist 0 [
        uihudtab $ui_hud_sizet $ui_hud_top (? $arg2 $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
            uihmeter $ui_hud_weappart $ui_hud_mtrw $ui_hud_mtrh $ui_hud_weapcol2 $ui_hud_colour2 [@ui_hud_weapcol1]
        ]
        uihudskin $ui_hud_sizew $ui_hud_sizew (? $arg2 $ui_hud_colour3 $ui_hud_colour1) $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
            uivlist 0 [
                uialign 0 -1
                uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 $ui_hud_barw $ui_hud_barw [uialign 0 0]
                caseif (& (= $[@[ui_hud_weapidx]ammosub1] 0) (= $[@[ui_hud_weapidx]ammosub2] 0)) [ //if ammosub is 0 for both primary/secondary, display inf ammo
                    uitext "8" $ui_text [uitextlimit 1; uialign 0 1; uitextrotate 1]
                ] (& (<=f $ui_hud_weappart 0.25) (!= $[@[ui_hud_weapidx]ammostore] 0) ) [ //if ammo is below 25% max and weapon has limited ammo (ammostore != 0) pulse warning colors
                    uitext (concatword $ui_hud_weapammo "/" $ui_hud_weapstore) $ui_text [uitextlimit 1; uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn); uialign 0 1]
                ] () [
                    if ( || (= $[@[ui_hud_weapidx]ammoitem] 1) (!= $[@[ui_hud_weapidx]ammostore] 0)) [ //if weapon has ammocap or only picks up 1 ammo per pickup (rocket, grenade, mine), display ammo in gun + ammo in storage
                        uitext (concatword $ui_hud_weapammo "/" $ui_hud_weapstore) $ui_text [uitextlimit 1; uialign 0 1]
                    ] [ //else display ammo in gun and infinity for ammo in storage
                        uihlist -.007 [ //magic number to help align rotated "8"
                            uitextright (concatword $ui_hud_weapammo "/") $ui_text [uitextlimit 1; uialign 0 1]
                            uitextleft "8" $ui_textbig [uitextlimit 1; uialign 0 0; uitextrotate 1]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_weapmini = [
    ui_hud_weapidx = (at $weapname $arg1)
    ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
    ui_hud_weapsize = (divf (-f $ui_hud_sizew (*f $ui_padtiny 6)) 3)
    uispace $ui_padtiny $ui_padtiny [
        uihudskin $ui_hud_weapsize $ui_hud_weapsize $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
            uialign 1 1
            uiclamp 1 1 1 1
            uivlist 0 [
                uialign 0 0
                uiclamp 1 1 1 1
                uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 0 0 [uialign 0 0; uiclamp 1 1 1 1]
            ]
        ] [uialign 1 1]
    ]
]

ui_hud_weaponinventory = [
    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
    if $weaponhud [
        ui_hud_weapons = ""
        loopinventoryrev $ui_hud_focus 0 0 ui_hud_weapon [
            if (!= $ui_hud_weapon $ui_hud_weapselect) [if (!=s $ui_hud_weapons "") [ui_hud_weapons = (concat $ui_hud_weapons $ui_hud_weapon)] [ui_hud_weapons = $ui_hud_weapon]]
        ]
        ui_hud_numweaps = (listlen $ui_hud_weapons)
        ui_hud_weaplist = (ceil (divf $ui_hud_numweaps 3))
        ui_hud_weapmod = (mod $ui_hud_numweaps 3)
        ui_hud_weapiter = 0
        if (= $ui_hud_weapmod 0) [ui_hud_weapmod = 3]
        uihlist 0 [
            uialign 1 1
            loop i $ui_hud_weaplist [
                k = (min $ui_hud_numweaps $ui_hud_weapmod)
                uivlist 0 [
                    loop j $k [
                        ui_hud_weapon = (at $ui_hud_weapons $ui_hud_weapiter)
                        ui_hud_weapiter = (+ $ui_hud_weapiter 1)
                        uialign 1 1
                        ui_hud_weapmini $ui_hud_weapon
                    ]
                ]
                ui_hud_weapmod = 3
            ]
        ]
        ui_hud_weapdisplay $ui_hud_weapselect
    ] [
        loopinventoryrev $ui_hud_focus 0 0 ui_hud_weapon [ui_hud_weapdisplay $ui_hud_weapon (= $ui_hud_weapselect $ui_hud_weapon)]
    ]
]

ui_hud_rightinventory = [
    if (= $ui_hud_state 0) [ui_hud_weaponinventory]
]

ui_hud_statebar = [
    ui_hud_statename = (at $ui_hud_statenames $arg1)
    ui_hud_statetex = $[@(at $ui_hud_statetexs $arg1)]
    ui_hud_statecolour = $ui_default
    if (= $arg1 0) [
        if (= $ui_hud_team $teamidxneutral) [ui_hud_statetex = $playertex] [ui_hud_statetex = $[team@(at $teamnames $ui_hud_team)tex]]
        ui_hud_statename = $[team@(at $teamnames $ui_hud_team)name]
        ui_hud_statecolour = $[team@(at $teamnames $ui_hud_team)colour]
    ]
    uihudskin $ui_hud_sizew $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uispace 0 $ui_padtiny [
            uialign 0 -1
            uiimage $ui_hud_statetex $ui_hud_statecolour 0 $ui_hud_barw $ui_hud_barw [uialign 0 -1]
        ]
        uitext $ui_hud_statename $ui_textbig [uitextlimit 1; uicolourset $ui_hud_statecolour; uialign 0 1]
    ]
]

ui_hud_timestate = [
    uihudskin $ui_hud_sizew $ui_hud_sizew $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uispace $ui_padtiny $ui_padtiny [
            uivlist 0 [
                caseif $gamepaused [
                    uitext Paused $ui_textlarge [uitextlimit 1; uicolourset $colourorange; uialign 0 0]
                ] (= (gamemode) $modeidxediting) [
                    uitext Editing $ui_textlarge [uitextlimit 1; uicolourset $colourgreen; uialign 0 0]
                    uitext (? (= $mapvariant 2) "Alt" "Normal") $ui_text [uitextlimit 1; uicolourset $colouryellow; uialign 0 0]
                ] () [
                    ui_hud_timeremain = (getgametimeremain)
                    ui_hud_timecolour = (at $ui_hud_timecols (getgamestate))
                    if (getgametimelimit) [
                        uitext (getgamestatestr 1) $ui_text [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]; uialign 0 0]
                        uitext (timestr $ui_hud_timeremain $showtimestyle) $ui_textlarge [uitextlimit 1; uicolourset (? (|| (!= (getgamestate) 5) (< $ui_hud_timeremain 60000)) (getclientrescolour $ui_hud_focus $pulseidxwarn) $colourwhite); uialign 0 0]
                    ] [
                        uitext (getgamestatestr 1) $ui_textlarge [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]; uialign 0 0]
                    ]
                ]
                if ($showrealtime) [
                    uitext (substr (gettime) 11 5)
                ]
            ]
        ]
    ] [uialign 1 -1]
]



//used by ui_hud_showscore
ui_hud_scoregroupdifferential = [
    if (= (getclientscoregroup $ui_hud_focus) 0) [
        result (concatword "+" (- (getscoretotal (getclientscoregroup $ui_hud_focus)) (getscoretotal 1)) )
    ] [
        result (- (getscoretotal (getclientscoregroup $ui_hud_focus)) (getscoretotal 0))
    ]
]

//used by ui_hud_showscore
ui_hud_scoreplayerdifferential = [
    if (= $mutsbitffa (& $mutsbitffa (mutators))) [
        if (= (getclientrank $ui_hud_focus) 1) [ //if in first place
            result (concatword " (^f7+" (- (getclientpoints $ui_hud_focus) (getclientpoints (getrankclientffa 2))) "^f4)" )
        ] [ //else if behind (not 1st place)
            result (concatword " (^f7" (- (getclientpoints $ui_hud_focus) (getclientpoints (getrankclientffa 1)) ) "^f4)" )
        ]
    ] [
        result ""
    ]
]

//topleft display of player's score, kills and deaths; includes team ranking and points if applicable
ui_hud_showscore = [
    refreshscoreboard
    uivlist 0 [//                                                           %1  rank                                                                                                                %2  #name                     %3  #points                     %4  "point(s)"                                      %5 pointsdiff                    ; %6 #kills                    %7 "kill(s)"                                    , %8  #deaths                     %9 "death(s)"
        uitextleft (format "%1%2:^f7 %3^f4 %4%5;^f7 %6^f4 %7,^f7 %8^f4 %9" (if (= $mutsbitffa (& $mutsbitffa (mutators))) [concatword (ordinalize (getclientrank $ui_hud_focus)) " "] [result ""]) (getclientname $ui_hud_focus) (getclientpoints $ui_hud_focus) (pluralize (getclientpoints $ui_hud_focus) "point") (ui_hud_scoreplayerdifferential) (getclientfrags $ui_hud_focus) (pluralize (getclientfrags $ui_hud_focus) "kill") (getclientdeaths $ui_hud_focus) (pluralize (getclientdeaths $ui_hud_focus) "death") )
        if (!= $mutsbitffa (& $mutsbitffa (mutators))) [ //if not ffa display team readout below
            uitextleft (format "%1 %2^f4: ^f7%3^f4 points (^f7%4^f4)" (ordinalize (+ 1 (getclientscoregroup $ui_hud_focus))) (concatword "^f[" (getteamcolour (getclientteam $ui_hud_focus)) "]" (getteamname (getclientteam $ui_hud_focus))) (getscoretotal (getclientscoregroup $ui_hud_focus)) (ui_hud_scoregroupdifferential) )
        ]
        uialign -1 1
    ]
]

// 1:dist 2:offset 3:border 4:w 5:h 6:blip 7:smallblip 8:playernames 9:distblend 10:compass
ui_hud_compassdirs = [0 90 180 270]
ui_hud_compassnames = [N E S W]
ui_hud_radar = [
    uiradar $arg1 $arg2 $arg3 $arg4 $arg5 [
        if $arg10 [
            loop ui_hud_compass 4 [
                ui_hud_compassdir = (getcameraoffyaw (at $ui_hud_compassdirs $ui_hud_compass))
                ui_hud_compassname = (at $ui_hud_compassnames $ui_hud_compass)
                uiradarblip $bliptex 0xFF000000 $ui_hud_compassdir $ui_hud_compassdir $arg1 (*f $arg6 1.2) (*f $arg6 1.2) [
                    uivlist 0 [uitext $ui_hud_compassname $ui_textradar]
                ]
            ]
        ]
        // only do the clients loop if it is allowed by mode (disabled in hard e.g.)
        loopclientsif 0 0 ui_hud_radarclient [&& (getclientradarallow $ui_hud_radarclient)] [ //only loop through the clients which are validly displayable
            //get some info about the client
            ui_hud_radarclientdir = (getclientradardir $ui_hud_radarclient)
            ui_hud_radarclientyaw = (getclientradaryaw $ui_hud_radarclient)
            ui_hud_radarclientvel = (getclientvelocity $ui_hud_radarclient)
            ui_hud_radarclientdist = (getclientradardist $ui_hud_radarclient)
            ui_hud_radarclientstate = (getclientstate $ui_hud_radarclient)
            ui_hud_radarclientdominated = (&& (= $ui_hud_radarclientstate 0) (getclientisdominated $ui_hud_radarclient $ui_hud_focus))
            ui_hud_radarclientpcolour = (getclientcolour $ui_hud_radarclient -1 2)
            //first, take care of dominated players, which get special rules for radar visibility
            if $ui_hud_radarclientdominated [
                ui_hud_radarclientcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                uiradarblip $playerbliptex (| 0x80000000 $ui_hud_radarclientpcolour) $ui_hud_radarclientdir $ui_hud_radarclientyaw $ui_hud_radarclientdist $arg6 $arg6 [
                    uiimage $dominatedtex $ui_hud_radarclientcolour 0 $arg6 $arg6
                    if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset $ui_hud_radarclientcolour]]]
                ]
            ] [ //now do normal players
                ui_hud_radarclientblend = (? (= $ui_hud_radarclientstate 0) 255 160)
                if $ui_hud_radarclientblend [ //check if radar is already blended out
                    if (>f $arg9 0) [
                        if (>f $ui_hud_radarclientdist $arg9) [ //set blend to zero if client is beyond the radar range (so it doesn't get rendered)
                            ui_hud_radarclientblend = 0
                        ] [
                            ui_hud_radarclientblend = (clamp (*f $ui_hud_radarclientblend (-f 1 (divf $ui_hud_radarclientdist $arg9))) 1 255) //blend by distance for those not culled
                        ]
                    ]
                    if $ui_hud_radarclientblend [ //if the client is set to 0, don't bother rendering
                        //get the client's name/team colours since it's actually going to get rendered
                        ui_hud_radarclientteam = (getclientteam $ui_hud_radarclient)
                        ui_hud_radarclienttcolour = $[team@(at $teamnames $ui_hud_radarclientteam)colour]
                        ui_hud_radarclientblend2 = (clamp (*f $ui_hud_radarclientblend 0.75) 1 255)
                        //add effects for players affected by residuals (burn, bleed, shock, buff)
                        doif (getclientburning $ui_hud_radarclient) [
                            ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxburn)
                        ] (getclientbleeding $ui_hud_radarclient) [
                            ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxbleed)
                        ] (getclientshocking $ui_hud_radarclient) [
                            ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxshock)
                        ] (getclientbuffing $ui_hud_radarclient) [
                            ui_hud_radarclientpcolour = (getclientrescolour $ui_hud_radarclient $pulseidxbuff)
                        ]
                        uiradarblip $[@(at $ui_hud_statetex2 $ui_hud_radarclientstate)] (? (= $ui_hud_radarclientstate 0) (| (<< $ui_hud_radarclientblend2 24) $ui_hud_radarclientpcolour) (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)) $ui_hud_radarclientdir (? (= $ui_hud_radarclientstate 0) $ui_hud_radarclientyaw $ui_hud_radarclientdir) $ui_hud_radarclientdist $arg6 $arg6 [
                            if (= $ui_hud_radarclientstate 0) [uiimage $bliptex (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour) 0 $arg7 $arg7]
                            if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)]]]
                        ]
                    ]
                ]
            ]
        ]
        //gamemode specific radar objects (points, bombs, flags, etc.)

        //capture the flag
        case (gamemode) $modeidxcapture [
            loopcaptureif 0 0 ui_hud_radarcapture [getcaptureradarallow $ui_hud_radarcapture] [
                ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture)
                ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture)
                ui_hud_radarcaptureteam = (getcaptureteam $ui_hud_radarcapture)
                ui_hud_radarcapturetcolour = $[team@(at $teamnames $ui_hud_radarcaptureteam)colour]
                ui_hud_radarcaptureowner = (getcaptureowner $ui_hud_radarcapture)
                ui_hud_radarcapturedrop = (getcapturedroptime $ui_hud_radarcapture)
                ui_hud_radarcapturetex = ""
                doif (&& $ui_hud_carryaffinity (= $ui_hud_radarcaptureteam $ui_hud_team)) [
                    if (|| (< $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureowner $ui_hud_focus)) [
                        ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture 1)
                        ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture 1)
                    ]
                    ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                    ui_hud_radarcapturetex = $arrowtex
                ] (|| $ui_hud_radarcapturedrop (&& (>= $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureteam $ui_hud_team) (!= $ui_hud_radarcaptureowner $ui_hud_focus))) [
                    ui_hud_radarcapturetex = $flagtex
                    ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                ] (!= $ui_hud_radarcaptureowner $ui_hud_focus) [
                    ui_hud_radarcapturetex = $flagtex
                ]
                //if the flag met any of the above conditions, render it
                if (!=s $ui_hud_radarcapturetex "") [
                    uiradarblip $ui_hud_radarcapturetex $ui_hud_radarcapturetcolour $ui_hud_radarcapturedir $ui_hud_radarcapturedir $ui_hud_radarcapturedist (*f $arg6 1.65) (*f $arg6 1.65) [
                        if (>= $ui_hud_radarcaptureowner 0) [uialign 0 1]
                        uivlist 0 [uispace 0 (*f $arg7 1.5); uitext $[team@(at $teamnames $ui_hud_radarcaptureteam)name] $ui_textradar [uicolourset (| $ui_transor $ui_hud_radarcapturetcolour)]]
                    ]
                ]
            ]
        //defend and capture
        ] $modeidxdefend [
            loopdefendif 0 0 ui_hud_radardefend [getdefendradarallow $ui_hud_radardefend] [
                //get the info about the relevant affinity
                ui_hud_radardefenddir = (getdefendradardir $ui_hud_radardefend)
                ui_hud_radardefenddist = (getdefendradardist $ui_hud_radardefend)
                ui_hud_radardefendteam = (getdefendowner $ui_hud_radardefend)
                ui_hud_radardefendtcolour = $[team@(at $teamnames $ui_hud_radardefendteam)colour]
                ui_hud_radardefendname = (getdefendname $ui_hud_radardefend)
                //render the dac affinities
                uiradarblip $pointtex $ui_hud_radardefendtcolour $ui_hud_radardefenddir $ui_hud_radardefenddir $ui_hud_radardefenddist (*f $arg6 1.65) (*f $arg6 1.65) [
                    uivlist 0 [uispace 0 (*f $arg7 2); uitext $ui_hud_radardefendname $ui_textradar [uialign 0 1; uitextwrap 0.04; uicolourset (| $ui_transor $ui_hud_radardefendtcolour)]]
                ]
            ]
        //bomber-ball
        ] $modeidxbomber [
            loopbomberif 0 0 ui_hud_radarbomber [getbomberradarallow $ui_hud_radarbomber] [
                ui_hud_radarbomberdir = (getbomberradardir $ui_hud_radarbomber)
                ui_hud_radarbomberteam = (getbomberteam $ui_hud_radarbomber)
                ui_hud_radarbomberowner = (getbomberowner $ui_hud_radarbomber)
                ui_hud_radarbombertex = ""
                ui_hud_radarbombername = ""
                doif (!= $ui_hud_radarbomberteam $teamidxneutral) [ //if in team-bb, point out the enemy base to kill
                    if (&& $ui_hud_carryaffinity (!= $ui_hud_radarbomberteam $ui_hud_team)) [
                        ui_hud_radarbombertex = $arrowtex
                        ui_hud_radarbombername = "Destroy"
                    ]
                ] (&& (= $ui_hud_radarbomberteam $teamidxneutral) (!= $ui_hud_radarbomberowner $ui_hud_focus)) [ //if in hold, point out the bomb
                    ui_hud_radarbombertex = $bombtex
                    ui_hud_radarbombername = "Bomb"
                ]
                //if the bomber affinity met the above conditions, render it
                if (!=s $ui_hud_radarbombertex "") [
                    ui_hud_radarbomberdist = (getbomberradardist $ui_hud_radarbomber)
                    uiradarblip $ui_hud_radarbombertex (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_hud_radarbomberdir $ui_hud_radarbomberdir $ui_hud_radarbomberdist (*f $arg6 1.65) (*f $arg6 1.65) [
                        if (>= $ui_hud_radarbomberowner 0) [uialign 0 1]
                        if (!=s $ui_hud_radarbombername "") [uivlist 0 [uispace 0 (*f $arg7 1.5); uitext $ui_hud_radarbombername $ui_textradar [uicolourset (| $ui_transor (getclientrescolour $ui_hud_focus $pulseidxdisco))]]]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_minimap = [
    uihudskin $ui_hud_sizeh $ui_hud_sizeh $ui_hud_colour1 $ui_hud_colour2 $ui_hud_colour4 $ui_hud_damagecrit [
        uispace $ui_padtiny $ui_padtiny [
            uiminimap $radarskintex $ui_hud_colour $minimapdist 0.1 $ui_hud_sizeh $ui_hud_sizeh [
                ui_hud_radar $minimapdist (*f (divf 1 $ui_hud_sizeh) $minimapoffset) $minimapborder $ui_hud_sizeh $ui_hud_sizeh $minimapblipsize $minimapsmallblipsize $minimapplayernames $minimapdistblend $minimapcompass
            ]
        ]
    ] [uialign 1 1]
]

ui_hud_midradar = [
    if (= $ui_hud_state 0) [
        ui_hud_radar $radardist (*f (divf 1 $radarsize) (? $radaraspect (*f $radaroffset (uiaspect)) $radaroffset)) 0 (? $radaraspect (*f $radarsize (uiaspect)) $radarsize) $radarsize $radarblipsize $radarsmallblipsize $radarplayernames $radardistblend 0
    ]
]

ui_hud_righttop = [
    uivlist $ui_padsmall [
        if (!= $ui_hud_focus $ui_hud_clientnum) [ui_hud_statebar (getclientstate $ui_hud_clientnum)] [ui_hud_statebar $ui_hud_state]
        ui_hud_timestate
    ]
]

ui_hud_lefttop = [
    uivlist $ui_padsmall [
        if (&& (!= (gamemode) $modeidxediting) (!= (gamemode) $modeidxrace) (= $ui_hud_state 0)) [ //$modeidxrace = 6, $modeidxediting = 1
            ui_hud_showscore
        ]
        if $showconsole [ui_hud_showconsole]
    ]
]

ui_hud_progress = [
    ui_hud_progresstype = (getprogresstype)
    ui_hud_loadtitle = (getprogresstitle)
    if $ui_hud_progresstype [
        if (! (getprogresswait)) [uiexclusive 1]
        uiblurskin 0.3 0 $ui_menu_a [
            uispace $ui_padwin $ui_padwin [
                uivlist 0 [
                    if (getloadedmap) [
                        uitext (getmaptitle $mapname) $ui_textlarge
                        if (!=s (getmapauthor $mapname) "") [
                            uitext (concat "by" (getmapauthor $mapname)) $ui_textbig
                        ]
                        uispace 0 $ui_padbutton
                        uitext (format "^fs^fy%1^fS" (gamename (gamemode) (mutators) 0 32)) $ui_text
                        if (= $botabilities 1024) [
                            uitext "^f4 Sandbox Mode" $ui_text
                            uifill 0 $ui_padsmall
                        ]
                        ui_loading_modetexs = (modetexlist (gamemode) (mutators))
                        looplist ui_loading_modetex $ui_loading_modetexs [uiimage $ui_loading_modetex $ui_default 0 $ui_imgbut $ui_imgbut]
                        uitext (modedesc (gamemode) (mutators) 5) $ui_textstatus
                        if (> (mutators) 0) [
                            loop i $mutsidxnum [
                                ui_loading_mut = (<< 1 $i)
                                if (&& [& (mutators) $ui_loading_mut] [! (& (mutsimplied (gamemode)) $ui_loading_mut)]) [
                                    ui_loading_muttxt = (mutsdesc (gamemode) $ui_loading_mut 4)
                                    if (!=s $ui_loading_muttxt "") [uitext (concatword "^fa" $ui_loading_muttxt) $ui_textstatus]
                                ]
                            ]
                        ]
                    ] [
                        uitext (? (!=s $ui_hud_loadtitle "") $ui_hud_loadtitle "Loading..") $ui_textbig
                    ]
                    if (isconnected) [
                        uispace 0 $ui_padbutton
                        if (!=s $connectname "") [
                            uitext (format "^faOn: ^fw%1:[%2]" $connectname $connectport) $ui_textstatus
                            if (!=s $serverdesc "") [uitext (format "^"^fs%1^fS^"" $serverdesc) $ui_textstatus]
                        ] [uitext (format "^faPlaying Offline" $connectname $connectport) $ui_textstatus]
                    ]
                ]
            ]
        ]
        uispace $ui_padwin $ui_padwin [
            uialign 0 1
            uioffset 0 -0.4 [
                uiblurskin 0 0 $ui_menu_a [
                    uispace $ui_padwin $ui_padnormal [uitext (tipshow) $ui_text]
                ]
            ]
        ]
    ] [uiexclusive 0]
    if (!=s $ui_hud_loadtitle "") [
        uispace $ui_padwin $ui_padwin [
            uialign -1 1
            uihlist 0 [
                uialign -1 1
                uiblurskin 0 0 $ui_menu_a [
                    uispace $ui_padsmall $ui_padsmall [
                        if (>f $progressamt 0) [
                            uihmeter $progressamt $ui_buttonzs $ui_buttonzz $colourdarkred $ui_disa [@colourred]
                        ] [
                            uihthrob (divf (mod (getmillis -1) 1000) 1000) $ui_buttonzs $ui_buttonzz $colourdarkred $ui_disa [@colourred]
                        ]
                    ]
                ]
                uispace $ui_padsmall
                uitext $ui_hud_loadtitle $ui_text [uitextalign -1; uialign -1 0]
            ]
        ]
    ]
    uispace $ui_padwin $ui_padwin [
        uialign -1 -1
        uihlist 0 [
            uialign -1 -1
            uihlist $ui_padtiny [
                uialign -1 -1
                ui_hud_lefttop
            ]
        ]
    ]
]

ui_hud_headsup = [
    uiexclusive $ui_hud_commandopen
    if (isconnected) [
        uispace $ui_padwin $ui_padwin [
            uialign 1 -1
            uihlist 0 [
                uialign 1 -1
                uihlist $ui_padtiny [
                    uialign 1 -1
                    ui_hud_righttop
                ]
            ]
        ]
        uispace $ui_padwin $ui_padwin [
            uialign -1 1
            uihlist $ui_padsmall [
                uialign -1 1
                uivlist $ui_padtiny [
                    uialign -1 1
                    ui_hud_leftbar
                ]
                uihlist $ui_padtiny [
                    uialign -1 1
                    ui_hud_leftinventory
                ]
            ]
        ]
        uispace $ui_padwin $ui_padwin [
            uialign 1 1
            uihlist $ui_padsmall [
                uialign 1 1
                uihlist $ui_padtiny [
                    uialign 1 1
                    ui_hud_rightinventory
                ]
                uivlist $ui_padtiny [
                    uialign 1 1
                    ui_hud_rightbar
                ]
                uivlist $ui_padtiny [
                    uialign 1 1
                    ui_hud_timers
                ]
            ]
        ]
        uispace $ui_padtiny $ui_padtiny [
            ui_hud_midradar
        ]
    ]
    uispace $ui_padwin $ui_padwin [
        uialign -1 -1
        uihlist 0 [
            uialign -1 -1
            uihlist $ui_padtiny [
                uialign -1 -1
                ui_hud_lefttop
            ]
        ]
    ]
]

newui "hud" [
    ui_hud_init
    uiallowinput 0
    uiclamp 1 1 1 1
    caseif (getprogressing) [
        ui_hud_progress
    ] $ui_hud_showhud [
        ui_hud_headsup
    ] [uiexclusive 0]
] [] [] 16
