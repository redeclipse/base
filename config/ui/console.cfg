defvar showconsole 0 1 2
toggleconsole = [ showconsole (mod (+ $showconsole 1) 3) ]

defvarp conopentime 0 150 $varidxmax
deffvarp conopenblend 0 1 1
deffvarp conopenbgblend 0 0.95 1

defvarp condebugintime 0 250 $varidxmax
defvarp condebugouttime 0 10000 $varidxmax
defvarp condebugfadetime 0 500 $varidxmax
defvarp condebuglinesamt 0 5 $varidxmax
deffvarp condebugblend 0 1 1

defvarp condebugopenamt 0 15 $varidxmax
defvarp condebugopenlines 0 100 $varidxmax

defvarp coneventintime 0 250 $varidxmax
defvarp coneventouttime 0 10000 $varidxmax
defvarp coneventfadetime 0 500 $varidxmax
defvarp coneventlinesamt 0 0 $varidxmax
deffvarp coneventblend 0 1 1

defvarp coneventopenamt 0 5 $varidxmax
defvarp coneventopenlines 0 100 $varidxmax

// 1:count 2:outtime 3:fadetime 4:intime 5:blend 6:size 7:textsize
ui_console_draw = [
    local console_blend console_colour console_time console_millis console_growth console_ctime
    loopcondebugrev (- $arg1) 0 console_line [
        console_blend = $arg5
        console_colour = (getcondebugcolour $console_line)
        console_time = (getcondebugreftime $console_line)
        console_millis = (- $totalmillis $console_time)
        console_growth = 1.0
        caseif (&& $arg2 (> $console_millis $arg2)) [
            console_ctime = (- $console_millis $arg2)
            if (&& $arg3 (< $console_ctime $arg3)) [
                console_growth = (-f 1 (divf $console_ctime $arg3))
                console_blend = (*f $console_growth $arg5)
            ] [
                console_blend = 0.0
                console_growth = 0.0
            ]
        ] (&& $arg4 (< $console_millis $arg4)) [
            console_growth = (divf $console_millis $arg4)
            console_blend = (*f $console_growth $arg5)
        ]
        if (>f $console_blend 0.0) [
            uitext (getcondebugcref $console_line) $arg7 [
                uistyle lefttop
                uitextalign -1
                uitextwrap $arg6
                uitextgrowth $console_growth
                uicolourset $console_colour
                uicolourblend $console_blend
            ]
        ]
    ]
]

ui_event_draw = [
    local console_blend console_colour console_time console_millis console_growth console_ctime
    loopconeventrev (- $arg1) 0 console_line [
        console_blend = $arg5
        console_colour = (getconeventcolour $console_line)
        console_time = (getconeventreftime $console_line)
        console_millis = (- $totalmillis $console_time)
        console_growth = 1.0
        caseif (&& $arg2 (> $console_millis $arg2)) [
            console_ctime = (- $console_millis $arg2)
            if (&& $arg3 (< $console_ctime $arg3)) [
                console_growth = (-f 1 (divf $console_ctime $arg3))
                console_blend = (*f $console_growth $arg5)
            ] [
                console_blend = 0.0
                console_growth = 0.0
            ]
        ] (&& $arg4 (< $console_millis $arg4)) [
            console_growth = (divf $console_millis $arg4)
            console_blend = (*f $console_growth $arg5)
        ]
        if (>f $console_blend 0.0) [
            uitext (getconeventcref $console_line) $arg7 [
                uistyle lefttop
                uitextalign -1
                uitextwrap $arg6
                uitextgrowth $console_growth
                uicolourset $console_colour
                uicolourblend $console_blend
            ]
        ]
    ]
]

ui_console_colour_var = [
    if (>= $arg1 0) [
        result (format "%1 (%2, %3, %4)" (tohex $arg1 6) (& (>> $arg1 16) 0xFF) (& (>> $arg1 8) 0xFF) (& $arg1 0xFF))
    ] [
        result $arg1
    ]
]

ui_console_current_idname = [
    local current_command
    if (=s (substr $consolebuf 0 1) "/") [
        current_command = (substr $consolebuf 1)
    ] [
        current_command = $consolebuf
    ]

    local idname_idx; idname_idx = 0
    local chrlist; chrlist = ";()[]^"$"
    loop i (strlen chrlist) [
        local new_idx; new_idx = 0
        local idx_add; idx_add = 0
        while [ (!= $new_idx -1) ] [
            new_idx = (strstr (substr $current_command $idx_add) (substr $chrlist $i 1))
            idx_add = (+ $idx_add $new_idx 1)
            if (&& (!= $new_idx -1) (> $idx_add $idname_idx ))[
                idname_idx = $idx_add
            ]
        ]
    ]

    while [ ( =s (substr (substr $current_command $idname_idx) 0 1) " ") ] [
        idname_idx = (+ $idname_idx 1)
    ]

    local idname; idname = (substr $current_command $idname_idx)
    local idname_end; idname_end = (strstr $idname " ")
    if ( = $idname_end -1 ) [
        idname_end = (strstr $idname "^t")
    ]
    if ( = $idname_end -1 ) [
        idname_end = (strlen $idname)
    ]
    idname = (substr $idname 0 $idname_end)

    if (&& (strlen $idname) ( >= (getvartype $idname) 0)) [
        result $idname
    ] [
        result ""
    ]
]

ui_console_usage_text = [
    local textstyle; textstyle = [ uistyle lefttop; uitextalign -1; uitextwrap $arg2 ]
    local idname; idname = $arg1
    local idtype; idtype = (getvartype $idname)
    local idflags; idflags = (getvarflags $idname)
    local idtype_s; idtype_s = ""
    if (|| (& $idflags $idfbitclient) (& $idflags $idfbitserver)) [
        idtype_s = (concatif $idtype_s (? (& $idflags $idfbitclient) "Game" "Server"))
    ]
    if (!= $idtype $ididxcommand) [
        if (& $idflags $idfbitinit) [ idtype_s = (concatif $idtype_s "Initialiser")]
        if (& $idflags $idfbitpersist) [ idtype_s = (concatif $idtype_s "Persistent")]
        if (& $idflags $idfbitreadonly) [ idtype_s = (concatif $idtype_s "Read-only")]
        if (& $idflags $idfbitmap) [ idtype_s = (concatif $idtype_s "Map")]
    ]

    idtype_s = (concatif $idtype_s $[idname@(at $ididxname $idtype)])

    uitext (concatword "^fAType: ^fw" $idtype_s) $ui_textsmaller $textstyle

    case $idtype $ididxvar [
        if (& $idflags $idfbithex) [
            if (= (getvarmax $idname) 0xFFFFFF) [
                uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (ui_console_colour_var (getvarmin $idname)) (ui_console_colour_var (getvarmax $idname))) $ui_textsmaller $textstyle
                if (= (getvardef $idname) $$idname) [
                    uitext (format "^fACurrent: ^fw%1^fA [^fs^f[%2]#^fS] ^fA(default)" (ui_console_colour_var (getvardef $idname)) $$idname) $ui_textsmaller $textstyle
                ] [
                    uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2^fA [^fs^f[%3]#^fS]" (ui_console_colour_var (getvardef $idname)) (ui_console_colour_var $$idname) $$idname) $ui_textsmaller $textstyle
                ]
            ] [
                uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (tohex (getvarmin $idname)) (tohex (getvarmax $idname))) $ui_textsmaller $textstyle
                if (= (getvardef $idname) $$idname) [
                    uitext (format "^fACurrent: ^fw%1 ^fA(default)" (tohex (getvardef $idname))) $ui_textsmaller $textstyle
                ] [
                    uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (tohex (getvardef $idname)) (tohex $$idname)) $ui_textsmaller $textstyle
                ]
            ]
        ] [
            uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (getvarmin $idname) (getvarmax $idname)) $ui_textsmaller $textstyle
            if (= (getvardef $idname) $$idname) [
                uitext (format "^fACurrent: ^fw%1 ^fA(default)" (getvardef $idname)) $ui_textsmaller $textstyle
            ] [
                uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (getvardef $idname) $$idname) $ui_textsmaller $textstyle
            ]
        ]
    ] $ididxfvar [
        uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (getfvarmin $idname) (getfvarmax $idname)) $ui_textsmaller $textstyle
        if (=f (getfvardef $idname) $$idname) [
            uitext (format "^fACurrent: ^fw%1 ^fA(default)" (getfvardef $idname)) $ui_textsmaller $textstyle
        ] [
            uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (getfvardef $idname) $$idname) $ui_textsmaller $textstyle
        ]
    ] $ididxsvar [
        if (=s (getsvardef $idname) $$idname) [
            uitext (format "^fACurrent: ^fw%1 ^fA(default)" (getsvardef $idname)) $ui_textsmaller $textstyle
        ] [
            uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (getsvardef $idname) $$idname) $ui_textsmaller $textstyle
        ]
    ] $ididxcommand [
        if (strlen (getvarargs $idname)) [
            uitext (format "^fAArguments: ^fw%1 ^fA(^fw%2^fA)" (strlen (getvarargs $idname)) (getvarargs $idname)) $ui_textsmaller $textstyle
        ] [
            uitext "^fAArguments: ^fwnone" $ui_textsmaller $textstyle
        ]
    ]

    local idusage_s; idusage_s = (concatword "^fAUsage: ^fw/" $idname)

    if (&& (= $idtype $ididxvar) (> (getvarfields $idname) 1)) [
        idusage_s = (concat $idusage_s "<bitfield>")
        uitext $idusage_s $ui_textsmaller $textstyle
        loop i (getvarfields $idname) [
            uitext (format "^t^fA%1 = %2" (<< 1 $i) (getvarfields $idname $i)) $ui_textsmaller $textstyle
        ]
    ] [
        if (getvarfields $idname) [
            loop i (getvarfields $idname) [
                if (strlen (getvarfields $idname $i)) [
                    idusage_s = (concat $idusage_s (concatword "<" (getvarfields $idname $i) ">"))
                ]
            ]
        ] [
            case $idtype $ididxalias [
                idusage_s = (concat $idusage_s "<arguments>")
            ] $ididxvar [
                idusage_s = (concat $idusage_s "<integer>")
            ] $ididxfvar [
                idusage_s = (concat $idusage_s "<float>")
            ] $ididxsvar [
                idusage_s = (concat $idusage_s "<string>")
            ] $ididxcommand [
                loop i (strlen (getvarargs $idname)) [
                    local c; c = (substr (getvarargs $idname) $i 1)
                    cases $c s [n = "<string>"
                    ] i [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] b [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] n [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] f [n = "<float>"
                    ] g [n = "<float>"
                    ] t [n = "<null>"
                    ] e [n = "<command>"
                    ] r [n = "<ident>"
                    ] "^$" [n = "<ident>"
                    ] () [n = "<?>"]
                    idusage_s = (concat $idusage_s $n)
                ]
            ]
        ]
        uitext $idusage_s $ui_textsmaller $textstyle
    ]

    if (strlen (getvardesc $idname)) [
        uitext (concatword "^fADescription: ^fw" (getvardesc $idname)) $ui_textsmaller $textstyle
    ]

    if (= $idtype $ididxalias) [
        idusage_alias = (getalias $idname)
        if (>= (strlen $idusage_alias) 256) [
            idusage_alias = (concat (substr $idusage_alias 0 252) "..")
        ]
        uitext (concatword "^fAContents: ^fw" $idusage_alias) $ui_textsmaller $textstyle
    ]
]

ui_console_body = [
    uistyle lefttop
    uistyle clampxy
    uiontop 1
    uimenu 1
    uiallowinput 1
    uizindex 2

    local console_growth console_closing console_texth console_idname console_height console_size
    console_texth = 0.0
    uiclip 0 0 0 0 [ uiclipforced 1; uitext "Dummy" $ui_textsmaller [ console_texth = $uilasth ] ]
    console_growth = 1
    console_closing = 0
    if (> $consolemillis 0) [
        console_growth = (clampf (divf (- $totalmillis $consolemillis) $conopentime) 0.0 1.0)
    ] [
        console_growth = (-f 1.0 (clampf (divf (- $totalmillis (- 0 $consolemillis)) $conopentime) 0.0 1.0))
        if (<=f $console_growth 0.0) [ console_closing = 1 ]
    ]

    uiclip 0 0 0 0 [
        uistyle lefttop
        uicolour $ui_menu $uiaspect 0 [
            uistyle lefttop
            uispace $ui_padsmall $ui_padsmall [
                uistyle clampx
                uistyle lefttop
                uivlist 0 [
                    uistyle clampx
                    uistyle lefttop

                    if $coneventlines [
                        uiborderedimageclamped $skinshadowtex $ui_line 0 $ui_texborder $ui_screenborder 0 0 [
                            uistyle clampx
                            uistyle lefttop
                            uispace $ui_padnormal $ui_padnormal [
                                uistyle clampx
                                uistyle lefttop
                                uihlist 0 [
                                    uistyle clampx
                                    uistyle lefttop
                                    console_height = (*f $console_texth $coneventopenamt)
                                    uiscroll $uilastw $console_height [
                                        uistyle clampx
                                        uistyle lefttop
                                        uifill $uilastw $console_height [
                                            uistyle clampx
                                            uistyle lefttop
                                            uivlist 0 [
                                                uistyle clampx
                                                uistyle lefttop
                                                ui_event_draw $coneventopenlines 0 0 $coneventintime 1 $uilastw $ui_textsmaller
                                            ]
                                        ]
                                        uiclipinverted 1
                                        uiclipinvscroll 1
                                    ]
                                    uivscroll $ui_slidersize $console_height
                                ]
                            ]
                        ]
                    ]

                    if $condebuglines [
                        uiborderedimageclamped $skinshadowtex $ui_line 0 $ui_texborder $ui_screenborder 0 0 [
                            uistyle clampx
                            uistyle lefttop
                            uispace $ui_padnormal $ui_padnormal [
                                uistyle clampx
                                uistyle lefttop
                                uihlist 0 [
                                    uistyle clampx
                                    uistyle lefttop
                                    console_height = (*f $console_texth $condebugopenamt)
                                    uiscroll $uilastw $console_height [
                                        uistyle clampx
                                        uistyle lefttop
                                        uifill $uilastw $console_height [
                                            uistyle clampx
                                            uistyle lefttop
                                            uivlist 0 [
                                                uistyle clampx
                                                uistyle lefttop
                                                ui_console_draw $condebugopenlines 0 0 $condebugintime 1 $uilastw $ui_textsmaller
                                            ]
                                        ]
                                        uiclipinverted 1
                                        uiclipinvscroll 1
                                    ]
                                    uivscroll $ui_slidersize $console_height
                                ]
                            ]
                        ]
                    ]

                    uiborderedimageclamped $skinshadowtex $ui_line 0 $ui_texborder $ui_screenborder 0 0 [
                        uistyle lefttop
                        uistyle clampx
                        uispace $ui_padsmall $ui_padsmall [
                            uistyle lefttop
                            uistyle clampx
                            local console_width; console_width = $uilastw
                            uitext $consolebuf $ui_textsmall [
                                uistyle lefttop
                                uitextwrap $console_width
                                uitextpos $consolepos
                                uitextalign -1
                            ]
                        ]
                    ]

                    console_idname = (ui_console_current_idname)

                    if $console_idname [
                        uihlist 0 [
                            uistyle clampx
                            uistyle lefttop
                            uiborderedimageclamped $skinshadowtex $ui_line 0 $ui_texborder $ui_screenborder 0 0 [
                                uistyle clampx
                                uistyle lefttop
                                uispace $ui_padsmall $ui_padsmall [
                                    uistyle clampx
                                    uistyle lefttop
                                    uivlist 0 [
                                        uistyle clampx
                                        uistyle lefttop
                                        ui_console_usage_text $console_idname $uilastw
                                    ]
                                ]
                            ] [
                                uistyle clampx
                                uistyle lefttop
                            ] 0 1
                        ]
                    ]
                ]
            ]

            uicolourblend (*f $conopenbgblend $console_growth)
            uipropagate [ uicolourblend (*f $conopenblend $console_growth) ]
        ]

        console_size = (*f $uilasthprev $console_growth)

        uiclipsizeh $console_size
        uiclipoffsety (-f $uilasthprev $console_size)
    ]

    if $console_closing [
        hideui $uiname $uisurfacetype
    ]
]

newui "console" $SURFACE_FOREGROUND [
    ui_console_body
] [] [
    if (> $consolemillis 0) [ closeconsole ] // window was destroyed, take away input, etc
]
