defvarp showconsole 0 1 1
defsvarp consolelevels [1 2]
defvarp allconsole 0 0 1
defsvarp allconsolelevels [0 1 2]
defsvarp openconsolelevels [0 1 2 3]

defvar fullconsole 0 0 1
toggleconsole = [fullconsole (! $fullconsole)]

defvarp conintime 0 250 $varidxmax
defvarp conouttime 0 10000 $varidxmax
defvarp confadetime 0 500 $varidxmax
defvarp conlines 0 5 $varidxmax
defvarp conlinesopen 0 7 $varidxmax
deffvarp conblend 0 1 1

defvarp chatintime 0 250 $varidxmax
defvarp chatouttime 0 10000 $varidxmax
defvarp chatfadetime 0 500 $varidxmax
defvarp chatlines 0 6 $varidxmax
deffvarp chatblend 0 1 1

defsvarp ui_hud_connames [Debug Event Frag Chat]
defsvarp ui_hud_concols [red orange yellow green]

// 1:console 2:count 3:outtime 4:fadetime 5:intime 6:blend 7:size 8:textsize 9:children
ui_hud_displayconsole = [
    loopconlinesrev $arg1 $arg2 0 ui_hud_consoleline [
        ui_hud_consoleblend = (ceil (*f 255 $arg6))
        ui_hud_consoletime = (getconlinereftime $arg1 $ui_hud_consoleline)
        ui_hud_consolemillis = (- (getmillis 1) $ui_hud_consoletime)
        ui_hud_consolegrowth = 1.0
        caseif (&& $arg3 (> $ui_hud_consolemillis $arg3)) [
            ui_hud_consolectime = (- $ui_hud_consolemillis $arg3)
            if (&& $arg4 (< $ui_hud_consolectime $arg4)) [
                ui_hud_consolegrowth = (-f 1 (divf $ui_hud_consolectime $arg4))
                ui_hud_consoleblend = (clamp (*f $ui_hud_consolegrowth $arg6 255) 0 255)
            ] [ui_hud_consoleblend = 0]
        ] (&& $arg5 (< $ui_hud_consolemillis $arg5)) [
            ui_hud_consolegrowth = (divf $ui_hud_consolemillis $arg5)
            ui_hud_consoleblend = (clamp (*f $ui_hud_consolegrowth $arg6 255) 0 255)
            ui_hud_consolegrowth = (-f $ui_hud_consolegrowth)
        ]
        if $ui_hud_consoleblend [
            uitext (getconlinecref $arg1 $ui_hud_consoleline) $arg8 [
                uicolourset (| (<< $ui_hud_consoleblend 24) $colourwhite)
                uitextwrap $arg7
                uitextalign -1
                uialign -1 1
                if (<f $ui_hud_consolegrowth 1) [uitextgrowth $ui_hud_consolegrowth]
            ]
        ]
    ]
]

ui_console_colour_var = [
    result (format "%1 (%2, %3, %4)" (tohex $arg1 6) (& (>> $arg1 16) 0xFF) (& (>> $arg1 8) 0xFF) (& $arg1 0xFF))
]

ui_console_current_idname = [
    if $ui_hud_commandopen [
        local current_command
        current_command = (getcommandbuf)
        if (=s (substr $current_command 0 1) "/") [
            current_command = (substr $current_command 1)

            // Get start of last id into idname_idx
            local idname_idx
            idname_idx = 0
            local chrlist
            chrlist = ";()[]^"$"
            loop i (strlen chrlist) [
                // strrchr-like functionality
                local new_idx
                new_idx = 0
                local idx_add
                idx_add = 0
                while [ (!= $new_idx -1) ] [
                    new_idx = (strstr (substr $current_command $idx_add) (substr $chrlist $i 1))
                    idx_add = (+ $idx_add $new_idx 1)
                    // If character found and after last found, use this index
                    if (&& (!= $new_idx -1) (> $idx_add $idname_idx ))[
                        idname_idx = $idx_add
                    ]
                ]
            ]

            // Skip any leading whitespace
            while [ ( =s (substr (substr $current_command $idname_idx) 0 1) " ") ] [
                idname_idx = (+ $idname_idx 1)
            ]

            // Get single id into idname, stopping at next space/tab.
            local idname
            idname = (substr $current_command $idname_idx)
            local idname_end
            idname_end = (strstr $idname " ")
            if ( = $idname_end -1 ) [
                idname_end = (strstr $idname "^t")
            ]
            if ( = $idname_end -1 ) [
                idname_end = (strlen $idname)
            ]
            idname = (substr $idname 0 $idname_end)

            if (&& (strlen $idname) ( >= (getvartype $idname) 0)) [
                result $idname
            ] [
                result ""
            ]
        ] [
            result ""
        ]
    ] [
        result ""
    ]
]

ui_console_usage_text = [
    local textstyle
    textstyle = [uialign -1 1; uitextalign -1; uitextwrap (-f (uiaspect) 1.15)]
    local idname
    idname = $arg1
    local idtype
    idtype = (getvartype $idname)
    local idflags
    idflags = (getvarflags $idname)
    local idtype_s
    idtype_s = ""
    // IDF_CLIENT || IDF_SERVER
    if (|| (& $idflags $idfbitclient) (& $idflags $idfbitserver)) [
        idtype_s = (concatif $idtype_s (? (& $idflags $idfbitclient) "Client" "Server"))
    ]
    // not ID_COMMAND
    if (!= $idtype $ididxcommand) [
        // IDF_INIT
        if (& $idflags $idfbitinit) [ idtype_s = (concatif $idtype_s "Initialiser")]
        // IDF_PERSIST
        if (& $idflags $idfbitpersist) [ idtype_s = (concatif $idtype_s "Persistent")]
        // IDF_READONLY
        if (& $idflags $idfbitreadonly) [ idtype_s = (concatif $idtype_s "Read-only")]
        // IDF_WORLD
        if (& $idflags $idfbitworld) [ idtype_s = (concatif $idtype_s "World")]
    ]

    idtype_s = (concatif $idtype_s $[idname@(at $ididxname $idtype)])

    uitext $idtype_s $ui_textstatus $textstyle

    case $idtype $ididxvar [
        if (& $idflags $idfbithex) [
            if (= (getvarmax $idname) 0xFFFFFF) [
                uitext (format "^faMin: ^fw%1^fa, Max: ^fw%2" (ui_console_colour_var (getvarmin $idname)) (ui_console_colour_var (getvarmax $idname))) $ui_textstatus $textstyle
                if (= (getvardef $idname) $$idname) [
                    uitext (format "^faCurrent: ^fw%1^fa [^fs^f[%2]#^fS] ^fa(default)" (ui_console_colour_var (getvardef $idname)) $$idname) $ui_textstatus $textstyle
                ] [
                    uitext (format "^faDefault: ^fw%1^fa, Current: ^fy%2^fa [^fs^f[%3]#^fS]" (ui_console_colour_var (getvardef $idname)) (ui_console_colour_var $$idname) $$idname) $ui_textstatus $textstyle
                ]
            ] [
                uitext (format "^faMin: ^fw%1^fa, Max: ^fw%2" (tohex (getvarmin $idname)) (tohex (getvarmax $idname))) $ui_textstatus $textstyle
                if (= (getvardef $idname) $$idname) [
                    uitext (format "^faCurrent: ^fw%1 ^fa(default)" (tohex (getvardef $idname))) $ui_textstatus $textstyle
                ] [
                    uitext (format "^faDefault: ^fw%1^fa, Current: ^fy%2" (tohex (getvardef $idname)) (tohex $$idname)) $ui_textstatus $textstyle
                ]
            ]
        ] [
            uitext (format "^faMin: ^fw%1^fa, Max: ^fw%2" (getvarmin $idname) (getvarmax $idname)) $ui_textstatus $textstyle
            if (= (getvardef $idname) $$idname) [
                uitext (format "^faCurrent: ^fw%1 ^fa(default)" (getvardef $idname)) $ui_textstatus $textstyle
            ] [
                uitext (format "^faDefault: ^fw%1^fa, Current: ^fy%2" (getvardef $idname) $$idname) $ui_textstatus $textstyle
            ]
        ]
    ] $ididxfvar [
        uitext (format "^faMin: ^fw%1^fa, Max: ^fw%2" (getfvarmin $idname) (getfvarmax $idname)) $ui_textstatus $textstyle
        if (= (getfvardef $idname) $$idname) [
            uitext (format "^faCurrent: ^fw%1 ^fa(default)" (getfvardef $idname)) $ui_textstatus $textstyle
        ] [
            uitext (format "^faDefault: ^fw%1^fa, Current: ^fy%2" (getfvardef $idname) $$idname) $ui_textstatus $textstyle
        ]
    ] $ididxsvar [
        if (= (getsvardef $idname) $$idname) [
            uitext (format "^faCurrent: ^fw%1 ^fa(default)" (getsvardef $idname)) $ui_textstatus $textstyle
        ] [
            uitext (format "^faDefault: ^fw%1^fa, Current: ^fy%2" (getsvardef $idname) $$idname) $ui_textstatus $textstyle
        ]
    ] $ididxcommand [
        if (strlen (getvarargs $idname)) [
            uitext (format "^faArguments: ^fw%1 ^fa(^fw%2^fa)" (strlen (getvarargs $idname)) (getvarargs $idname)) $ui_textstatus $textstyle
        ] [
            uitext "^fAArguments: ^fwnone" $ui_textstatus $textstyle
        ]
    ]

    local idusage_s
    idusage_s = (concatword "Usage: ^fa/" $idname)

    // ID_VAR Bitfield
    if (&& (= $idtype $ididxvar) (> (getvarfields $idname) 1)) [
        idusage_s = (concat $idusage_s "<bitfield>")
        uitext $idusage_s $ui_textstatus $textstyle
        loop i (getvarfields $idname) [
            uitext (format "^t^fa%1 = %2" (<< 1 $i) (getvarfields $idname $i)) $ui_textstatus $textstyle
        ]
    ] [
        if (getvarfields $idname) [
            loop i (getvarfields $idname) [
                if (strlen (getvarfields $idname $i)) [
                    idusage_s = (concat $idusage_s (concatword "<" (getvarfields $idname $i) ">"))
                ]
            ]
        ] [
            case $idtype $ididxalias [
                idusage_s = (concat $idusage_s "<arguments>")
            ] $ididxvar [
                idusage_s = (concat $idusage_s "<integer>")
            ] $ididxfvar [
                idusage_s = (concat $idusage_s "<float>")
            ] $ididxsvar [
                idusage_s = (concat $idusage_s "<string>")
            ] $ididxcommand [
                loop i (strlen (getvarargs $idname)) [
                    local c
                    c = (substr (getvarargs $idname) $i 1)
                    cases $c s [n = "<string>"
                    ] i [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] b [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] n [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] f [n = "<float>"
                    ] g [n = "<float>"
                    ] t [n = "<null>"
                    ] e [n = "<command>"
                    ] r [n = "<ident>"
                    ] "^$" [n = "<ident>"
                    ] () [n = "<?>"]
                    idusage_s = (concat $idusage_s $n)
                ]
            ]
        ]
        uitext $idusage_s $ui_textstatus $textstyle
    ]

    if (strlen (getvardesc $idname)) [
        uitext (concatword "^fa" (getvardesc $idname)) $ui_textstatus $textstyle
    ]

    if (= $idtype $ididxalias) [
        idusage_alias = (getalias $idname)
        if (>= (strlen $idusage_alias) 256) [
            idusage_alias = (concat (substr $idusage_alias 0 252) "..")
        ]
        uitext (concatword "^faContents: ^fw" $idusage_alias) $ui_textsmall $textstyle
    ]
]

ui_hud_showconsole = [
    uivlist 0 [
        uialign -1 -1
        if $ui_hud_commandopen [
            uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                uialign -1 -1
                uispace $ui_padsmall $ui_padsmall [
                    uialign -1 -1
                    uivlist 0 [
                        uialign -1 -1
                        loop i 4 [
                            if (getconlines $i) [
                                uihlist 0 [
                                    uialign -1 -1
                                    uipad 0 0 $ui_screenborder $ui_screenborder [
                                        uialign -1 -1
                                        uivtabskin 0 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                                            uispace $ui_padsmall $ui_padsmall [
                                                uitext (at $ui_hud_connames $i) $ui_textsmall [
                                                    uitextrotate 3
                                                    uicolourset $[colour@(at $ui_hud_concols $i)]
                                                ]
                                            ]
                                        ]
                                    ]
                                    uiskinborder (*f (uiaspect) 0.9) 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                                        uiclamp 1 1 1 1
                                        uialign -1 -1
                                        uispace $ui_padnormal $ui_padsmall [
                                            uiclamp 1 1 1 1
                                            uialign -1 -1
                                            uivlist 0 [
                                                uiclamp 1 1 1 1
                                                uialign -1 -1
                                                ui_hud_displayconsole $i (- $conlinesopen) 0 0 0 1 (-f (*f (uiaspect) 0.9) $ui_padnormal $ui_padnormal $ui_padnormal) $ui_text [uialign -1 -1]
                                            ]
                                        ]
                                    ] [uiclamp 1 1 1 1; uialign -1 -1]
                                ]
                            ]
                            uispace 0 $ui_padtiny
                        ]
                        uiskinborder 0 0 $ui_menu $ui_menu $ui_menu (getclientrescolour $ui_hud_focus $pulseidxflash) (getclientrescolour $ui_hud_focus $pulseidxflash) (getclientrescolour $ui_hud_focus $pulseidxflash) [
                            uiclamp 1 1
                            uialign -1 -1
                            uihlist 0 [
                                uialign -1 -1
                                ui_hud_commandicon = (getcommandicon)
                                if (=s $ui_hud_commandicon "") [ui_hud_commandicon = $inputtex]
                                uispace $ui_padsmall $ui_padsmall [uialign -1 -1; uiimage $ui_hud_commandicon (getcommandcolour) 0 0.0225 0.0225 [uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]]
                                uitext (getcommandbuf) $ui_textlarge [uitextwrap (-f (*f (uiaspect) 0.9) $ui_padnormal $ui_padnormal); uitextpos (getcommandpos); uitextalign -1; uialign -1 1]
                            ]
                        ] [uiclamp 1 1; uialign -1 -1]
                        local idname
                        idname = (ui_console_current_idname)
                        if $idname [
                            uispace 0 $ui_padtiny
                            uihlist 0 [
                                uialign -1 -1
                                uipad 0 0 $ui_screenborder $ui_screenborder [
                                    uialign -1 -1
                                    uivtabskin 0 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                                        uispace $ui_padsmall $ui_padsmall [
                                            uitext "Desc" $ui_textsmall [
                                                uitextrotate 3
                                                uicolourset $colourgrey
                                            ]
                                        ]
                                    ]
                                ]
                                uiskinborder (*f (uiaspect) 0.9) 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                                    uiclamp 1 1 1 1
                                    uialign -1 -1
                                    uispace $ui_padnormal $ui_padsmall [
                                        uiclamp 1 1 1 1
                                        uialign -1 -1
                                        uivlist 0 [
                                            uiclamp 1 1 1 1
                                            uialign -1 -1
                                            ui_console_usage_text $idname
                                        ]
                                    ]
                                ] [uiclamp 1 1 1 1; uialign -1 -1]
                            ]
                        ]
                    ]
                ]
            ] [uialign -1 -1]
        ] [
            looplist i (? (|| $fullconsole $allconsole) $allconsolelevels (? $ui_hud_commandopen $openconsolelevels $consolelevels)) [
                ui_hud_displayconsole $i (- $conlines) (? $fullconsole 0 $conouttime) (? $fullconsole 0 $confadetime) (? $fullconsole 0 $conintime) (? $fullconsole 1 $conblend) 1 $ui_textsmall [uialign -1 -1]
            ]
        ]
    ]
]
