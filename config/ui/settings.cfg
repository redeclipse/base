//for settings ui (internal temp vars)
ui_settings_tab = 0

//for binds ui (internal temp vars)
ui_binds_current_key = ""
ui_binds_entry_normal = ""
ui_binds_entry_edit = ""
ui_binds_entry_spectator = ""
ui_binds_entry_wait = ""

//persistent string for keyboard layout type (as defined in keyboards.cfg)
setpersist keyboardlayout 1
keyboardlayout = "pc104"


//This is the main settings menu as seen from the main menu.
uimenu "settings" "Settings" "textures/icons/settings" [
    hidechanges 1
    uihlist 0 [
        uiclamp 1 1 1 1
        // side tab, in settings menu
        uivtab (+f $ui_slidersize 0.48) 0.4 $ui_tabw $ui_tabh ui_settings_tab [
            0 "Graphics"        []
            1 "Display"         []
            2 "Audio"           []
            3 "Gameplay"        []
            4 "Controls"        []
            5 "User Interface"  []
            6 "Autoexec"        []
        ] [
            uiscroll 0.48 0.4 [
                uialign -1 0
                uifill 0.48 0.4 [
                    uialign 0 -1
                    uipad $ui_padbutton $ui_padbutton $ui_padbutton $ui_padbutton [
                        uialign 0 -1
                        uiclamp 1 1
                        uitable 0 $ui_padsmall [
                            uialign -1 -1
                            uiclamp 1 1
                            case $ui_settings_tab 0 [ // Graphics Tab
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Shadow Resolution"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "smsize" [
                                        10 "^f0Very Low"    []
                                        11 "^f0Low"         []
                                        12 "^f2Medium"      []
                                        13 "^f6High"        []
                                        14 "^f3Very High"   []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Shadow Filtering"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "smfilter" [
                                        0 "^f4Off"          []
                                        1 "^f0Low"          []
                                        2 "^f2Medium"       []
                                        3 "^f3High"         []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [uicheckbox "Global Illumination" $gi $ui_checksize [gi = (! $gi)]; uialign -1 0]
                                    uiselect 0.2 0.025 "rhtaps" [
                                        12 @(format "%1Low"     (? $gi "^f0" "^f4"))    []
                                        20 @(format "%1Medium"  (? $gi "^f2" "^f4"))    []
                                        32 @(format "%1High"    (? $gi "^f3" "^f4"))    []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [uicheckbox "SSAO" $ao $ui_checksize [ao = (! $ao)]; uialign -1 0]
                                    uiselect 0.2 0.025 "aotaps" [
                                        5  @(format "%1Low"     (? $ao "^f0" "^f4"))    [aobilateral 3]
                                        8  @(format "%1Medium"  (? $ao "^f2" "^f4"))    [aobilateral 4]
                                        12 @(format "%1High"    (? $ao "^f3" "^f4"))    [aobilateral 4]
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [uicheckbox "Volumetric Lighting" $volumetric $ui_checksize [volumetric = (! $volumetric)]; uialign -1 0]
                                    uiselect 0.2 0.025 "volsteps" [
                                        12 @(format "%1Low"     (? $volumetric "^f0" "^f4"))    [volbilateral 1]
                                        16 @(format "%1Medium"  (? $volumetric "^f2" "^f4"))    [volbilateral 2]
                                        24 @(format "%1High"    (? $volumetric "^f3" "^f4"))    [volbilateral 2]
                                    ] [uialign 1 0]
                                ]
                                uibar 1 0
                                //Water Settings: Reflections,Caustics, & Animation checkboxes
                                uitablerow [
                                    uivlist 0 [
                                        uihlist $ui_padbutton [
                                            uifill $ui_checksize
                                            uitext "Water"
                                            uialign -1 0
                                        ]
                                        uialign -1 0
                                    ]
                                    uivlist 0 [
                                        uifill 0.2
                                        uivlist $ui_padbutton [
                                            uihlist $ui_padbutton [
                                                uicheckbox "Reflections" $waterreflect $ui_checksize [waterreflect = (! $waterreflect)]
                                                uialign -1 0
                                            ]
                                            uihlist $ui_padbutton [
                                                uicheckbox "Caustics" $caustics $ui_checksize [caustics = (! $caustics)]
                                                uialign -1 0
                                            ]
                                            uihlist $ui_padbutton [
                                                uicheckbox "Animation" $vertwater $ui_checksize [vertwater = (! $vertwater)]
                                                uialign -1 0
                                            ]
                                            uialign -1 0
                                        ]
                                        uialign 1 0
                                    ]
                                ]

                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Texture Quality"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "texreduce" [
                                        12 "^f0Very Low"    []
                                        6 "^f2Low"          []
                                        3 "^f6Medium"       []
                                        0 "^f3High"         []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Reflection Map Quality"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "envmapsize" [
                                        4 "^f0Low"          []
                                        6 "^f2Medium"       []
                                        8 "^f6High"         []
                                        10 "^f3Very High"   []
                                    ] [uialign 1 0]
                                ]

                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Soft Particles" $softparticles $ui_checksize [softparticles = (! $softparticles)]
                                        uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Bloodstains" $stains $ui_checksize [stains = (! $stains)]
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "stainfade" [
                                        2000  @(format "[%1Instant Fade]"    (? $stains "^f0" "^f4"))   [maxstaintris 2048]
                                        15000 @(format "[%1Quick Fade]"      (? $stains "^f2" "^f4"))   [maxstaintris 4096]
                                        30000 @(format "[%1Moderate Fade]"   (? $stains "^f6" "^f4"))   [maxstaintris 8192]
                                        60000 @(format "[%1Slow Fade]"       (? $stains "^f3" "^f4"))   [maxstaintris 8192]
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Grass" $grass $ui_checksize [grass = (! $grass)]
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "grassdist" [
                                        128     @(format "%1Near"       (? $grass "^f0" "^f4")) []
                                        256     @(format "%1Moderate"   (? $grass "^f2" "^f4")) []
                                        512     @(format "%1Far"        (? $grass "^f6" "^f4")) []
                                        1024    @(format "%1Distant"    (? $grass "^f3" "^f4")) []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Map Effects"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "mapeffects" [
                                        0 "^f0Low"          []
                                        1 "^f2Medium"       []
                                        2 "^f3High"         []
                                    ] [uialign 1 0]
                                ]

                            ] 1 [ // Display Tab
                                //Render scale slider
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Render Scale"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "gscale" 25 100 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext (format "^f[%1]%2%%" (rgbtohex @(hsltorgb (*f (- 100 $gscale) 1.6) 1 0.5)) $gscale)
                                                // HSL allows for changing just hue with respect to slider value, is then converted to RGB and then hex.
                                                // Number preceding gscale is the offset, number afterward is the rate of hue change; 100 and 1.6 here respectively.
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                //Fullscreen toggle
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Fullscreen" $fullscreen $ui_checksize [fullscreen = (! $fullscreen)]
                                        uialign -1 0
                                    ]
                                ]
                                uibar 1 0
                                //Texture filtering: anisotropic and bi/tri-linear texture filtering
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Anisotropic Filtering"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "aniso" [
                                        0       "^f4Off"    []
                                        2       "^f02x"     []
                                        4       "^f24x"     []
                                        8       "^f68x"     []
                                        16      "^f316x"    []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Texture Filtering"
                                        uialign -1 0
                                    ]
                                    uivlist $ui_padbutton [
                                        uifill 0.2 0
                                        uihlist 0 [
                                            uihlist 0 [
                                                uicheckbox "Bilinear" $bilinear $ui_checksize [bilinear = (! $bilinear)]
                                                uialign -1 0
                                            ]
                                            uihlist 0 [
                                                uicheckbox "Trilinear" $trilinear $ui_checksize [trilinear = (! $trilinear)]
                                                uialign 1 0
                                            ]
                                            uiclamp 1 0
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                //Various kinds of antialiasing here: morphological, temporal, FXAA, multisample
                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Morphological AA" $smaa $ui_checksize [smaa = (! $smaa)]
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "smaaquality" [
                                        0   @(format "%1Low"            (? $smaa "^f0" "^f4"))      [smaacoloredge 0]
                                        1   @(format "%1Medium"         (? $smaa "^f2" "^f4"))      [smaacoloredge 0]
                                        2   @(format "%1High"           (? $smaa "^f6" "^f4"))      [smaacoloredge 0]
                                        3   @(format "[%1Very High]"    (? $smaa "^f3" "^f4"))      [smaacoloredge 1]
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Temporal AA" $tqaa $ui_checksize [tqaa = (! $tqaa)]
                                        uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "FXAA" $fxaa $ui_checksize [fxaa = (! $fxaa)]
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "fxaaquality" [
                                        0   @(format "%1Low"            (? $fxaa "^f0" "^f4"))    []
                                        1   @(format "%1Medium"         (? $fxaa "^f2" "^f4"))    []
                                        2   @(format "%1High"           (? $fxaa "^f6" "^f4"))    []
                                        3   @(format "[%1Very High]"    (? $fxaa "^f3" "^f4"))    []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Multisample AA"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "msaa" [
                                        0   "^f4Off"    []
                                        2   "^f02x"     []
                                        4   "^f24x"     []
                                        8   "^f68x"     []
                                        16  "^f316x"    []
                                    ] [uialign 1 0]
                                ]
                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Gamma"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "gamma" 30 170 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $gamma
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Framerate Cap"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "maxfps" 5 200 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $maxfps

                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "Vertical Sync" $vsync $ui_checksize [vsync = (! $vsync)]
                                        uialign -1 0
                                    ]
                                ]

                            ] 2 [ // Audio Tab
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Master Volume"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "mastervol" 0 255 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext (format "%1%%" (toint (*f (divf $mastervol 255) 100)))
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Game Volume"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "soundvol" 0 255 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext (format "%1%%" (toint (*f (divf $soundvol 255) 100)))
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Music Volume"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "musicvol" 0 255 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext (format "%1%%" (toint (*f (divf $musicvol 255) 100)))
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Map Music"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "musictype" [
                                        0   "Off"             []
                                        3   "By Map"          []//note that option 3 is used here because musictype 1 seems to shuffle (?)
                                        2   "Random/Loop"     []
                                        5   "Random/Shuffle"  []
                                    ] [uialign 1 0]
                                ]

                            ] 3 [ // Gameplay Tab
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Player Model Brightness"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "fullbrightmodels" 0 200 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext (format "%1%%" (toint (+f (divf $fullbrightmodels 2) 100)))
                                            ]
                                        ]
                                        uialign 1 0
                                    ]

                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Race Mode Player Opacity"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "playerghostblend" 0 1 .125 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext (format "%1%%" (toint (*f $playerghostblend 100)))
                                            ]
                                        ]
                                        uialign 1 0
                                    ]

                                ]
                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "First Person FOV"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "firstpersonfov" 90 150 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $firstpersonfov
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Third Person FOV"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "thirdpersonfov" 90 150 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $thirdpersonfov
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Spectator FOV"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "specfov" 90 150 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $specfov
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Editing FOV"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "editfov" 90 150 1 0.2 0.025 [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $editfov
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uibar 1 0
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "First Person Bob" $firstpersonbob $ui_checksize [firstpersonbob = (! $firstpersonbob)]
                                        uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uicheckbox "First Person Sway" $firstpersonsway $ui_checksize [firstpersonsway = (! $firstpersonsway)]
                                        uialign -1 0
                                    ]
                                ]

                            ] 4 [ // Controls Tab
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Mouse Sensitivity"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "sensitivity" 1 50 1 $ui_buttonzw $ui_buttonzh [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $sensitivity
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Zoom Sensitivity Multiplier"
                                        uialign -1 0
                                    ]
                                    uihlist $ui_padbutton [
                                        uihorzslider "zoomsensitivity" 0 5 .02 $ui_buttonzw $ui_buttonzh [
                                            uivlist 0 [
                                                uifill 0.050 0
                                                uitext $zoomsensitivity
                                            ]
                                        ]
                                        uialign 1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Invert Mouse Y-Axis" $mouseinvert $ui_checksize [mouseinvert = (! $mouseinvert)]
                                      uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uifill 0 0
                                    uihlist $ui_padbutton [
                                        uichecktextbutton "Customize Keybinds" "" 0 $ui_buttonzw $ui_buttonzh [
                                            uiopen binds
                                        ] $colourwhite $ui_default
                                      uialign 1 0
                                    ]
                                ]

                            ] 5 [ // User Interface Tab
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Show FPS"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "showfps" [
                                        0 "^f4Off"              []
                                        1 "^f7Avg Only"         []
                                        2 "^f7Avg & +Max-Min"   []
                                        3 "^f7Avg & Range"      []
                                        4 "^f7Avg & Frametime"  []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Show Map Stats"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "showstats" [
                                        0 "^f4Off"          []
                                        1 "^f7Edit Only"    []
                                        2 "^f7Always"       []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                        uifill $ui_checksize
                                        uitextleft "Time Display Style"
                                        uialign -1 0
                                    ]
                                    uiselect 0.2 0.025 "showtimestyle" [
                                        0 "^f7724.5s"      []
                                        1 "^f712:34.567"   []
                                        2 "^f712:34.5"     []
                                        3 "^f712:34"       []
                                        4 "^f712m34s"      []
                                    ] [uialign 1 0]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Show Real Time" $showrealtime $ui_checksize [showrealtime = (! $showrealtime)]
                                      uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Show HUD" $showhud $ui_checksize [showhud = (! $showhud)]
                                      uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Show Minimap" $showminimap $ui_checksize [showminimap = (! $showminimap)]
                                      uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Show Moves Remaining" $showmovecount $ui_checksize [showmovecount = (! $showmovecount)]
                                      uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Show Weight" $showweight $ui_checksize [showweight = (! $showweight)]
                                      uialign -1 0
                                    ]
                                ]
                                uitablerow [
                                    uihlist $ui_padbutton [
                                      uicheckbox "Show Speed" $showvelocity $ui_checksize [showvelocity = (! $showvelocity)]
                                      uialign -1 0
                                    ]
                                ]

                            ] 6 [ //Autoexec Tab
                                uitablerow [
                                    uitext "Not implemented yet. ^nCheck again later!"
                                ]
                            ]
                        ]
                    ]
                ]
            ]
            uihlist 0 [uivscroll $ui_slidersize 0.4 3; uialign 1 0; uiclamp 0 1]
        ]
    ]
] [] [if (pendingchanges) [showui "changes"]] //for settings that require a refresh: MSAA, texreduce, anisotropic, etc

//ui for selecting keyboard layout to display in binds menu
uimenu "keyboard" "Select Keyboard" "textures/keys/a" [
    uivlist $ui_padsmall [
        uichecktextbutton "ANSI QWERTY (US)" "" 0 $ui_buttonw $ui_buttonh [
                keyboardlayout = "pc104"
                uiclose "keyboard"
            ] $colourwhite $ui_default
        uichecktextbutton "ANSI Dvorak (US)" "" 0 $ui_buttonw $ui_buttonh [
                keyboardlayout = "dvorak"
                uiclose "keyboard"
            ] $colourwhite $ui_default
    ]
] [] [showui "binds"]

//ui for changing keybinds
//for documentation on how to add keyboards to this ui, see keyboards.cfg
uimenu "binds" "Key Binds" "textures/icons/settings" [
    uivlist $ui_padsmall [
        uichecktextbutton "Select Layout" "" 0 $ui_buttonw $ui_buttonh [ //button for changing layout
            uiopen "keyboard"
        ] $colourwhite $ui_default
        uibar 1 0
        uihlist $ui_padsmall [ //keyboard rendering
            uivlist $ui_padsmall [
                loop j $ui_binds_@[keyboardlayout]_rows [ //loops through keyboard rows
                    uihlist $ui_padsmall [ //hlist since the keyboard config is defined in rows
                        loop i (listlen $[ui_binds_@[keyboardlayout]_@j]) [ //loop through items in each configured row list
                            ui_binds_temp_key = (sublist $[ui_binds_@[keyboardlayout]_@j] $i 1)
                            //key rendering
                            if (& (!=s "_" $ui_binds_temp_key) (!=s "." $ui_binds_temp_key) ) [// "_" and "." are spacers;if found, do not attempt to uiimage and instead uifill 1 key width
                                if (< (strlen $ui_binds_temp_key) 3) [ //if the key is short (length <3 {includes alphanumeric chars}, it can automatically be rendered at normal size without checking (layouts with nonstandard alphanumeric keys will require this to be changed or omitted)
                                    uiimgbutton (concatword "<grey>textures/keys/" $ui_binds_temp_key) $ui_checksize $ui_checksize [
                                        ui_binds_current_key = $ui_binds_temp_key //once a key is selected, change the bind vars to match selected key
                                        ui_binds_entry_normal = (getbind $ui_binds_current_key)
                                        ui_binds_entry_edit = (geteditbind $ui_binds_current_key)
                                        ui_binds_entry_spectator = (getspecbind $ui_binds_current_key)
                                        ui_binds_entry_wait = (getwaitbind $ui_binds_current_key)
                                    ] [] [] [] 0 [] "0xffffff" [] 1
                                ] [
                                    if (!= -1 (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] ) ) [ //if the key is found to be long make it larger
                                        l = (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] )
                                        uiimgbutton (concatword "<grey>textures/keys/" $ui_binds_temp_key) ( *f (sublist $ui_binds_@[keyboardlayout]_keyscale $l 1) $ui_checksize) $ui_checksize [
                                            ui_binds_current_key = $ui_binds_temp_key //once a key is selected, change the bind vars to match selected key
                                            ui_binds_entry_normal = (getbind $ui_binds_current_key)
                                            ui_binds_entry_edit = (geteditbind $ui_binds_current_key)
                                            ui_binds_entry_spectator = (getspecbind $ui_binds_current_key)
                                            ui_binds_entry_wait = (getwaitbind $ui_binds_current_key)
                                        ] [] [] [] 0 [] "0xffffff" [] 1
                                    ] [ //else display standard size
                                        uiimgbutton (concatword "<grey>textures/keys/" $ui_binds_temp_key) $ui_checksize $ui_checksize [
                                            ui_binds_current_key = $ui_binds_temp_key //once a key is selected, change the bind vars to match selected key
                                            ui_binds_entry_normal = (getbind $ui_binds_current_key)
                                            ui_binds_entry_edit = (geteditbind $ui_binds_current_key)
                                            ui_binds_entry_spectator = (getspecbind $ui_binds_current_key)
                                            ui_binds_entry_wait = (getwaitbind $ui_binds_current_key)
                                        ] [] [] [] 0 [] "0xffffff" [] 1 //else just display at standard size
                                    ]
                                ]
                            ] [ //else fill in with the appropriate space
                                if (=s "_" (sublist $[ui_binds_@[keyboardlayout]_@j] $i 1)) [ //if "_", insert a full space
                                    uifill $ui_checksize $ui_checksize
                                ] [ //else insert a half space
                                    uifill (divf $ui_checksize 2) $ui_checksize
                                ]
                            ]
                        ]
                    ]
                ]
            ]
            uivlist $ui_padsmall [ //column for numpad vertical keys
                loop i (listlen $[ui_binds_@[keyboardlayout]_vert]) [
                    ui_binds_temp_key = (sublist $ui_binds_@[keyboardlayout]_vert $i 1)
                    if (& (!=s "_" $ui_binds_temp_key) (!=s "." $ui_binds_temp_key) ) [ // "_" and "." are spacers; does not attempt to uiimage and instead uifill 1 key width (for "_") and 1/2 key width (for ".")
                        if (!= -1 (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] ) ) [ //if the key is found to be long make it larger
                            l = (listfind k $ui_binds_@[keyboardlayout]_largekeys [=s $k $ui_binds_temp_key ] )
                            uiimgbutton (concatword "<grey>textures/keys/" $ui_binds_temp_key) $ui_checksize ( *f (sublist $ui_binds_@[keyboardlayout]_keyscale $l 1) $ui_checksize) [
                                ui_binds_current_key = $ui_binds_temp_key //once a key is selected, change the bind vars to match selected key
                                    ui_binds_entry_normal = (getbind $ui_binds_current_key)
                                    ui_binds_entry_edit = (geteditbind $ui_binds_current_key)
                                    ui_binds_entry_spectator = (getspecbind $ui_binds_current_key)
                                    ui_binds_entry_wait = (getwaitbind $ui_binds_current_key)
                            ] [] [] [] 0 [] "0xffffff" [] 1
                        ] [ //else display standard size
                            uiimgbutton (concatword "<grey>textures/keys/" $ui_binds_temp_key) $ui_checksize $ui_checksize [
                                ui_binds_current_key = $ui_binds_temp_key //once a key is selected, change the bind vars to match selected key
                                ui_binds_entry_normal = (getbind $ui_binds_current_key)
                                ui_binds_entry_edit = (geteditbind $ui_binds_current_key)
                                ui_binds_entry_spectator = (getspecbind $ui_binds_current_key)
                                ui_binds_entry_wait = (getwaitbind $ui_binds_current_key)
                            ] [] [] [] 0 [] "0xffffff" [] 1 //else just display at standard size
                        ]
                    ] [ //else fill in with the appropriate space
                        if (=s "_" (sublist $ui_binds_@[keyboardlayout]_vert $i 1)) [ //if "_", insert a full space
                            uifill $ui_checksize $ui_checksize
                        ] [ //else insert a half-space
                            uifill (divf $ui_checksize 2) $ui_checksize
                        ]
                    ]
                ]
            ]
        ]
        uibar 1 0
        uitext (concat "This is the" $ui_binds_current_key "key")
        uibar 1 0
        uihlist $ui_padsmall [
            uivlist $ui_padsmaller [
                uitext "Set Normal Bind"
                uitext "Set Editing Bind"
                uitext "Set Spectator Bind"
                uitext "Set Wait Bind"
            ]
            uivlist $ui_padsmaller [ //thirty-six character entry fields for each type of bind (normal, edit, spectator)
                uiinput "ui_binds_entry_normal" 36 [bind $ui_binds_current_key $ui_binds_entry_normal] 1 0 []
                uiinput "ui_binds_entry_edit" 36 [editbind $ui_binds_current_key $ui_binds_entry_edit] 1 0 []
                uiinput "ui_binds_entry_spectator" 36 [specbind $ui_binds_current_key $ui_binds_entry_spectator] 1 0 []
                uiinput "ui_binds_entry_wait" 36 [waitbind $ui_binds_current_key $ui_binds_entry_wait] 1 0 []
            ]
        ]
    ]
    uialign -1 0
]
