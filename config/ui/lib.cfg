defvarp uisounds 0 1 2
defvarp uiborders 0 1 1
defvarp uishadows 0 0 1

sound_press = (soundslot S_PRESS)
sound_back = (soundslot S_BACK)
sound_change = (soundslot S_ACTION)

ui_interacted = 0
ui_lastinteract = 0
ui_freecursor = 1

uiwidth = [
    result (*f $uiaspect $arg1)
]

// 1:<image> 2:<colour> 3:<alpha> 4:<minw> 5:<minh> 6:<children>
uiaspectimage = [
    uiimage $arg1 $arg2 $arg3 (*f $arg4 (gettexaspect $arg1)) $arg5 $arg6
]

uipre = [
    ui_interacted = 0
]

uipost = [
    if $ui_interacted [
        if (= $ui_lastinteract 0) [if (= $uisounds 2) [sound $sound_change]]
        ui_lastinteract = 1
    ] [
        ui_lastinteract = 0
    ]
]

uitoggle = [
    mouseoverride 0
    local topname; topname = (uitopwindow $arg2)
    if (&& (!=s $topname "") (!=s $topname "hud")) [
        if (gameui_has_panel_overlay) [
            gameui_overlay_close
        ] [
            gameui_close
        ]
    ] [
        gameui_main_handler
    ]
]

uiclose = [
    local topname; topname = (uitopwindow $arg2)
    if (&& (!=s $topname "") (!=s $topname "hud")) [
        if $uisounds [sound $sound_back]
        ui_interacted = 0
        ui_lastinteract = 1
    ]
    hideui "tooltip" $SURFACE_FOREGROUND
    hideui $arg1 $arg2
]

uiopen = [
    hideui "tooltip" $SURFACE_FOREGROUND
    showui $arg1 $arg2
]

uicursorset = [
    uihold [uicursortype 1] [uihover [uicursortype 1; ui_interacted = 1]]
]

uihighlighting = 0

uisethighlight = [
    uihighlighting = 1
]

uigethighlight = [
    result $uihighlighting
    uihighlighting = 0
]

uiinteract = [
    local highlighting
    highlighting = (uigethighlight)

    uihold [
        uicolourset $arg3
    ] [
        uihover [ highlighting = 1 ]

        if $highlighting [
            uicolourset $arg2
        ] [
            uicolourset $arg1
        ]
    ]
]

uidorotcolour = [
    uicolouradd $colourpink
    uicolourrotate (divf (mod $clockmillis 1000) 1000)
]

uidoxoffset = [
    if $ui_useoffcs [
        uifillcoord 0 $ui_offcoord 0
        uifillcoord 1 1 0
        uifillcoord 2 $ui_offcoord 1
        uifillcoord 3 0 1
    ]
]

// uiskin 1:<w> 2:<h> 3:<bgcolour> 4:<bghover> 5:<bghold> 6:<children> 7:[interact]
uiskin = [
    uiborderedimageclamped $skintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        if $arg7 [uiinteract $arg3 $arg4 $arg5 1]
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg6
    ]
]
uiblurskin = [
    uiborderedimageclamped $blurskintex $arg3 0 $ui_blurborder $ui_screenblur $arg1 $arg2 [
        doargs $arg4
    ]
]

// uiborder 1:<w> 2:<h> 3:<bordercolour> 4:<borderhover> 5:<borderhold> 6:<children> 7:[interact] 8:[forced]
uiborder = [
    if (|| $arg8 $uiborders) [
        uiborderedimageclamped $skinbordertex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
            if $arg7 [uiinteract $arg3 $arg4 $arg5]
            if $uishadows [uiimageshadow $ui_menu_s]
            uicolourdir 1
            doargs $arg6
        ]
    ] [
        uitarget $arg1 $arg2 [
            if $arg7 [uiinteract $arg3 $arg4 $arg5]
            doargs $arg6
        ]
    ]
]

// uiskinborder 1:<w> 2:<h> 3:<bgcolour> 4:<bghover> 5:<bghold> 6:<bordercolour> 7:<borderhover> 8:<borderhold> 9:<children> 10:[children2] 11:[interact] 12:[forced]
uiskinborder = [
    uiborderedimageclamped $skintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        if $arg11 [uiinteract $arg3 $arg4 $arg5 1]
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if (|| $arg12 $uiborders) [
            uiborderedimageclamped $skinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                uicolourdir 1
                doargs $arg9
            ]
        ] [
            uitarget $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                doargs $arg9
            ]
        ]
    ]
]

uitabskin = [
    uiborderedimageclamped $tabskintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        if $arg11 [uiinteract $arg3 $arg4 $arg5 1]
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if $uiborders [
            uiborderedimageclamped $tabskinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                uicolourdir 1
                doargs $arg9
            ]
        ] [
            uitarget $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                doargs $arg9
            ]
        ]
    ]
]

uivltabskin = [
    uiborderedimageclamped $vltabskintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        if $arg11 [uiinteract $arg3 $arg4 $arg5 1]
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if $uiborders [
            uiborderedimageclamped $vltabskinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                uicolourdir 1
                doargs $arg9
            ]
        ] [
            uitarget $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                doargs $arg9
            ]
        ]
    ]
]

uivrtabskin = [
    uiborderedimageclamped $vrtabskintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        if $arg11 [uiinteract $arg3 $arg4 $arg5 1]
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if $uiborders [
            uiborderedimageclamped $vrtabskinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                uicolourdir 1
                doargs $arg9
            ]
        ] [
            uitarget $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                doargs $arg9
            ]
        ]
    ]
]

uiitabskin = [
    uiborderedimageclamped $itabskintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        if $arg11 [uiinteract $arg3 $arg4 $arg5 1]
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if $uiborders [
            uiborderedimageclamped $itabskinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                uicolourdir 1
                doargs $arg9
            ]
        ] [
            uitarget $arg1 $arg2 [
                if $arg11 [uiinteract $arg6 $arg7 $arg8]
                doargs $arg9
            ]
        ]
    ]
]

uitabh = [
    uiborderedimageclamped $tabskintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        uiinteract $arg3 $arg4 $arg5 1
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if $uiborders [
            uiborderedimageclamped $tabskinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                uiinteract $arg6 $arg7 $arg8
                uicolourdir 1
                doargs $arg9
            ]
        ] [
            uitarget $arg1 $arg2 [
                uiinteract $arg6 $arg7 $arg8
                doargs $arg9
            ]
        ]
    ]
]

uitabv = [
    uiborderedimageclamped $vltabskintex $arg3 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
        uiinteract $arg3 $arg4 $arg5 1
        if $uishadows [uiimageshadow $ui_menu_s]
        doargs $arg10
        if $uiborders [
            uiborderedimageclamped $vltabskinbordertex $arg6 0 $ui_texborder $ui_screenborder $arg1 $arg2 [
                uiinteract $arg6 $arg7 $arg8
                uicolourdir 1
                doargs $arg9
                uistyle clampxy
            ]
        ] [
            uitarget $arg1 $arg2 [
                uiinteract $arg6 $arg7 $arg8
                doargs $arg9
                uistyle clampxy
            ]
        ]
        uistyle clampxy
    ]
]

// uiselect 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:[id "name" [action] ..] 5:(children) 6:tooltip
uiselect = [
    uihlist 0 [
        uiarrowbutton $arg2 $arg2 90 [
            local i; i = 0
            local t; t = [] // Target
            looplist3 v n c $arg4 [
                if (= (at $arg4 (* $i 3)) (getvar $arg3)) [
                    t = (- $i 1)
                ]
                i = (+ $i 1)
            ]
            i = 0
            looplist3 v n c $arg4 [
                if (= $i $t) [
                    if (<= $i (div (listlen $arg4) 3))
                    [@arg3] $v
                    doargs $c
                ]
                i = (+ $i 1)
            ]
        ] 0.5 []

        uiskinborder (-f $arg1 (*f $arg2 2)) $arg2 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
            local custom; custom = 1
            looplist3 v n c $arg4 [
                if (= $v (getvar $arg3)) [
                        uitextaligned $n
                        //uirelease [doargs $arg4]
                        custom = 0
                ]
            ]
            if $custom [uitextaligned "^fWCustom"]
            uistyle clampxy
        ] [] 1 // There's probably a uibutton or something that does the same thing

        uiarrowbutton $arg2 $arg2 270 [
            local i; i = 0
            local t; t = [] // Target
            looplist3 v n c $arg4 [
                if (= (at $arg4 (* $i 3)) (getvar $arg3)) [
                    t = (+ $i 1)
                ]
                i = (+ $i 1)
            ]; i = 0
            looplist3 v n c $arg4 [
                if (= $i $t) [
                    if (<= $i (div (listlen $arg4) 3))
                    [@arg3] $v
                    doargs $c
                ]
                i = (+ $i 1)
            ]
            uistyle clampx
        ] 0.5 []

        doargs $arg5
        if (uifocus+? 0 1) [
                if (!=s $arg6 "") [uitip $arg6]
        ]
    ]
]

// uitableskin 1:<bgcolour> 2:<bghover> 3:<bghold> 4:<bordercolour> 5:<borderhover> 6:<borderhold> 7:<children> 8:<row-children> 9:[interact]
uitableskin = [
    uitablerow [
        doargs $arg7
        uiskinborder 0.01 0.01 $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [doargs $arg8] [] $arg9
    ]
]

uihudskin = [
    uiskinborder $arg1 $arg2 $arg3 $arg3 $arg3 $arg4 $arg4 $arg4 [
        uipad (*f $ui_screenborder 0.5) (*f $ui_screenborder 0.5) (*f $ui_screenborder 0.5) (*f $ui_screenborder 0.5) [doargs $arg7]
    ] [
        uicolouradd $arg5
        if $arg6 [uicolourrotate (divf (mod $clockmillis 1000) 1000)]
        doargs $arg8
    ] 0
]

uihudtab = [
    uispace $ui_padtiny 0 [
        uitabskin $arg1 $arg2 $arg3 $arg3 $arg3 $arg4 $colourpink $colourred [
            uicursorset
            uirelease [doargs $arg9] [
                uihover [
                    if $arg10 [uitip $arg10]
                ]
            ]
            uipad (*f $ui_screenborder 0.5) (*f $ui_screenborder 0.5) (*f $ui_screenborder 0.5) 0 [doargs $arg7]
        ] [doargs $arg8] 1
    ]
]

uihudvtab = [
    uispace 0 $ui_padtiny [
        uivltabskin $arg1 (-f $arg2 (*f $ui_padtiny 2)) $arg3 $arg3 $arg3 $arg4 $arg4 $arg4 [uispace $ui_padtiny $ui_padtiny [doargs $arg7]] [
            uicolouradd $arg5
            if $arg6 [uicolourrotate (divf (mod $clockmillis 1000) 1000)]
            uispace $ui_padtiny $ui_padtiny [doargs $arg8]
        ] 0
    ]
]

uihudmenu = [
    uispace $ui_padtiny 0 [
        uiitabskin (-f $arg1 (*f $ui_padtiny 2)) $arg2 $arg3 $arg3 $arg3 $arg4 $arg4 $arg4 [uispace $ui_padtiny $ui_padtiny [doargs $arg7]] [
            uicolouradd $arg5
            if $arg6 [uicolourrotate (divf (mod $clockmillis 1000) 1000)]
            uispace $ui_padtiny $ui_padtiny [doargs $arg8]
        ] 0
    ]
]

uisizeskin = [
    if $arg7 [] [arg7 = 0]
    uiskinborder $arg1 $arg2 $arg3 $arg3 $arg3 $arg4 $arg4 $arg4 [
        doargs $arg6
        uispace (+f $arg7 $ui_padbutton) $ui_padsmaller [
            uistyle clampxy
            doargs $arg5
        ]
    ] [] $arg8
]

uielemskin = [
    if $arg5 [] [arg5 = 0]
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uistyle clampxy
        doargs $arg4
        uispace (+f $arg5 $ui_padbutton) $ui_padsmaller [
            uistyle clampxy
            doargs $arg3
        ]
    ] [uistyle clampxy] $arg6
]

uibackskin = [
    if $arg5 [] [arg5 = 0]
    uiskinborder 0 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uistyle clampxy
        doargs $arg4
        uispace (+f $arg5 $ui_padbutton) $ui_padsmaller [
            uihlist 0 [doargs $arg3]
        ]
    ] [uistyle clampxy] $arg6
]

uielemspace = [
    if $arg3 [] [arg3 = 0]
    uispace 0 0 [
        uistyle clampxy
        doargs $arg2
        uispace (+f $arg3 $ui_padbutton) $ui_padsmaller [
            uistyle clampxy
            doargs $arg1
        ]
    ] [uistyle clampxy]
]

// uibar 1:<clampx> 2:<clampy>
uibar = [
    uiline $ui_line 0 0 [uiclamp $arg1 $arg1 $arg2 $arg2]
]

// uidrawtriangle 1:<colour> 2:<w> 3:<h> 4:<orient> 5:[outline]
uidrawtriangle = [
    if $arg1 [] [arg1 = $ui_bline]
    uigroup [
        uitriangle $arg1 $arg2 $arg3 $arg4
        if $arg5 [uitriangleoutline $arg5 $arg2 $arg3 $arg4]
    ]
]

// uidrawcircle 1:<colour> 2:<size> 3:[outline]
uidrawcircle = [
    if $arg1 [] [arg1 = $ui_bline]
    uigroup [
        uicircle $arg1 $arg2
        if $arg3 [uicircleoutline $arg3 $arg2]
    ]
]

// uititle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
uititle = [
    uiskinborder $arg1 $arg2 $ui_menu $ui_menu $ui_menu $ui_line $ui_line $ui_line [
        uistyle clampxy
        doargs $arg1
    ]
]

// uitextaligned 1:<text> 2:[scale] 3:[align]  4:[children]
uitextaligned = [
    if $arg2 [] [arg2 = $ui_text]
    uitext $arg1 $arg2 [
        uialign (clamp $arg3 -1 1) 0
        uitextalign (clamp $arg3 -2 2)
        doargs $arg4
    ]
]

uitextleft = [
    if $arg2 [] [arg2 = $ui_text]
    uitextaligned $arg1 $arg2 -1 $arg3
]

uitextright = [
    if $arg2 [] [arg2 = $ui_text]
    uitextaligned $arg1 $arg2 1 $arg3
]

// uiwraptextalign 1:<text> 2:[scale] 3:[align] 4:[wrap] 5:[children]
uiwraptextalign = [
    if $arg2 [] [arg2 = $ui_text]
    uitext $arg1 $arg4 $arg2 [
        uitextalign (clamp $arg3 -2 2)
        uialign (clamp $arg3 -1 1) 0
        doargs $arg5
    ]
]

uiwraptextleft = [
    if $arg2 [] [arg2 = $ui_text]
    uiwraptextalign $arg1 $arg2 -1 $arg3 $arg4
]

uiwraptextright = [
    if $arg2 [] [arg2 = $ui_text]
    uiwraptextalign $arg1 $arg2 1 $arg3 $arg4
]

// uibuttonarea 1:(BUTTON-WIDTH) 2:(BUTTON-HEIGHT) 3:[on-release] 4:(ALIGNMENT) 5:[children] 6:DISABLED 7:[alt-release] 8:[tooltip] 9:[no-offset]
uibuttonarea = [
    uiskinborder $arg1 $arg2 (? $arg6 $ui_maind $ui_main1) (? $arg6 $ui_maind $ui_main2) (? $arg6 $ui_maind $ui_main3) (? $arg6 $ui_disa $ui_line) (? $arg6 $ui_disa $colourred) (? $arg6 $ui_disa $colourpink) [
        uistyle clampx
        if $arg9 [] [uidoxoffset]
        ui_buttonarea_temp = $ui_default
        ui_buttonarea_line = $ui_line
        ui_buttonarea_hover = 0
        if $arg6 [ui_buttonarea_temp = $ui_disable; ui_buttonarea_line = $ui_disable] [
            uicursorset
            uihold [ui_buttonarea_temp = $colourpink; ui_buttonarea_line = $colourpink] [uihover [ui_buttonarea_temp = $colourred; ui_buttonarea_line = $colourred; ui_buttonarea_hover = 1]]
            uirelease [doargs $arg3]
            uialtrelease [doargs $arg7]
            uitip $arg8
        ]
        uipropchild [uistyle clampxy]
        uispace (? $arg9 0 (? $ui_useoffcs $ui_offspace $ui_offpad)) 0 [
            uistyle clampxy
            doargs $arg5
        ]
    ] [if $arg9 [] [uidoxoffset]] 1
]

// uibuttonf 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour] 13:[tooltip] 14:[text-size]
uibuttonf = [
    if $arg9 [] [arg9 = $ui_padbutton]
    if $arg11 [] [arg11 = $ui_default]
    if $arg12 [] [arg12 = $ui_default]
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        ui_buttonf_size = 0
        if $arg2 [
            ui_buttonf_size = (*f $arg4 0.8)
            uispace $arg9 0 [
                uistyle clampx
                uihlist 0 [
                    uialign (? $arg1 -1 0) 0
                    uiimage $arg2 (? $arg8 $ui_buttonarea_temp $arg12) 0 $ui_buttonf_size $ui_buttonf_size [uiimageshadow $ui_menu_s]
                ]
            ]
        ]
        if $arg1 [
            uispace (+f $arg9 $ui_buttonf_size) 0 [
                uistyle clampx
                uihlist 0 [
                    uialign $arg6 0
                    uicolourtext $arg1 (? $arg8 $ui_buttonarea_temp $arg11) $arg14
                ]
            ]
        ]
        doargs $arg7
    ] $arg8 $arg10 $arg13
]

// uibuttonm 1:<text> 2:<image> 3:[on-release] 4:DISABLED 5:[tooltip] 6:[children] 7:[alt-release]
uibuttonm = [
    uibuttonf $arg1 $arg2 $ui_buttonw $ui_buttonh $arg3 0 $arg6 $arg4 $ui_padbutton $arg7 $ui_default $ui_default $arg5 $ui_textbig
]

// uibuttonw 1:<text> 2:[on-release] 3:DISABLED 4:[colour] 5:[tooltip] 6:[children] 7:[alt-release]
uibuttonw = [
    uibuttonf $arg1 "" 0 $ui_buttonh $arg2 0 $arg6 $arg3 $ui_padbutton $arg7 $arg4 $arg4 $arg5 $ui_text
]

// uibuttonz 1:<text> 2:[on-release] 3:DISABLED 4:[colour] 5:[tooltip] 6:[size] 7:[text-size] 8:[padding-size] 9:[children] 10:[alt-release]
uibuttonz = [
    uibuttonf $arg1 "" 0 $arg6 $arg2 0 $arg9 $arg3 $arg8 $arg10 $arg4 $arg4 $arg5 $arg7
]

// uibutton 1:<text> 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[on-release] 5:(ALIGNMENT) 6:[children] 7:DISABLED 8:[padding] 9:[alt-release] 10:[text-colour] 11:[tooltip] 12:[text-size]
uibutton = [
    uibuttonf $arg1 "" $arg2 $arg3 $arg4 $arg5 $arg6 $arg7 $arg8 $arg9 $arg10 $ui_default $arg11 $arg12
]

// uibuttonimg 1:<text> 2:<image> 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:DISABLED 9:[padding] 10:[alt-release] 11:[text-colour] 12:[image-colour] 13:[tooltip] 14:[text-size] 15:[no-offset]
uibuttonimg = [
    if $arg14 [] [arg14 = $ui_text]
    uibuttonarea $arg3 $arg4 $arg5 $arg6 [
        uispace (? (>= $numargs 9) $arg9 $ui_padbutton) 0 [
            uistyle clampx
            uihlist 0 [
                uialign $arg6 0
                if $arg2 [
                    ui_size = (*f $arg4 0.85)
                    uiimage $arg2 (? (|| $arg8 (! $arg12)) $ui_buttonarea_temp $arg12) 0 $ui_size $ui_size [uiimageshadow $ui_menu_s]
                ]
                if $arg1 [
                    if $arg2 [uispace (*f (? (>= $numargs 9) $arg9 $ui_padbutton) 0.35)]
                    uicolourtext $arg1 (? (|| $arg8 (< $numargs 11)) $ui_buttonarea_temp $arg11) $arg14
                ]
            ]
        ]
        doargs $arg7
    ]  $arg8 $arg10 $arg13 $arg15
]

// uiimgbutton 1:<image> 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[on-release] 5:(ALIGNMENT) 6:[children] 7:DISABLED 8:[padding] 9:[alt-release] 10:[image-colour] 11:[tooltip] 12:[no-offset]
uiimgbutton = [
    uibuttonarea $arg2 $arg3 $arg4 $arg5 [
        uispace (? (>= $numargs 8) $arg8 $ui_padbutton) (? (>= $numargs 8) $arg8 $ui_padbutton) [
            uistyle clampxy
            uihlist 0 [
                uistyle clampxy
                uialign $arg5 0
                uiimage $arg1 (? (|| $arg7 (< $numargs 10)) $ui_buttonarea_temp $arg10) 0 $arg2 $arg3 [
                    uioutline $ui_buttonarea_line 0 0 [if $ui_buttonarea_hover [uidorotcolour]]
                    uiimageshadow $ui_menu_s
                    uiprev [uistyle clampxy]
                    @@@@arg6
                ]
            ]
        ]
    ]  $arg7 $arg9 $arg11 $arg12
]

// uicheckbox 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-ON2 7:0xcolour-BG 8:[children] 9:[tooltip] 10:DISABLED 11:[text-size]
uicheckbox = [
    if $arg2 [] [arg2 = 0]
    if $arg6 [] [arg6 = $colourmagenta]
    if $arg5 [] [arg5 = $colourred]
    if $arg7 [] [arg7 = $ui_bline]
    uitarget 0 0 [
        ui_checkbox_temp = $arg7
        ui_checkbox_hover = 0
        if $arg10 [ui_checkbox_temp = $ui_disable] [
            uicursorset
            uihold [ui_checkbox_temp = $colourpink] [uihover [ui_checkbox_temp = $colourred; ui_checkbox_hover = 1]]
            uirelease [doargs $arg4]
        ]
        doargs $arg8
        uipropchild [uistyle clampxy]
        uihlist 0 [
            uiimage $checkboxtex $ui_checkbox_temp 0 $arg3 $arg3 [
                uipropchild [uistyle clampxy]
                uiimageshadow $ui_menu_s
                uicolourdir 1
                if $ui_checkbox_hover [uidorotcolour]
                if $arg2 [
                    uiimage $checkboxontex (? $arg10 $colouryellow (? $arg2 $arg5 $arg6)) 0 $arg3 $arg3 [uistyle centermiddle; uiimageshadow $ui_menu_s]
                ]
            ]
            if $arg1 [
                uispace $ui_padsmall 0 [
                    uicolourtext $arg1 $ui_checkbox_temp $arg11 [if $ui_checkbox_hover [uidorotcolour]]
                ]
            ]
        ]
    ]
    if (&& (uifocus+? 0 1) (!=s $arg9 "")) [uitip $arg9]
]
//1:<title> 2:[on-release] 3:0xcolour-ON 4:0xcolour-ON2 5:0xcolour-BG 6:[children] 7:DISABLED 8:[text-size]
uitextexec = [
    if $arg5 [] [arg5 = $ui_bline]
    if $arg4 [] [arg4 = $colourmagenta]
    if $arg3 [] [arg3 = $colourred]
    uitarget 0 0 [
        ui_textexec_temp = $arg5
        ui_textexec_hover = 0
        if $arg7 [ui_textexec_temp = $ui_disable] [
            uicursorset
            uihold [ui_textexec_temp = $colourpink] [uihover [ui_textexec_temp = $colourred; ui_checkbox_hover = 1]]
            uirelease [doargs $arg2]
        ]
        doargs $arg7
        uipropchild [uistyle clampxy]
        if $arg1 [
            uispace $ui_padsmall 0 [
                uicolourtext $arg1 $ui_textexec_temp $arg8 [if $ui_textexec_hover [uidorotcolour]]
            ]
        ]
    ]
]

// uicheckbutton 1:<image> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children] 8:DISABLED
uicheckbutton = [
    if $arg2 [] [arg2 = 0]
    if $arg6 [] [arg6 = $ui_line]
    if $arg5 [] [arg5 = $colourred]
    uiskinborder $arg3 $arg3 (? $arg8 $ui_maind $ui_main1) (? $arg8 $ui_maind $ui_main2) (? $arg8 $ui_maind $ui_main3) (? $arg8 $ui_disa (? $arg2 $arg5 $ui_line)) (? $arg8 $ui_disa $colourred) (? $arg8 $ui_disa $colourpink) [
        ui_checkbutton_temp = (? $arg2 $arg5 $arg6)
        if $arg8 [ui_checkbutton_temp = (? $arg8 $colouryellow $ui_disable)] [
            uicursorset
            uihold [ui_checkbutton_temp = $colourpink] [uihover [ui_checkbutton_temp = $colourred]]
            uirelease [doargs $arg4]
        ]
        doargs $arg7
        uipropchild [uistyle clampxy]
        uihlist 0 [
            uistyle clampx
            uistyle centermiddle
            ui_size = (*f $arg3 0.9)
            uiimage $arg1 $ui_checkbutton_temp 0 $ui_size $ui_size [uistyle centermiddle; uiimageshadow $ui_menu_s]
        ]
    ] [] 1
]

// uichecktext 1:<text> 2:[condition] 3:(W) 4:(H) 5:[on-release] 6:0xcolour-ON 7:0xcolour-BG 8:[children] 9:DISABLED 10:[text-size]
uichecktext = [
    if $arg2 [] [arg2 = 0]
    if $arg7 [] [arg7 = $ui_line]
    if $arg6 [] [arg6 = $colourred]
    if $arg10 [] [arg10 = $ui_textsmaller]
    uiskinborder $arg3 $arg4 $ui_main1 $ui_main2 $ui_main3 (? $arg2 $arg6 $ui_line) $colourred $colourpink [
        ui_checktext_temp = (? $arg2 $arg6 $arg7)
        ui_checktext_hover = 0
        uidoxoffset
        if $arg9 [ui_checktext_temp = $ui_disable] [
            uicursorset
            uihold [ui_checktext_temp = $colourpink] [uihover [ui_checktext_temp = $colourred; ui_checktext_hover = 1]]
            uirelease [doargs $arg5]
        ]
        doargs $arg8
        uipropchild [uistyle clampxy]
        uispace (? $ui_useoffcs $ui_offspace $ui_offpad) 0 [
            uistyle clampxy
            uihlist 0 [
                uistyle clampx
                uistyle centermiddle
                uispace $ui_padsmall 0 [
                    uistyle clampx
                    uistyle centermiddle
                    uicolourtext $arg1 $ui_checktext_temp $arg10 [if $ui_checktext_hover [uidorotcolour]]
                ]
            ]
        ]
    ] [uidoxoffset] 1
]

// uichecktextbutton 1:<text> 2:<image> 3:[condition] 4:(W) 5:(H) 6:[on-release] 7:0xcolour-ON 8:0xcolour-BG 9:[children] 10:tooltip 11:DISABLED 12:[text-size]
uichecktextbutton = [
    if $arg3 [] [arg3 = 0]
    if $arg8 [] [arg8 = $ui_line]
    if $arg7 [] [arg7 = $colourred]
    if $arg12 [] [arg12 = $ui_textsmaller]
    uiskinborder $arg4 $arg5 $ui_main1 $ui_main2 $ui_main3 (? $arg3 $arg7 $ui_line) $colourred $colourpink [
        ui_checktext_temp = (? $arg3 $arg7 $arg8)
        ui_checktext_hover = 0
        uidoxoffset
        if $arg11 [ui_checktext_temp = $ui_disable] [
            uicursorset
            uihold [ui_checktext_temp = $colourpink] [uihover [ui_checktext_temp = $colourred; ui_checktext_hover = 1]]
            uirelease [doargs $arg6]
        ]
        doargs $arg9
        uipropchild [uistyle clampxy]
        uispace (? $ui_useoffcs $ui_offspace $ui_offpad) 0 [
            uistyle clampxy
            uihlist 0 [
                uistyle leftmiddle
                ui_size = (*f $arg5 0.8)
                uispace $ui_padsmall 0 [
                    uiimage $arg2 $arg7 0 $ui_size $ui_size [uistyle centermiddle; uiimageshadow $ui_menu_s]
                ]
            ]
            uihlist 0 [
                uistyle clampx
                uistyle centermiddle
                uispace $ui_padsmall 0 [
                    uistyle clampx
                    uistyle centermiddle
                    uicolourtext $arg1 $ui_checktext_temp $arg12 [if $ui_checktext_hover [uidorotcolour]]
                ]
            ]
        ]
    ] [uidoxoffset] 1
    if (uifocus+? 0 1) [
        if (!=s $arg10 "") [uitip $arg10]
    ]
]

// uiradio 1:<title> 2:[condition] 3:(SIZE) 4:[on-release] 5:0xcolour-ON 6:0xcolour-BG 7:[children] 8:DISABLED 9:[text-size]
uiradio = [
    if $arg2 [] [arg2 = 0]
    if $arg6 [] [arg6 = $ui_bline]
    if $arg5 [] [arg5 = $colourred]
    uitarget 0 0 [
        ui_radio_temp = $arg6
        ui_radio_hover = 0
        if $arg8 [ui_radio_temp = $ui_disable] [
            uicursorset
            uihold [ui_radio_temp = $colourpink] [uihover [ui_radio_temp = $colourred; ui_radio_hover = 1]]
            uirelease [doargs $arg4]
        ]
        doargs $arg7
        uipropchild [uistyle clampxy]
        uihlist 0 [
            uiimage $radioboxtex $ui_radio_temp 0 $arg3 $arg3 [
                uipropchild [uistyle clampxy]
                uiimageshadow $ui_menu_s
                uicolourdir 1
                if $ui_radio_hover [uidorotcolour]
                if $arg2 [uiimage $radioboxontex (? $arg8 $colouryellow $arg5) 0 $arg3 $arg3 [uistyle centermiddle; uiimageshadow $ui_menu_s]]
            ]
            if $arg1 [
                uispace $ui_padsmall 0 [
                    uicolourtext $arg1 $ui_radio_temp $arg9 [if $ui_radio_hover [uidorotcolour]]
                ]
            ]
        ]
    ]
]

// uiarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
uiarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uiskinborder $arg1 $arg2 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
        doargs $arg6
        uirelease [doargs $arg4]
        uipropchild [uistyle clampxy]
        uidrawtriangle $ui_bline $arrowsize $arrowsize $arg3 $ui_line
    ] [] 1
]

uicmdbuffer = []

// Note: uicmdbuffer.push replaces any duplicate instances of a given command.
// uicmdbuffer.push 1:[contents]
uicmdbuffer.push = [
        if (=s $uicmdbuffer []) [
            uicmdbuffer = (concatword "[" $arg1 "]")
        ] [
            local i; i = 0
            local found; found = 0
            looplist e $uicmdbuffer [
                if (=s (sublist $arg1 0 1) (sublist $e 0 1)) [
                    uicmdbuffer = (listsplice $uicmdbuffer (concatword "[" $arg1 "]") $i 1)
                    found = 1
                ]
                i = (+ $i 1)
            ]
            if (! $found) [
                uicmdbuffer = (concat $uicmdbuffer (concatword "[" $arg1 "]"))
            ]
        ]
]

// Note: [contents] only needs the command name. Anything after the first space will be discarded.
// uicmdbuffer.pop 1:[contents]
uicmdbuffer.pop = [
    local r; r = 0
    local i; i = 0
    looplist e $uicmdbuffer [
        if (=s (sublist $arg1 0 1) (sublist $e 0 1)) [
            r = $e
            uicmdbuffer = (listsplice $uicmdbuffer "" $i 1)
        ]
        i = (+ $i 1)
    ]
    result $r
]

// uihtab 1:[FILL-W] 2:[FILL-H] 3:(TAB-W) 4:(TAB-H) 5:VAR 6:[id "name" [action] ..] 7:[contents]
uihtab = [
    if $arg3 [] [arg3 = $ui_tabw]
    if $arg4 [] [arg4 = $ui_tabh]
    uivlist 0 [
        uihlist 0 [
            looplist3 v n c $arg6 [
                uispace $ui_padsmaller 0 [
                    uitabh $arg3 $arg4 (? (=s $$arg5 $v) $ui_main2 $ui_main1) (? (=s $$arg5 $v) $ui_main1 $ui_main2) $ui_main3 (? (=s $$arg5 $v) $ui_line $ui_disa) (? (=s $$arg5 $v) $ui_line $colourred) (? (=s $$arg5 $v) $ui_line $colourpink) [
                        if (!=s $$arg5 $v) [
                            uicursorset
                            uirelease [$arg5 = $v]
                            doargs $c
                        ]
                        uidoxoffset
                        uispace (? $ui_useoffcs $ui_offspace $ui_offpad) 0 [
                            uispace $ui_padbutton $ui_padsmall [uicolourtext $n (? (=s $$arg5 $v) $colourwhite $colourgrey)]
                        ]
                    ] [uidoxoffset]
                ]
            ]
        ]
        uiborder $arg1 $arg2 $ui_line $ui_line $ui_line [
            doargs $arg7
        ] 0 1
    ]
]

// uivtab 1:[FILL-W] 2:[FILL-H] 3:(TAB-W) 4:(TAB-H) 5:VAR 6:[id "name" [action] ..] 7:[contents]
uivtab = [
    if $arg3 [] [arg3 = $ui_tabw]
    if $arg4 [] [arg4 = $ui_tabh]
    uihlist 0 [
        uivlist 0 [
            looplist3 v n c $arg6 [
                uispace 0 $ui_padsmaller [
                    uitabv $arg3 $arg4 (? (=s $$arg5 $v) $ui_main2 $ui_main1) (? (=s $$arg5 $v) $ui_main1 $ui_main2) $ui_main3 (? (=s $$arg5 $v) $ui_line $ui_disa) (? (=s $$arg5 $v) $ui_line $colourred) (? (=s $$arg5 $v) $ui_line $colourpink) [
                        if (!=s $$arg5 $v) [
                            uicursorset
                            uirelease [$arg5 = $v]
                            doargs $c
                        ]
                        uidoxoffset
                        uispace (? $ui_useoffcs $ui_offspace $ui_offpad) 0 [
                            uispace $ui_padbutton $ui_padsmall [uicolourtext $n (? (=s $$arg5 $v) $colourwhite $colourgrey)]
                        ]
                    ] [uidoxoffset]
                    uistyle clampxy
                ]
            ]
            uistyle clampx 1 0
            uistyle centertop
        ]
        uiborder $arg1 $arg2 $ui_line $ui_line $ui_line [
            doargs $arg7
        ] 0 1
    ]
]

// uivscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uivscroll = [
    if $arg3 [] [arg3 = 1.0]
    if $arg4 [] [arg4 = 0.5]
    uiskinborder $arg1 $arg2 $ui_main1 $ui_main1 $ui_main1 $ui_line $colourred $colourpink [
        uicursorset
        uivlist 0 [
            uistyle clampxy
            uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg1 $arg1 0 [] $arg4]
            uivscrollbar [
                uifill $arg1 (-f $arg2 (*f $arg1 2)) [uiprev [uistyle clampxy]]
                uiscrollbutton [
                    uiskinborder $arg1 0 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uistyle clampxy
                        uipropchild [uistyle clampxy]
                        uivlist 0.004 [loop i 3 [uiline $ui_line (*f $arg1 0.5)]]
                    ] [uistyle clampxy] 1
                ]
            ]
            uiscrollarrow $arg3 [uiarrowbutton $arg1 $arg1 180 [] $arg4]
        ]
    ] [] 1
]

// uihscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
uihscroll = [
    if $arg3 [] [arg3 = 1.0]
    if $arg4 [] [arg4 = 0.5]
    uiskinborder $arg1 $arg2 $ui_main1 $ui_main1 $ui_main1 $ui_line $colourred $colourpink [
        uicursorset
        uihlist 0 [
            uistyle clampxy
            uiscrollarrow (-f 0 $arg3) [uiarrowbutton $arg2 $arg2 90 [] $arg4]
            uihscrollbar [
                uifill (-f $arg1 (*f $arg2 2)) $arg2 [uiprev [uistyle clampxy]]
                uiscrollbutton [
                    uiskinborder 0 $arg2 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uistyle clampxy
                        uipropchild [uistyle clampxy]
                        uihlist 0.004 [loop i 3 [uiline $ui_line (*f $arg2 0.5)]]
                    ] [uistyle clampxy] 1
                ]
            ]
            uiscrollarrow $arg3 [uiarrowbutton $arg2 $arg2 270 [] $arg4]
        ]
    ] [] 1
]

// uivertslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
uivertslider = [
    if $arg9 [] [arg9 = 0.5]
    uiskinborder $arg6 $arg5 $ui_main1 $ui_main1 $ui_main1 $ui_line $colourred $colourpink [
        uicursorset
        uivlist 0 [
            uisliderarrow -1 [uiarrowbutton $arg5 $arg5 0 [] $arg9]
            uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
                uifill (-f $arg6 (*f $arg5 2)) $arg5 [uiprev [uistyle clampxy]]
                uisliderbutton [
                    uiskinborder 0 $arg2 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uistyle clampxy
                        uipropchild [uistyle clampxy]
                        uispace (? (>= $numargs 10) $arg11 $ui_padbutton) 0 $arg7
                    ] [uistyle clampxy] 1
                ]
            ]
            uisliderarrow 1 [uiarrowbutton $arg5 $arg5 180 [] $arg9]
        ]
    ] [] 1
]

// uihorzslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[tooltip] 9:[on-change] 10:(ARROW-%-SIZE)
uihorzslider = [
    if $arg10 [] [arg10 = 0.5]
    uiskinborder $arg5 $arg6 $ui_main1 $ui_main1 $ui_main1 $ui_line $colourred $colourpink [
        uicursorset
        uihlist 0 [
            uisliderarrow -1 [uiarrowbutton $arg6 $arg6 90 [] $arg10]
            uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg9 [
                uifill (-f $arg5 (*f $arg6 2)) $arg6 [uiprev [uistyle clampxy]]
                uisliderbutton [
                    uiskinborder 0 $arg6 $ui_main2 $ui_main3 $ui_main1 $ui_line $colourred $colourpink [
                        uistyle clampxy
                        uipropchild [uistyle clampxy]
                        uispace (? (>= $numargs 10) $arg11 $ui_padbutton) 0 $arg7
                    ] [uistyle clampxy]
                ]
            ]
            uisliderarrow 1 [uiarrowbutton $arg6 $arg6 270 [] $arg10]
        ]
    ] [] 1
    if (uifocus+? 0 1) [
        if (!=s $arg8 "") [uitip $arg8]
    ]
]

// uivcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change] 9:[colour3, colour4, colourN]
uivcolourslider = [
    uifill $arg6 $arg7 [
        uipad $ui_screenborder $ui_screenborder $ui_screenborder $ui_screenborder [
            uistyle clampxy
            uicursorset
            uiborderedimageclamped $skintex $arg5 0 $ui_texborder $ui_screenborder 0 0 [
                uistyle clampxy
                uicolouradd $arg4
                uicolourdir 0
                if $uishadows [uiimageshadow $ui_menu_s]
                looplist ui_vcolourslider_col [@@arg9] [uicolouradd $ui_vcolourslider_col]
            ]
            uipad $ui_screenborder $ui_screenborder $ui_screenborder $ui_screenborder [
                uistyle clampxy
                uihslider $arg1 $arg2 $arg3 1 $arg8 [
                    uistyle clampxy
                    uisliderbutton [uihgradient $ui_line $ui_default $ui_screenborder $arg7 [uicolouradd $ui_line]]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $skinbordertex $ui_line 0 $ui_texborder $ui_screenborder 0 0 [
                    uiinteract $ui_line $colourred $colourpink
                    uicolourdir 0
                    uistyle clampxy
                ]
            ] [
                uitarget 0 0 [
                    uiinteract $ui_line $colourred $colourpink
                    uistyle clampxy
                ]
            ]
        ]
    ]
    uigroup [
        uicursorset
        uiborderedimageclamped $skintex $arg5 0 $ui_texborder $ui_screenborder $arg6 $arg7 [
            uistyle centerbottom
            uicolouradd $arg4
            if $uishadows [uiimageshadow $ui_menu_s]
            looplist ui_vcolourslider_col [@@arg9] [uicolouradd $ui_vmeter_col]
        ]
        uivslider $arg1 $arg2 $arg3 1 $arg8 [
            uifill $arg6 (*f 0.98 $arg7)
            uisliderbutton [uiline $ui_default $arg6 0]
        ]
    ]
]

// uihcolourslider 1:VAR 2:MIN 3:MAX 4:0xcolour1 5:0xcolour2 6:X 7:Y 8:[on-change] 9:[colour3, colour4, colourN]
uihcolourslider = [
    uifill $arg6 $arg7 [
        uipad $ui_screenborder $ui_screenborder $ui_screenborder $ui_screenborder [
            uistyle clampxy
            uicursorset
            uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder 0 0 [
                uistyle clampxy
                uicolouradd $arg5
                uicolourdir 1
                if $uishadows [uiimageshadow $ui_menu_s]
                looplist ui_hcolourslider_col [@@arg9] [uicolouradd $ui_hcolourslider_col]
            ]
            uipad $ui_screenborder $ui_screenborder $ui_screenborder $ui_screenborder [
                uistyle clampxy
                uihslider $arg1 $arg2 $arg3 1 $arg8 [
                    uistyle clampxy
                    uisliderbutton [uivgradient $ui_line $ui_default $ui_screenborder $arg7 [uicolouradd $ui_line]]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $skinbordertex $ui_line 0 $ui_texborder $ui_screenborder 0 0 [
                    uiinteract $ui_line $colourred $colourpink
                    uicolourdir 1
                    uistyle clampxy
                ]
            ] [
                uitarget 0 0 [
                    uiinteract $ui_line $colourred $colourpink
                    uistyle clampxy
                ]
            ]
        ]
    ]
]

//uilistslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
uilistslider = [
    if $arg7 [] [arg7 = 0.5]
    uihlist 0 [
        uiarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        uifill (-f $arg4 (*f $arg5 2)) $arg5 [
            uicolour $ui_main2
            uipropchild [uistyle clampxy]
            if (=s $arg8 "") [
                uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result "custom"]) 0.7
            ] arg8
        ]
        uiarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]

//uiinput 1:VAR 2:LENGTH 3:[on-change] 4:SCALE 5:[immediate] 6:[prompt] 7:[promptcolour] 8:[tooltip] 9:[children] 10:[children2]
uiinput = [
    if $arg7 [] [arg7 = $colourgrey]
    uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
        uicursorset
        doargs $arg10
        uispace $ui_padsmall $ui_padsmaller [
            ui_input_prompt = ""
            if (uifocus+? 0 1) [
                if (&& (!=s $arg6 "") (=s $$arg1 "")) [ui_input_prompt = $arg6]
                if (!=s $arg8 "") [uitip $arg8]
            ]
            uifield $arg1 $arg2 $arg3 $arg4 $arg5 [
                @(? (!=s $ui_input_prompt "") [uicolourtext $ui_input_prompt $arg7 (*f $arg4 $ui_texttiny)])
                @arg9
            ]
            uicolour // workaround for stale color states
        ]
    ] [] 1
]

//uimlinput 1:VAR 2:LENGTH 3:HEIGHT 4:LIMIT 5:[on-change] 6:SCALE 7:[immediate] 8:[prompt] 9:[promptcolour] 10:[tooltip] 11:[children] 12:[children2]
uimlinput = [
    if $arg9 [] [arg9 = $colourgrey]
    uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
        uicursorset
        doargs $arg12
        uispace $ui_padsmall $ui_padsmaller [
            ui_input_prompt = ""
            if (uifocus+? 0 1) [
                if (&& (!=s $arg8 "") (=s $$arg1 "")) [ui_input_prompt = $arg8]
                if (!=s $arg10 "") [uitip $arg10]
            ]
            uimlfield $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 $arg7 [
                @(? (!=s $ui_input_prompt "") [uicolourtext $ui_input_prompt $arg9 (*f $arg6 $ui_texttiny)])
                @arg11
            ]
            uicolour // workaround for stale color states
        ]
    ] [] 1
]

uirgbsliders = [
    // arg1 is an alias that takes a hex colour value
    colr = (& (>> $$arg1 16) 0xFF)
    colg = (& (>> $$arg1 8) 0xFF)
    colb = (& $$arg1 0xFF)
    uivlist 0 [
        uistyle clampx
        loop i 3 [
            n = [col@(at "r g b" $i)]
            uihlist 0 [
                uihcolourslider $n 0 255 0x000000 (at [0xFF0000 0x00FF00 0x0000FF] $i) $ui_rgbw $ui_rgbh [[@@@@@arg1] = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)]
            ]
        ]
    ]
]

uihexpreview = [
    uiskinborder $ui_hexprev 0 $arg1 $arg1 $arg1 $arg2 $arg2 $arg2 [
        uispace $ui_padsmall $ui_padbutton [
            uivlist $ui_padbutton [
                uitext (format "^fw%1^fa,^fw%2^fa,^fw%3" (& (>> $arg1 16) 0xFF) (& (>> $arg1 8) 0xFF) (& $arg1 0xFF))
                uitext (hexcolour $arg1)
            ]
        ]
    ]
]

// player preview easter egg
ui_playerprev_eggon = 0
ui_playerprev_eggonres = 0
ui_playerprev_eggonrestime = 0
ui_playerprev_eggtime = 0
ui_playerprev_easteregg = [
    uirelease [
        ui_playerprev_eggoff = (- $clockmillis $ui_playerprev_eggtime)
        if (< $ui_playerprev_eggoff 250) [
            ui_playerprev_eggon = (! $ui_playerprev_eggon)
            if $ui_playerprev_eggon [
                ui_playerprev_eggonres = (rnd $W_R_MAX)
                ui_playerprev_eggonrestime = $clockmillis
            ]
        ] [ui_playerprev_eggtime = $clockmillis]
    ]
    if $ui_playerprev_eggon [
        ui_playerprev_eggmillis = (- $clockmillis $ui_playerprev_eggonrestime)
        if (< $ui_playerprev_eggmillis 5000) [
            uiplayerpreviewresidual $ui_playerprev_eggonres $ui_playerprev_eggonrestime 5000 1000
        ] [ui_playerprev_eggon = 0]
    ]
]

// 1:amt 2:w 3:h 4:colour 5:outline 6:colourlist 7:children 8:children2
uivmeter = [
    uifill $arg2 $arg3 [
        uistyle centermiddle
        uigroup [
            uiborderedimageclamped $skintex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg8
            ]
            if (>f $arg1 0) [
                ui_vmeter_h = (*f $arg3 (clampf $arg1 0.0 1.0))
                uiclip $arg2 $ui_vmeter_h 0 (-f $arg3 $ui_vmeter_h) [
                    uistyle centerbottom
                    uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uistyle centerbottom
                        looplist ui_vmeter_col [@@arg6] [uicolouradd $ui_vmeter_col]
                        @@arg7
                    ]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $skinbordertex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                    uistyle clampxy
                    @@arg8
                ]
            ] [
                uitarget $arg2 $arg3 [
                    uistyle clampxy
                    @@arg8
                ]
            ]
        ]
    ]
]

uivmetera = [
    uifill $arg2 $arg3 [
        uialign 0 $arg9
        uigroup [
            uiborderedimageclamped $tabskintex $arg6 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                uialign 0 $arg9
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg10
                @@arg11
            ]
            if (>f $arg1 0) [
                ui_vmeter_h = (*f $arg3 (clampf $arg1 0.0 1.0))
                ui_vmeter_z = (? (< $arg9 0) 0 (-f $arg3 $ui_vmeter_h))
                uiclip $arg2 $ui_vmeter_h 0 $ui_vmeter_z [
                    uialign 0 $arg9
                    uiborderedimageclamped $tabskintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uialign 0 $arg9
                        looplist ui_vmeter_col [@@arg7] [uicolouradd $ui_vmeter_col]
                        @@arg8
                        @@arg11
                    ]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $tabskinbordertex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                    uistyle clampxy
                    uialign 0 $arg9
                    @@arg10
                    @@arg11
                ]
            ] [
                uitarget $arg2 $arg3 [
                    uistyle clampxy
                    uialign 0 $arg9
                    @@arg10
                    @@arg11
                ]
            ]
        ]
    ]
]

uihmeter = [
    uifill $arg2 $arg3 [
        uistyle centermiddle
        uigroup [
            uiborderedimageclamped $skintex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg8
            ]
            if (>f $arg1 0) [
                ui_hmeter_w = (*f $arg2 (clampf $arg1 0.0 1.0))
                uiclip $ui_hmeter_w $arg3 0 0 [
                    uistyle leftmiddle
                    uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uistyle leftmiddle
                        uicolourdir 1
                        looplist ui_hmeter_col [@@arg6] [uicolouradd $ui_hmeter_col]
                        @@arg7
                    ]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $skinbordertex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                    uistyle clampxy
                    @@arg8
                ]
            ] [
                uitarget $arg2 $arg3 [
                    uistyle clampxy
                    @@arg8
                ]
            ]
        ]
    ]
]

uihmetera = [
    uifill $arg2 $arg3 [
        uialign $arg9 0
        uigroup [
            uiborderedimageclamped $skintex $arg6 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                uialign $arg9 0
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg10
                @@arg11
            ]
            if (>f $arg1 0) [
                ui_hmeter_w = (*f $arg2 (clampf $arg1 0.0 1.0))
                ui_hmeter_z = (? (< $arg9 0) 0 (-f $arg2 $ui_hmeter_w))
                uiclip $ui_hmeter_w $arg3 $ui_hmeter_z 0 [
                    uialign $arg9 0
                    uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uialign $arg9 0
                        uicolourdir 1
                        looplist ui_hmeter_col [@@arg7] [uicolouradd $ui_hmeter_col]
                        @@arg8
                        @@arg11
                    ]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $skinbordertex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                    uistyle clampxy
                    uialign $arg9 0
                    @@arg10
                    @@arg11
                ]
            ] [
                uitarget $arg2 $arg3 [
                    uistyle clampxy
                    uialign $arg9 0
                    @@arg10
                    @@arg11
                ]
            ]
        ]
    ]
]

uivthrob = [
    uifill $arg2 $arg3 [
        uistyle centermiddle
        uigroup [
            ui_vthrob_h = (*f $arg3 0.125)
            ui_vthrob_s = (-f $arg3 $ui_vthrob_h)
            ui_vthrob_t = (*f (clampf $arg1 0.0 1.0) 2)
            if (>f $ui_vthrob_t 1) [ui_vthrob_t = (-f 2 $ui_vthrob_t)]
            ui_vthrob_y = (*f $ui_vthrob_s $ui_vthrob_t)
            uiborderedimageclamped $skintex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg8
            ]
            uipad 0 0 $ui_vthrob_y 0 [
                uistyle centertop
                uiclip $arg2 $ui_vthrob_h 0 $ui_vthrob_y [
                    uistyle centertop
                    uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uistyle centertop
                        looplist ui_vthrob_col [@@arg6] [uicolouradd $ui_vthrob_col]
                        @@arg7
                    ]
                ]
            ]
        ]
    ]
]

uihthrob = [
    uifill $arg2 $arg3 [
        uistyle centermiddle
        uigroup [
            ui_hthrob_w = (*f $arg2 0.125)
            ui_hthrob_s = (-f $arg2 $ui_hthrob_w)
            ui_hthrob_t = (*f (clampf $arg1 0.0 1.0) 2)
            if (>f $ui_hthrob_t 1) [ui_hthrob_t = (-f 2 $ui_hthrob_t)]
            ui_hthrob_x = (*f $ui_hthrob_s $ui_hthrob_t)
            uiborderedimageclamped $skintex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg8
            ]
            uipad $ui_hthrob_x 0 0 0 [
                uistyle leftmiddle
                uiclip $ui_hthrob_w $arg3 $ui_hthrob_x 0 [
                    uistyle leftmiddle
                    uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uistyle leftmiddle
                        uicolourdir 1
                        looplist ui_hthrob_col [@@arg6] [uicolouradd $ui_hthrob_col]
                        @@arg7
                    ]
                ]
            ]
        ]
    ]
]

uihthroba = [
    uifill $arg2 $arg3 [
        uistyle centermiddle
        uigroup [
            ui_hthrob_w = (*f $arg2 0.125)
            ui_hthrob_s = (-f $arg2 $ui_hthrob_w)
            ui_hthrob_t = (*f (clampf $arg1 0.0 1.0) 2)
            if (>f $ui_hthrob_t 1) [ui_hthrob_t = (-f 2 $ui_hthrob_t)]
            ui_hthrob_x = (*f $ui_hthrob_s $ui_hthrob_t)
            uiborderedimageclamped $skintex $arg6 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                uistyle clampxy
                if $uishadows [uiimageshadow $ui_menu_s]
                @@arg9
            ]
            uipad $ui_hthrob_x 0 0 0 [
                uistyle leftmiddle
                uiclip $ui_hthrob_w $arg3 $ui_hthrob_x 0 [
                    uistyle leftmiddle
                    uiborderedimageclamped $skintex $arg4 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                        uistyle leftmiddle
                        uicolourdir 1
                        looplist ui_hthrob_col [@@arg7] [uicolouradd $ui_hthrob_col]
                        @@arg8
                    ]
                ]
            ]
            if $uiborders [
                uiborderedimageclamped $skinbordertex $arg5 0 $ui_texborder $ui_screenborder $arg2 $arg3 [
                    uistyle clampxy
                    uialign $arg9 0
                    @@arg10
                ]
            ] [
                uitarget $arg2 $arg3 [
                    uistyle clampxy
                    uialign $arg9 0
                    @@arg10
                ]
            ]
        ]
    ]
]

uicursorpos = [
    concat $uicursorx $uicursory
]

uicursorpxx = [
    toint (*f (divf $uicursorx $uiaspect) $screenw)
]

uicursorpxy = [
    - $screenh (toint (*f $uicursory $screenh))
]

defvarp doubleclicktime 100 200 1000

ui_last_press = 0
ui_last_press_id = ""

// 1:<id> 2:<code>
uidoublepress = [
    uipress [
        local cur_millis millis_diff
        cur_millis = $totalmillis
        millis_diff = (- $cur_millis $ui_last_press)
        if (&& (=s $ui_last_press_id $arg1) (<= $millis_diff $doubleclicktime)) [
            doargs $arg2
        ]
        ui_last_press = $cur_millis
        ui_last_press_id = $arg1
    ]
]
