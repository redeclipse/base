ui_score_gscolour = ["^fr" "^fr" "^fr" "^fr" "^fr" "^fg" "^fzoy" "^fg"]

//ui_score_alivecolour:
//Inputs: none
//Outputs: ^f7 (white) or ^f4 (grey) depending upon if $ui_score_player is alive or not
ui_score_alivecolour = [
    if (> (getclientlastspawn ($ui_score_player)) (getclientlastdeath ($ui_score_player)) ) [
        result "^f7"
    ] [
        result "^f4"
    ]
]

uimenu "scoreboard" "Scoreboard" $deadtex [
    refreshscoreboard
    uivlist $ui_padbutton [
        uiclamp 1 1 1 1
        uialign 0 -1
        uivlist 0 [
            if (!=s $maptitle "") [
                uitext $maptitle $ui_text [uitextlimit -0.6; uicolourset $colourorange]
                if (!=s $mapauthor "") [
                    uihlist $ui_padnormal [
                        uitext by $ui_textsmaller
                        uitext $mapauthor $ui_text [uitextlimit -0.5; uicolourset $colouryellow]
                    ]
                ]
            ]
            if (!= (gamemode) $modeidxediting) [
                uitext (concatword (getgamestatestr 1) ", ^fs" (at $ui_score_gscolour (getgamestate)) (timestr (getgametimeremain) $showtimestyle) "^fS remaining")
            ]
        ]
        uispace $ui_padbutton
        uitable 0 0 [
            uiclamp 1 1 1 1
            ui_score_count = (numscoregroups)
            ui_score_race = (= (gamemode) $modeidxrace)
            ui_score_racetime = (&& $ui_score_race (! (& (mutators) $mutsbitgsp1)))
            uitableheader [
                uiclamp 1 1 1 1
                uielemspace [uitext "Name" $ui_text]
                uielemspace [
                    if $ui_score_race [
                        if $ui_score_racetime [uitext "Fastest" $ui_text] [uitext "Laps" $ui_text]
                    ] [uitext "Points" $ui_text]
                ]
                uielemspace [uitext "Frags" $ui_text]
                uielemspace [uitext "Deaths" $ui_text]
                if (isonline) [
                    uielemspace [uitext "CN" $ui_text]
                    uielemspace [uitext "Ping" $ui_text]
                    uielemspace [uitext "Rate" $ui_text]
                    uielemspace [uitext "Handle" $ui_text]
                ]
            ]
            ui_score_colour = 0xFFFFFF
            ui_score_colour1 = 0x111111
            ui_score_colour2 = 0x888888
            ui_score_spacer = 0
            loopscoregroups ui_score_curgroup [
                if $ui_score_spacer [uitablerow [uiclamp 1 1 1 1] [uiclamp 1 1 1 1; uispace 0 $ui_padsmall]]
                ui_score_spacer = 1
                ui_score_team = (getscoreteam $ui_score_curgroup)
                ui_score_teamid = (at $teamnames $ui_score_team)
                ui_score_colour = (getteamcolour $ui_score_team)
                ui_score_colour1 = (modcolour $ui_score_colour 0.1)
                ui_score_colour2 = (modcolour $ui_score_colour 0.5)
                if $ui_score_team [
                    uitablerow [uiclamp 1 1 1 1] [
                        uiclamp 1 1 1 1
                        uielemskin $ui_score_colour2 $ui_score_colour1 [
                            uihlist 0 [
                                uialign -1 0
                                uitextleft (concat "Team" $[team@[ui_score_teamid]name]) $ui_text
                            ]
                            uihlist 0 [
                                uialign 1 0
                                ui_score_total = (getscoretotal $ui_score_curgroup)
                                if $ui_score_race [
                                    if $ui_score_racetime [
                                        uitextright (concat "Fastest:" (? (> $ui_score_total 0) (timestr $ui_score_total $scoreracestyle) "-")) $ui_text
                                    ] [uitextright (concat "Laps:" $ui_score_total) $ui_text]
                                ] [uitextright (concat "Score:" $ui_score_total) $ui_text]
                                uispace $ui_padsmall
                                uiimage $[team@[ui_score_teamid]tex] $ui_score_colour 0 $ui_iconbigsize $ui_iconbigsize
                            ]
                        ]
                    ]
                ] [
                    uitablerow [uiclamp 1 1 1 1] [
                        uiclamp 1 1 1 1
                        uielemskin $ui_score_colour2 $ui_score_colour1 [
                            uihlist 0 [
                                uialign -1 0
                                uitextleft Players $ui_text
                            ]
                            uihlist 0 [
                                uialign 1 0
                                uiimage $[team@[ui_score_teamid]tex] $ui_score_colour 0 $ui_iconbigsize $ui_iconbigsize
                            ]
                        ]
                    ]
                ]
            ] [
                if $ui_score_spacer [uitablerow [uiclamp 1 1 1 1] [uiclamp 1 1 1 1; uispace 0 $ui_padsmall]]
                ui_score_spacer = 1
                ui_score_colour = (getteamcolour 0)
                ui_score_colour1 = (modcolour $ui_score_colour 0.1)
                ui_score_colour2 = (modcolour $ui_score_colour 0.5)
                uitablerow [uiclamp 1 1 1 1] [
                    uiclamp 1 1 1 1
                    uielemskin $ui_score_colour2 $ui_score_colour1 [
                        uihlist 0 [
                            uialign -1 0
                            uitextleft "Spectators" $ui_text
                        ]
                        uihlist 0 [
                            uialign 1 0
                            uiimage $spectatortex $ui_score_colour 0 $ui_iconbigsize $ui_iconbigsize
                        ]
                    ]
                ]
            ] [
                ui_score_alt = 0
                ui_score_rcol1 = (modcolour $ui_score_colour 0.2)
                ui_score_rcol2 = (modcolour $ui_score_colour 0.4)
                loopscoreboard ui_score_player $ui_score_curgroup [
                    ui_score_self = (= $ui_score_player (getplayercn))
                    ui_score_ccol1 = (? $ui_score_self $ui_score_colour $ui_score_colour1)
                    ui_score_ccol2 = (? $ui_score_alt $ui_score_rcol2 $ui_score_rcol1)
                    uitablerow [
                        uiclamp 1 1 1 1
                        uielemspace [uitext (concatword (ui_score_alivecolour) (getclientname $ui_score_player 0 0)) $ui_textsmall] [
                            uialign -1 0
                            uispace $ui_padbutton 0 [uialign -1 0; uiimage (getclientprivtex $ui_score_player) (getclientcolour $ui_score_player -1 2) 0 $ui_iconsize $ui_iconsize [uialign -1 0]]
                        ] (+f $ui_iconsize $ui_padbutton)
                        uielemspace [
                            if $ui_score_racetime [
                                ui_score_cptime = (getclientcptime $ui_score_player)
                                uitext (? (> $ui_score_cptime 0) (timestr $ui_score_cptime $scoreracestyle) "-") $ui_textsmall
                            ] [uitext (getclientpoints $ui_score_player) $ui_textsmall]
                        ]
                        uielemspace [uitext (getclientfrags $ui_score_player) $ui_textsmall]
                        uielemspace [uitext (getclientdeaths $ui_score_player) $ui_textsmall]
                        if (isonline) [
                            uielemspace [uitext $ui_score_player $ui_textsmall]
                            uielemspace [uitext (getclientping $ui_score_player) $ui_textsmall]
                            uielemspace [uitext (getclientpj $ui_score_player) $ui_textsmall]
                            uielemspace [uitext (getclienthandle $ui_score_player) $ui_textsmall]
                        ]
                    ] [
                        uiclamp 1 1 1 1
                        uielemskin $ui_score_ccol2 $ui_score_ccol1 [uiclamp 1 1 1 1] [uiclamp 1 1 1 1]
                    ]
                    ui_score_alt = (! $ui_score_alt)
                ]
            ]
        ]
    ]
] [] [] [
    ui_menu_title_scoreboard = (gamename (gamemode) (mutators) 0 32)
    ui_menu_icon_scoreboard = (modetexlist (gamemode) (mutators) -1)
] 0 [togglescores 0] 2 2 1
