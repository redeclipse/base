T_EDIT_OUTLINE_NONE = 0
T_EDIT_OUTLINE_WIREFRAME = 1
T_EDIT_OUTLINE_NORMAL = 2

tool_edit_outline = 0
tool_grabbing = 0

// 1:<description> 2:<tags>
tool_edit_register_control = [
    tool_register_control $arg1 ["settings" "editor" @arg2] "Editor"
]

tool_goto_control_Editor = [
    toolpanel_open tool_edit_settings left [
        p_title = "Editor settings"
    ]
]

tool_edit_outline_sync = [
    caseif $wireframe [
        tool_edit_outline = @T_EDIT_OUTLINE_WIREFRAME
    ] $outline [
        tool_edit_outline = @T_EDIT_OUTLINE_NORMAL
    ]
]

tool_edit_outline_set = [
    wireframe (= $tool_edit_outline @T_EDIT_OUTLINE_WIREFRAME)
    outline (= $tool_edit_outline @T_EDIT_OUTLINE_NORMAL)
]

tool_cube_edit_undercursor = [
    uitext (concatword "^fA" $tool_cube_edit_text ": ^fw" $tool_cube_edit_count) $ui_tool_text_size_xs
]

tool_sel_millis = 0

tool_cube_edit_last_millis = 0
tool_cube_edit_count       = 0
tool_cube_edit_text        = ""
tool_cube_edit_last_mode   = -1
tool_cube_edit_last_sel    = 0

tool_edit_proc = [
    if (< (- $totalmillis $tool_cube_edit_last_millis) 2000) [
        toolview_undercursor = tool_cube_edit_undercursor
    ]
]
tool_proc_add tool_edit_proc

tool_has_edge_sel = [
    || [
        getenginestat $ESTAT_SELCX
    ] [
        getenginestat $ESTAT_SELCY
    ] [
        & (getenginestat $ESTAT_SELCXS) 1
    ] [
        & (getenginestat $ESTAT_SELCYS) 1
    ]
]

// 1:<dir> 2:<mode>
tool_edit_face = [
    local _mode _needs_reset
    _mode = (|| (tool_has_edge_sel) (!= $arg2 1))
    _needs_reset = (|| [
        != $tool_cube_edit_last_mode $_mode
    ] [
        != $tool_cube_edit_last_sel $tool_sel_millis
    ])

    tool_cube_edit_last_sel = $tool_sel_millis

    if ($_needs_reset) [
        tool_cube_edit_count = 0
    ]

    tool_cube_edit_last_millis = $totalmillis

    if $_mode [
        if (& (getenginestat $ESTAT_ORIENT) 1) [
            tool_cube_edit_count = (- 8 (editface $arg1 $arg2))
        ] [
            tool_cube_edit_count = (editface $arg1 $arg2)
        ]

        tool_cube_edit_text  = "Cut face"
        tool_cube_edit_last_mode = 1
    ] [
        tool_cube_edit_count = (+ $tool_cube_edit_count (editface $arg1 $arg2))
        tool_cube_edit_text  = "Edit cubes"
        tool_cube_edit_last_mode = 0
    ]

]

// 1:<axis>
tool_focus_ent_axis = [
    if (!= (entmoveaxis) $arg1) [
        entmoveaxis $arg1
    ] [
        entmoveaxis -1
    ]
]

// 1:<axis>
tool_focus_moving_axis = [
    if (!= (movingaxis) $arg1) [
        movingaxis $arg1
    ] [
        movingaxis -1
    ]
]

// 1:<axis>
tool_focus_sel_orient = [
    local _orient
    _orient = (* $arg1 2)

    if (!= $selorient $_orient) [
        selorient $_orient
    ] [
        selorient (+ $_orient 1)
    ]

    tool_sel_millis = $totalmillis
]

// 1:<axis>
tool_action_focus_axis = [
    caseif [entmoving] [
        tool_focus_ent_axis $arg1
    ] [moving] [
        tool_focus_moving_axis $arg1
    ] [1] [
        tool_focus_sel_orient $arg1
    ]
]



////////////////////////////
// AUXILIARY KEY HANDLERS //
////////////////////////////



tool_context_handlers = []

tool_add_context_handler = [
    append tool_context_handlers $arg1
]

tool_handle_context = [
    local _handled _handler
    _handled = 0

    loopwhile i (listlen $tool_context_handlers) [!= $_handled 1] [
        _handler = (at $tool_context_handlers $i)
        _handled = ($_handler)
    ]
]

// 1:<modifier>
tool_modifier_handler = [
    caseif (&& (>= $arg1 11) (<= $arg1 20) (! (enthavesel)) (tool_nav_quicksel (- $arg1 11))) [
        result 1
    ] (1) [
        result 0
    ]
]

tool_esc_handler = [
    if (tool_nav_escape) [] [
        entmoving 0
        cancelsel
    ]
]

tool_enter_handler = [
    if (tool_nav_enter) [] [
        gameui_chat
    ]
]

tool_leftclick_handler = [
    caseif [$moving] [
        moving 0
    ] [$entmoving] [
        entmoving 0
    ] [$blendpaintmode] [
        if $curshiftmod rotateblendbrush paintblendmap
    ] (1) [
        if $curshiftmod editextend editdrag
    ]
]

tool_rightclick_press_time = 0
tool_rightclick_context_threshold = 200

tool_rightclick_handler = [
    if (&& $ui_freecursor (|| [! $curshiftmod] [$entmoving])) [
        mouseoverride 1
        tool_rightclick_press_time = $totalmillis
    ] [
        mouseoverride 0
        if $blendpaintmode [
            rotateblendbrush
        ] [
            editextend
        ]
    ]
    onrelease [
        mouseoverride 0
        if (< (- $totalmillis $tool_rightclick_press_time) $tool_rightclick_context_threshold) [
            tool_handle_context
        ]
    ]
]



/////////////
// ACTIONS //
/////////////



tool_action ta_menu [
    p_short_desc = "Main menu"
    p_icon       = "textures/icon"
    p_category   = "Editor"
    p_code       = [
        gameui_main_handler
    ]
]

tool_action ta_undo [
    p_short_desc = "Undo"
    p_icon       = "<grey>textures/icons/edit/undo"
    p_category   = "Editor"
    p_code       = [
        local _last_undo
        _last_undo = (undo)
        case $_last_undo @UNDO_CUBE [
            tool_info_show_action "Undo map edit" ta_undo
        ] @UNDO_ENT [
            tool_info_show_action "Undo entity edit" ta_undo
        ]
    ]
]

tool_action ta_redo [
    p_short_desc = "Redo"
    p_icon       = "<grey>textures/icons/edit/redo"
    p_category   = "Editor"
    p_code       = [
        local _last_redo
        _last_redo = (redo)
        case $_last_redo @UNDO_CUBE [
            tool_info_show_action "Redo map edit" ta_redo
        ] @UNDO_ENT [
            tool_info_show_action "Redo entity edit" ta_redo
        ]
    ]
]

tool_action ta_copy [
    p_short_desc = "Copy"
    p_icon       = "<grey>textures/icons/edit/copy"
    p_category   = "Editor"
    p_code       = [
        if (tool_copy_do_op $TOOL_COPY) [
            editcopy
        ]
    ]
]

tool_action ta_paste [
    p_short_desc = "Paste"
    p_icon       = "<grey>textures/icons/edit/paste"
    p_category   = "Editor"
    p_code       = [
        if (tool_copy_do_op $TOOL_PASTE) [
            editpaste
        ]
    ]
]

tool_action ta_edit_settings [
    p_short_desc = "Editor settings"
    p_icon       = "<grey>textures/icons/settings"
    p_category   = "Editor"
    p_code       = [
        toolpanel_toggle tool_edit_settings left [
            p_title = "Editor settings"
        ]
    ]
]

tool_action ta_edit_bindings [
    p_short_desc = "Editor bindings"
    p_long_desc  = "Change map editor key bindings"
    p_icon       = "<grey>textures/icons/settings"
    p_category   = "Editor"
    p_code       = [
        toolpanel_toggle tool_edit_bindings left [
            p_title = "Editor bindings"
        ]
    ]
]

tool_action ta_search [
    p_short_desc = "Search action"
    p_long_desc  = "Search for an editor action"
    p_icon       = "<grey>textures/icons/edit/find"
    p_category   = "Editor"
    p_code       = [
        if (toolpanel_isopen tool_search) [] [
            tool_search_init
        ]

        tool_search_focus = 1

        toolpanel_open tool_search center [
            p_title      = "Search action"
            p_nav_enable = 1
        ]
    ]
]

tool_action ta_cycle_outline [
    p_short_desc = "Cycle outline mode"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        if (= $tool_action_ctx $TA_CTX_KEY) [
            tool_edit_outline_sync

            local _val
            _val = (+ $tool_edit_outline 1)
            _val = (circclamp $_val $T_EDIT_OUTLINE_NONE (+ 1 $T_EDIT_OUTLINE_NORMAL))

            tool_edit_outline = $_val
            tool_edit_outline_set
        ] [
            tool_toolbar_focus_wireframe
        ]
    ]
]

tool_action ta_gridpower [
    p_short_desc = "Gridpower"
    p_long_desc  = "Change selection grid size"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_type       = $TA_TYPE_SCROLL
    p_code       = [
        domodifier 1
    ]
]

tool_action ta_pushface [
    p_short_desc = "Push cube face"
    p_long_desc  = "Push the selected face of a cube"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_type       = $TA_TYPE_SCROLL
    p_code       = [
        domodifier 2
    ]
]

tool_action ta_pushcorner [
    p_short_desc = "Push cube corner"
    p_long_desc  = "Push the pointed corner of a cube"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_type       = $TA_TYPE_SCROLL
    p_code       = [
        domodifier 3
    ]
]

tool_action ta_rotate [
    p_short_desc = "Rotate selection"
    p_long_desc  = "Rotates the contents of the selection"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_type       = $TA_TYPE_SCROLL
    p_code       = [
        domodifier 4
    ]
]

tool_action ta_flip [
    p_short_desc = "Flip selection"
    p_long_desc  = "Flip the contents of the selection"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        editflip
    ]
]

tool_action ta_cut [
    p_short_desc = "Cut selection"
    p_long_desc  = "Cut and move selection"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        editcut
    ]
]

tool_action ta_cycleside [
    p_short_desc = "Cycle cube side"
    p_long_desc  = "Cycles between sides of the selection"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_type       = $TA_TYPE_SCROLL
    p_code       = [
        domodifier 21
    ]
]

tool_action ta_focus_axis_x [
    p_short_desc = "Focus axis: X"
    p_long_desc  = "Focuses selection to X axis"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        tool_action_focus_axis 0
    ]
]

tool_action ta_focus_axis_y [
    p_short_desc = "Focus axis: Y"
    p_long_desc  = "Focuses selection to Y axis"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        tool_action_focus_axis 1
    ]
]

tool_action ta_focus_axis_z [
    p_short_desc = "Focus axis: Z"
    p_long_desc  = "Focuses selection to Z axis"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        tool_action_focus_axis 2
    ]
]

tool_action ta_toggle_fullbright [
    p_short_desc = "Toggle fullbright mode"
    p_icon       = "<grey>textures/icons/edit/light"
    p_category   = "Editor"
    p_code       = [
        fullbright (! $fullbright)
    ]
]

tool_action ta_cursor_mode [
    p_short_desc = "Toggle cursor mode"
    p_icon       = $cursortex
    p_category   = "Editor"
    p_code       = [
        if (= (getclientstate $playercn) $CS_EDITING) [
            ui_freecursor = (? $ui_freecursor 0 $clockmillis)
        ]
    ]
]

tool_action ta_cycle_gridview [
    p_short_desc = "Cycle grid view"
    p_long_desc  = "Cycles between different grid selection views"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Editor"
    p_code       = [
        togglegrid
    ]
]

tool_action ta_grab [
    p_short_desc = "Grab selection"
    p_long_desc  = "Grab selection in order to move it"
    p_icon       = "<grey>textures/icons/flagtaken"
    p_category   = "Editor"
    p_code       = [
        if (&& [enthavesel] [! $entmoving]) [
            entmoving 1 $curshiftmod
        ] [
            if (&& $hassel [! $moving]) [
                moving 1
            ]
        ]

        if (= $tool_action_ctx $TA_CTX_KEY) [
            tool_grabbing = 1
            onrelease [
                tool_grabbing = 0
            ]
        ]
    ]
]
