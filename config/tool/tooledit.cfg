T_EDIT_OUTLINE_NONE = 0
T_EDIT_OUTLINE_WIREFRAME = 1
T_EDIT_OUTLINE_NORMAL = 2

tool_edit_outline = 0
tool_edit_outline_colour = []

tool_edit_outline_sync = [
    @(tool_colour_fromint tool_edit_outline_colour outlinecolour)

    caseif $wireframe [
        tool_edit_outline = @T_EDIT_OUTLINE_WIREFRAME
    ] $outline [
        tool_edit_outline = @T_EDIT_OUTLINE_NORMAL
    ]
]

tool_edit_outline_set = [
    wireframe (= $tool_edit_outline @T_EDIT_OUTLINE_WIREFRAME)
    outline (= $tool_edit_outline @T_EDIT_OUTLINE_NORMAL)

    @(tool_colour_toint outlinecolour tool_edit_outline_colour)
]

tool_action_cycle_outline = [
    tool_edit_outline_sync
    tool_edit_outline = (circclamp (+ $tool_edit_outline 1) @T_EDIT_OUTLINE_NONE @(+ 1 $T_EDIT_OUTLINE_NORMAL))
    tool_edit_outline_set
]

tool_action_edit_settings = [
    toolpanel_toggle tool_edit_settings "Editor settings" left
]

tool_action_edit_bindings = [
    toolpanel_toggle tool_edit_bindings "Editor bindings" left
]

tool_action_search = [
    if (toolpanel_getpanel tool_search) [] [
        tool_search_init
    ]

    tool_search_focus = 1

    toolpanel_show tool_search "Search action" mid [
        uit_nav_enable = 1
    ]
]

tool_action_main_menu = [
    toolpanel_toggle tool_main_menu "Main menu" left
]

tool_action_undo = [
    local last_undo
    last_undo = (undo)
    case $last_undo @UNDO_CUBE [
        tool_info_show_action "Undo world edit" ta_undo
    ] @UNDO_ENT [
        tool_info_show_action "Undo entity edit" ta_undo
    ]
]

tool_action_redo = [
    local last_redo
    last_redo = (redo)
    case $last_redo @UNDO_CUBE [
        tool_info_show_action "Redo world edit" ta_redo
    ] @UNDO_ENT [
        tool_info_show_action "Redo entity edit" ta_redo
    ]
]

tool_action_grabngrid = [
    if (&& (enthavesel) (= $entdragmode $ENTDRAG_PRESSKEY)) [
        entmoving (! (entmoving)) 1
    ] [
        domodifier 1
    ]
]

// 1:<axis>
tool_focus_ent_axis = [
    if (!= (entmoveaxis) $arg1) [
        entmoveaxis $arg1
    ] [
        entmoveaxis -1
    ]
]

// 1:<axis>
tool_focus_sel_orient = [
    local orient
    orient = (* $arg1 2)

    if (!= $selorient $orient) [
        selorient $orient
    ] [
        selorient (+ $orient 1)
    ]
]

// 1:<axis>
tool_action_focus_axis = [
    if (entmoving) [
        tool_focus_ent_axis $arg1
    ] [
        tool_focus_sel_orient $arg1
    ]
]

tool_action ta_menu [
    short_desc "Main menu"
    long_desc "Open main menu"
    icon "textures/icon"
    category "Editor"
    action [
        tool_action_main_menu
    ]
]

tool_action ta_undo [
    short_desc "Undo"
    long_desc "Undo last edit"
    icon "textures/icons/edit/undo"
    category "Editor"
    action [
        tool_action_undo
    ]
]

tool_action ta_redo [
    short_desc "Redo"
    long_desc "Redo last edit"
    icon "textures/icons/edit/redo"
    category "Editor"
    action [
        tool_action_redo
    ]
]

tool_action ta_copy [
    short_desc "Copy"
    long_desc "Copy"
    icon "textures/icons/edit/copy"
    category "Editor"
    action [
        editcopy
    ]
]

tool_action ta_paste [
    short_desc "Paste"
    long_desc "Paste"
    icon "textures/icons/edit/paste"
    category "Editor"
    action [
        editpaste
    ]
]

tool_action ta_edit_settings [
    short_desc "Editor settings"
    long_desc "Change map editor settings"
    icon "textures/icons/settings"
    category "Editor"
    action [
        tool_action_edit_settings
    ]
]

tool_action ta_edit_bindings [
    short_desc "Editor bindings"
    long_desc "Change map editor key bindings"
    icon "textures/icons/settings"
    category "Editor"
    action [
        tool_action_edit_bindings
    ]
]

tool_action ta_search [
    short_desc "Search action"
    long_desc "Search for an editor action"
    icon "textures/icons/edit/find"
    category "Editor"
    action [
        tool_action_search
    ]
]

tool_action ta_cycle_outline [
    short_desc "Cycle outline mode"
    long_desc "Cycle outline mode"
    icon "textures/icons/edit/cube"
    category "Editor"
    action [
        tool_action_cycle_outline
    ]
]

tool_action ta_grabngrid [
    short_desc "Gridpower/Grab"
    long_desc "Change selection grid size or grab selection"
    icon "textures/icons/edit/cube"
    category "Editor"
    type @TOOL_ACTION_SCROLL
    action [
        tool_action_grabngrid
    ]
]

tool_action ta_pushface [
    short_desc "Push cube face"
    long_desc "Push the selected face of a cube"
    icon "textures/icons/edit/cube"
    category "Editor"
    type @TOOL_ACTION_SCROLL
    action [
        domodifier 2
    ]
]

tool_action ta_pushcorner [
    short_desc "Push cube corner"
    long_desc "Push the pointed corner of a cube"
    icon "textures/icons/edit/cube"
    category "Editor"
    type @TOOL_ACTION_SCROLL
    action [
        domodifier 3
    ]
]

tool_action ta_rotate [
    short_desc "Rotate selection"
    long_desc "Rotates the contents of the selection"
    icon "textures/icons/edit/cube"
    category "Editor"
    type @TOOL_ACTION_SCROLL
    action [
        domodifier 4
    ]
]

tool_action ta_flip [
    short_desc "Flip selection"
    long_desc "Flip the contents of the selection"
    icon "textures/icons/edit/cube"
    category "Editor"
    action [
        editflip
    ]
]

tool_action ta_cut [
    short_desc "Cut selection"
    long_desc "Cut and move selection"
    icon "textures/icons/edit/cube"
    category "Editor"
    action [
        editcut
    ]
]

tool_action ta_cycleside [
    short_desc "Cycle cube side"
    long_desc "Cycles between sides of the selection"
    icon "textures/icons/edit/cube"
    category "Editor"
    type @TOOL_ACTION_SCROLL
    action [
        domodifier 21
    ]
]

tool_action ta_focus_axis_x [
    short_desc "Focus axis: X"
    long_desc "Focuses selection to X axis"
    icon "textures/icons/edit/cube"
    category "Editor"
    action [
        tool_action_focus_axis 0
    ]
]

tool_action ta_focus_axis_y [
    short_desc "Focus axis: Y"
    long_desc "Focuses selection to Y exis"
    icon "textures/icons/edit/cube"
    category "Editor"
    action [
        tool_action_focus_axis 1
    ]
]

tool_action ta_focus_axis_z [
    short_desc "Focus axis: Z"
    long_desc "Focuses selection to Z exis"
    icon "textures/icons/edit/cube"
    category "Editor"
    action [
        tool_action_focus_axis 2
    ]
]
