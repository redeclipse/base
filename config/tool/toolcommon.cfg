tool_modes = [
    dm
    ctf
    dac
    bb
    race
]

tool_muts = [
    ffa
    coop
    insta
    medi
    kaboom
    duel
    surv
    classic
    ons
    vamp
    resize
    hard
    arena
]

tool_team_names = [
    "Neutral"
    "Alpha"
    "Omega"
]

tool_weapon_names = [
    [@@clawlongname]
    [@@pistollongname]
    [@@swordlongname]
    [@@shotgunlongname]
    [@@smglongname]
    [@@flamerlongname]
    [@@plasmalongname]
    [@@zapperlongname]
    [@@riflelongname]
    [@@grenadelongname]
    [@@minelongname]
    [@@rocketlongname]
]

tool_weapon_pickup_names = [
    @(sublist $tool_weapon_names 2)
]

tool_palette_ids = [
    [
        "No palette"
        "Dark fire"
        "Bright fire"
        "Disco"
        "Shock"
        "Bleed"
        "Buff"
        "Warning"
        "Regen"
        "Flash"
    ] [
        @@tool_team_names
    ] [
        @@tool_weapon_names
    ]
]

tool_palette_enforce_vals = [0 @(listlen $tool_team_names) 13]

UNDO_NONE = 0
UNDO_CUBE = 1
UNDO_ENT = 2

// 1:<dest> 2:<src>
tool_colour_fromint = [
    result [
        local _r _g _b
        inttorgb $@arg2 _r _g _b
        set @arg1 [@(divf $_r 255) @(divf $_g 255) @(divf $_b 255)]
    ]
]

// 1:<values> 2:<blue fix> 3:<zero fix>
tool_colour_toint = [
    local _r _g _b
    _r = (min (toint (round (*f (at $arg1 0) 255))) 255)
    _g = (min (toint (round (*f (at $arg1 1) 255))) 255)
    _b = (min (toint (round (*f (at $arg1 2) 255))) 255)

    if $arg2 [
        if (&& [= $_r 0] [= $_g 0] [> $_b 0]) [
            _r = 1
            _g = 1
        ]
    ]

    if $arg3 [
        if (&& [= $_r 0] [= $_g 0] [= $_b 0]) [
            _r = 1
            _g = 1
            _b = 1
        ]
    ]

    rgbtoint $_r $_g $_b
]

// 1:<integer value> 2:<h/s/v> 3:<mod value>
tool_colour_hsvmod_int = [
    local _vec _hue _sat _val
    _vec = []

    @(tool_colour_fromint [_vec] [arg1])
    _vec = (rgbtohsv $_vec)

    _hue = (at $_vec 0)
    _sat = (at $_vec 1)
    _val = (at $_vec 2)

    cases $arg2 h [
        _hue = (mod (+ $_hue $arg3) 360)
    ] s [
        _sat = (minf (*f $_sat $arg3) 1)
    ] v [
        _val = (minf (*f $_val $arg3) 1)
    ]

    _vec = [@_hue @_sat @_val]
    tool_colour_toint (hsvtorgb $_vec)
]

// 1:<colour integer 1> 2:<colour integer 2> 3:<factor> 4:<comp mask>
tool_colour_lerp_int = [
    local _vec1 _vec2 _vec3 _hue1 _hue2 _sat1 _sat2 _val1 _val2

    if (=s $arg4) [
        arg4 = [ 1 1 1 ]
    ]

    @(tool_colour_fromint [_vec1] [arg1])
    @(tool_colour_fromint [_vec2] [arg2])

    _vec1 = (rgbtohsv $_vec1); _vec2 = (rgbtohsv $_vec2)
    _hue1 = (at $_vec1 0);     _hue2 = (at $_vec2 0)
    _sat1 = (at $_vec1 1);     _sat2 = (at $_vec2 1)
    _val1 = (at $_vec1 2);     _val2 = (at $_vec2 2)

    caseif (=f $_sat1 0) [
        _hue1 = $_hue2
    ] (=f $_sat2 0) [
        _hue2 = $_hue1
    ]

    _vec3 = []
    append _vec3 (lerp360 $_hue1 $_hue2 (*f $arg3 (at $arg4 0)))
    append _vec3 (lerpf   $_sat1 $_sat2 (*f $arg3 (at $arg4 1)))
    append _vec3 (lerpf   $_val1 $_val2 (*f $arg3 (at $arg4 2)))

    tool_colour_toint (hsvtorgb $_vec3)
]

// 1:<colour integer>
tool_colour_rem_alpha = [
    if (colourhasalpha $arg1) [
        tohex (& $arg1 0x00FFFFFF)
    ] [
        result $arg1
    ]
]

// 1:<colour integer>
tool_colour_get_alpha = [
    if (colourhasalpha $arg1) [
        tohex (& (>> $arg1 24) 0xFF)
    ] [
        result 0xFF
    ]
]

// 1:<colour integer> 2:<alpha>
tool_colour_add_alpha = [
    if (colourhasalpha $arg1) [
        arg1 = (tool_colour_rem_alpha $arg1)
    ]

    tohex (| $arg1 (<< $arg2 24))
]

tool_procs = []

// 1:<function>
tool_proc_add = [
    if (listhas $tool_procs $arg1) [] [
        append tool_procs $arg1
    ]
]

// 1:<function>
tool_proc_remove = [
    local _index
    _index = (listfind=s $tool_procs $arg1)

    if (>= $_index 0) [
        tool_procs = (listsplice $tool_procs [] $_index 1)
    ]
]

tool_proc = [
    looplist proc $tool_procs [
        $proc
    ]
]

// 1:<index>
p_list = [
    result [
        p_get = [(at $arg1 @@arg1)]
        p_set = [(listsplice $$arg2 $arg1 @@arg1 1)]
        p_id = @arg1
    ]
]

// 1:<integer> 2:<byte>
tool_get_byte = [
    & (>> $arg1 (* $arg2 8)) 0xFF
]

// 1:<integer> 2:<byte val> 3:<byte>
tool_set_byte = [
    local _val _mask
    _mask = (^ (<< 0xFF (* $arg3 8)))
    _val  = (& $arg1 $_mask)

    | $_val (<< $arg2 (* $arg3 8))
]

// Returns a unique name.
// If a name already exists, instance number is appened at the end, e.g.:
// 'Item #2', 'Item #3', etc...
// 1:<requested name> 2:<existing names> 3:<index prefix>
tool_get_name = [
    local _base_name _base_name_len _new_name _idx_pos _idx _idx_prefix_len
    _base_name      = $arg1
    _base_name_len  = (strlen $_base_name)
    _new_name       = $arg1
    _idx            = 1
    _idx_prefix_len = (strlen $arg3)

    // Find the base name if requested name contains an index suffix
    // Ensure the remaining characters are all digits making up the index
    _idx_pos = (strstr $_base_name $arg3)
    if (&& [> $_idx_pos -1] [is_integer (substr $_base_name (+ $_idx_pos $_idx_prefix_len))]) [
        // Remove the index suffix, that's the base name
        _base_name = (substr $_base_name 0 $_idx_pos)
    ]

    // Add and increment the index suffix if name already exists
    while [listhas $arg2 $_new_name] [
        _idx = (+ $_idx 1)

        // Don't add the suffix for the first index
        if (> $_idx 1) [
            _new_name = (concatword $_base_name $arg3 $_idx)
        ]
    ]

    result $_new_name
]

tool_image_exts = ["jpg" "png" "tga"]
tool_sound_exts = ["wav" "ogg"]

TOOL_FILE_ANY = 0
TOOL_FILE_IMAGE = 1
TOOL_FILE_SOUND = 2
TOOL_FILE_CONFIG = 3
TOOL_FILE_ASSETPACK = 4

// 1:<type>
tool_file_type_exts = [
    case $arg1 @TOOL_FILE_ANY [
        result []
    ] @TOOL_FILE_IMAGE [
        result [@@tool_image_exts]
    ] @TOOL_FILE_SOUND [
        result [@@tool_sound_exts]
    ] @TOOL_FILE_CONFIG [
        result "cfg"
    ]
]

// 1:<file> 2:<type>
tool_file_is_type = [
    local _exts
    _exts = (tool_file_type_exts $arg2)

    if (!=s $_exts) [
        >= (listfind=s $_exts (fileext $arg1)) 0
    ] [
        result 1
    ]
]

// 1:<file>
tool_file_isimage = [
    tool_file_is_type $arg1 @TOOL_FILE_IMAGE
]

// 1:<file>
tool_file_ishidden = [
    = (strstr $arg1 ".") 0
]

// Strips texture commands from a texture path
// E.g. "<invert><mad:0/0.5/0.2>folder/image.jpg" -> "folder/image.jpg"
// 1:<path>
tool_path_striptexcmds = [
    local _bracket
    _bracket = (strrstr $arg1 ">")

    if (>= $_bracket 0) [
        substr $arg1 (+ $_bracket 1)
    ] [
        result $arg1
    ]
]

TOOL_VERSION = 3

tool_on_mapload_handlers = []

// 1:<handler>
tool_on_mapload = [
    append tool_on_mapload_handlers $arg1
]

tool_remember_version_on_mapload = [
    looplist _handler $tool_on_mapload_handlers [
        $_handler
    ]

    worldmeta tool_saved_version $TOOL_VERSION
]

onevent $CMD_EVENT_MAPLOAD tool_remember_version_on_mapload
