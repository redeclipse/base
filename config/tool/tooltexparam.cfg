tool_tex_param_names = []
tool_tex_param_getters = []
tool_tex_param_setters = []

tool_tex_shader_param_names = []
tool_tex_shader_param_getters = []
tool_tex_shader_param_setters = []
tool_tex_shader_param_filters = []

tool_tex_params_needs_sync = 0
tool_tex_params_last_commit = 0

tool_tex_param_next_idx = 0
tool_tex_shader_param_next_idx = 0

// 1:<name> 2:<shader filter> 3:<getter> 4:<setter>
tool_tex_add_shader_param = [
    append tool_tex_shader_param_names $arg1
    append tool_tex_shader_param_filters [[@@arg2]]
    append tool_tex_shader_param_getters [[@@arg3]]
    append tool_tex_shader_param_setters [[@@arg4]]

    [tool_tex_shader_param_idx_@arg1] = $tool_tex_shader_param_next_idx
    tool_tex_shader_param_next_idx = (+ $tool_tex_shader_param_next_idx 1)
]

// 1:<name> 2:<getter> 3:<setter>
tool_tex_add_param = [
    append tool_tex_param_names $arg1
    append tool_tex_param_getters [[@@arg2]]
    append tool_tex_param_setters [[@@arg3]]

    [tool_tex_param_idx_@arg1] = $tool_tex_param_next_idx
    tool_tex_param_next_idx = (+ $tool_tex_param_next_idx 1)
]

tool_tex_add_shader_params = [
    tool_tex_add_shader_param glowcolor "glow"
    tool_tex_add_shader_param pulseglowspeed "pulse" [
        [tool_tex_pulseglowspeed_val@arg1] = (at $[tool_tex_pulseglowspeed@arg1] 0)
    ] [
        tool_tex_pulseglowspeed = $tool_tex_pulseglowspeed_val
    ]
    tool_tex_add_shader_param pulseglowcolor "pulse"
    tool_tex_add_shader_param pulseglowoffset "pulse" [
        [tool_tex_pulseglowoffset_val@arg1] = (at $[tool_tex_pulseglowoffset@arg1] 0)
    ] [
        tool_tex_pulseglowoffset = $tool_tex_pulseglowoffset_val
    ]
    tool_tex_add_shader_param gloss "" [
        [tool_tex_gloss_val@arg1] = (toint (at $[tool_tex_gloss@arg1] 0))
    ] [
        tool_tex_gloss = $tool_tex_gloss_val
    ]
    tool_tex_add_shader_param specscale "spec" [
        [tool_tex_specscale_val@arg1] = (at $[tool_tex_specscale@arg1] 0)
    ] [
        tool_tex_specscale = $tool_tex_specscale_val
    ]
    tool_tex_add_shader_param parallaxscale "parallax" [
        [tool_tex_parallaxscale_low@arg1] = (at $[tool_tex_parallaxscale@arg1] 0)
        [tool_tex_parallaxscale_high@arg1] = (at $[tool_tex_parallaxscale@arg1] 1)
    ] [
        tool_tex_parallaxscale = [@tool_tex_parallaxscale_low @tool_tex_parallaxscale_high]
    ]
    tool_tex_add_shader_param envmin "env"
    tool_tex_add_shader_param envscale "env"
    tool_tex_add_shader_param triplanarbias "triplanar" [
        [tool_tex_triplanarbias_x@arg1] = (at $[tool_tex_triplanarbias@arg1] 0)
        [tool_tex_triplanarbias_y@arg1] = (at $[tool_tex_triplanarbias@arg1] 1)
        [tool_tex_triplanarbias_z@arg1] = (at $[tool_tex_triplanarbias@arg1] 2)
    ] [
        tool_tex_triplanarbias = [@tool_tex_triplanarbias_x @tool_tex_triplanarbias_y @tool_tex_triplanarbias_z]
    ]
    tool_tex_add_shader_param normalscale "bump" [
        [tool_tex_normalscale_val@arg1] = (at $[tool_tex_normalscale@arg1] 0)
    ] [
        tool_tex_normalscale = $tool_tex_normalscale_val
    ]
]

tool_tex_add_params = [
    tool_tex_add_param vscale
    tool_tex_add_param voffset [
        [tool_tex_voffsetx@arg1] = (at $[tool_tex_voffset@arg1] 0)
        [tool_tex_voffsety@arg1] = (at $[tool_tex_voffset@arg1] 1)
    ] [
        tool_tex_voffset = [@tool_tex_voffsetx @tool_tex_voffsety]
    ]
    tool_tex_add_param vscroll [
        [tool_tex_vscrollx@arg1] = (at $[tool_tex_vscroll@arg1] 0)
        [tool_tex_vscrolly@arg1] = (at $[tool_tex_vscroll@arg1] 1)
    ] [
        tool_tex_vscroll = [@tool_tex_vscrollx @tool_tex_vscrolly]
    ]
    tool_tex_add_param vrotate
    tool_tex_add_param vangle [
        [tool_tex_vangle_nudge@arg1] = (toint (round (*f 8 (tan $[tool_tex_vangle@arg1]))))
    ] [
        tool_tex_vangle = (atan (divf $tool_tex_vangle_nudge 8))
    ]
    tool_tex_add_param vlayer
    tool_tex_add_param vdetail
    tool_tex_add_param valpha [
        [tool_tex_valpha_front@arg1] = (at $[tool_tex_valpha@arg1] 0)
        [tool_tex_valpha_back@arg1] = (at $[tool_tex_valpha@arg1] 1)
    ] [
        tool_tex_valpha = [@tool_tex_valpha_front @tool_tex_valpha_back]
    ]
    tool_tex_add_param vcolour
    tool_tex_add_param vpalette [
        [tool_tex_vpalette_set@arg1] = (at $[tool_tex_vpalette@arg1] 0)
        [tool_tex_vpalette_id@arg1] = (at $[tool_tex_vpalette@arg1] 1)
        [tool_tex_vpalette_enforce@arg1] = 0

        local vpalette_set vpalette_id vpalette_id_enforce
        vpalette_set = $[tool_tex_vpalette_set@arg1]
        vpalette_id = $[tool_tex_vpalette_id@arg1]
        vpalette_id_enforce = (at $tool_palette_enforce_vals $vpalette_set)

        if (&& $vpalette_id_enforce (>= $vpalette_id $vpalette_id_enforce)) [
            [tool_tex_vpalette_id@arg1] = (mod $vpalette_id $vpalette_id_enforce)
            [tool_tex_vpalette_enforce@arg1] = 1
        ]
    ] [
        local vpalette_id
        vpalette_id = $tool_tex_vpalette_id

        if $tool_tex_vpalette_enforce [
            vpalette_id = (+ $vpalette_id (at $tool_palette_enforce_vals $tool_tex_vpalette_set))
        ]

        tool_tex_vpalette = [@tool_tex_vpalette_set @vpalette_id]
    ]
    tool_tex_add_param vshadow [
        [tool_tex_vshadow_en@arg1] = (>=f $[tool_tex_vshadow@arg1] 0)

        if $[tool_tex_vshadow_en@arg1] [
            [tool_tex_vshadow_alpha@arg1] = $[tool_tex_vshadow@arg1]
        ] [
            [tool_tex_vshadow_alpha@arg1] = 1
        ]
    ] [
        if $tool_tex_vshadow_en [
            tool_tex_vshadow = $tool_tex_vshadow_alpha
        ] [
            tool_tex_vshadow = -1
        ]
    ]
    tool_tex_add_param vrefract [
        [tool_tex_vrefract_strength@arg1] = (at $[tool_tex_vrefract@arg1] 0)
        [tool_tex_vrefract_colour@arg1] = (sublist $[tool_tex_vrefract@arg1] 1)
    ] [
        tool_tex_vrefract = [@tool_tex_vrefract_strength @tool_tex_vrefract_colour]
    ]
    tool_tex_add_param vcoastscale
]

tool_tex_populate_params = [
    tool_tex_param_names = []
    tool_tex_param_getters = []
    tool_tex_param_setters = []
    tool_tex_param_next_idx = 0

    tool_tex_shader_param_names = []
    tool_tex_shader_param_getters = []
    tool_tex_shader_param_setters = []
    tool_tex_shader_param_filters = []
    tool_tex_shader_param_next_idx = 0

    tool_tex_add_params
    tool_tex_add_shader_params

    tool_tex_params_num = $tool_tex_param_next_idx
    tool_tex_shader_params_num = $tool_tex_shader_param_next_idx
]

tool_tex_params_init = [
    tool_tex_populate_params
]

tool_tex_params_init

// 1:<default>
tool_tex_params_get = [
    local slot var_suffix param_name param_getter

    if $arg1 [
        slot = $tool_tex_variantsrc
        var_suffix = "_default"
    ] [
        slot = $tool_tex_cur
        var_suffix = ""
    ]

    loop i $tool_tex_params_num [
        param_name = (at $tool_tex_param_names $i)
        param_getter = (at $tool_tex_param_getters $i)

        [tool_tex_@param_name@var_suffix] = ([get@param_name] $slot)
        param_getter $var_suffix
    ]

    loop i $tool_tex_shader_params_num [
        param_name = (at $tool_tex_shader_param_names $i)
        param_getter = (at $tool_tex_shader_param_getters $i)

        [tool_tex_@param_name@var_suffix] = (getvshaderparam $slot $param_name)
        param_getter $var_suffix
    ]
]

tool_tex_params_sync = [
    local prev_tex
    prev_tex = $tool_tex_cur

    tool_focus_tex (getseltex 1) 1

    if (|| $tool_tex_params_needs_sync (!= $prev_tex $tool_tex_cur)) [
        tool_tex_params_needs_sync = 0
        tool_tex_params_get
        tool_tex_params_get 1
    ]
]

// 1:<param idx>
tool_tex_param_needs_commit = [
    local param_name
    param_name = (at $tool_tex_param_names $arg1)

    !=s ([get@param_name] $tool_tex_cur) $[tool_tex_@[param_name]]
]

// 1:<param idx>
tool_tex_shader_param_needs_commit = [
    local sparam_name needs_commit
    sparam_name = (at $tool_tex_shader_param_names $arg1)

    !=s (getvshaderparam $tool_tex_cur $sparam_name) $[tool_tex_@sparam_name]
]

// 1:<param idx>
tool_tex_accepts_param = [
    local shader_name sparam_filter
    shader_name = (getvshadername $tool_tex_cur)
    sparam_filter = (at $tool_tex_shader_param_filters $arg1)

    >= (strstr $shader_name $sparam_filter) 0
]

tool_tex_params_can_commit = [
    local interval cur_delta
    interval = (? (isonline) 500 250)
    cur_delta = (- (getmillis) $tool_tex_params_last_commit)

    >= $cur_delta $interval
]

// 1:<param idx> 2:<force>
tool_tex_param_commit = [
    if (|| $arg2 [tool_tex_params_can_commit]) [
        local param_name
        param_name = (at $tool_tex_param_names $arg1)
        do [
            @param_name @[tool_tex_@param_name]
        ]
        tool_tex_params_needs_sync = 1
        tool_tex_params_last_commit = (getmillis)
    ]
]

// 1:<param idx> 2:<force>
tool_tex_shader_param_commit = [
    if (|| $arg2 [tool_tex_params_can_commit]) [
        local sparam_name
        sparam_name = (at $tool_tex_shader_param_names $arg1)
        vdelta [
            do [
                vshaderparam @sparam_name @[tool_tex_@sparam_name]
            ]
        ]
        tool_tex_params_needs_sync = 1
        tool_tex_params_last_commit = (getmillis)
    ]
]

// 1:<force>
tool_tex_commit_all_params = [
    loop i $tool_tex_params_num [
        if (tool_tex_param_needs_commit $i) [
            tool_tex_param_commit $i $arg1
        ]
    ]

    loop i $tool_tex_shader_params_num [
        if (tool_tex_shader_param_needs_commit $i) [
            tool_tex_shader_param_commit $i $arg1
        ]
    ]
]

// 1:<param idx> 2:<commit>
tool_tex_param_change = [
    local param_setter
    param_setter = (at $tool_tex_param_setters $arg1)

    param_setter

    if (= $arg2 1) [
        tool_tex_param_commit $arg1
    ]
]

// 1:<param idx> 2:<commit>
tool_tex_shader_param_change = [
    local sparam_setter
    sparam_setter = (at $tool_tex_shader_param_setters $arg1)

    sparam_setter

    if (= $arg2 1) [
        tool_tex_shader_param_commit $arg1
    ]
]

// 1:<param idx>
tool_tex_shader_default = [
    local sparam_name
    sparam_name = (at $tool_tex_shader_param_names $arg1)

    result $(concatword [tool_tex_@sparam_name] _default)
]

// 1:<param idx> 2:<commit>
tool_tex_shader_param_init = [
    local sparam_name
    sparam_name = (at $tool_tex_shader_param_names $arg1)

    [tool_tex_@sparam_name] = (tool_tex_shader_default $arg1)
    tool_tex_shader_param_change $arg2
]

tool_tex_param_edit_begin = [
    tool_tex_params_needs_sync = 1
]

// 1:<commit>
tool_tex_param_edit_end = [
    if $arg1 [
        tool_tex_commit_all_params 1
    ]
]
