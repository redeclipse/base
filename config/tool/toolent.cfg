T_ENT_UI_EDIT = 0
T_ENT_UI_UNIFY = 1
T_ENT_UI_SEARCH = 2
T_ENT_UI_PLACER = 3

exec "config/tool/toolentparam.cfg"

tool_ent_types_env = [
    "light"
    "lightfx"
    "mapmodel"
    "envmap"
    "particles"
    "sound"
    "decal"
    "wind"
    "rail"
    "camera"
]

tool_ent_types_game = [
    "playerstart"
    "weapon"
    "teleport"
    "actor"
    "trigger"
    "pusher"
    "affinity"
    "checkpoint"
    "route"
]

defvarp enteditingonpanel 0 0 1 [
    entediting (? (toolpanel_isopen tool_ent) 1 0)
]

tool_ent_ui_mode = $T_ENT_UI_EDIT

tool_ent_placer_random = 1
tool_ent_placer_templates = []
tool_ent_placer_template_focus = -1

// 1:<attr idx> 2:<value>
tool_ent_placer_template_update = [
    if (>= $tool_ent_placer_template_focus 0) [
        local template
        template = (at $tool_ent_placer_templates $tool_ent_placer_template_focus)
        template = (listsplice $template $arg2 (+ $arg1 1) 1)
        tool_ent_placer_templates = (listsplice $tool_ent_placer_templates [[@@template]] $tool_ent_placer_template_focus 1)
    ] [
        error "tool_ent_placer_template_update error: invalid state!"
    ]
]

// 1:<index>
tool_ent_placer_template_setfocus = [
    tool_ent_placer_template_focus = $arg1
]

// 1:<index>
tool_ent_placer_template_isfocus = [
    = $arg1 $tool_ent_placer_template_focus
]

tool_ent_placer_template_hasfocus = [
    >= $tool_ent_placer_template_focus 0
]

tool_ent_placer_template_get = [
    if (tool_ent_placer_template_hasfocus) [
        at $tool_ent_placer_templates $tool_ent_placer_template_focus
    ]
]

// 1:<type>
tool_ent_placer_template_add = [
    local type_idx num_attrs template
    type_idx = (indexof $tool_ent_types $arg1)
    num_attrs = (entityattrs $type_idx 1)

    template = $arg1
    loop i $num_attrs [
        append template 0
    ]

    append tool_ent_placer_templates [[@@template]]
    tool_ent_placer_template_setfocus (- (listlen $tool_ent_placer_templates) 1)
]

tool_ent_placer_template_froment = [
    append tool_ent_placer_templates [[@@(entget)]]
    tool_ent_placer_template_setfocus (- (listlen $tool_ent_placer_templates) 1)
]

tool_ent_placer_template_duplicate = [
    if (tool_ent_placer_template_hasfocus) [
        append tool_ent_placer_templates [[@@(tool_ent_placer_template_get)]]
        tool_ent_placer_template_setfocus (- (listlen $tool_ent_placer_templates) 1)
    ]
]

// 1:<index>
tool_ent_placer_template_remove = [
    tool_ent_placer_templates = (listsplice $tool_ent_placer_templates "" $arg1 1)

    if (>= $tool_ent_placer_template_focus (listlen $tool_ent_placer_templates)) [
        tool_ent_placer_template_focus = -1
    ]
]

tool_ent_placer_canplace = [
    && (= $tool_ent_ui_mode @T_ENT_UI_PLACER) [!=s $tool_ent_placer_templates ""]
]

tool_ent_placer_place = [
    if (tool_ent_placer_canplace) [
        local temp_idx
        if $tool_ent_placer_random [
            temp_idx = (rnd (listlen $tool_ent_placer_templates))
        ] [
            temp_idx = $tool_ent_placer_template_focus
        ]

        if (>= $tool_ent_placer_template_focus 0) [
            doargs [
                newent @(at $tool_ent_placer_templates $temp_idx)
            ]
        ]
    ]
]

tool_ent_unify_type = 0
tool_ent_unify_mask = 0

tool_ent_unify_perform = [
    local ent ent_type type_idx num_attrs
    ent = (entget)
    ent_type = (at $ent 0)
    type_idx = (indexof $tool_ent_types $ent_type)
    num_attrs = (entityattrs $type_idx)

    entloop [
        if $tool_ent_unify_type [
            enttype $ent_type
        ]

        if (=s (enttype) $ent_type) [
            loop attridx $num_attrs [
                if (& $tool_ent_unify_mask (<< 1 $attridx)) [
                    entattr $attridx (at $ent (+ $attridx 1))
                ]
            ]
        ]
    ]
]

tool_ent_search_operators = [
    *
    =
    !=
    >
    <
    >=
    <=
    &
]

tool_ent_search_reset_filters = [
    tool_ent_search_mask = 0

    loop attridx 23 [
        [tool_ent_search_attr_@attridx] = 0
        [tool_ent_search_attr_op_@attridx] = 0
    ]
]

tool_ent_search_get_type = [
    if (tool_ent_has_sel) [
        local type_idx
        type_idx = (indexof $tool_ent_types (enttype))

        tool_ent_search_type = $type_idx
    ]
]

// 1:<attr idx>
tool_ent_search_get_attr = [
    if (tool_ent_has_sel) [
        [tool_ent_search_attr_@arg1] = (entattr $arg1)
    ]
]

tool_ent_search_get_filters = [
    if (tool_ent_has_sel) [
        local type_idx num_attrs
        type_idx = (indexof $tool_ent_types (enttype))
        num_attrs = (entityattrs $type_idx 1)

        tool_ent_search_type = $type_idx

        loop attridx $num_attrs [
            [tool_ent_search_attr_@attridx] = (entattr $attridx)
            [tool_ent_search_attr_op_@attridx] = 1
        ]
    ] [
        tool_ent_search_reset_filters
    ]
]

tool_ent_search_type = 0
tool_ent_search_insel = 0
tool_ent_search_reset_filters

tool_ent_search_filter_proc = [
    local res

    res = (=s (enttype) (at $tool_ent_types $tool_ent_search_type))

    local type_idx num_attrs op
    type_idx = (indexof $tool_ent_types (enttype))
    num_attrs = (entityattrs $type_idx 1)

    loopwhile attridx $num_attrs res [
        op = (at $tool_ent_search_operators $[tool_ent_search_attr_op_@attridx])
        if (&& [!=s $op "*"] [! ($op (entattr $attridx) $tool_ent_search_attr_@attridx)]) [
            res = 0
        ]
    ]

    result $res
]

tool_ent_search_find = [
    if $tool_ent_search_insel [
        entselect [&& insel tool_ent_search_filter_proc]
    ] [
        entselect tool_ent_search_filter_proc
    ]
]

// 1:<mode> 2:<init>
tool_ent_toggle_ui_mode = [
    if (&& [toolpanel_isopen tool_ent] [= $tool_ent_ui_mode $arg1]) [
        tool_ent_ui_mode = $T_ENT_UI_EDIT
    ] [
        do $arg2
        tool_ent_ui_mode = $arg1
    ]
]

tool_action_ents = [
    toolpanel_toggle tool_ent "Entities" right
]

tool_action_ent_unify = [
    toolpanel_show tool_ent "Entities" right
    tool_ent_toggle_ui_mode $T_ENT_UI_UNIFY [
        tool_ent_unify_mask = 0
    ]
]

tool_action_ent_search = [
    tool_ent_search_get_filters
    tool_ent_toggle_ui_mode $T_ENT_UI_SEARCH
    toolpanel_show tool_ent "Entities" right
]

tool_action_ent_placer = [
    tool_ent_toggle_ui_mode $T_ENT_UI_PLACER
    toolpanel_show tool_ent "Entities" right
]

tool_action_ent_delete = [
    local num_ents ent_type
    num_ents = (getenginestat 15)

    if (> $num_ents 0) [
        if (= $num_ents 1) [
            ent_type = (enttype)
        ]

        delent

        if (= $num_ents 1) [
            tool_info_show_action (concat "Removed entity:" $ent_type) ta_ent_delete
        ] [
            tool_info_show_action (concat "Removed" $num_ents "entities") ta_ent_delete
        ]
    ] [
        tool_info_show_action "No entities selected" ta_ent_delete
    ]
]

newent_oncamera = [
    local entdrop_old
    entdrop_old = $entdrop

    entdrop 0
    newent $arg1
    entdrop $entdrop_old
]

TOOL_NEWENT_DEFAULT = 0
TOOL_NEWENT_ONCAMERA = 1

tool_newent_mode = $TOOL_NEWENT_DEFAULT

tool_newent = [
    local cmd
    cmd = newent

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        cmd = newent_oncamera
    ]

    toolpanel_show tool_ent "Entities" right
    $cmd $arg1

    tool_newent_mode = $TOOL_NEWENT_DEFAULT
]

tool_action_add_env = [
    local text
    text = "Add environment entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append text "^n^fAOn camera"
    ]

    toolpanel_show_piemenu [
        uit_text = [@@text]
        uit_item_names = $tool_ent_types_env
        uit_size = 0.25
        uit_nav_enable = 1
        uit_on_select = [
            tool_newent (at $tool_ent_types_env $arg1)
        ]
    ]
]

tool_action_add_game = [
    local text
    text = "Add game entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append text "^n^fAOn camera"
    ]

    toolpanel_show_piemenu [
        uit_text = [@@text]
        uit_item_names = $tool_ent_types_game
        uit_size = 0.25
        uit_nav_enable = 1
        uit_on_select = [
            tool_newent (at $tool_ent_types_game $arg1)
        ]
    ]
]

tool_add_ent = [
    if (= $tool_action_ctx $TOOL_ACTION_CTX_KEY) [
        local text
        text = "New entity"

        if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
            append text "^n^fAOn camera"
        ]

        toolpanel_show_piemenu [
            uit_text = [@@text]
            uit_item_names = [
                "Environment entities"
                "Game entities"
            ]
            uit_size = 0.25
            uit_nav_enable = 1
            uit_on_select = [
                if $arg1 [
                    tool_action_add_game
                ] [
                    tool_action_add_env
                ]
            ]
        ]
    ] [
        toolpanel_show tool_ent_add "" menu [
            uit_position = (uicursorpos)
            uit_width = 0.3
        ]
    ]
]

tool_action_ent_add = [
    tool_newent_mode = $TOOL_NEWENT_DEFAULT

    if (tool_ent_placer_canplace) [
        tool_ent_placer_place
    ] [
        tool_add_ent
    ]
]

tool_action_ent_add_on_camera = [
    tool_newent_mode = $TOOL_NEWENT_ONCAMERA
    tool_add_ent
]

tool_action_ent_findinsel = [
    entselect insel
]

tool_action ta_ents [
    short_desc "Entities panel"
    long_desc "Open the entities panel"
    icon "textures/icons/edit/ent"
    category "Entities"
    action [
        tool_action_ents
    ]
]

tool_action ta_ent_sel_links [
    short_desc "Select linked entities"
    long_desc "Select linked entities"
    icon "textures/icons/shock"
    category "Entities"
    action [
        selentlinks
    ]
]

tool_action ta_ent_unify [
    short_desc "Unify entities"
    long_desc "Unify properties of all selected entities"
    icon "textures/icons/editing"
    category "Entities"
    action [
        tool_action_ent_unify
    ]
]

tool_action ta_ent_search [
    short_desc "Search entities"
    long_desc "Search entities by their properties"
    icon "textures/icons/editing"
    category "Entities"
    action [
        tool_action_ent_search
    ]
]

tool_action ta_ent_placer [
    short_desc "Entity placer"
    long_desc "Open entity placer"
    icon "textures/icons/attack"
    category "Entities"
    action [
        tool_action_ent_placer
    ]
]

tool_action ta_ent_delete [
    short_desc "Delete entities"
    long_desc "Delete selected entities"
    icon "textures/icons/action"
    category "Entities"
    action [
        tool_action_ent_delete
    ]
]

tool_action ta_ent_add [
    short_desc "Add entity"
    long_desc "Add a new entity. Places templates when using entity placer."
    icon "textures/icons/action"
    category "Entities"
    action [
        tool_action_ent_add
    ]
]

tool_action ta_ent_add_on_camera [
    short_desc "Add entity on camera"
    long_desc "Add a new entity at the current camera position"
    icon "textures/icons/action"
    category "Entities"
    action [
        tool_action_ent_add_on_camera
    ]
]

tool_action ta_ent_chain_link [
    short_desc "Chain-link entities"
    long_desc "Links selected entities in an ordered chain"
    icon "textures/icons/shock"
    category "Entities"
    action [
        entlink
    ]
]

tool_action ta_ent_star_link [
    short_desc "Star-link entities"
    long_desc "Links selected entities in a star pattern"
    icon "textures/icons/shock"
    category "Entities"
    action [
        entlink 1
    ]
]

tool_action ta_ent_cyclefocus [
    short_desc "Cycle entity focus"
    long_desc "Cycles focus between selected entities"
    icon "textures/icons/edit/ent"
    category "Entities"
    type @TOOL_ACTION_SCROLL
    action [
        domodifier 10; onrelease entautoview
    ]
]

tool_action ta_ent_deselect [
    short_desc "Deselect entities"
    long_desc "Deselect entities"
    icon "textures/icons/edit/ent"
    category "Entities"
    action [
        entcancel
    ]
]

tool_action ta_ent_selinsel [
    short_desc "Select entities in selection"
    long_desc "Select entities inside of world selection"
    icon "textures/icons/edit/cube"
    category "Entities"
    action [
        entselect insel
    ]
]
