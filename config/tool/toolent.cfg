tool_goto_control_Entities = [
    tool_ent_panel_open
]

// 1:<description> 2:<tags> 3:<param>
tool_ent_register_control = [
    tool_register_control $arg1 $arg2 "Entities" $arg3
]

T_ENT_UI_EDIT = 0
T_ENT_UI_UNIFY = 1
T_ENT_UI_SEARCH = 2
T_ENT_UI_TEMPLATES = 3

exec "config/tool/toolentparam.cfg"

tool_ent_types_env = [
    "light"
    "lightfx"
    "mapmodel"
    "envmap"
    "particles"
    "sound"
    "soundenv"
    "decal"
    "wind"
    "rail"
    "camera"
    "mapui"
]

tool_ent_types_game = [
    "playerstart"
    "weapon"
    "teleport"
    "actor"
    "trigger"
    "pusher"
    "physics"
    "affinity"
    "checkpoint"
    "route"
]

defvarp enteditingonpanel 0 0 1 [
    entediting (? (toolpanel_isopen tool_ent) 1 0)
]

tool_ent_ui_mode = $T_ENT_UI_EDIT

exec "config/tool/toolenttemp.cfg"

// 1:<toggle>
tool_ent_panel_open = [
    (? $arg1 toolpanel_toggle toolpanel_open) tool_ent right [
        p_title       = "Entities"
        p_clear_stack = 1
    ]
]

tool_ent_diff_list = [
    local _ents _ent _diff _diffs _diff_list _attr
    _ents = []
    _ent = []
    _diff = []
    _diffs = []
    _diff_list = []
    _attr = 0

    enthoverloopread [
        append _ents [[@@(concatword "#" $entindex " " (entget))]]
    ]

    while [> $_attr -1] [
        local _val _cur_val _is_diff _has_val
        _val = (at (at $_ents 0) $_attr)
        _is_diff = 0
        _has_val = 0

        loop i (- (listlen $_ents) 1) [
            _ent = (at $_ents (+ $i 1))
            _cur_val = (at $_ent $_attr)
            if (!=s $_cur_val) [
                _has_val = 1
            ]
            if (!= $_val $_cur_val) [
                _is_diff = 1
            ]
        ]

        if $_is_diff [
            append _diffs $_attr
        ]

        if $_has_val [
            _attr = (+ $_attr 1)
        ] [
            _attr = -1
        ]
    ]

    loop i (listlen $_ents) [
        _ent = (at $_ents $i)
        _diff = []
        loop j (listlen $_ent) [
            if (< $j 2) [
                append _diff @(at $_ent $j)
            ] [
                if (listhas $_diffs $j) [
                    local _type_idx _attr_name
                    _type_idx = (indexof $tool_ent_types (at $_ent 1))
                    _attr_name = (getentattr $_type_idx (- $j 2))
                    append _diff (concatword "^fA" $_attr_name ": ^fw" (at $_ent $j))
                ]
            ]
        ]
        append _diff_list [[@@_diff]]
    ]

    result $_diff_list
]

tool_ent_context_handler = [
    if (> $numenthover 1) [
        local _ents _entidxs
        _ents = (tool_ent_diff_list)
        _entidxs = []

        enthoverloopread [
            append _entidxs $entindex
        ]

        toolpanel_open_menu [
            p_nav_enable = 1
            p_item_names = [
                "Select all"
                @@_ents
            ]
            p_item_text_size = $ui_tool_text_size_xs_unscaled
            p_on_select = [
                if $arg1 [
                    enttoggleidx (at [@@@@_entidxs] (- $arg1 1))
                ] [
                    entselect [
                        listhas [@@@@@_entidxs] $entindex
                    ]
                ]
            ]
        ]

        result 1
    ] [
        result 0
    ]
]
tool_add_context_handler tool_ent_context_handler

tool_ent_unify_type = 0
tool_ent_unify_mask = 0

tool_ent_unify_perform = [
    local _ent _ent_type _type_idx _num_attrs
    _ent = (entget)
    _ent_type = (at $_ent 0)
    _type_idx = (indexof $tool_ent_types $_ent_type)
    _num_attrs = (entityattrs $_type_idx)

    entloop [
        if $tool_ent_unify_type [
            enttype $_ent_type
        ]

        if (=s (enttype) $_ent_type) [
            loop attridx $_num_attrs [
                if (& $tool_ent_unify_mask (<< 1 $attridx)) [
                    entattr $attridx (at $_ent (+ $attridx 1))
                ]
            ]
        ]
    ]
]

tool_ent_search_operators = [
    *
    =
    !=
    >
    <
    >=
    <=
    &
]

tool_ent_search_reset_filters = [
    tool_ent_search_mask = 0

    loop attridx 23 [
        [tool_ent_search_attr_@attridx] = 0
        [tool_ent_search_attr_op_@attridx] = 0
    ]
]

tool_ent_search_get_type = [
    if (tool_ent_has_sel) [
        local _type_idx
        _type_idx = (indexof $tool_ent_types (enttype))

        tool_ent_search_type = $_type_idx
    ]
]

// 1:<attr idx>
tool_ent_search_get_attr = [
    if (tool_ent_has_sel) [
        [tool_ent_search_attr_@arg1] = (entattr $arg1)
    ]
]

tool_ent_search_get_filters = [
    if (tool_ent_has_sel) [
        local _type_idx _num_attrs
        _type_idx = (indexof $tool_ent_types (enttype))
        _num_attrs = (entityattrs $_type_idx 1)

        tool_ent_search_type = $_type_idx

        loop attridx $_num_attrs [
            [tool_ent_search_attr_@attridx] = (entattr $attridx)
            [tool_ent_search_attr_op_@attridx] = 1
        ]
    ] [
        tool_ent_search_reset_filters
    ]
]

tool_ent_search_type = 0
tool_ent_search_insel = 0
tool_ent_search_reset_filters

tool_ent_search_filter_proc = [
    local _res _type_idx _num_attrs _op
    if $tool_ent_search_type [
        _res = (=s (enttype) (at $tool_ent_types $tool_ent_search_type))

        _type_idx = (indexof $tool_ent_types (enttype))
        _num_attrs = (entityattrs $_type_idx 1)

        loopwhile attridx $_num_attrs _res [
            _op = (at $tool_ent_search_operators $[tool_ent_search_attr_op_@attridx])
            if (&& [!=s $_op "*"] [! ($_op (entattr $attridx) $tool_ent_search_attr_@attridx)]) [
                _res = 0
            ]
        ]
    ] [
        _res = 1
    ]

    result $_res
]

tool_ent_drag_last_snap_mod = 0
tool_ent_drag_last_entselsnap = $entselsnap
tool_ent_drag_last_entmoving = $entmoving

// Handles SHIFT modifier when dragging, to toggle entselsnap
tool_ent_drag_proc = [
    // Init entmove
    if (= $entmoving 1) [
        tool_ent_drag_last_entselsnap = $entselsnap
        tool_ent_drag_last_snap_mod = 0
    ]

    // Called every frame during entmove
    if (entmoving) [
        local _key_mods _snap_mod
        _key_mods = (getkeymodifiers)
        _snap_mod = (? (& $_key_mods $KMOD_SHIFT) 1 0)

        if (!= $_snap_mod $tool_ent_drag_last_snap_mod) [
            entselsnap (! $entselsnap)
        ]

        tool_ent_drag_last_snap_mod = $_snap_mod
    ]

    // Called at the end of entmove
    if (&& [! $entmoving] [$tool_ent_drag_last_entmoving]) [
        entselsnap $tool_ent_drag_last_entselsnap
    ]

    tool_ent_drag_last_entmoving = $entmoving
]

tool_proc_add tool_ent_drag_proc

tool_ent_search_find = [
    if $tool_ent_search_insel [
        entselect [&& insel tool_ent_search_filter_proc]
    ] [
        entselect tool_ent_search_filter_proc
    ]
]

// 1:<mode> 2:<init>
tool_ent_toggle_ui_mode = [
    if (&& [toolpanel_isopen tool_ent] [= $tool_ent_ui_mode $arg1]) [
        tool_ent_ui_mode = $T_ENT_UI_EDIT
    ] [
        do $arg2
        tool_ent_ui_mode = $arg1
    ]
]

newent_oncamera = [
    local _entdrop_old
    _entdrop_old = $entdrop

    entdrop 0
    newent $arg1
    entdrop $_entdrop_old
]

TOOL_NEWENT_DEFAULT = 0
TOOL_NEWENT_ONCAMERA = 1

tool_newent_mode = $TOOL_NEWENT_DEFAULT

tool_newent = [
    local _cmd
    _cmd = newent

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        _cmd = newent_oncamera
    ]

    tool_ent_panel_open
    $_cmd $arg1

    tool_newent_mode = $TOOL_NEWENT_DEFAULT
]

// 1:<popup type> 2:<on pick>
# tool_ent_pick_env = [
    local _text
    _text = "Add environment entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append _text "^n^fAOn camera"
    ]

    $arg1 [
        p_text = [#1 _text]
        p_item_names = $tool_ent_types_env
        p_size = 0.25
        p_nav_enable = 1
        p_on_select = [
            local _on_pick
            _on_pick = [#1 arg2]
            _on_pick (at $tool_ent_types_env $arg1)
        ]
    ]
]

// 1:<menu type> 2:<on pick>
# tool_ent_pick_game = [
    local _text
    _text = "Add game entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append _text "^n^fAOn camera"
    ]

    $arg1 [
        p_text = [#1 _text]
        p_item_names = $tool_ent_types_game
        p_size = 0.25
        p_nav_enable = 1
        p_on_select = [
            local _on_pick
            _on_pick = [#1 arg2]
            _on_pick (at $tool_ent_types_game $arg1)
        ]
    ]
]

// 1:<on pick> 2:<action context>
# tool_ent_pick = [
    local _menutype _menupos_override _text
    _text = "New entity"

    if (|| [= $arg2 $TA_CTX_KEY] [= $arg2 $TA_CTX_SEARCH]) [
        _menutype = toolpanel_open_pie
    ] [
        _menutype = toolpanel_open_menu
    ]

    _menupos_override = (? (= $arg2 $TA_CTX_SEARCH) [
        p_position = [#1(uiwidth 0.5) 0.5]
    ] [])

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append _text "^n^fAOn camera"
    ]

    $_menutype [
        p_text = [#1 _text]
        p_item_names = [
            "Environment entities"
            "Game entities"
        ]
        p_size = 0.25
        p_nav_enable = 1
        p_on_select = [
            if $arg1 [
                tool_ent_pick_game #1 _menutype [#1 arg1]
            ] [
                tool_ent_pick_env #1 _menutype [#1 arg1]
            ]
        ]
        @_menupos_override
    ]
]

tool_add_ent = [
    tool_ent_pick [
        tool_newent $arg1
    ] $tool_action_ctx
]

tool_action_ent_findinsel = [
    entselect insel
]

tool_action ta_ents [
    p_short_desc = "Entities panel"
    p_long_desc  = "Open the entities panel"
    p_icon       = "<grey>textures/icons/edit/ent"
    p_category   = "Entities"
    p_code       = [
        tool_ent_panel_open 1
    ]
]

tool_action ta_ent_cycle_snapmode [
    p_short_desc = "Cycle snap mode"
    p_long_desc  = "Cycle entity snap mode"
    p_icon       = "<grey>textures/icons/edit/ent"
    p_category   = "Entities"
    p_code       = [
        cyclevar entselsnapmode
        tool_info_show "Entity snap mode" [
            p_subtext = (at [
                "To grid"
                "To entities"
            ] $entselsnapmode)
        ]
    ]
]

tool_action ta_ent_sel_links [
    p_short_desc = "Select linked entities"
    p_icon       = "<grey>textures/icons/edit/chainsel"
    p_category   = "Entities"
    p_code       = [
        selentlinks
    ]
]

tool_action ta_ent_unify [
    p_short_desc = "Unify entities"
    p_long_desc  = "Unify properties of all selected entities"
    p_icon       = "<grey>textures/icons/edit/entunify"
    p_category   = "Entities"
    p_code       = [
        tool_ent_panel_open
        tool_ent_toggle_ui_mode $T_ENT_UI_UNIFY [
            tool_ent_unify_mask = 0
        ]
    ]
]

tool_action ta_ent_search [
    p_short_desc = "Search entities"
    p_long_desc  = "Search entities by their properties"
    p_icon       = "<grey>textures/icons/edit/find"
    p_category   = "Entities"
    p_code       = [
        tool_ent_search_get_filters
        tool_ent_toggle_ui_mode $T_ENT_UI_SEARCH
        tool_ent_panel_open
    ]
]

tool_action ta_ent_placer [
    p_short_desc = "Entity templates"
    p_icon       = "<grey>textures/icons/edit/entplace"
    p_category   = "Entities"
    p_code       = [
        tool_ent_toggle_ui_mode $T_ENT_UI_TEMPLATES
        tool_ent_panel_open
    ]
]

tool_action ta_ent_delete [
    p_short_desc = "Delete entities"
    p_long_desc  = "Delete selected entities"
    p_icon       = "<grey>textures/icons/edit/entdel"
    p_category   = "Entities"
    p_code       = [
        local _num_ents _ent_type
        _num_ents = (getenginestat 15)

        if (> $_num_ents 0) [
            if (= $_num_ents 1) [
                _ent_type = (enttype)
            ]

            delent

            if (= $_num_ents 1) [
                tool_info_show_action "Removed entity" ta_ent_delete [
                    p_subtext = [@@_ent_type]
                ]
            ] [
                tool_info_show_action (concat "Removed" $_num_ents "entities") ta_ent_delete
            ]
        ] [
            tool_info_show_action "No entities selected" ta_ent_delete
        ]
    ]
]

tool_action ta_ent_add [
    p_short_desc = "Add entity"
    p_icon       = "<grey>textures/icons/edit/entadd"
    p_category   = "Entities"
    p_code       = [
        tool_newent_mode = $TOOL_NEWENT_DEFAULT
        tool_add_ent
    ]
]

tool_action ta_ent_add_on_camera [
    p_short_desc = "Add entity on camera"
    p_long_desc  = "Add a new entity at the current camera position"
    p_icon       = "<grey>textures/icons/edit/entadd"
    p_category   = "Entities"
    p_code       = [
        tool_newent_mode = $TOOL_NEWENT_ONCAMERA
        tool_add_ent
    ]
]

tool_action ta_ent_chain_link [
    p_short_desc = "Chain-link entities"
    p_long_desc  = "Links selected entities in an ordered chain"
    p_icon       = "<grey>textures/icons/edit/chain"
    p_category   = "Entities"
    p_code       = [
        entlink
    ]
]

tool_action ta_ent_star_link [
    p_short_desc = "Star-link entities"
    p_long_desc  = "Links selected entities in a star pattern"
    p_icon       = "<grey>textures/icons/edit/chain"
    p_category   = "Entities"
    p_code       = [
        entlink 1
    ]
]

tool_action ta_ent_unlink [
    p_short_desc = "Unlink entities"
    p_long_desc  = "Unlinks selected entities"
    p_icon       = "<grey>textures/icons/edit/chain"
    p_category   = "Entities"
    p_code       = [
        entunlink
    ]
]

tool_action ta_ent_cyclefocus [
    p_short_desc = "Cycle entity focus"
    p_long_desc  = "Cycles focus between selected entities"
    p_icon       = "<grey>textures/icons/edit/ent"
    p_category   = "Entities"
    p_type       = $TA_TYPE_SCROLL
    p_code       = [
        domodifier 10
        onrelease entautoview
    ]
]

tool_action ta_ent_deselect [
    p_short_desc = "Deselect entities"
    p_icon       = "<grey>textures/icons/edit/ent"
    p_category   = "Entities"
    p_code       = [
        entcancel
    ]
]

tool_action ta_ent_selinsel [
    p_short_desc = "Select entities in selection"
    p_long_desc  = "Select entities inside of map selection"
    p_icon       = "<grey>textures/icons/edit/cube"
    p_category   = "Entities"
    p_code       = [
        tool_ent_panel_open
        entselect insel
    ]
]
