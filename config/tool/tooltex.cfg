exec "config/tool/tooltexparam.cfg"

TEX_DIFFUSE = 0
TEX_NORMAL  = 1
TEX_GLOW    = 2
TEX_ENVMAP  = 3
TEX_SPEC    = 4
TEX_DEPTH   = 5
TEX_ALPHA   = 6
TEX_DISPMAP = 7
TEX_TYPES   = 8

tool_tex_cur           = -1
tool_tex_cur_edit      = -1
tool_tex_variantsrc    = -1
tool_tex_replace       = -1
tool_tex_autoapply_old = 0
tool_tex_autoapply     = 0
tool_tex_variant_edit  = 0
tool_tex_replace_sel   = 0

tool_tex_edit        = 0
tool_tex_sync_params = 0

tool_tex_props_disabled = 0

tool_tex_blend_brushes = []

defvarp texeditdefaultautoapply 0 1 1

tool_goto_control_Textures = [
    tool_tex_editvariant
]

// 1:<description> 2:<tags>
tool_tex_register_control = [
    tool_register_control $arg1 $arg2 "Textures"
]

loop brushidx (blendbrush -1) [
    append tool_tex_blend_brushes (getblendbrushname $brushidx)
]

tool_tex_can_focus = [
    = $tool_tex_edit 0
]

// 1:<slot> 2:<edit mode>
tool_focus_tex = [
    tool_tex_cur_edit = $arg1
    if $arg2 [] [
        tool_tex_cur = $arg1
    ]
    tool_tex_variantsrc = (getslottex (getvindex $arg1))
]

// 1:<from> 2:<to> 3:<insidefaces>
tool_tex_cull = [
    local _oldtexnum _cullednum
    _oldtexnum = $numvslots

    if $arg3 [
        fixinsidefaces
    ]

    compactvslots 1 $arg1 $arg2

    _cullednum = (- $_oldtexnum $numvslots)
    tool_info_show (format "Culled %1 texture%2" $_cullednum (? (= $_cullednum 1) "" "s"))
]

// 1:<slot>
tool_tex_cull_from = [
    tool_tex_cull $arg1 -1
]

// 1:<slot>
tool_tex_cull_group = [
    local _from _to _slot
    _from = -1
    _to = -1
    _slot = $arg1

    while [&& [>= $_slot 0] [< $_from 0]] [
        if (!=s (gettexgroup $arg1) (gettexgroup $_slot)) [
            _from = (+ $_slot 1)
        ]

        _slot = (- $_slot 1)
    ]

    _slot = $arg1

    while [&& [>= $_slot 0] [< $_to 0]] [
        if (!=s (gettexgroup $arg1) (gettexgroup $_slot)) [
            _to = (- $_slot 1)
        ]

        _slot = (+ $_slot 1)
    ]

    if (> $_from 0) [
        tool_tex_cull $_from $_to
    ]
]

filename = [
    listlast (strreplace (strreplace $arg1 "/" " ") "\" " ")
]

// 1:<slot>
tool_tex_remove = [
    local _src
    _src = (getvindex $arg1)

    if (!= $_src $arg1) [
        replacetex $_src $arg1
    ] [
        replacetex 1 $arg1
    ]

    compactvslots 1 $arg1 $arg1
]

tool_action_tex_editcancel = [
    tool_tex_param_edit_end 0
    tool_tex_edit = 0
]

tool_action_tex_editconfirm = [
    tool_tex_param_edit_end 1
    tool_tex_edit = 0
]

tool_tex_on_close = [
    if $tool_tex_edit [
        tool_action_tex_editcancel

        if $texeditdefaultautoapply [
            tool_tex_autoapply = $tool_tex_autoapply_old
        ]
    ]
]

tool_tex_editvariant = [
    toolpanel_open tool_textures right [
        p_title = "Textures"
    ]

    tool_tex_edit = 1
    tool_tex_param_edit_begin

    if $texeditdefaultautoapply [
        tool_tex_autoapply_old = $tool_tex_autoapply
        tool_tex_autoapply     = 1
    ]
]

// 1:<slot>
tool_tex_editslot = [
    tool_tex_cur = $arg1

    toolpanel_open tool_texeditslot right [
        p_title = "Edit texture slot"
    ]
]

// 1:<skip current>
tool_tex_editslot_checkhints = [
    tool_texeditslot_allowedhints = [n e er eR erR s Ss p g gG m T v]

    local _add_hint
    _add_hint = [
        if (< (strstr $tool_texeditslot_hints $arg1) 0) [
            appendword tool_texeditslot_hints $arg1
        ]
    ]

    tool_texeditslot_specfromdiffuse = (> (gettexbpp $[tool_texeditslot_tex@TEX_DIFFUSE] 1) 3)

    if (!=s $[tool_texeditslot_tex@TEX_NORMAL] "") [
        tool_texeditslot_heightfromnormal = (> (gettexbpp $[tool_texeditslot_tex@TEX_NORMAL] 1) 3)
    ] [
        tool_texeditslot_heightfromnormal = 0
    ]

    if $arg1 [] [
        // Build shader hints based on current textures

        tool_texeditslot_hints = ""

        // Diffuse alpha is used as specular map
        if $tool_texeditslot_specfromdiffuse [
            _add_hint "s"
            _add_hint "S"
        ]

        if (!=s $[tool_texeditslot_tex@TEX_NORMAL] "") [
            _add_hint "n"

            // Normalmap alpha is used as heightmap
            if $tool_texeditslot_heightfromnormal [
                _add_hint "p"
            ]
        ]

        if (!=s $[tool_texeditslot_tex@TEX_GLOW] "") [
            _add_hint "g"
        ]

        if (!=s $[tool_texeditslot_tex@TEX_ENVMAP] "") [
            _add_hint "e"
            _add_hint "r"
        ]

        if (!=s $[tool_texeditslot_tex@TEX_SPEC] "") [
            _add_hint "s"
            _add_hint "S"
        ]

        if (!=s $[tool_texeditslot_tex@TEX_DEPTH] "") [
            _add_hint "p"
        ]

        if (!=s $[tool_texeditslot_tex@TEX_ALPHA] "") [
            _add_hint "m"
        ]

        if (!=s $[tool_texeditslot_tex@TEX_DISPMAP] "") [
            _add_hint "v"
        ]
    ]

    // Filter out forbidden hint combinations

    if (!=s $tool_texeditslot_hints "") [
        looprev i (listlen $tool_texeditslot_allowedhints) [
            local _hint _check
            _hint = (at $tool_texeditslot_allowedhints $i)

            if (! (strhaschars $tool_texeditslot_hints $_hint)) [
                _check = $tool_texeditslot_hints
                loop j (strlen $_hint) [
                    if (< (strstr $tool_texeditslot_hints (substr $_hint $j 1)) 0) [
                        appendword _check (substr $_hint $j 1)
                    ]
                ]

                if (< (findworldshader $_check) 0) [
                    tool_texeditslot_allowedhints = (listsplice $tool_texeditslot_allowedhints "" $i 1)
                ]
            ]
        ]
    ]
]

tool_tex_editslot_apply = [
    local _shaderidx _settex
    _shaderidx = (findworldshader $tool_texeditslot_hints)

    if (>= $_shaderidx 0) [
        _shader = (at $worldshaders $_shaderidx 0)
    ] [
        _shader = "stdworld"
    ]

    // 1:<type> 2:<path>
    _settex = [
        if (!=s $arg2 "") [
            texture $arg1 $arg2
        ] [
            remtexture $arg1
        ]
    ]

    editslot $tool_tex_cur [
        _settex 0 $[tool_texeditslot_tex@TEX_DIFFUSE]
        _settex n $[tool_texeditslot_tex@TEX_NORMAL]
        _settex g $[tool_texeditslot_tex@TEX_GLOW]
        _settex s $[tool_texeditslot_tex@TEX_SPEC]
        _settex z $[tool_texeditslot_tex@TEX_DEPTH]
        _settex a $[tool_texeditslot_tex@TEX_ALPHA]
        _settex e $[tool_texeditslot_tex@TEX_ENVMAP]
        _settex v $[tool_texeditslot_tex@TEX_DISPMAP]

        changeslotshader $_shader
    ]
]

tool_action ta_textures [
    p_short_desc = "Texture panel"
    p_long_desc  = "Open the texture panel"
    p_icon       = "textures/icons/edit/texture"
    p_category   = "Textures"
    p_code       = [
        toolpanel_toggle tool_textures right [
            p_title = "Textures"
        ]
    ]
]

tool_action ta_tex_apply [
    p_short_desc = "Apply texture"
    p_long_desc  = "Apply currenly picked texture"
    p_icon       = "textures/icons/attack"
    p_category   = "Textures"
    p_code       = [
        settex $tool_tex_cur
    ]
]

tool_action ta_tex_apply_allfaces [
    p_short_desc = "Apply texture on all faces"
    p_long_desc  = "Apply currenly picked texture on all faces"
    p_icon       = "textures/icons/attack"
    p_category   = "Textures"
    p_code       = [
        local _allfaces_old
        _allfaces_old = $allfaces

        allfaces 1
        settex $tool_tex_cur
        allfaces = $_allfaces_old
    ]
]

tool_action ta_tex_get [
    p_short_desc = "Get texture"
    p_long_desc  = "Get texture from current in-world selection"
    p_icon       = "textures/icons/arrow"
    p_category   = "Textures"
    p_code       = [
        tool_focus_tex (getseltex 1)
        tool_info_show "Selected texture" [
            p_children = [
                uivslotview @@tool_tex_cur $ui_toolinfo_action_image_size $ui_toolinfo_action_image_size
            ]
        ]
    ]
]

tool_action ta_tex_editvariant [
    p_short_desc = "Toggle texture variant edit"
    p_long_desc  = "Toggle editing of texture variant parameters"
    p_icon       = "textures/icons/voices"
    p_category   = "Textures"
    p_code       = [
        if (&& (toolpanel_isopen tool_textures) $tool_tex_edit) [
            tool_tex_param_edit_end 0
            tool_tex_edit = 0

            if $texeditdefaultautoapply [
                tool_tex_autoapply = $tool_tex_autoapply_old
            ]
        ] [
            tool_tex_editvariant
        ]
    ]
]

tool_action ta_recalc [
    p_short_desc = "Recalculate world verticies"
    p_long_desc  = "Recalculate world vertices, normals and blending"
    p_icon       = "textures/icons/editing"
    p_category   = "Textures"
    p_code       = [
        calclight
    ]
]

tool_action ta_tex_cull [
    p_short_desc = "Cull textures"
    p_long_desc  = "Cull unused texture slots and optimize inside faces"
    p_icon       = "textures/icons/editing"
    p_category   = "Textures"
    p_code       = [
        tool_confirm_prompt "Do you want to cull all textures?" [
            tool_tex_cull 0 -1 1
        ] [
            p_noundo_warn = 1
        ]
    ]
]

tool_action ta_tex_replace [
    p_short_desc = "Replace textures"
    p_long_desc  = "Replace texture instances with a different one"
    p_icon       = "textures/icons/editing"
    p_category   = "Textures"
    p_code       = [
        tool_tex_edit = 0
        toolpanel_toggle tool_texreplace right [
            p_title = "Replace texture"
        ]
    ]
]

tool_action ta_tex_paint [
    p_short_desc = "Blendmap paint panel"
    p_long_desc  = "Open texture blend painting panel"
    p_icon       = "textures/icons/editing"
    p_category   = "Textures"
    p_code       = [
        tool_tex_edit = 0
        toolpanel_toggle tool_texpaint right [
            p_title = "Texture blend painting"
        ]
    ]
]

tool_action ta_toggle_blankgeom [
    p_short_desc = "Toggle blank geometry mode"
    p_long_desc  = "Toggle blank geometry mode, disabling rendering of diffuse textures"
    p_icon       = "textures/icons/edit/texture"
    p_category   = "Textures"
    p_code       = [
        blankgeom (! $blankgeom)
    ]
]
