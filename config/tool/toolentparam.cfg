tool_ent_focus = []
tool_ent_cur_attr = []
tool_ent_type = []
tool_ent_subtype = []
tool_ent_compatible = 0
tool_ent_edit = 0
tool_ent_delta_edit_on = 0

tool_ent_cached = ""

// 1:<attr idx>
tool_ent_cached_attr = [
    at $tool_ent_cached (+ $arg1 1)
]

tool_ent_has_sel = [
    getenginestat 15
]

tool_ent_multiple = [
    > (getenginestat 15) 1
]

tool_ent_delta_edit = [
    && $tool_ent_edit $tool_ent_delta_edit_on (tool_ent_multiple)
]

T_ENT_NODELTA = 1

// 1:<type> 2:<name>
tool_ent_attr_idx_var = [
    concatword "[tool_ent_@[" $arg1 "]_idx_@" $arg2 "]"
]

// 1:<index>
tool_ent_set_cur_attr = [
    concatword "tool_ent_cur_attr = [tool_ent_attr_@" $arg1 "]"
]

tool_ent_types = $enttypelist

// 1:<type> 2:<name>
tool_ent_attr_idx_map_generic = [
    result $@(tool_ent_attr_idx_var arg1 arg2)
]

// 1:<index>
tool_ent_attr_idx_unmap_generic = [
    result $arg1
]

tool_ent_attr_init_vals = [
    looplist type $tool_ent_types [
        [tool_ent_getters_@type] = []
        [tool_ent_setters_@type] = []
        [tool_ent_postget_@type] = []
        [tool_ent_preset_@type] = []
        [tool_ent_flags_@type] = []
        [tool_ent_attr_subtype_@type] = -1
        [tool_ent_attr_idx_map_@type] = $tool_ent_attr_idx_map_generic
        [tool_ent_attr_idx_unmap_@type] = $tool_ent_attr_idx_unmap_generic
    ]
]

tool_ent_attr_init_vals

// 1:<type> 2:<name> 3:<flags> 4:<getter> 5:<setter>
tool_ent_add_attr = [
    local cur_attr_str getter setter

    append [tool_ent_flags_@arg1] $arg3
    append [tool_ent_getters_@arg1] [[@@arg4]]
    append [tool_ent_setters_@arg1] [[@@arg5]]

    // Indices might get remapped afterwards for some entity types
    @(tool_ent_attr_idx_var arg1 arg2) = $tool_ent_param_next_idx

    tool_ent_param_next_idx = (+ $tool_ent_param_next_idx 1)
]

// 1:<type> 2:<name> 3:<subtype>
tool_ent_attr_idx_map = [
    [tool_ent_attr_idx_map_@arg1] $arg1 $arg2 $arg3
]

// 1:<type> 2:<index> 3:<subtype>
tool_ent_attr_idx_unmap = [
    [tool_ent_attr_idx_unmap_@arg1] $arg2 $arg3
]

// 1:<index>
tool_ent_attr_idx_unmap_cached = [
    result $[tool_ent_attr_@[arg1]_unmap]
]

// 1:<type> 2:<name> 3:<subtype>
tool_ent_attr = [
    local attr_index
    attr_index = (tool_ent_attr_idx_map $arg1 $arg2 $arg3)

    result [tool_ent_attr_@attr_index]
]

// 1:<type> 2:<attr>
tool_ent_aflags = [
    local attr_idx
    attr_idx = $@(tool_ent_attr_idx_var arg1 arg2)
    result (at $[tool_ent_flags_@arg1] $attr_idx)
]

// 1:<type> 2:<suffix>
tool_ent_palindex_getter = [
    result [
        local pal_enforce test
        pal_enforce = (at $tool_palette_enforce_vals $(tool_ent_attr @arg1 palette@arg2 $tool_ent_subtype))

        if (&& $pal_enforce (>= $$tool_ent_cur_attr $pal_enforce)) [
            [tool_ent_attr_palindex@@@[arg2]_val] = (mod $$tool_ent_cur_attr $pal_enforce)
            [tool_ent_attr_palindex@@@[arg2]_enforce] = 1
        ] [
            [tool_ent_attr_palindex@@@[arg2]_val] = $$tool_ent_cur_attr
            [tool_ent_attr_palindex@@@[arg2]_enforce] = 0
        ]
    ]
]

// 1:<type> 2:<suffix>
tool_ent_palindex_setter = [
    result [
        local palindex
        palindex = $[tool_ent_attr_palindex@@[arg2]_val]

        if $[tool_ent_attr_palindex@@[arg2]_enforce] [
            palindex = (+ $palindex (at $tool_palette_enforce_vals $(tool_ent_attr @@arg1 palette@@arg2 $tool_ent_subtype)))
        ]

        $tool_ent_cur_attr = $palindex
    ]
]

// 1:<suffix>
tool_ent_colour_getter = [
    result [
        @@(tool_colour_fromint tool_ent_attr_colour@arg1 "$tool_ent_cur_attr")
    ]
]

// 1:<suffix>
tool_ent_colour_setter = [
    result [
        @@(tool_colour_toint "$tool_ent_cur_attr" tool_ent_attr_colour@arg1)
    ]
]

// 1:<type>
tool_ent_fxlevel_getter = [
    result [
        caseif (>= $$tool_ent_cur_attr 0) [
            tool_ent_attr_fxlevel_1 = (<= $$tool_ent_cur_attr 1)
            tool_ent_attr_fxlevel_2 = (|| $tool_ent_attr_fxlevel_1 [= $$tool_ent_cur_attr 2])
            tool_ent_attr_fxlevel_3 = (|| $tool_ent_attr_fxlevel_2 [= $$tool_ent_cur_attr 3])
        ] (< $$tool_ent_cur_attr 0) [
            local fxlevel
            fxlevel = (- $$tool_ent_cur_attr)

            tool_ent_attr_fxlevel_1 = (& $fxlevel 1)
            tool_ent_attr_fxlevel_2 = (!= (& $fxlevel (<< 1 1)))
            tool_ent_attr_fxlevel_3 = (!= (& $fxlevel (<< 1 2)))
        ]
    ]
]

// 1:<type>
tool_ent_fxlevel_setter = [
    result [
        local fxlevel
        fxlevel = 0
        fxlevel = (| $fxlevel $tool_ent_attr_fxlevel_1)
        fxlevel = (| $fxlevel (<< $tool_ent_attr_fxlevel_2 1))
        fxlevel = (| $fxlevel (<< $tool_ent_attr_fxlevel_3 2))

        $tool_ent_cur_attr = (at [0 -1 -2 -3 3 -5 2 0] $fxlevel)
    ]
]

// 1:<attr name> 2:<flag names> 3:<negative filter>
tool_ent_flags_getter = [
    local code flag srcvar
    srcvar = "$tool_ent_cur_attr"

    // some flags support negative filtering when bitmask is below 0
    if $arg3 [
        srcvar = "mask"
        append code "local mask^n"
        append code "mask = (? (< $$tool_ent_cur_attr 0) (~ (* $$tool_ent_cur_attr -1)) $$tool_ent_cur_attr)^n"
    ]

    loop i (listlen $arg2) [
        flag = (at $arg2 $i)

        append code (concatword tool_ent_attr_ $arg1 _ $flag " =")

        if $i [
            append code (concatword "(!= (& $" $srcvar " (<< 1 " $i ")))")
        ] [
            append code (concatword "(& $" $srcvar " 1)")
        ]

        append code "^n"
    ]

    result $code
]

// 1:<attr name> 2:<flag names>
tool_ent_flags_setter = [
    local code flag

    code = "$tool_ent_cur_attr = 0^n"

    loop i (listlen $arg2) [
        flag = (at $arg2 $i)

        append code "$tool_ent_cur_attr ="
        if $i [
            append code (concatword "(| $$tool_ent_cur_attr (<< $tool_ent_attr_" $arg1 "_" $flag " " $i "))")
        ] [
            append code (concatword "(| $$tool_ent_cur_attr $tool_ent_attr_" $arg1 "_" $flag ")")
        ]
        append code "^n"
    ]

    result $code
]

exec "config/tool/ents/light.cfg"
exec "config/tool/ents/mapmodel.cfg"
exec "config/tool/ents/playerstart.cfg"
exec "config/tool/ents/envmap.cfg"
exec "config/tool/ents/particles.cfg"
exec "config/tool/ents/sound.cfg"
exec "config/tool/ents/lightfx.cfg"
exec "config/tool/ents/decal.cfg"
exec "config/tool/ents/wind.cfg"
exec "config/tool/ents/weapon.cfg"
exec "config/tool/ents/teleport.cfg"
exec "config/tool/ents/actor.cfg"
exec "config/tool/ents/trigger.cfg"
exec "config/tool/ents/pusher.cfg"
exec "config/tool/ents/affinity.cfg"
exec "config/tool/ents/checkpoint.cfg"
exec "config/tool/ents/route.cfg"
exec "config/tool/ents/rail.cfg"
exec "config/tool/ents/camera.cfg"

tool_ent_add_attrs_outline = []

// 1:<type>
tool_ent_attr_idx_unmap_cache = [
    local attridx_unmap subtype_idx
    subtype_idx = $[tool_ent_attr_subtype_@arg1]

    if (>= $subtype_idx 0) [
        loop i (entityattrs (indexof $tool_ent_types $arg1)) [
            attridx_unmap = (tool_ent_attr_idx_unmap $arg1 $i $tool_ent_subtype)
            [tool_ent_attr_@[i]_unmap] = $attridx_unmap
        ]
    ] [
        loop i (entityattrs (indexof $tool_ent_types $arg1)) [
            [tool_ent_attr_@[i]_unmap] = $i
        ]
    ]
]

tool_ent_last_edit = 0

tool_ent_get_func = [
    case $tool_ent_ui_mode @T_ENT_UI_EDIT [
        result entget
    ] @T_ENT_UI_PLACER [
        result tool_ent_placer_template_get
    ]
]

tool_ent_set_func = [
    case $tool_ent_ui_mode @T_ENT_UI_EDIT [
        at [
            entattr
            entsmodify
        ] (tool_ent_delta_edit)
    ] @T_ENT_UI_PLACER [
        result tool_ent_placer_template_update
    ]
]

// 1:<type> 2:<attr idx list>
tool_ent_attr_change = [
    local attr_setter set_func
    set_func = (tool_ent_set_func)

    [tool_ent_preset_@arg1]

    looplist attridx $arg2 [
        attr_setter = (at $[tool_ent_setters_@arg1] (tool_ent_attr_idx_unmap_cached $attridx))
        @@(tool_ent_set_cur_attr attridx)
        attr_setter

        if $set_func [
            $set_func $attridx $[tool_ent_attr_@attridx]
        ]
    ]
]

tool_ent_last_type = ""
tool_ent_last_subtype = -1

tool_ent_sync_attrs = [
    if $tool_ent_type [
        local getters attridx_unmap
        getters = $[tool_ent_getters_@tool_ent_type]

        loop attridx (entityattrs (indexof $tool_ent_types $tool_ent_type)) [
            attridx_unmap = (tool_ent_attr_idx_unmap_cached $attridx)
            entlast [
                [tool_ent_attr_@attridx] = (tool_ent_cached_attr @attridx)
            ]
            @@(tool_ent_set_cur_attr attridx)
            do (at $getters $attridx_unmap)
        ]

        [tool_ent_postget_@tool_ent_type]
    ]
]

tool_ent_sync_subtype = [
    local subtype_idx
    subtype_idx = $[tool_ent_attr_subtype_@tool_ent_type]
    tool_ent_subtype = 0

    if (>= $subtype_idx 0) [
        tool_ent_subtype = (tool_ent_cached_attr $subtype_idx)
    ]

    if (|| [!=s $tool_ent_type $tool_ent_last_type] [!= $tool_ent_subtype $tool_ent_last_subtype]) [
        tool_ent_last_type = $tool_ent_type
        tool_ent_last_subtype = $tool_ent_subtype
        tool_ent_attr_idx_unmap_cache $tool_ent_type
    ]
]

tool_ent_check_multi_compat = [
    entloopread [
        if (!=s $tool_ent_type (enttype)) [
            tool_ent_compatible = 0
        ]

        if (&& (> $subtype_idx -1) [!= $tool_ent_subtype (entattr $subtype_idx)]) [
            tool_ent_compatible = 0
        ]
    ]
]

tool_ent_sync = [
    local subtype_idx get_func
    subtype_idx = -1
    get_func = (tool_ent_get_func)

    tool_ent_cached = (get_func)

    entlast [
        tool_ent_type = (at $tool_ent_cached 0)
        if $tool_ent_type [
            subtype_idx = $[tool_ent_attr_subtype_@tool_ent_type]
            tool_ent_sync_subtype
        ]
    ]

    tool_ent_compatible = 1

    if (= $tool_ent_ui_mode @T_ENT_UI_EDIT) [
        @@tool_ent_check_multi_compat
    ]

    if (! (tool_ent_delta_edit)) tool_ent_sync_attrs
]

tool_ent_add_attrs_none = []

tool_ent_populate_attrs = [
    looplist type $tool_ent_types [
        tool_ent_param_next_idx = 0
        [tool_ent_add_attrs_@type]
    ]
]

tool_ent_populate_attrs
