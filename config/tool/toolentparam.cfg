tool_ent_focus = []
tool_ent_cur_attr = []
tool_ent_type = []
tool_ent_subtype = []
tool_ent_compatible = 0
tool_ent_edit = 0
tool_ent_delta_edit_on = 0

tool_ent_cached = ""

// 1:<attr idx>
tool_ent_cached_attr = [
    at $tool_ent_cached (+ $arg1 1)
]

tool_ent_has_sel = [
    getenginestat 15
]

tool_ent_multiple = [
    > (getenginestat 15) 1
]

tool_ent_delta_edit = [
    && $tool_ent_edit $tool_ent_delta_edit_on (tool_ent_multiple)
]

T_ENT_NODELTA = 1

// 1:<type> 2:<name>
tool_ent_attr_idx_var = [
    concatword "[tool_ent_@[" $arg1 "]_idx_@" $arg2 "]"
]

// 1:<index>
tool_ent_set_cur_attr = [
    concatword "tool_ent_cur_attr = [tool_ent_attr_@" $arg1 "]"
]

tool_ent_types = $enttypelist

// 1:<type> 2:<name>
tool_ent_attr_idx_map_generic = [
    result $@(tool_ent_attr_idx_var arg1 arg2)
]

// 1:<index>
tool_ent_attr_idx_unmap_generic = [
    result $arg1
]

tool_ent_attr_init_vals = [
    looplist type $tool_ent_types [
        [tool_ent_getters_@type] = []
        [tool_ent_setters_@type] = []
        [tool_ent_postget_@type] = []
        [tool_ent_preset_@type] = []
        [tool_ent_flags_@type] = []
        [tool_ent_attr_subtype_@type] = -1
        [tool_ent_attr_idx_map_@type] = $tool_ent_attr_idx_map_generic
        [tool_ent_attr_idx_unmap_@type] = $tool_ent_attr_idx_unmap_generic
    ]
]

tool_ent_attr_init_vals

// 1:<type> 2:<name> 3:<flags> 4:<getter> 5:<setter>
tool_ent_add_attr = [
    append [tool_ent_flags_@arg1] $arg3
    append [tool_ent_getters_@arg1] [[@@arg4]]
    append [tool_ent_setters_@arg1] [[@@arg5]]

    // Indices might get remapped afterwards for some entity types
    @(tool_ent_attr_idx_var arg1 arg2) = $tool_ent_param_next_idx

    tool_ent_param_next_idx = (+ $tool_ent_param_next_idx 1)
]

// 1:<type> 2:<name> 3:<subtype>
tool_ent_attr_idx_map = [
    [tool_ent_attr_idx_map_@arg1] $arg1 $arg2 $arg3
]

// 1:<type> 2:<index> 3:<subtype>
tool_ent_attr_idx_unmap = [
    [tool_ent_attr_idx_unmap_@arg1] $arg2 $arg3
]

// 1:<index>
tool_ent_attr_idx_unmap_cached = [
    result $[tool_ent_attr_@[arg1]_unmap]
]

// 1:<type> 2:<name> 3:<subtype>
tool_ent_attr = [
    local _attr_index
    _attr_index = (tool_ent_attr_idx_map $arg1 $arg2 $arg3)

    result [tool_ent_attr_@_attr_index]
]

// 1:<type> 2:<attr>
tool_ent_aflags = [
    local _attr_idx
    _attr_idx = $@(tool_ent_attr_idx_var arg1 arg2)
    result (at $[tool_ent_flags_@arg1] $_attr_idx)
]

// 1:<type> 2:<suffix>
# tool_ent_palindex_getter [
    result [
        local _pal_enforce
        _pal_enforce = (at $tool_palette_enforce_vals $(tool_ent_attr @arg1 palette@arg2 $tool_ent_subtype))

        if (&& $_pal_enforce (>= $$tool_ent_cur_attr $_pal_enforce)) [
            [tool_ent_attr_palindex#1[arg2]_val] = (mod $$tool_ent_cur_attr $_pal_enforce)
            [tool_ent_attr_palindex#1[arg2]_enforce] = 1
        ] [
            [tool_ent_attr_palindex#1[arg2]_val] = $$tool_ent_cur_attr
            [tool_ent_attr_palindex#1[arg2]_enforce] = 0
        ]
    ]
]

// 1:<type> 2:<suffix>
tool_ent_palindex_setter = [
    result [
        local _palindex
        _palindex = $[tool_ent_attr_palindex@@[arg2]_val]

        if $[tool_ent_attr_palindex@@[arg2]_enforce] [
            _palindex = (+ $_palindex (at $tool_palette_enforce_vals $(tool_ent_attr @@arg1 palette@@arg2 $tool_ent_subtype)))
        ]

        $tool_ent_cur_attr = $_palindex
    ]
]

// 1:<type>
tool_ent_fxlevel_getter = [
    result [
        caseif (>= $$tool_ent_cur_attr 0) [
            tool_ent_attr_fxlevel_1 = (<= $$tool_ent_cur_attr 1)
            tool_ent_attr_fxlevel_2 = (|| $tool_ent_attr_fxlevel_1 [= $$tool_ent_cur_attr 2])
            tool_ent_attr_fxlevel_3 = (|| $tool_ent_attr_fxlevel_2 [= $$tool_ent_cur_attr 3])
        ] (< $$tool_ent_cur_attr 0) [
            local _fxlevel
            _fxlevel = (- $$tool_ent_cur_attr)

            tool_ent_attr_fxlevel_1 = (& $_fxlevel 1)
            tool_ent_attr_fxlevel_2 = (!= (& $_fxlevel (<< 1 1)))
            tool_ent_attr_fxlevel_3 = (!= (& $_fxlevel (<< 1 2)))
        ]
    ]
]

// 1:<type>
tool_ent_fxlevel_setter = [
    result [
        local _fxlevel
        _fxlevel = 0
        _fxlevel = (| $_fxlevel $tool_ent_attr_fxlevel_1)
        _fxlevel = (| $_fxlevel (<< $tool_ent_attr_fxlevel_2 1))
        _fxlevel = (| $_fxlevel (<< $tool_ent_attr_fxlevel_3 2))

        $tool_ent_cur_attr = (at [0 -1 -2 -3 3 -5 2 0] $_fxlevel)
    ]
]

// 1:<attr name> 2:<flag names> 3:<negative filter>
tool_ent_flags_getter = [
    local _code _flag _srcvar
    _srcvar = "$tool_ent_cur_attr"

    // some flags support negative filtering when bitmask is below 0
    if $arg3 [
        _srcvar = "_mask"
        append _code "local _mask^n"
        append _code "_mask = (? (< $$tool_ent_cur_attr 0) (~ (* $$tool_ent_cur_attr -1)) $$tool_ent_cur_attr)^n"
    ]

    loop i (listlen $arg2) [
        _flag = (at $arg2 $i)

        append _code (concatword tool_ent_attr_ $arg1 _ $_flag " =")

        if $i [
            append _code (concatword "(!= (& $" $_srcvar " (<< 1 " $i ")))")
        ] [
            append _code (concatword "(& $" $_srcvar " 1)")
        ]

        append _code "^n"
    ]

    result $_code
]

// 1:<attr name> 2:<_flag names>
tool_ent_flags_setter = [
    local _code _flag

    _code = "$tool_ent_cur_attr = 0^n"

    loop i (listlen $arg2) [
        _flag = (at $arg2 $i)

        append _code "$tool_ent_cur_attr ="
        if $i [
            append _code (concatword "(| $$tool_ent_cur_attr (<< $tool_ent_attr_" $arg1 "_" $_flag " " $i "))")
        ] [
            append _code (concatword "(| $$tool_ent_cur_attr $tool_ent_attr_" $arg1 "_" $_flag ")")
        ]
        append _code "^n"
    ]

    result $_code
]

exec "config/tool/ents/light.cfg"
exec "config/tool/ents/mapmodel.cfg"
exec "config/tool/ents/playerstart.cfg"
exec "config/tool/ents/envmap.cfg"
exec "config/tool/ents/particles.cfg"
exec "config/tool/ents/sound.cfg"
exec "config/tool/ents/lightfx.cfg"
exec "config/tool/ents/decal.cfg"
exec "config/tool/ents/wind.cfg"
exec "config/tool/ents/weapon.cfg"
exec "config/tool/ents/teleport.cfg"
exec "config/tool/ents/actor.cfg"
exec "config/tool/ents/trigger.cfg"
exec "config/tool/ents/pusher.cfg"
exec "config/tool/ents/affinity.cfg"
exec "config/tool/ents/checkpoint.cfg"
exec "config/tool/ents/route.cfg"
exec "config/tool/ents/rail.cfg"
exec "config/tool/ents/camera.cfg"

tool_ent_add_attrs_outline = []

// 1:<type>
tool_ent_attr_idx_unmap_cache = [
    local _attridx_unmap _subtype_idx
    _subtype_idx = $[tool_ent_attr_subtype_@arg1]

    if (>= $_subtype_idx 0) [
        loop i (entityattrs (indexof $tool_ent_types $arg1)) [
            _attridx_unmap = (tool_ent_attr_idx_unmap $arg1 $i $tool_ent_subtype)
            [tool_ent_attr_@[i]_unmap] = $_attridx_unmap
        ]
    ] [
        loop i (entityattrs (indexof $tool_ent_types $arg1)) [
            [tool_ent_attr_@[i]_unmap] = $i
        ]
    ]
]

tool_ent_last_edit = 0

tool_ent_get_func = [
    case $tool_ent_ui_mode @T_ENT_UI_EDIT [
        result entget
    ] @T_ENT_UI_PLACER [
        result tool_ent_placer_template_get
    ]
]

tool_ent_set_func = [
    case $tool_ent_ui_mode @T_ENT_UI_EDIT [
        at [
            entattr
            entsmodify
        ] (tool_ent_delta_edit)
    ] @T_ENT_UI_PLACER [
        result tool_ent_placer_template_update
    ]
]

// 1:<type> 2:<attr idx list>
tool_ent_attr_change = [
    local _attr_setter _set_func
    _set_func = (tool_ent_set_func)

    [tool_ent_preset_@arg1]

    looplist attridx $arg2 [
        _attr_setter = (at $[tool_ent_setters_@arg1] (tool_ent_attr_idx_unmap_cached $attridx))
        @@(tool_ent_set_cur_attr attridx)
        _attr_setter

        if $_set_func [
            $_set_func $attridx $[tool_ent_attr_@attridx]
        ]
    ]
]

tool_ent_last_type = ""
tool_ent_last_subtype = -1

tool_ent_sync_attrs = [
    if $tool_ent_type [
        local _getters _attridx_unmap
        _getters = $[tool_ent_getters_@tool_ent_type]

        loop attridx (entityattrs (indexof $tool_ent_types $tool_ent_type)) [
            _attridx_unmap = (tool_ent_attr_idx_unmap_cached $attridx)
            entlast [
                [tool_ent_attr_@attridx] = (tool_ent_cached_attr @attridx)
            ]
            @@(tool_ent_set_cur_attr attridx)
            do (at $_getters $_attridx_unmap)
        ]

        [tool_ent_postget_@tool_ent_type]
    ]
]

tool_ent_sync_subtype = [
    local _subtype_idx
    _subtype_idx = $[tool_ent_attr_subtype_@tool_ent_type]
    tool_ent_subtype = 0

    if (>= $_subtype_idx 0) [
        tool_ent_subtype = (tool_ent_cached_attr $_subtype_idx)
    ]

    if (|| [!=s $tool_ent_type $tool_ent_last_type] [!= $tool_ent_subtype $tool_ent_last_subtype]) [
        tool_ent_last_type = $tool_ent_type
        tool_ent_last_subtype = $tool_ent_subtype
        tool_ent_attr_idx_unmap_cache $tool_ent_type
    ]
]

tool_ent_check_multi_compat = [
    entloopread [
        if (!=s $tool_ent_type (enttype)) [
            tool_ent_compatible = 0
        ]

        if (&& (> $_subtype_idx -1) [!= $tool_ent_subtype (entattr $_subtype_idx)]) [
            tool_ent_compatible = 0
        ]
    ]
]

tool_ent_sync = [
    local _subtype_idx _get_func
    _subtype_idx = -1
    _get_func = (tool_ent_get_func)

    tool_ent_cached = (_get_func)

    entlast [
        tool_ent_type = (at $tool_ent_cached 0)
        if $tool_ent_type [
            _subtype_idx = $[tool_ent_attr_subtype_@tool_ent_type]
            tool_ent_sync_subtype
        ]
    ]

    tool_ent_compatible = 1

    if (= $tool_ent_ui_mode @T_ENT_UI_EDIT) [
        @@tool_ent_check_multi_compat
    ]

    if (! (tool_ent_delta_edit)) tool_ent_sync_attrs
]

tool_ent_add_attrs_none = []

tool_ent_populate_attrs = [
    looplist type $tool_ent_types [
        tool_ent_param_next_idx = 0
        [tool_ent_add_attrs_@type]
    ]
]

tool_ent_populate_attrs
