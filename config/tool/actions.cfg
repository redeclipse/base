tool_actions = []
tool_actions_categories = []
tool_actions_num = 0

tool_do_action = [
    doargs (listassoc=s $$arg1 action)
]

TBI_KEY = 0
TBI_KMOD = 1

TOOL_ACTION_PRESS = 0
TOOL_ACTION_SCROLL = 1

// 1:<name> 2:<key> 3:<modifiers>
tool_set_bind_info = [
    [tb_@arg1] [ [@@arg2] @arg3 ]
]

// 1:<name>
tool_get_bind_info = [
    result $[tb_@arg1]
]

// 1:<name>
tool_has_bind_info = [
    result (identexists [tb_@arg1])
]

// 1:<name>
tool_str_bind_info = [
    local bind_info key modifiers
    bind_info = (tool_get_bind_info $arg1)
    key = (at $bind_info @TBI_KEY)
    modifiers = (at $bind_info @TBI_KMOD)

    result (prettybindinfo $key $modifiers)
]

// 1:<name>
toolunbind = [
    local bind_info key modifiers
    bind_info = (tool_get_bind_info $arg1)
    key = (at $bind_info @TBI_KEY)
    modifiers = (at $bind_info @TBI_KMOD)

    if $key [
        editbind $key [] $modifiers
        tool_set_bind_info $arg1 "" 0
    ]
]

// 1:<key> 2:<key modifier mask>
gettoolbind = [
    local binding prefix offset
    prefix = "tool_do_action "
    binding = (geteditbind $arg1 $arg2)
    offset = (strstr $binding $prefix)

    if (> $offset -1) [
        offset = (+ $offset (strlen $prefix))
        binding = (substr $binding $offset)
        result (at $binding 0)
    ] [
        result ""
    ]
]

// 1:<key> 2:<name> 3:<key modifier mask>
toolbind = [
    local old_action
    old_action = (gettoolbind $arg1 $arg3)

    toolunbind $arg2

    if $old_action [
        tool_set_bind_info $old_action "" 0
    ]

    tool_set_bind_info $arg2 $arg1 $arg3

    editbind $arg1 [
        tool_do_action @arg2
    ] $arg3
]

// 1:<category>
tool_action_add_category = [
    if (! (listhas $tool_actions_categories $arg1)) [
        append tool_actions_categories $arg1
        tool_actions_categories = (sortlist $tool_actions_categories a b [naturalsort $a $b])
        [tool_actions_@arg1] = []
    ]
]

// 1:<action name> 2:<prop>
tool_action_prop = [
    result [ (listassoc=s $@arg1 @arg2) ]
]

// 1:<name> 2:<properties>
tool_action = [
    local newaction
    newaction = []

    append newaction [name @arg1]
    append newaction $arg2

    if (! (listhas $tool_actions $arg1)) [
        append tool_actions $arg1
    ]

    $arg1 = $newaction

    if (! (tool_has_bind_info $arg1)) [
        defsvarp [tb_@arg1] ""
    ]

    local cat
    cat = @(tool_action_prop [$arg1] category)

    tool_action_add_category $cat
    append [tool_actions_@cat] $tool_actions_num

    tool_actions_num = (+ $tool_actions_num 1)
]
